definition {

	macro changeLocale {
		Click(locator1 = "ContentPerformance#LOCALE_BUTTON");

		Click(
			key_locale = "${locale}",
			locator1 = "Translation#DROPDOWN_MENU_ITEM");
	}

	macro checkLanguagesOrder {
		Click(locator1 = "ContentPerformance#LANGUAGE_DROPDOWN_BUTTON");

		var list1 = ListUtil.newListFromString("1,2,3,4,5,6,7,8,9,10,11,12,13");
		var list2 = ListUtil.newListFromString("English,Arabic,Catalan,Chinese,Dutch,Finnish,French,German,Hungarian,Japanese,Portuguese,Spanish,Swedish");
		var size = ListUtil.size("${list1}");
		var i = "0";

		while ("${i}" != "${size}") {
			var index = ListUtil.get("${list1}", "${i}");
			var language = ListUtil.get("${list2}", "${i}");

			AssertTextEquals.assertPartialText(
				key_index = "${index}",
				locator1 = "Translation#DROPDOWN_MENU_ITEM_INDEX",
				value1 = "${language}");

			var i = ${i} + 1;
			var i = StringUtil.valueOf("${i}");
		}
	}

	macro clickDoNotShowMeThisAgain {
		Click(locator1 = "ContentPerformance#CONTENT_PERFORMANCE_DO_NOT_SHOW_AGAIN");
	}

	macro connectDXPtoAnalyticsCloudNoSync {
		ACDXPSettings.goToInstanceSettingsAC();

		ACDXPSettings.connectAnalyticsCloud();

		ACDXPSettings.assertSuccessfulConnection();
	}

	macro openContentPerformance {
		if (!(IsVisible(locator1 = "ContentPerformance#SIDEBAR"))) {
			Click(locator1 = "ContentPerformance#CONTENT_PERFORMANCE_BUTTON");
		}

		while (IsNotVisible(locator1 = "ContentPerformance#SIDEBAR")) {
			WaitForElementPresent(locator1 = "ContentPerformance#SIDEBAR");
		}
	}

	macro showMetricsButton {
		Click(locator1 = "ContentDashboard#ROW_PANEL_WC");

		Click(locator1 = "ContentPerformance#ICON_VIEW_METRICS");

		while (IsNotVisible(locator1 = "ContentDashboard#CONTENT_PERFORMANCE_SIDEBAR")) {
			WaitForElementPresent(locator1 = "ContentDashboard#CONTENT_PERFORMANCE_SIDEBAR");
		}
	}

	macro viewTotalViews {
		ContentPerformance.openContentPerformance();

		while (IsElementPresent(locator1 = "ContentPerformance#TOTAL_VIEWS_EMPTY")) {
			Refresh();
		}

		AssertVisible(
			key_numberViews = "${numberViews}",
			locator1 = "ContentPerformance#TOTAL_VIEWS_NUMBER");
	}

	macro viewTrafficViews {
		ContentPerformance.openContentPerformance();

		while (IsElementPresent(locator1 = "ContentPerformance#TOTAL_VIEWS_EMPTY")) {
			Refresh();
		}

		AssertVisible(
			key_numberViews = "${numberViews}",
			key_trafficChannel = "${trafficChannel}",
			locator1 = "ContentPerformance#TRAFFIC_CHANNELS_VALUE");
	}

}