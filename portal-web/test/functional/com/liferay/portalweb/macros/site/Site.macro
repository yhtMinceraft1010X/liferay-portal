definition {

	var siteMember = "User";
	var siteType = "Open";

	macro activateInactive {
		var key_siteName = "${siteName}";

		Search.searchCP(searchTerm = "${siteName}");

		LexiconTable.clickEllipsisItem(
			item = "Activate",
			tableEntry = "${siteName}");

		Alert.viewSuccessMessage();
	}

	macro addAllMembersCP {
		Navigator.gotoNavItem(navItem = "Users");

		LexiconEntry.gotoAdd();

		SelectFrame(locator1 = "IFrame#MODAL_BODY");

		PortletEntry.selectAll();

		SelectFrameTop();

		Pause(locator1 = "3000");

		AssertClick(
			locator1 = "Button#DONE",
			value1 = "Done");

		Alert.viewSuccessMessage();
	}

	macro addBlankCP {
		Site.addCP(
			membershipType = "${membershipType}",
			parentSiteName = "${parentSiteName}",
			singleSiteTypePermission = "${singleSiteTypePermission}",
			siteName = "${siteName}",
			siteTemplateName = "${siteTemplateName}",
			siteType = "Blank",
			uncheckPropagation = "${uncheckPropagation}");
	}

	macro addCP {
		LexiconEntry.gotoAdd();

		if ("${singleSiteTypePermission}" != "true") {
			if (!(isSet(siteTemplateName))) {
				var siteTemplateName = "${siteType} Site";
			}

			LexiconCard.clickCard(card = "${siteTemplateName}");

			SelectFrame(locator1 = "IFrame#MODAL_BODY");

			PortletEntry.inputName(name = "${siteName}");

			Button.clickAdd();

			SelectFrameTop();

			Alert.viewSuccessMessage();
		}

		if ("${siteType}" == "Site Template") {
			if ("${uncheckPropagation}" == "true") {
				Click(
					key_itemName = "Pages",
					locator1 = "ListGroupItem#ITEM_TEXT");

				Navigator.gotoNavTab(navTab = "Pages");

				Uncheck.uncheckToggleSwitch(locator1 = "Checkbox#ENABLE_PROPAGATION");

				PortletEntry.save();

				Navigator.gotoBack();
			}
		}

		while (IsElementNotPresent(key_itemName = "Site Configuration", locator1 = "ListGroupItem#ITEM_TEXT")) {
			WaitForPageLoad();
		}

		WaitForElementPresent(
			key_itemName = "Site Configuration",
			locator1 = "ListGroupItem#ITEM_TEXT");

		Click(
			key_itemName = "Site Configuration",
			locator1 = "ListGroupItem#ITEM_TEXT");

		if ("${membershipType}" == "Private") {
			Select(
				locator1 = "SitesEditSite#MEMBERSHIP_TYPE",
				value1 = "Private");
		}
		else if ("${membershipType}" == "Restricted") {
			Select(
				locator1 = "SitesEditSite#MEMBERSHIP_TYPE",
				value1 = "Restricted");
		}
		else {
			Select(
				locator1 = "SitesEditSite#MEMBERSHIP_TYPE",
				value1 = "Open");
		}

		if (isSet(parentSiteName)) {
			WaitForLiferayEvent.initializeLiferayEventLog();

			Click(
				key_fieldLabel = "Parent Site",
				key_text = "Change",
				locator1 = "Button#ANY_WITH_LABEL");

			var key_parentSiteName = "${parentSiteName}";

			SelectFrame(locator1 = "IFrame#MODAL_BODY");

			Search.searchCP(searchTerm = "${parentSiteName}");

			var key_site = "${parentSiteName}";

			AssertTextEquals(
				locator1 = "ContentRow#ENTRY_CONTENT_ROW_TD_1_SITE_LINK",
				value1 = "${parentSiteName}");

			Click(locator1 = "ContentRow#ENTRY_CONTENT_ROW_TD_1_SITE_LINK");

			var key_parentSiteName = "${parentSiteName}";

			SelectFrameTop();
		}

		ScrollWebElementIntoView(locator1 = "Button#SAVE");

		PortletEntry.save();

		AssertTextEquals(
			locator1 = "SitesEditSite#DETAILS_NAME",
			value1 = "${siteName}");
	}

	macro addCustomCP {
		LexiconEntry.gotoAdd();

		LexiconCard.clickCard(card = "${siteTemplateName}");

		SelectFrame(locator1 = "IFrame#MODAL_BODY");

		PortletEntry.inputName(name = "${siteName}");

		Button.clickAdd();

		SelectFrameTop();

		Alert.viewSuccessMessage();

		Click(
			key_itemName = "Site Configuration",
			locator1 = "ListGroupItem#ITEM_TEXT");

		AssertTextEquals(
			locator1 = "SitesEditSite#DETAILS_NAME",
			value1 = "${siteName}");
	}

	macro addLocalizedName {
		Click(
			key_itemName = "Site Configuration",
			locator1 = "ListGroupItem#ITEM_TEXT");

		var list1 = ListUtil.newListFromString("${localeList}");
		var list2 = ListUtil.newListFromString("${localizedNameList}");
		var size = ListUtil.size("${list1}");
		var i = "0";

		while ("${i}" != "${size}") {
			var locale = ListUtil.get("${list1}", "${i}");
			var localizedName = ListUtil.get("${list2}", "${i}");

			PortletEntry.inputLocalizedName(
				language = "${locale}",
				localizedName = "${localizedName}");

			var i = ${i} + 1;
			var i = StringUtil.valueOf("${i}");
		}

		PortletEntry.save();
	}

	macro addSiteTemplateCP {
		Site.addCP(
			membershipType = "${membershipType}",
			parentSiteName = "${parentSiteName}",
			siteName = "${siteName}",
			siteTemplateName = "${siteTemplateName}",
			siteType = "Site Template",
			uncheckPropagation = "${uncheckPropagation}");
	}

	macro addVirtualHostsURLCP {
		Click(
			key_itemName = "Site Configuration",
			locator1 = "ListGroupItem#ITEM_TEXT");

		Navigator.gotoNavTab(navTab = "Site URL");

		if (isSet(pageVirtualHosts)) {
			Type(
				key_text = "Virtual Host",
				locator1 = "TextInput#ANY",
				value1 = "${pageVirtualHostURL}");
		}

		if (isSet(publicVirtualHosts)) {
			Type(
				key_text = "Public Pages Virtual Host",
				locator1 = "TextInput#ANY",
				value1 = "${publicPageVirtualHostURL}");
		}

		if (isSet(privateVirtualHosts)) {
			Type(
				key_text = "Private Pages Virtual Host",
				locator1 = "TextInput#ANY",
				value1 = "${privatePageVirtualHostURL}");
		}

		if (isSet(stagingPages)) {
			Type(
				key_text = "Staging Pages",
				locator1 = "TextInput#ANY",
				value1 = "${stagingPages}");
		}

		if (isSet(stagingPublicPages)) {
			Type(
				key_text = "Staging Public Pages",
				locator1 = "TextInput#ANY",
				value1 = "${stagingPublicPages}");
		}

		if (isSet(stagingPrivatePages)) {
			Type(
				key_text = "Staging Private Pages",
				locator1 = "TextInput#ANY",
				value1 = "${stagingPrivatePages}");
		}

		PortletEntry.save();
	}

	macro approveJoinSiteRestrictedViaMySitesPG {
		var key_userScreenName = "${userScreenName}";

		Click(locator1 = "Icon#INFO");

		AssertTextEquals.assertPartialText(
			locator1 = "SiteMemberships#MEMBERSHIP_TYPE",
			value1 = "Membership Type: Restricted");

		AssertElementPresent(locator1 = "SiteMemberships#REQUEST_MESSAGE");

		Click(locator1 = "Icon#HEADER_VERTICAL_ELLIPSIS");

		Click(locator1 = "SiteMemberships#REQUEST_MESSAGE");

		LexiconEntry.changeDisplayStyle(displayStyle = "table");

		AssertTextEquals(
			locator1 = "SiteMembershipsViewMembershipRequests#USER_NAME",
			value1 = "${userFirstName} ${userLastName}");

		if (!(isSet(memebershipComment))) {
			var memebershipComment = "Please allow me to join the Restricted Site.";
		}

		AssertTextEquals(
			locator1 = "SiteMembershipsViewMembershipRequests#USER_COMMENTS",
			value1 = "${memebershipComment}");

		LexiconTable.clickEllipsisItem(
			item = "Reply",
			tableEntry = "${userFirstName} ${userLastName}");

		Select(
			locator1 = "SiteMembershipsViewMembershipRequests#STATUS",
			value1 = "Approve");

		Type(
			locator1 = "SiteMembershipsViewMembershipRequests#REPLY_COMMENTS_AREA",
			value1 = "You may join the Restricted Site.");

		Button.clickSave();

		VerifyElementPresent(locator1 = "Message#SUCCESS");

		Navigator.gotoNavItem(navItem = "Approved");

		LexiconEntry.changeDisplayStyle(displayStyle = "table");

		AssertTextEquals(
			locator1 = "SiteMembershipsViewMembershipRequests#USER_NAME",
			value1 = "${userFirstName} ${userLastName}");

		AssertTextEquals(
			locator1 = "SiteMembershipsViewMembershipRequests#REPLIER_NAME",
			value1 = "Test Test");
	}

	macro assertMemberListNotVisibleCP {
		for (var userEmail : list "${usersEmailsList}") {
			var userName = StringUtil.replace("${userEmail}", "@liferay.com", "");

			Site.assertMemberNotVisibleCP(
				userFirstName = "${userName}",
				userLastName = "${userName}",
				userScreenName = "${userName}");
		}
	}

	macro assertMemberNotVisibleCP {
		var key_userScreenName = "${userScreenName}";

		AssertTextNotPresent(
			locator1 = "SiteMemberships#USER_TABLE_NAME",
			value1 = "${userFirstName} ${userLastName}");

		AssertTextNotPresent(
			locator1 = "SiteMemberships#USER_TABLE_SCREEN_NAME",
			value1 = "${userScreenName}");
	}

	macro assignDefaultSiteTeam {
		Click(
			key_itemName = "Users",
			locator1 = "ListGroupItem#ITEM_TEXT");

		Click(locator1 = "SiteSettingsDefaultUserAssociations#TEAMS_SELECT");

		SelectFrame(locator1 = "IFrame#MODAL_BODY");

		LexiconEntry.changeDisplayStyle(displayStyle = "table");

		var key_rowEntry = "${teamName}";

		AssertClick(
			locator1 = "ContentRow#ENTRY_CONTENT_ENTRY_NAME_LINK",
			value1 = "${teamName}");

		SelectFrame(value1 = "relative=top");

		PortletEntry.save();

		var key_teamName = "${teamName}";

		AssertTextEquals(
			locator1 = "SiteSettingsDefaultUserAssociations#TEAMS_SELECTED",
			value1 = "${teamName}");
	}

	macro assignOrganizationAsMemberCP {
		var key_card = "${orgName}";

		Navigator.gotoNavItem(navItem = "Organizations");

		LexiconEntry.gotoAdd();

		SelectFrame(locator1 = "IFrame#MODAL_BODY");

		LexiconEntry.changeDisplayStyle(displayStyle = "cards");

		AssertTextEquals(
			locator1 = "Card#CARD_TITLE",
			value1 = "${orgName}");

		LexiconCard.checkCardCheckbox(card = "${orgName}");

		SelectFrameTop();

		Button.clickDone();

		Alert.viewSuccessMessage();
	}

	macro assignSiteRoleCP {
		LexiconCard.clickEllipsisItem(
			card = "${resourceName}",
			item = "Assign Roles");

		SelectFrame(locator1 = "IFrame#MODAL_BODY");

		LexiconCard.checkCardCheckbox(card = "${roleTitle}");

		SelectFrameTop();

		Button.clickDone();

		Alert.viewSuccessMessage();
	}

	macro assignSiteRoleToUserGroup {
		var key_userGroupName = "${userGroupName}";

		Navigator.gotoNavItem(navItem = "User Groups");

		LexiconEntry.changeDisplayStyle(displayStyle = "table");

		AssertTextEquals(
			locator1 = "UserGroups#USER_GROUP_TABLE_NAME",
			value1 = "${userGroupName}");

		Click(locator1 = "SiteMembershipsUserGroups#USER_GROUP_TABLE_ACTIONS");

		AssertClick(
			key_menuItem = "Assign Roles",
			locator1 = "MenuItem#ANY_MENU_ITEM",
			value1 = "Assign Roles");

		SelectFrame(locator1 = "IFrame#MODAL_BODY");

		LexiconEntry.changeDisplayStyle(displayStyle = "cards");

		LexiconCard.checkCardCheckbox(card = "${roleName}");

		SelectFrameTop();

		Button.clickDone();

		Alert.viewSuccessMessage();
	}

	macro assignUserAsMemberCP {
		var key_cardText = "${userScreenName}";

		WaitForLiferayEvent.initializeLiferayEventLog();

		LexiconEntry.gotoAdd();

		AssertVisible(locator1 = "Button#DONE");

		if (isSet(depotName)) {
			SelectFrame(locator1 = "IFrame#ADD_USERS_TO_ASSET_LIBRARY");
		}
		else {
			SelectFrame(locator1 = "IFrame#ADD_USERS_TO_SITE");
		}

		Pause(locator1 = "3000");

		LexiconEntry.changeDisplayStyle(displayStyle = "cards");

		if (isSet(userMiddleName)) {
			LexiconCard.checkCardCheckbox(card = "${userFirstName} ${userMiddleName} ${userLastName}");
		}
		else {
			LexiconCard.checkCardCheckbox(card = "${userFirstName} ${userLastName}");
		}

		SelectFrameTop();

		Button.clickDone();

		Alert.viewSuccessMessage();
	}

	macro assignUserGroupAsMemberCP {
		Navigator.gotoNavItem(navItem = "User Groups");

		Pause(locator1 = "5000");

		LexiconEntry.gotoAdd();

		SelectFrame(locator1 = "IFrame#MODAL_BODY");

		Pause(locator1 = "3000");

		LexiconEntry.selectEntry(rowEntry = "${userGroupName}");

		SelectFrameTop();

		Button.clickDone();

		Alert.viewSuccessMessage();
	}

	macro assignUserSitesCP {
		var key_userScreenName = "${userScreenName}";

		AssertTextEquals(
			locator1 = "UsersAndOrganizations#USER_TABLE_SCREEN_NAME",
			value1 = "${userScreenName}");

		AssertClick(
			locator1 = "UsersAndOrganizations#USER_TABLE_SCREEN_NAME",
			value1 = "${userScreenName}");

		UserNavigator.gotoMemberships();

		AssertClick(
			locator1 = "UsersAndOrganizationsEditUser#SITES_SELECT_LINK",
			value1 = "Select");

		SelectFrame(locator1 = "IFrame#SELECT_USER_SITES");

		var key_siteName = "${siteName}";

		Type(
			locator1 = "TextInput#SITE_SEARCH_KEYWORDS",
			value1 = "${siteName}");

		KeyPress(
			locator1 = "TextInput#SEARCH",
			value1 = "\RETURN");

		AssertTextEquals.assertPartialText(
			locator1 = "UsersAndOrganizationsEditUser#SITES_TABLE_NAME",
			value1 = "${siteName}");

		Click(locator1 = "SitesSelectSite#SITES_TABLE_CHOOSE_SITE_LINK");

		SelectFrame(value1 = "relative=top");

		PortletEntry.save();
	}

	macro changeSiteLogo {
		ProductMenu.gotoPortlet(
			category = "Site Builder",
			portlet = "Pages");

		if (IsElementPresent(locator1 = "Icon#HEADER_VERTICAL_ELLIPSIS")) {
			Click(locator1 = "Icon#HEADER_VERTICAL_ELLIPSIS");

			MenuItem.click(menuItem = "Configuration");
		}
		else {
			PagesAdmin.togglePrivacy(privacy = "${pageName}");

			Click(
				key_pageName = "${pageName}",
				locator1 = "Icon#COG_PAGE");
		}

		Panel.expandPanel(panel = "Logo");

		PagesAdmin.uploadLogo(logoFile = "${logoFile}");
	}

	macro configureCurrentLanguagesCP {
		if (isSet(category)) {
			Click(
				key_itemName = "${category}",
				locator1 = "ListGroupItem#ITEM_TEXT");
		}
		else {
			Click(
				key_itemName = "Localization",
				locator1 = "ListGroupItem#ITEM_TEXT");
		}

		Check.checkNotVisible(
			index = "2",
			locator1 = "SiteSettingsDisplaySettings#LANGUAGES_RADIO");

		if (isSet(currentSiteLanguages)) {
			while (IsElementPresent(index = "1", key_position = "left", locator1 = "SiteSettingsDisplaySettings#LANGUAGES_SELECT_OPTION")) {
				Click(
					index = "1",
					key_position = "left",
					locator1 = "SiteSettingsDisplaySettings#LANGUAGES_SELECT_OPTION");

				Click(locator1 = "SiteSettingsDisplaySettings#TRANSFER_ITEM_LEFT_TO_RIGHT");
			}

			var defaultSiteLanguage = "${defaultSiteLanguage}";

			Select(
				locator1 = "SiteSettingsDisplaySettings#DEFAULT_LANGUAGE_SELECT_FIELD",
				value1 = "${defaultSiteLanguage}");

			while (IsElementPresent(index = "2", key_position = "right", locator1 = "SiteSettingsDisplaySettings#LANGUAGES_SELECT_OPTION")) {
				var currentSiteLanguage = selenium.getText("//div[contains(@class,'listbox-right')]//select/option");

				if ("${currentSiteLanguage}" == "${defaultSiteLanguage}") {
					Click(
						index = "2",
						key_position = "right",
						locator1 = "SiteSettingsDisplaySettings#LANGUAGES_SELECT_OPTION");

					Click(locator1 = "SiteSettingsDisplaySettings#TRANSFER_ITEM_RIGHT_TO_LEFT");
				}
				else {
					Click(
						index = "1",
						key_position = "right",
						locator1 = "SiteSettingsDisplaySettings#LANGUAGES_SELECT_OPTION");

					Click(locator1 = "SiteSettingsDisplaySettings#TRANSFER_ITEM_RIGHT_TO_LEFT");
				}
			}

			for (var currentSiteLanguage : list "${currentSiteLanguages}") {
				if ("${currentSiteLanguage}" != "${defaultSiteLanguage}") {
					DoubleClick(
						key_optionValue = "${currentSiteLanguage}",
						key_position = "left",
						locator1 = "SiteSettingsDisplaySettings#LANGUAGES_SELECT_SPECIFIC_OPTION");

					Click(locator1 = "SiteSettingsDisplaySettings#TRANSFER_ITEM_LEFT_TO_RIGHT");

					AssertTextEquals.assertPartialText(
						key_position = "right",
						locator1 = "SiteSettingsDisplaySettings#LANGUAGES_SELECT_FIELD",
						value1 = "${currentSiteLanguage}");
				}
			}
		}
		else if (isSet(defaultSiteLanguage)) {
			var defaultSiteLanguage = "${defaultSiteLanguage}";

			Select(
				locator1 = "SiteSettingsDisplaySettings#DEFAULT_LANGUAGE_SELECT_FIELD",
				value1 = "${defaultSiteLanguage}");
		}

		if (isSet(localizedSaveButton)) {
			var key_saveButton = "${localizedSaveButton}";

			AssertClick(
				locator1 = "Button#SAVE_LOCALIZED",
				value1 = "${localizedSaveButton}");
		}
		else {
			while (IsElementNotPresent(locator1 = "Button#SAVE")) {
				Pause(locator1 = "1000");
			}

			Click(locator1 = "Button#SAVE");
		}

		if (isSet(localizedSuccessMessage)) {
			var localizedSuccessMessage = "${localizedSuccessMessage}";

			VerifyElementPresent(locator1 = "Message#SUCCESS");
		}
		else {
			Alert.viewSuccessMessage();
		}
	}

	macro deactivateCP {
		var key_siteName = "${siteName}";

		Search.searchCP(searchTerm = "${siteName}");

		AssertTextEquals.assertPartialText(
			locator1 = "Sites#SITE_TABLE_NAME",
			value1 = "${siteName}");

		LexiconTable.clickEllipsisItem(
			item = "Deactivate",
			tableEntry = "${siteName}");

		AssertConfirm(value1 = "Are you sure you want to deactivate this?");

		if ("${successMessage}" == "true") {
			Alert.viewSuccessMessage();
		}
		else {
			Alert.viewRequestFailedToComplete();

			AssertTextEquals.assertPartialText(
				locator1 = "Message#ERROR_2",
				value1 = "The site cannot be deleted or deactivated because you are accessing the site.");
		}
	}

	macro deleteAllCP {
		PortletEntry.selectAll();

		Click(locator1 = "Icon#DELETE");

		AssertTextEquals(
			locator1 = "Modal#BODY",
			value1 = "Deleting a site is an action impossible to revert. All content will be removed and it will not be possible to recover it.");

		Click(
			key_text = "Delete",
			locator1 = "Modal#MODAL_FOOTER_BUTTON");

		Alert.viewSuccessMessage();
	}

	macro deleteCP {
		var key_siteName = "${siteName}";

		Search.searchCP(searchTerm = "Name");

		AssertTextEquals.assertPartialText(
			locator1 = "Sites#SITE_TABLE_NAME",
			value1 = "${siteName}");

		LexiconTable.clickEllipsisItem(
			item = "Delete",
			tableEntry = "${siteName}");

		AssertTextEquals(
			locator1 = "Modal#BODY",
			value1 = "Deleting a site is an action impossible to revert. All content will be removed and it will not be possible to recover it.");

		Click(
			key_text = "Delete",
			locator1 = "Modal#MODAL_FOOTER_BUTTON");

		Alert.viewSuccessMessage();

		AssertTextEquals(
			locator1 = "Message#EMPTY_INFO",
			value1 = "No sites were found.");

		AssertTextNotPresent(value1 = "${siteName}");
	}

	macro disableRecycleBinCP {
		Click(
			key_itemName = "Recycle Bin",
			locator1 = "ListGroupItem#ITEM_TEXT");

		Uncheck.uncheckToggleSwitchNoErrorNoAssert(locator1 = "Checkbox#ENABLE_RECYCLE_BIN");

		AssertConfirm(value1 = "Disabling the Recycle Bin prevents the restoring of content that has been moved to the Recycle Bin.");

		AssertNotChecked.assertNotCheckedNotVisible(locator1 = "Checkbox#ENABLE_RECYCLE_BIN");

		PortletEntry.save();
	}

	macro editCP {
		Click(
			key_itemName = "Site Configuration",
			locator1 = "ListGroupItem#ITEM_TEXT");

		AssertTextEquals(
			locator1 = "TextInput#NAME",
			value1 = "${siteName}");

		PortletEntry.inputName(name = "${siteNameEdit}");

		PortletEntry.save();

		AssertTextEquals(
			locator1 = "TextInput#NAME",
			value1 = "${siteNameEdit}");
	}

	macro editFriendlyURLCP {
		Click(
			key_itemName = "Site Configuration",
			locator1 = "ListGroupItem#ITEM_TEXT");

		Navigator.gotoNavTab(navTab = "Site URL");

		AssertTextEquals(
			locator1 = "SiteSettingsSiteURL#SITE_URL_FRIENDLY_URL_LABEL",
			value1 = "Friendly URL");

		Pause(locator1 = "5000");

		Type(
			locator1 = "TextInput#FRIENDLY_URL_SITE_SETTINGS",
			value1 = "${siteFriendlyURLEdit}");

		Button.clickSave();

		if ("${friendlyURLInvalid}" == "true") {
			Alert.viewRequestFailedToComplete();

			AssertTextEquals.assertPartialText(
				locator1 = "Message#ERROR_1",
				value1 = "The friendly URL has too many slashes.");

			AssertTextEquals(
				locator1 = "TextInput#FRIENDLY_URL_SITE_SETTINGS",
				value1 = "${siteFriendlyURLEdit}");
		}
		else {
			Alert.viewSuccessMessage();
		}
	}

	macro editGoogleAnalyticsIDCP {
		Click(
			key_itemName = "Analytics",
			locator1 = "ListGroupItem#ITEM_TEXT");

		Type(
			locator1 = "TextInput#GOOGLE_ANALYTICS_ID",
			value1 = "${googleAnalyticsId}");

		PortletEntry.save();
	}

	macro editPiwikCP {
		Click(
			key_itemName = "Analytics",
			locator1 = "ListGroupItem#ITEM_TEXT");

		Type(
			locator1 = "TextInput#PIWIK",
			value1 = "${piwik}");

		Button.click(button = "Save");

		if (isSet(confirmationText)) {
			AssertConfirm(value1 = "${confirmationText}");
		}

		Alert.viewSuccessMessage();

		WaitForLiferayEvent.initializeLiferayEventLog();
	}

	macro getSiteId {
		ProductMenu.gotoPortlet(
			category = "Configuration",
			portlet = "Site Settings");

		Click(
			key_itemName = "Site Configuration",
			locator1 = "ListGroupItem#ITEM_TEXT");

		AssertElementPresent(locator1 = "SitesEditSite#SITE_ID");

		var siteId = selenium.getElementValue("SitesEditSite#SITE_ID");

		return "${siteId}";
	}

	macro gotoSiteViaMySitesPG {
		LexiconList.clickListEntryTitle(listEntry = "${siteName}");
	}

	macro importSiteFromTempFolder {
		var siteLarFileName = TestCase.getDownloadedTempFileName(fileNamePattern = "Pages-*.lar");

		UploadTempFile.uploadTempFileHiddenNoMouseOver(
			locator1 = "TextInput#FILE",
			value1 = "${siteLarFileName}");

		Button.click(button = "Continue");

		Button.click(button = "Import");

		AssertTextEquals(
			locator1 = "ExportImport#CURRENT_AND_PREVIOUS_STATUS_1",
			value1 = "Successful");
	}

	macro inactivateCP {
		Click(
			key_itemName = "Site Configuration",
			locator1 = "ListGroupItem#ITEM_TEXT");

		AssertTextEquals(
			locator1 = "TextInput#NAME",
			value1 = "${siteName}");

		AssertTextEquals(
			locator1 = "SitesEditSite#ACTIVE_LABEL",
			value1 = "Active");

		Uncheck.uncheckToggleSwitch(locator1 = "Checkbox#ACTIVE");

		PortletEntry.save();
	}

	macro joinViaMySitesPG {
		var key_siteName = "${siteName}";

		AssertTextEquals(
			locator1 = "Message#EMPTY_INFO",
			value1 = "No sites were found.");

		Navigator.gotoNavItem(navItem = "Available Sites");

		Pause(locator1 = "3000");

		if ("${membershipType}" == "Private") {
			LexiconList.viewListEntryNotPresent(listEntry = "${siteName}");
		}
		else if ("${membershipType}" == "Restricted") {
			LexiconList.viewListEntryPresent(listEntry = "${siteName}");

			LexiconList.viewListEntrySubtitle(
				listEntry = "${siteName}",
				listEntrySubtitle = "1 User");

			LexiconList.clickEllipsisItem(
				item = "Request Membership",
				listEntry = "${siteName}",
				userRole = "${userRole}");

			if (!(isSet(memebershipComment))) {
				var memebershipComment = "Please allow me to join the Restricted Site.";
			}

			Type(
				locator1 = "TextArea#COMMENTS",
				value1 = "${memebershipComment}");

			Button.clickSave();

			Alert.viewSuccessMessageText(successMessage = "Your request was sent. You will receive a reply by email.");

			MySites.viewMembershipRequested(
				siteName = "${siteName}",
				userRole = "${userRole}");
		}
		else {
			LexiconList.viewListEntryPresent(listEntry = "${siteName}");

			LexiconList.viewListEntrySubtitle(
				listEntry = "${siteName}",
				listEntrySubtitle = "1 User");

			LexiconList.clickEllipsisItem(
				item = "Join",
				listEntry = "${siteName}",
				userRole = "${userRole}");

			Alert.viewSuccessMessage();

			LexiconList.viewListEntryPresent(listEntry = "${siteName}");

			LexiconList.viewListEntrySubtitle(
				listEntry = "${siteName}",
				listEntrySubtitle = "2 Users");

			LexiconList.clickEllipsis(
				listEntry = "${siteName}",
				userRole = "${userRole}");

			AssertVisible(
				key_menuItem = "Leave",
				locator1 = "MenuItem#ANY_MENU_ITEM",
				value1 = "Leave");
		}
	}

	macro leaveViaMySitesPG {
		LexiconList.viewListEntryPresent(listEntry = "${siteName}");

		LexiconList.viewListEntrySubtitle(
			listEntry = "${siteName}",
			listEntrySubtitle = "2 Users");

		Navigator.gotoNavItem(navItem = "Available Sites");

		LexiconList.viewListEntryPresent(listEntry = "${siteName}");

		LexiconList.viewListEntrySubtitle(
			listEntry = "${siteName}",
			listEntrySubtitle = "2 Users");

		LexiconList.clickEllipsisItem(
			item = "Leave",
			listEntry = "${siteName}",
			userRole = "${userRole}");

		Alert.viewSuccessMessage();

		LexiconList.viewListEntryPresent(listEntry = "${siteName}");

		LexiconList.viewListEntrySubtitle(
			listEntry = "${siteName}",
			listEntrySubtitle = "1 User");

		LexiconList.clickEllipsis(
			listEntry = "${siteName}",
			userRole = "${userRole}");

		AssertVisible(
			key_menuItem = "Join",
			locator1 = "MenuItem#ANY_MENU_ITEM",
			value1 = "Join");
	}

	macro limitMembership {
		Click(
			key_itemName = "Site Configuration",
			locator1 = "ListGroupItem#ITEM_TEXT");

		if ("${siteLimitMembership}" == "Yes") {
			Check.checkToggleSwitch(locator1 = "SitesEditSite#PARENT_SITE_LIMIT_MEMBERSHIP_CHECKBOX");
		}
		else if ("${siteLimitMembership}" == "No") {
			Uncheck.uncheckToggleSwitch(locator1 = "SitesEditSite#PARENT_SITE_LIMIT_MEMBERSHIP_CHECKBOX");
		}

		PortletEntry.save();
	}

	macro manageMembershipOptions {
		Click(
			key_itemName = "Site Configuration",
			locator1 = "ListGroupItem#ITEM_TEXT");

		Pause(locator1 = "5000");

		if ("${selectMembershipType}" == "true") {
			AssertTextEquals(
				locator1 = "SitesEditSite#MEMBERSHIP_OPTIONS_MEMBERSHIP_TYPE_LABEL",
				value1 = "Membership Type");

			Select(
				locator1 = "SitesEditSite#MEMBERSHIP_OPTIONS_MEMBERSHIP_TYPE",
				value1 = "${membershipType}");
		}

		if ("${allowManualMembershipManagement}" == "true") {
			Check.checkToggleSwitch(locator1 = "Checkbox#ALLOW_MANUAL_MEMBERSHIP_MANAGEMENT");
		}
		else if ("${allowManualMembershipManagement}" == "false") {
			Uncheck.uncheckToggleSwitch(locator1 = "Checkbox#ALLOW_MANUAL_MEMBERSHIP_MANAGEMENT");
		}

		PortletEntry.save();
	}

	macro openSiteMembershipsAdmin {
		Navigator.openWithAppendToBaseURL(urlAppend = "group/${siteURLKey}/~/control_panel/manage/-/site_memberships");
	}

	macro openSitesAdmin {
		Navigator.openWithAppendToBaseURL(urlAppend = "group/control_panel/manage/-/sites");
	}

	macro openSiteSettingsAdmin {
		Navigator.openWithAppendToBaseURL(urlAppend = "group/${siteURLKey}/~/control_panel/manage/-/site/settings");
	}

	macro openSiteTeamsAdmin {
		Navigator.openWithAppendToBaseURL(urlAppend = "group/${siteURLKey}/~/control_panel/manage/-/site_teams");
	}

	macro openToSiteSettingsPages {
		if ("${portlet}" == "Open Graph") {
			var configurationScreenKey = "site-configuration-open-graph";
		}

		if ("${portlet}" == "Pages") {
			var configurationScreenKey = "site-configuration-pages";
		}

		Navigator.openWithAppendToBaseURL(urlAppend = "group/${siteURLKey}/~/control_panel/manage/-/site/settings?_com_liferay_site_admin_web_portlet_SiteSettingsPortlet_mvcRenderCommandName=%2Fconfiguration_admin%2Fview_configuration_screen&_com_liferay_site_admin_web_portlet_SiteSettingsPortlet_configurationScreenKey=${configurationScreenKey}");
	}

	macro removeMemberCP {
		var key_cardText = "${userScreenName}";

		Search.searchCP(searchTerm = "${userScreenName}");

		if (isSet(userMiddleName)) {
			LexiconCard.viewCardTitle(card = "${userFirstName} ${userMiddleName} ${userLastName}");
		}
		else {
			LexiconCard.viewCardTitle(card = "${userFirstName} ${userLastName}");
		}

		LexiconCard.viewCardSubtitle(
			card = "${userFirstName} ${userLastName}",
			cardSubtitle = "${userScreenName}");

		LexiconCard.clickEllipsisItem(
			card = "${userScreenName}",
			item = "Remove Membership");

		Confirm();

		Alert.viewSuccessMessage();
	}

	macro removeOrganizationCP {
		LexiconEntry.changeDisplayStyle(displayStyle = "table");

		if (isSet(bulkEditing)) {
			for (var orgName : list "${orgNameList}") {
				Check(
					key_tableEntry = "${userGroupName}",
					locator1 = "LexiconTable#TABLE_ENTRY_CHECKBOX");
			}

			ClickNoError(locator1 = "Icon#DELETE");
		}
		else {
			Search.searchCP(searchTerm = "${orgName}");

			LexiconTable.clickEllipsisItem(
				item = "Remove Membership",
				tableEntry = "${orgName}");
		}

		Confirm();

		Alert.viewSuccessMessage();
	}

	macro removeUserGroupCP {
		LexiconEntry.changeDisplayStyle(displayStyle = "table");

		if (isSet(bulkEditing)) {
			for (var userGroupName : list "${userGroupNameList}") {
				Check(
					key_tableEntry = "${userGroupName}",
					locator1 = "LexiconTable#TABLE_ENTRY_CHECKBOX");
			}

			ClickNoError(locator1 = "Icon#DELETE");
		}
		else {
			Search.searchCP(searchTerm = "${userGroupName}");

			Click.waitForMenuToggleJSClick(
				key_rowEntry = "${userGroupName}",
				locator1 = "Icon#TABLE_VERTICAL_ELLIPSIS");

			MenuItem.clickNoError(menuItem = "Remove Membership");
		}

		Confirm();

		Alert.viewSuccessMessage();
	}

	macro searchCP {
		Search.searchCP(searchTerm = "Site Name");

		Site.viewSearchResultsCP(siteName = "Site Name");
	}

	macro searchMemberCP {
		var searchTerm = "${userEmailAddressSearchValue}";

		if (isSet(userScreenNameSearchValue)) {
			var searchTerm = "${userScreenNameSearchValue}";
		}
		else if (isSet(userFirstNameSearchValue)) {
			var searchTerm = "${userFirstNameSearchValue}";
		}
		else if (isSet(userLastNameSearchValue)) {
			var searchTerm = "${userLastNameSearchValue}";
		}

		Search.searchCP(searchTerm = "${searchTerm}");

		Site.viewMemberCP(
			userFirstName = "${userFirstName}",
			userLastName = "${userLastName}",
			userScreenName = "${userScreenName}");
	}

	macro searchMembershipCP {
		Search.searchCP(searchTerm = "${searchValue}");

		if ("${emptyResults}" == "true") {
			AssertElementPresent(locator1 = "Message#EMPTY_INFO");
		}
		else {
			LexiconEntry.changeDisplayStyle(displayStyle = "table");

			LexiconTable.viewTableEntryTitle(tableEntry = StringUtil.unquote("${searchValue}"));
		}
	}

	macro selectPageType {
		Click(
			key_itemName = "Pages",
			locator1 = "ListGroupItem#ITEM_TEXT");

		Navigator.gotoNavTab(navTab = "Pages");

		if ("${selectPublicPages}" == "true") {
			Select(
				locator1 = "SitesEditSite#PAGES_PUBLIC_PAGES_SITE_TEMPLATE_SELECT",
				value1 = "${siteTemplate}");
		}
		else if ("${selectPrivatePages}" == "true") {
			Select(
				locator1 = "SitesEditSite#PAGES_PRIVATE_PAGES_SITE_TEMPLATE_SELECT",
				value1 = "${siteTemplate}");

			if ("${checkPropagation}" == "true") {
				Check.checkToggleSwitch(locator1 = "Checkbox#PRIVATE_PAGES_ENABLE_PROPAGATION");
			}
		}

		PortletEntry.save();
	}

	macro selectParentSite {
		Click(
			key_itemName = "Site Configuration",
			locator1 = "ListGroupItem#ITEM_TEXT");

		Click(
			key_fieldLabel = "Parent Site",
			key_text = "Change",
			locator1 = "Button#ANY_WITH_LABEL");

		var key_parentSiteName = "${parentSiteName}";

		SelectFrame(locator1 = "SitesSelectSite#SELECT_SITE_IFRAME");

		Search.searchCP(searchTerm = "${parentSiteName}");

		if ("${chooseButtonAvailable}" == "false") {
			AssertElementNotPresent(
				key_parentSiteName = "${parentSiteName}",
				locator1 = "SitesSelectSite#SITES_TABLE_NAME");

			AssertTextEquals(
				locator1 = "Message#EMPTY_INFO",
				value1 = "No sites were found.");
		}

		Close(locator1 = "null");

		SelectFrame(value1 = "relative=top");
	}

	macro tearDownCP {
		ApplicationsMenu.gotoPortlet(
			category = "Sites",
			panel = "Control Panel",
			portlet = "Sites");

		Search.searchCP(searchTerm = "Name");

		AssertVisible(locator1 = "Link#CLEAR");

		if (IsElementNotPresent(locator1 = "Message#EMPTY_INFO")) {
			PortletEntry.selectAll();

			Click(locator1 = "Icon#DELETE");

			AssertTextEquals(
				locator1 = "Modal#BODY",
				value1 = "Deleting a site is an action impossible to revert. All content will be removed and it will not be possible to recover it.");

			Click(
				key_text = "Delete",
				locator1 = "Modal#MODAL_FOOTER_BUTTON");

			Alert.viewSuccessMessage();
		}
	}

	macro unassignSiteRoleToUserGroup {
		var key_userGroupName = "${userGroupName}";

		Navigator.gotoNavItem(navItem = "User Groups");

		Search.searchCP(searchTerm = "${userGroupName}");

		LexiconEntry.changeDisplayStyle(displayStyle = "table");

		AssertTextEquals(
			locator1 = "UserGroups#USER_GROUP_TABLE_NAME",
			value1 = "${userGroupName}");

		LexiconEntry.openEntryMenu(rowEntry = "${userGroupName}");

		MenuItem.click(menuItem = "Unassign Roles");

		SelectFrame(locator1 = "IFrame#MODAL_BODY");

		LexiconEntry.changeDisplayStyle(displayStyle = "cards");

		LexiconCard.checkCardCheckbox(card = "${roleName}");

		SelectFrameTop();

		Button.clickDone();

		Alert.viewSuccessMessage();
	}

	macro viewCannotAdd {
		LexiconEntry.gotoAdd();

		WaitForElementPresent.waitForLastScript();

		LexiconEntry.gotoEntry(rowEntry = "Blank Site");

		SelectFrame(locator1 = "IFrame#MODAL_BODY");

		PortletEntry.inputName(name = "${siteName}");

		Button.clickAdd();

		if ("${siteNameInvalid}" == "Duplicate") {
			Alert.viewErrorMessage(errorMessage = "Please enter a unique name.");
		}
		else {
			Alert.viewErrorMessage(errorMessage = "The Site Name cannot be blank, numeric or a reserved word such as null. The Site Name cannot contain the following invalid characters: *.");
		}
	}

	macro viewChildCP {
		Site.viewCP(siteName = "${siteName}");

		LexiconTable.clickEllipsisItem(
			item = "View Child Sites",
			tableEntry = "${siteName}");

		Breadcrumb.viewActiveEntry(breadcrumbName = "${siteName}");

		Site.viewCP(siteName = "${childSiteName}");
	}

	macro viewCP {
		var key_siteName = "${siteName}";

		AssertElementPresent(locator1 = "Sites#SITE_TABLE_CHECKBOX");

		AssertTextEquals.assertPartialText(
			locator1 = "Sites#SITE_TABLE_NAME",
			value1 = "${siteName}");

		AssertTextEquals(
			locator1 = "Sites#SITE_TABLE_TYPE",
			value1 = "${siteType}");

		if ("${siteMember}" == "User") {
			AssertTextEquals(
				key_columnName = "members",
				key_tableEntry = "${siteName}",
				locator1 = "LexiconTable#TABLE_ENTRY_INFO",
				value1 = "1 ${siteMember}");
		}

		AssertTextEquals(
			key_tableEntry = "${siteName}",
			key_tableEntryText = "Yes",
			locator1 = "LexiconTable#TABLE_ENTRY_TEXT",
			value1 = "Yes");

		AssertVisible(
			key_tableEntry = "${siteName}",
			locator1 = "LexiconTable#TABLE_ENTRY_ELLIPSIS");
	}

	macro viewCustomSiteLogoInDifferentViews {
		ProductMenuHelper.openProductMenu();

		ProductMenuHelper.openWorkspace();

		SelectFrame(locator1 = "IFrame#MODAL_BODY");

		Pause(locator1 = "3000");

		Navigator.gotoNavItem(navItem = "All Sites");

		LexiconEntry.changeDisplayStyle(displayStyle = "Cards");

		SiteSelector._searchSite(site = "${site}");

		AssertElementPresent(
			key_card = "${site}",
			locator1 = "Card#CARD_CUSTOM_LOGO");

		SiteSelector._searchSite(site = "${site}");

		LexiconEntry.changeDisplayStyle(displayStyle = "List");

		AssertElementPresent(
			key_listEntry = "${site}",
			locator1 = "LexiconList#LIST_CUSTOM_LOGO");
	}

	macro viewGlobalLandingDefault {
		var key_portletName = "${portletName}";
		var key_siteName = "${siteName}";

		AssertElementPresent(locator1 = "ProductMenu#PRODUCT_MENU_PANEL_SITE_ADMINISTRATION_SITE_NAME");

		AssertTextNotPresent(value1 = "You do not have the roles required to access this portlet");

		AssertTextEquals(
			locator1 = "ProductMenu#PRODUCT_MENU_PANEL_MENU_ITEM",
			value1 = "${portletName}");
	}

	macro viewLimitMembershipParentSiteCP {
		var key_userScreenName = "${userScreenName}";

		Navigator.gotoNavItem(navItem = "Users");

		LexiconEntry.gotoAdd();

		SelectFrame(locator1 = "IFrame#MODAL_BODY");

		AssertTextNotPresent(
			locator1 = "SiteMembershipsAddMembers#USER_TABLE_NAME",
			value1 = "${userFirstName} ${userLastName}");

		AssertTextNotPresent(
			locator1 = "SiteMembershipsAddMembers#USER_TABLE_SCREEN_NAME",
			value1 = "${userScreenName}");
	}

	macro viewMemberCP {
		LexiconCard.viewCardPresent(card = "${userFirstName} ${userLastName}");

		LexiconCard.viewCardTitle(card = "${userFirstName} ${userLastName}");

		LexiconCard.viewCardSubtitle(
			card = "${userFirstName} ${userLastName}",
			cardSubtitle = "${userScreenName}");

		AssertVisible(
			key_card = "${userFirstName} ${userLastName}",
			locator1 = "Card#CARD_ELLIPSIS");
	}

	macro viewMemberListCP {
		for (var userEmail : list "${usersEmailsList}") {
			var userName = StringUtil.replace("${userEmail}", "@liferay.com", "");

			Site.viewMemberCP(
				userFirstName = "${userName}",
				userLastName = "${userName}",
				userScreenName = "${userName}");
		}
	}

	macro viewMemberOrganizationCP {
		var key_tableEntry = "${orgName}";

		Navigator.gotoNavItem(navItem = "Organizations");

		LexiconEntry.changeDisplayStyle(displayStyle = "table");

		AssertTextEquals(
			locator1 = "LexiconTable#TABLE_ENTRY_TITLE",
			value1 = "${orgName}");

		if ("${toolTip}" == "true") {
			MouseOver(locator1 = "Icon#TOOLTIP");

			AssertTextEquals.assertPartialText(
				locator1 = "Message#TOOLTIP",
				value1 = "Users are not allowed to request membership. Only site administrators can add new members.");
		}
	}

	macro viewMemberUserGroupCP {
		Navigator.gotoNavItem(navItem = "User Groups");

		AssertTextEquals(
			key_userGroupName = "${userGroupName}",
			locator1 = "SiteMemberships#USER_GROUPS_TABLE_NAME",
			value1 = "${userGroupName}");
	}

	macro viewMySitesViaMySitesPG {
		AssertTextEquals(
			key_navItem = "My Sites",
			locator1 = "NavBar#NAV_ITEM_LINK",
			value1 = "My Sites");

		LexiconList.viewListEntryPresent(listEntry = "${siteName}");

		LexiconList.viewListEntrySubtitle(
			listEntry = "${siteName}",
			listEntrySubtitle = "${memberCount}");
	}

	macro viewNoMySitesViaMySitesPG {
		LexiconList.viewListEntryNotPresent(listEntry = "${siteName}");
	}

	macro viewNoSiteMembershipsRequest {
		Click(locator1 = "Icon#INFO");

		AssertElementNotPresent(locator1 = "SiteMemberships#REQUEST_MESSAGE");

		AssertTextEquals.assertPartialText(
			locator1 = "SiteMemberships#MEMBERSHIP_TYPE",
			value1 = "Membership Type: Restricted");

		AssertElementNotPresent(locator1 = "SiteMemberships#REQUEST_MESSAGE");

		LexiconEntry.gotoHeaderMenuItem(menuItem = "View Membership Requests");

		AssertElementNotPresent(locator1 = "SiteMembershipsViewMembershipRequests#USER_NAME");

		AssertTextNotPresent(
			locator1 = "SiteMembershipsViewMembershipRequests#USER_COMMENTS",
			value1 = "Please allow me to join the Restricted Site.");
	}

	macro viewPrivateVirtualHostURLUserLoginRedirect {
		AssertTextEquals(
			locator1 = "TextInput#EMAIL_ADDRESS",
			value1 = "${userEmailAddress}");

		AssertElementPresent(locator1 = "TextInput#PASSWORD");
	}

	macro viewSearchResultsCP {
		var key_siteName = "${siteName}";

		AssertElementPresent(
			key_tableEntry = "${siteName}",
			locator1 = "LexiconTable#TABLE_ENTRY_CHECKBOX");

		LexiconTable.viewTableEntryTitle(tableEntry = "${siteName}");

		LexiconTable.viewTableEntryText(
			tableEntry = "${siteName}",
			tableEntryText = "${siteType}");

		LexiconTable.viewTableEntryText(
			tableEntry = "${siteName}",
			tableEntryText = "Yes");

		AssertVisible(
			key_tableEntry = "${siteName}",
			locator1 = "LexiconTable#TABLE_ENTRY_ELLIPSIS");
	}

	macro viewSelectSiteViaPersonalMenuMySites {
		AssertElementPresent(locator1 = "Sites#SELECT_SITE_MODAL");

		AssertTextEquals(
			locator1 = "Sites#SELECT_SITE_MODAL_HEADER",
			value1 = "Select Site");

		SelectFrame(locator1 = "IFrame#MODAL_BODY");

		AssertElementPresent(
			key_siteName = "${siteName}",
			locator1 = "Sites#SELECT_SITE_MODAL_CARD_BODY_SITE_NAME");
	}

	macro viewSiteAdminPermissionsCP {
		var key_siteName = "${siteName}";

		Search.searchCP(searchTerm = "${siteName}");

		Click(
			key_tableEntry = "${siteName}",
			locator1 = "LexiconTable#TABLE_ENTRY_ELLIPSIS");

		AssertTextEquals(
			key_menuItem = "Add Child Site",
			locator1 = "MenuItem#ANY_MENU_ITEM",
			value1 = "Add Child Site");

		AssertTextEquals(
			key_menuItem = "Leave",
			locator1 = "MenuItem#ANY_MENU_ITEM",
			value1 = "Leave");

		AssertTextEquals(
			key_menuItem = "Deactivate",
			locator1 = "MenuItem#ANY_MENU_ITEM",
			value1 = "Deactivate");

		AssertTextEquals(
			key_menuItem = "Delete",
			locator1 = "MenuItem#ANY_MENU_ITEM",
			value1 = "Delete");
	}

	macro viewSiteClonedFromSiteTemplateCP {
		Click(
			key_itemName = "Other",
			locator1 = "ListGroupItem#ITEM_TEXT");

		Navigator.gotoNavTab(navTab = "Site Template");

		if ("${clonedFromTemplate}" == "true") {
			AssertTextEquals.assertPartialText(
				locator1 = "SiteSettingsSiteTemplate#CONTENT_CONTAINER",
				value1 = "These pages are linked to site template ${siteTemplateName} without propagation of changes.");
		}
		else {
			AssertTextEquals.assertPartialText(
				locator1 = "SiteSettingsSiteTemplate#CONTENT_CONTAINER",
				value1 = "This site is not related to a site template.");
		}
	}

	macro viewSiteFriendlyURLPG {
		AssertTextEquals(
			locator1 = "Home#SITE_NAME",
			value1 = "${siteName}");

		AssertLocation(value1 = "${sitePageURL}");
	}

	macro viewSiteMembershipsRequest {
		Click(locator1 = "Icon#INFO");

		AssertTextEquals.assertPartialText(
			locator1 = "SiteMemberships#MEMBERSHIP_TYPE",
			value1 = "Membership Type: Restricted");

		AssertElementPresent(locator1 = "SiteMemberships#REQUEST_MESSAGE");

		Click(locator1 = "SiteMemberships#REQUEST_MESSAGE");

		LexiconEntry.changeDisplayStyle(displayStyle = "table");

		AssertTextEquals(
			locator1 = "SiteMembershipsViewMembershipRequests#USER_NAME",
			value1 = "${userFirstName} ${userLastName}");

		AssertTextEquals(
			locator1 = "SiteMembershipsViewMembershipRequests#USER_COMMENTS",
			value1 = "Please allow me to join the Restricted Site.");
	}

	macro viewSiteRoleViaSiteMembershipsCP {
		LexiconEntry.changeDisplayStyle(displayStyle = "list");

		LexiconList.viewListEntryPresent(listEntry = "${resourceName}");

		LexiconList.viewListEntrySubtitle(
			listEntry = "${resourceName}",
			listEntrySubtitle = "${roleTitle}");
	}

	macro viewUserGroupSiteRoleViaSiteMembershipsCP {
		AssertTextEquals(
			key_userGroupName = "${resourceName}",
			locator1 = "SiteMemberships#USER_GROUPS_TABLE_NAME",
			value1 = "${resourceName}");

		AssertTextEquals(
			key_siteRoleName = "${roleTitle}",
			locator1 = "SiteMemberships#USER_GROUPS_TABLE_SITE_ROLES",
			value1 = "${roleTitle}");
	}

	macro viewVirtualHostsURLPG {
		var key_pageName = "${pageName}";

		AssertTextEquals(
			locator1 = "Home#SITE_NAME",
			value1 = "${siteName}");

		AssertTextEquals(
			locator1 = "Home#PAGE",
			value1 = "${pageName}");

		AssertLocation(value1 = "${sitePageURL}");
	}

}