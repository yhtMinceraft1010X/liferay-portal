definition {

	macro checkContentNameList {
		for (var contentName : list "${checkContentNameList}") {
			var key_contentName = "${contentName}";

			Check.checkToggleSwitch(locator1 = "PagesConfiguration#CONTENT_CHOOSE_CONTENT_CONTENT_CHECKBOX");
		}
	}

	macro checkPageName {
		ContentConfiguration.uncheckPages();

		for (var pageName : list "${checkPageName}") {
			var key_pageName = "${pageName}";

			if (IsElementNotPresent(locator1 = "PagesConfiguration#PAGES_SPECIFIC_PAGE_CHECKED")) {
				var key_pageName = "${pageName}";

				Click(locator1 = "PagesConfiguration#PAGES_SPECIFIC_PAGE_NAME");
			}
		}
	}

	macro checkPrivatePages {
		if (IsElementPresent(locator1 = "PagesConfiguration#PAGES_ALL_PUBLIC_PAGES_CHECKBOX")) {
			Click(locator1 = "PagesConfiguration#CHANGE_TO_PRIVATE_PAGES");
		}

		Click(locator1 = "PagesConfiguration#PAGES_ALL_PRIVATE_PAGES_CHECKBOX");

		Click(locator1 = "PagesConfiguration#PAGES_ALL_PRIVATE_PAGES_CHECKBOX");

		for (var pageName : list "${checkPrivatePages}") {
			var key_pageName = "${pageName}";

			if (IsElementNotPresent(locator1 = "PagesConfiguration#PAGES_SPECIFIC_PAGE_CHECKED")) {
				Click(locator1 = "PagesConfiguration#PAGES_SPECIFIC_PAGE_NAME");
			}
		}
	}

	macro checkPublicPages {
		if (IsElementPresent(locator1 = "PagesConfiguration#PAGES_ALL_PRIVATE_PAGES_CHECKBOX")) {
			Click(locator1 = "PagesConfiguration#CHANGE_TO_PUBLIC_PAGES");
		}

		ContentConfiguration.uncheckPages();

		for (var pageName : list "${checkPublicPages}") {
			var key_pageName = "${pageName}";

			if (IsElementNotPresent(locator1 = "PagesConfiguration#PAGES_SPECIFIC_PAGE_CHECKED")) {
				Click(locator1 = "PagesConfiguration#PAGES_SPECIFIC_PAGE_NAME");
			}
		}
	}

	macro contentDateRange {
		if (IsElementPresent(locator1 = "Button#PUBLISH")) {
			ScrollWebElementIntoView(locator1 = "Button#PUBLISH");
		}

		var browserType = PropsUtil.get("browser.type");

		if ("${contentDateRange}" == "All") {
			Click(locator1 = "StagingPublishToLive#CONTENT_ALL");
		}

		if ("${contentDateRange}" == "Date Range") {
			Click(locator1 = "Radio#CHOOSE_CONTENT_DATE_RANGE_CHANGE");

			var key_contentDateRange = "${contentDateRange}";

			Type(
				locator1 = "Select#DATE_RANGE_START_DATE",
				value1 = "${dateRangeStartDate}");

			Type(
				locator1 = "Select#DATE_RANGE_END_DATE",
				value1 = "${dateRangeEndDate}");
		}

		if (("${contentDateRange}" == "Last") && ("${browserType}" == "safari")) {
			Click.javaScriptClick(locator1 = "StagingPublishToLive#CONTENT_LAST");
		}
		else if ("${contentDateRange}" == "Last") {
			Click.clickNoMouseOverNoWaitForVisible(locator1 = "StagingPublishToLive#CONTENT_LAST");
		}

		if ("${browserType}" == "safari") {
			Click.javaScriptClick(locator1 = "Link#REFRESH_COUNTS");
		}
		else {
			Click(locator1 = "Link#REFRESH_COUNTS");
		}
	}

	macro contentDeletion {
		Click(locator1 = "StagingPublishToLive#CONTENT_CHOOSE_CONTENT_DELETION_CHECKBOX");
	}

	macro deleteMissingPages {
		Click(locator1 = "PagesConfiguration#PAGES_DELETE_MISSING_PAGES_CHECKBOX");
	}

	macro exportPermissions {
		Panel.expandPanel(panel = "Permissions");

		Click(locator1 = "Checkbox#PERMISSIONS");
	}

	macro pageDeletion {
		Click(locator1 = "StagingPublishToLive#CONTENT_CHOOSE_CONTENT_DELETION_CHECKBOX");

		AssertElementPresent(locator1 = "StagingPublishToLive#PAGES_CHOOSE_PAGE_DELETION_CHECKBOX");

		Click(locator1 = "StagingPublishToLive#PAGES_CHOOSE_PAGE_DELETION_CHECKBOX");
	}

	macro setTitle {
		Type(
			locator1 = "StagingPublishToLive#PUBLICATION_TITLE",
			value1 = "${publicationTitle}");
	}

	macro uncheckAllPages {
		if ((IsElementNotPresent(locator1 = "PagesConfiguration#PAGES_ALL_PAGES_CHECKED")) && (IsElementPresent(locator1 = "PagesConfiguration#PAGES_ANY_PAGE_CHECKED"))) {
			Click(locator1 = "PagesConfiguration#PAGES_ALL_PAGES_CHECKBOX");

			Click(locator1 = "PagesConfiguration#PAGES_ALL_PAGES_CHECKBOX");
		}
		else {
			Click(locator1 = "PagesConfiguration#PAGES_ALL_PAGES_CHECKBOX");
		}
	}

	macro uncheckAllPublicPages {
		if ((IsElementNotPresent(locator1 = "PagesConfiguration#PAGES_ALL_PUBLIC_PAGES_CHECKED")) && (IsElementPresent(locator1 = "PagesConfiguration#PAGES_ANY_PAGE_CHECKED"))) {
			Click(locator1 = "PagesConfiguration#PAGES_ALL_PUBLIC_PAGES_CHECKBOX");

			Click(locator1 = "PagesConfiguration#PAGES_ALL_PUBLIC_PAGES_CHECKBOX");
		}
		else {
			Click(locator1 = "PagesConfiguration#PAGES_ALL_PUBLIC_PAGES_CHECKBOX");
		}
	}

	macro uncheckContentNameList {
		for (var contentName : list "${uncheckContentNameList}") {
			Uncheck.uncheckToggleSwitch(
				key_contentName = "${contentName}",
				locator1 = "PagesConfiguration#CONTENT_CHOOSE_CONTENT_CONTENT_CHECKBOX");
		}
	}

	macro uncheckLogo {
		Click(locator1 = "PagesConfiguration#PAGES_LOGO_CHECKBOX");
	}

	macro uncheckPages {
		if (IsElementPresent(locator1 = "PagesConfiguration#PAGES_ALL_PAGES_CHECKBOX")) {
			ContentConfiguration.uncheckAllPages();
		}
		else {
			ContentConfiguration.uncheckAllPublicPages();
		}
	}

	macro uncheckSitePagesSettings {
		Click(locator1 = "PagesConfiguration#PAGES_SITE_PAGES_SETTINGS_CHECKBOX");
	}

	macro uncheckSiteTemplateSettings {
		Click(locator1 = "PagesConfiguration#PAGES_SITE_TEMPLATE_SETTINGS_CHECKBOX");
	}

	macro uncheckSubContent {
		var key_contentName = "${mainContent}";

		Click(locator1 = "ExportImport#CONTENT_CHOOSE_CONTENT_CONTENT_CHANGE_LINK");

		var key_subContent = "${uncheckSubContent}";

		Click(locator1 = "ExportImport#CONTENT_CHOOSE_CONTENT_SUBCONTENT_CHECKBOX");

		AssertClick(
			locator1 = "Button#OK",
			value1 = "OK");
	}

	macro uncheckSubSubContent {
		var key_contentName = "${contentName}";

		Check(locator1 = "PagesConfiguration#CONTENT_CHOOSE_CONTENT_CONTENT_CHECKBOX");

		AssertClick(
			locator1 = "ExportImport#CONTENT_CHOOSE_CONTENT_CONTENT_CHANGE_LINK",
			value1 = "Change");

		var key_subContent = "${subContent}";
		var key_subSubContent = "${uncheckSubSubContent}";

		Click(locator1 = "ExportImport#CONTENT_CHOOSE_CONTENT_SUB_SUBCONTENT_CHECKBOX");

		AssertClick(
			locator1 = "Button#OK",
			value1 = "OK");
	}

}