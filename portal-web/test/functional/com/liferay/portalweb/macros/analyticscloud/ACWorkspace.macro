definition {

	macro changeLanguage {
		var key_languageOption = "${languageOption}";
		var key_language = "${language}";

		if (IsElementPresent(locator1 = "ACSettings#SETTINGS_PAGE")) {
			Click(locator1 = "ACSettings#EXIT_SETTINGS_BUTTON");
		}

		Click(locator1 = "ACWorkspace#USER_MENU_OPTION");

		ACUtils.clickAnyButton(button = "${key_language}");

		ACUtils.clickAnyButton(button = "${key_languageOption}");

		TestUtils.hardRefresh();
	}

	macro configureWorkspaceSettings {
		if (isSet(workspaceName)) {
			Type(
				key_text = "Workspace Name",
				locator1 = "TextInput#ANY",
				value1 = "${workspaceName}");
		}

		if (isSet(timezone)) {
			Select(
				locator1 = "ACWorkspace#TIMEZONE_COUNTRY_SELECT_FIELD",
				value1 = "${timezone}");

			Select(
				locator1 = "Select#SELECT_ID",
				selectFieldId = "timeZoneId",
				value1 = "${timezoneId}");
		}

		Click(
			key_text = "Add Incident Report Contacts",
			locator1 = "TextInput#ANY");

		KeyPress(
			key_text = "Add Incident Report Contacts",
			locator1 = "TextInput#ANY",
			value1 = "\BACK_SPACE");

		Type(
			key_text = "Add Incident Report Contacts",
			locator1 = "TextInput#ANY",
			value1 = "test@liferay.com");

		Click(
			label = "Add Incident Report Contacts",
			locator1 = "ACUtils#LABEL");

		if (isSet(cancel)) {
			WaitForElementNotPresent(
				key_text = "Cancel",
				locator1 = "Button#ANY_DISABLED");

			Button.click(button = "Cancel");
		}
		else {
			WaitForElementPresent(locator1 = "Button#SAVE");

			Button.clickSave();
		}
	}

	macro createWorkspace {
		ACUtils.clickAnyButton(button = "Start Free Trial");

		Type(
			locator1 = "ACUtils#NAME_FIELD",
			value1 = "${workspaceName}");

		for (var email : list "${emailList}") {
			Type(
				locator1 = "ACWorkspace#INVITE_USERS_INPUT",
				value1 = "${email}");

			KeyPress(
				locator1 = "ACWorkspace#INVITE_USERS_INPUT",
				value1 = "\ENTER");
		}

		Click.clickAtNotVisible(locator1 = "ACDXPSettings#AC_GENERIC_CHECKBOX");

		Button.clickSubmitButton();

		WaitForElementPresent(
			key_textValue = "Welcome to Analytics Cloud",
			locator1 = "ACUtils#GENERIC_TEXT");
	}

	macro openWorkspace {
		if (isSet(workspaceName)) {
			var key_workspaceName = "${workspaceName}";

			Click(locator1 = "ACWorkspace#WORKSPACE_NAME");
		}

		if (!(isSet(workspaceName))) {
			Click(locator1 = "ACWorkspace#FARO_DEMO_WORKSPACE");
		}

		ACUtils.dismissOnboardingModal();
	}

	macro signOut {
		if (IsElementPresent(locator1 = "ACSettings#SETTINGS_PAGE")) {
			Click(locator1 = "ACSettings#EXIT_SETTINGS_BUTTON");
		}

		Click(locator1 = "ACWorkspace#USER_MENU_OPTION");

		Click(locator1 = "ACWorkspace#LOGOUT_WORKSPACE_OPTION");
	}

	macro switchWorkspace {
		Click(locator1 = "ACWorkspace#USER_MENU_OPTION");

		Click(locator1 = "ACWorkspace#SWITCH_WORKSPACE_OPTION");

		ACWorkspace.openWorkspace(workspaceName = "${workspaceName}");
	}

	macro viewWorkspaceSettings {
		if (isSet(workspaceName)) {
			AssertTextEquals(
				key_text = "Workspace Name",
				locator1 = "TextInput#ANY",
				value1 = "${workspaceName}");
		}

		if (isSet(timezone)) {
			AssertSelectedLabel(
				locator1 = "ACWorkspace#TIMEZONE_COUNTRY_SELECT_FIELD",
				value1 = "${timezone}");

			AssertSelectedLabel(
				locator1 = "Select#SELECT_ID",
				selectFieldId = "timeZoneId",
				value1 = "${timezoneId}");
		}
	}

}