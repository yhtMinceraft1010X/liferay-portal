definition {

	macro addMultipleSearchQuerySuccessfully {
		var key_queryList = "${query_list}";
		var flag = "0";
		var aux = "";

		for (var x : list "${key_queryList}") {
			if ("${flag}" == "0") {
				Click(locator1 = "Button#PLUS");

				Type(
					locator1 = "ACDefinitions#SEARCH_QUERY_INPUT",
					value1 = "${x}");
				flag = "1"
			}
			else {
				Click(
					key_query = "${aux}",
					locator1 = "ACDefinitions#SEARCH_QUERY_ADD_BUTTON");

				Type(
					key_index = "${flag}",
					locator1 = "ACDefinitions#SEARCH_QUERY_INPUT_INDEX",
					value1 = "${x}");

				flag = MathUtil.sum("${flag}", "1");
			}
			aux = "${x}";
		}

		Button.clickSave();

		Alert.viewSuccessMessage();
	}

	macro addSearchQuerySuccessfully {
		Click(locator1 = "Button#PLUS");

		Type(
			locator1 = "ACDefinitions#SEARCH_QUERY_INPUT",
			value1 = "${query}");

		Button.clickSave();

		Alert.viewSuccessMessage();
	}

	@summary = "Navigates to a definition in the Definitions screen. \
	@param definitionType the name of the definition that will be navigated to."
	macro assertIndividualAttributesSource {
		var key_dataSourceName = "${dataSourceName}";

		for (var attributeName : list "${attributeList}") {
			Type(
				locator1 = "ACSettings#SEARCH_BAR",
				value1 = "${attributeName}");

			ACDefinitions.openIndividualAttributeDetails(attributeName = "${attributeName}");

			AssertTextEquals(
				locator1 = "ACDefinitions#SOURCE_NAME",
				value1 = "${dataSourceName}");

			ACUtils.closeModal();
			ACUtils.clearSearchBar();
		}
	}

	macro deleteAllSearchQuery {
		ACUtils.launchAC();

		ACNavigation.goToSettings();

		ACSettings.goToDefinitions();

		ACDefinitions.goToDefinitionType(definitionType = "Search");

		while (IsElementPresent(key_query = "", locator1 = "ACDefinitions#SEARCH_QUERY_DELETE_BUTTON")) {
			Click(
				key_query = "",
				locator1 = "ACDefinitions#SEARCH_QUERY_DELETE_BUTTON");
		}

		Button.clickSave();
	}

	macro deleteSearchQuerySuccessfully {
		Click(
			key_query = "${query}",
			locator1 = "ACDefinitions#SEARCH_QUERY_DELETE_BUTTON");

		Button.clickSave();

		Alert.viewSuccessMessage();
	}

	macro editSearchQuerySuccessfully {
		Type(
			locator1 = "ACDefinitions#SEARCH_QUERY_INPUT",
			value1 = "${query}");

		Button.clickSave();

		Alert.viewSuccessMessage();
	}

	macro goToDefinitionType {
		var key_definitionType = "${definitionType}";
		Click(locator1 = "ACDefinitions#DEFINITION_TYPE");
	}

	macro noViewAddSearchQueryButton {
		AssertElementNotPresent(
			key_query = "${query}",
			locator1 = "ACDefinitions#SEARCH_QUERY_ADD_BUTTON");
	}

	macro noViewSearchQuery {
		AssertElementNotPresent(
			key_query = "${query}",
			locator1 = "ACDefinitions#PRESENT_SEARCH_QUERY_INPUT");
	}

	macro openIndividualAttributeDetails {
		var key_attributeName = "${attributeName}";
		Click(locator1 = "ACDefinitions#ATTRIBUTE_NAME");
	}

	macro viewSearchQuery {
		AssertElementPresent(
			key_query = "${query}",
			locator1 = "ACDefinitions#PRESENT_SEARCH_QUERY_INPUT");
	}

}