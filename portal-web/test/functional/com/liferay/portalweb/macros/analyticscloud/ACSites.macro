definition {

	macro viewActivitiesDuration {
		var valueNumber = "";
		var timeUnit = "";
		var flag = "1";

		for (var current : list "${date}") {
			var valueNumber = StringUtil.extractFirst("${current}", "-");
			var timeUnit = StringUtil.extractLast("${current}", "-");

			if ("${flag}" == "1") {
				AssertElementPresent(
					key_number = "${valueNumber}",
					key_unit = "${timeUnit}",
					locator1 = "ACSites#ACTIVITIES_CARD_SESSION_DURATION_FIRST_VALUE");

				var flag = ${flag} - 1;
			}
			else {
				AssertElementPresent(
					key_number = "${valueNumber}",
					key_unit = "${timeUnit}",
					locator1 = "ACSites#ACTIVITIES_CARD_SESSION_DURATION_FOLLOWING_VALUE");
			}
		}
	}

	macro viewActivitiesPercentage {
		AssertElementPresent(
			key_entry = "${entryName}",
			key_number = "${valueNumber}",
			locator1 = "ACSites#ACTIVITIES_CARD_ATRIBUTE_AND_VALUE");
	}

	macro viewGeomapAnalyticsLocationNames {
		for (var locationName : list "${locationList}") {
			AssertElementPresent(
				key_entry = "${locationName}",
				locator1 = "ACSites#GEOMAP_ANALYTICS_LOCATIONS_NAME");
		}
	}

	macro viewSiteOverviewDashboardCards {
		WaitForElementPresent(locator1 = "ACSites#ACTIVITIES_CARD");

		AssertElementPresent(locator1 = "ACSites#TOP_PAGES_CARD");

		AssertElementPresent(locator1 = "ACSites#ACQUISITIONS_CARD");

		AssertElementPresent(locator1 = "ACSites#VISITORS_BY_DAY_AND_TIME_CARD");

		AssertElementPresent(locator1 = "ACSites#SEARCH_TERMS_CARD");

		AssertElementPresent(locator1 = "ACSites#INTERESTS_CARD");

		AssertElementPresent(locator1 = "ACSites#SESSIONS_BY_LOCATION_CARD");

		AssertElementPresent(locator1 = "ACSites#SESSION_TECHNOLOGY_CARD");

		AssertElementPresent(locator1 = "ACSites#COHORT_ANALYSIS_CARD");
	}

	macro viewVisitedTopPages {
		var list1 = ListUtil.newListFromString("${pageNameList}");
		var list2 = ListUtil.newListFromString("${positionList}");
		var size = ListUtil.size("${list1}");
		var i = "0";

		while ("${i}" != "${size}") {
			var pageName = ListUtil.get("${list1}", "${i}");
			var position = ListUtil.get("${list2}", "${i}");

			while (IsElementNotPresent(key_index = "${position}", key_pageName = "${pageName}", locator1 = "ACSites#VISITED_TOP_PAGES_CARD") && (maxIterations = "50")) {
				TestUtils.hardRefresh();

				ACTimeFilter.clickTimeFilterButton(cardName = "Top Pages");

				ACTimeFilter.setLast24Hours();
			}

			AssertElementPresent(
				key_index = "${position}",
				key_pageName = "${pageName}",
				locator1 = "ACSites#VISITED_TOP_PAGES_CARD");

			var i = ${i} + 1;
			var i = StringUtil.valueOf("${i}");
		}
	}

	macro viewVisitorsOnGraph {
		ACTimeFilter.clickTimeFilterButton();

		ACTimeFilter.setLast24Hours();

		while (IsElementNotPresent(locator1 = "ACIndividualsDashboard#ACTIVE_INDIVIDUAL_CHART") && (maxIterations = "50")) {
			TestUtils.hardRefresh();

			ACTimeFilter.clickTimeFilterButton();

			ACTimeFilter.setLast24Hours();
		}

		Click.mouseDownMouseUp(locator1 = "ACIndividualsDashboard#ACTIVE_INDIVIDUAL_CHART");

		var list1 = ListUtil.newListFromString("${visitorsTypeList}");
		var list2 = ListUtil.newListFromString("${valueVisitorsList}");
		var size = ListUtil.size("${list1}");
		var i = "0";

		while ("${i}" != "${size}") {
			var visitorsType = ListUtil.get("${list1}", "${i}");
			var valueVisitors = ListUtil.get("${list2}", "${i}");

			while (IsElementNotPresent(key_value = "${valueVisitors}", key_visitors = "${visitorsType}", locator1 = "ACSites#TOOLTIP_UNIQUE_VISITORS") && (maxIterations = "50")) {
				TestUtils.hardRefresh();

				ACTimeFilter.clickTimeFilterButton();

				ACTimeFilter.setLast24Hours();

				Click.mouseDownMouseUp(locator1 = "ACIndividualsDashboard#ACTIVE_INDIVIDUAL_CHART");
			}

			AssertElementPresent(
				key_value = "${valueVisitors}",
				key_visitors = "${visitorsType}",
				locator1 = "ACSites#TOOLTIP_UNIQUE_VISITORS");

			var i = ${i} + 1;
			var i = StringUtil.valueOf("${i}");
		}
	}

}