definition {

	macro addSiteAndPage {
		JSONGroup.addGroup(groupName = "Site Name");

		if (!(isSet(layoutName))) {
			layoutName = "AC Page";
		}

		JSONLayout.addPublicLayout(
			groupName = "Site Name",
			layoutName = "${layoutName}");
	}

	macro addWidgetToPage {
		if (!(isSet(layoutName))) {
			layoutName = "AC Page";
		}

		JSONLayout.addWidgetToPublicLayout(
			groupName = "Site Name",
			layoutName = "${layoutName}",
			widgetName = "${widgetName}");
	}

	macro assertDataSourceName {
		var itemName = "${dataSourceName}";

		AssertElementPresent(locator1 = "ACSettings#ITEM_ON_LIST");
	}

	@summary = "Asserts a list of items by the name"
	macro assertItemsName {
		for (var key_itemName : list "${itemList}") {
			AssertElementPresent(
				itemName = "${key_itemName}",
				locator1 = "ACSettings#ITEM_ON_LIST");
		}
	}

	macro assertXAxisTimedGraph {
		var key_parentName = "${parentName}";
		AssertElementPresent(locator1 = "ACUtils#SVG_TIMED_GRAPH");
	}

	macro clearSearchBar {
		Click(locator1 = "ACUtils#CLEAR_SEARCH_BAR_BUTTON");
	}

	macro closeAllSessions {
		var analyticsCloudAsahCuratorURL = PropsUtil.get("analytics.cloud.asah.curator.url");

		var cleanCurl = '''
			${analyticsCloudAsahCuratorURL}/admin/sessions/close \
			-H 'Content-Type: application/json' \
			-d '{
				"query": {
					"match_all": {}
				}
			}'
		''';

		com.liferay.poshi.runner.util.JSONCurlUtil.post("${cleanCurl}");
	}

	macro closeModal {
		Click(locator1 = "ACUtils#MODAL_CLOSE_BUTTON");
	}

	macro createBlogsAndAddToPage {
		if (!(isSet(layoutName))) {
			layoutName = "AC Page";
		}

		ACUtils.addWidgetToPage(
			layoutName = "${layoutName}",
			widgetName = "Blogs");

		Navigator.gotoSitePage(
			pageName = "${layoutName}",
			siteName = "Site Name");

		if (!(isSet(entryTitle))) {
			entryTitle = "Blogs AC Title";
		}
		Blogs.addEntry(
			entryContent = "This is the blog test for AC",
			entrySubtitle = "Blogs AC Subtitle",
			entryTitle = "${entryTitle}");

		Navigator.gotoSitePage(
			pageName = "AC Page",
			siteName = "Site Name");
	}

	macro createDMAndAddToPage {
		if (!(isSet(layoutName))) {
			layoutName = "AC Page";
		}

		ACUtils.addWidgetToPage(
			layoutName = "${layoutName}",
			widgetName = "Documents and Media");

		Navigator.gotoSitePage(
			pageName = "${layoutName}",
			siteName = "Site Name");

		if (!(isSet(dmDocumentTitle))) {
			dmDocumentTitle = "DM AC Title";
		}

		if (!(isSet(dmDocumentFile))) {
			dmDocumentFile = "Document_1.jpg";
		}

		DMDocument.enableActionsMenuOnPortlet();

		DMDocument.addPG(
			dmDocumentFile = "${dmDocumentFile}",
			dmDocumentTitle = "${dmDocumentTitle}");

		Navigator.gotoSitePage(
			pageName = "AC Page",
			siteName = "Site Name");
	}

	macro createFormAndAddToPage {
		if (!(isSet(formACTitle))) {
			var formACTitle = "Created Form Name";
		}

		if (!(isSet(layoutName))) {
			layoutName = "AC Page";
		}

		ACUtils.addWidgetToPage(
			layoutName = "${layoutName}",
			widgetName = "Form");

		Navigator.gotoSitePage(
			pageName = "${layoutName}",
			siteName = "Site Name");

		ProductMenu.gotoPortlet(
			category = "Content &amp; Data",
			portlet = "Forms");

		FormsAdminNavigator.gotoAddForm();

		Form.editName(formTitle = "${formACTitle}");

		Form.addTextField(fieldPositionNumber = "0");

		Form.closeSidebar();

		Button.clickPublish();

		Navigator.gotoSitePage(
			pageName = "${layoutName}",
			siteName = "Site Name");

		FormPortletNavigator.gotoSelectForm();

		FormPortletConfiguration.selectForm(formTitle = "${formACTitle}");

		PortletEntry.save();
	}

	macro createWCAndAddToPage {
		if (!(isSet(layoutName))) {
			layoutName = "AC Page";
		}

		ACUtils.addWidgetToPage(
			layoutName = "${layoutName}",
			widgetName = "Web Content Display");

		Navigator.gotoSitePage(
			pageName = "${layoutName}",
			siteName = "Site Name");

		if (!(isSet(webContentTitle))) {
			webContentTitle = "Web Content AC Title";
		}

		if (!(isSet(webContentRichText))) {
			webContentRichText = "This is the web content test for AC";
		}

		if (isSet(webContentRichText)) {
			webContentRichText = "${webContentRichText}";
		}

		WebContent.addPGViaWCD(
			fieldFieldLabel = "content",
			webContentRichText = "${webContentRichText}",
			webContentTitle = "${webContentTitle}");

		Pause(locator1 = "500");

		Navigator.gotoSitePage(
			pageName = "AC Page",
			siteName = "Site Name");
	}

	macro launchAC {
		var analyticsCloudURL = PropsUtil.get("analytics.cloud.url");

		Open(locator1 = "${analyticsCloudURL}");

		if (IsElementPresent(locator1 = "ACLogin#EMAIL_LOGIN_FIELD")) {
			ACLogin.loginAs();
		}

		ACWorkspace.openWorkspace();
	}

	macro launchDXP {
		Navigator.openURL();

		if (!(isSet(userEmailAddress))) {
			userEmailAddress = "test@liferay.com";
		}

		if (IsElementNotPresent(locator1 = "ApplicationsMenu#APPLICATIONS_MENU")) {
			User.firstLoginPG(userEmailAddress = "${userEmailAddress}");
		}
	}

	macro searchBar {
		Click(locator1 = "ACSettings#SEARCH_BAR");

		Type(
			locator1 = "ACSettings#SEARCH_BAR",
			value1 = "${searchItem}");

		KeyPress(
			locator1 = "ACSettings#SEARCH_BAR",
			value1 = "\ENTER");
	}

	macro tearDownAC {
		ACUtils.launchAC();

		ACSettings.deleteAllProperties();

		ACSettings.deleteAllDataSource();
	}

	macro tearDownDXP {
		ACUtils.launchDXP();

		ACDXPSettings.disconnectDXPFromAnalyticsCloud();
	}

}