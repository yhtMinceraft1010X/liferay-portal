definition {

	macro addCP {
		var key_resourceName = "${resourceName}";

		if (isSet(modelResource)) {
			CustomFields.openToAddResourceCustomField(
				baseURL = "${baseURL}",
				modelResource = "${modelResource}");
		}
		else {
			AssertClick(
				locator1 = "CustomFields#RESOURCE_TABLE_RESOURCE_LINK",
				value1 = "${resourceName}");

			LexiconEntry.gotoAdd();
		}

		var key_customFieldType = "${customFieldType}";

		Click(locator1 = "CustomFields#CUSTOM_FIELD_TYPE");

		var customFieldKey = StringUtil.lowerCase("${customFieldName}");

		PortletEntry.inputName(name = "${customFieldKey}");

		if ("${customFieldType}" == "Input Field") {
			var key_customInputDataType = "${customInputDataType}";

			Select(
				locator1 = "CustomFields#CUSTOM_INPUT_FIELD_DATA_TYPE",
				value1 = "${customInputDataType}");

			if (isSet(startingValue)) {
				Type(
					key_fieldLabel = "Starting Value",
					locator1 = "CustomFields#CUSTOM_INPUT_FIELD",
					value1 = "${startingValue}");
			}
		}
		else if (("${customFieldType}" == "Dropdown") || ("${customFieldType}" == "Checkbox") || ("${customFieldType}" == "Radio")) {
			if (isSet(startingValue)) {
				PortletEntry.inputTextArea(
					fieldId = "Value",
					text = "${startingValue}");
			}
			else {
				PortletEntry.inputTextArea(
					fieldId = "Value",
					text = "${customFieldType}");
			}
		}

		if (isSet(nameLocalization)) {
			if ("${nameLocalization}" == "false") {
				Click(locator1 = "CustomFields#CUSTOM_FIELDS_ADVANCED_PROPERTIES");

				WaitForElementPresent(
					key_toggleSwitchLabel = "Localize Field Name",
					locator1 = "ToggleSwitch#ANY_TOGGLE_SWITCH");

				Uncheck.uncheckToggleSwitch(
					key_toggleSwitchLabel = "Localize Field Name",
					locator1 = "ToggleSwitch#ANY_TOGGLE_SWITCH");
			}
			else {
				Click(locator1 = "CustomFields#CUSTOM_FIELDS_ADVANCED_PROPERTIES");

				WaitForElementPresent(
					key_toggleSwitchLabel = "Localize Field Name",
					locator1 = "ToggleSwitch#ANY_TOGGLE_SWITCH");

				Check.checkToggleSwitch(
					key_toggleSwitchLabel = "Localize Field Name",
					locator1 = "ToggleSwitch#ANY_TOGGLE_SWITCH");
			}
		}

		if (isSet(customBooleanDefaultValue)) {
			Select(
				locator1 = "CustomFields#CUSTOM_BOOLEAN_DEFAULT_VALUE",
				value1 = "${customBooleanDefaultValue}");
		}

		if ("${localization}" == "true") {
			Check(
				checkboxName = "Make Field Localizable",
				locator1 = "Checkbox#ANY_CHECKBOX");
		}

		if ("${searchable}" == "true") {
			Check.checkToggleSwitch(
				key_toggleSwitchLabel = "Searchable",
				locator1 = "ToggleSwitch#ANY_TOGGLE_SWITCH");
		}

		Button.clickSave();

		var key_customFieldName = "${customFieldName}";

		AssertTextEquals(
			locator1 = "CustomFieldsEditResource#CUSTOM_FIELDS_TABLE_NAME",
			value1 = "${customFieldName}");
	}

	macro deleteCP {
		var key_resourceName = "${resourceName}";

		AssertClick(
			locator1 = "CustomFields#RESOURCE_TABLE_RESOURCE_LINK",
			value1 = "${resourceName}");

		var key_customFieldName = "${customFieldName}";

		AssertVisible(locator1 = "CustomFieldsEditResource#CUSTOM_FIELDS_TABLE_NAME");

		Pause(locator1 = "3000");

		LexiconEntry.openEntryMenu(rowEntry = "${customFieldName}");

		MenuItem.clickNoError(menuItem = "Delete");

		AssertConfirm(value1 = "Are you sure you want to delete this? It will be deleted immediately.");

		VerifyElementPresent(locator1 = "Message#SUCCESS");

		AssertElementNotPresent(locator1 = "CustomFieldsEditResource#CUSTOM_FIELDS_TABLE_NAME");
	}

	macro editCP {
		var key_resourceName = "${resourceName}";

		AssertClick(
			locator1 = "CustomFields#RESOURCE_TABLE_RESOURCE_LINK",
			value1 = "${resourceName}");

		var key_customFieldName = "${customFieldName}";

		AssertVisible(locator1 = "CustomFieldsEditResource#CUSTOM_FIELDS_TABLE_NAME");

		Pause(locator1 = "3000");

		LexiconEntry.openEntryMenu(rowEntry = "${customFieldName}");

		MenuItem.click(menuItem = "Edit");

		if (isSet(startingValue)) {
			if ("${customFieldType}" == "Text Field") {
				Type(
					key_fieldLabel = "Starting Value",
					locator1 = "CustomFields#CUSTOM_INPUT_FIELD",
					value1 = "${startingValue}");
			}
		}

		PortletEntry.save();
	}

	macro openCustomFieldAdmin {
		Navigator.openWithAppendToBaseURL(
			baseURL = "${baseURL}",
			urlAppend = "group/control_panel/manage?p_p_id=com_liferay_expando_web_portlet_ExpandoPortlet");
	}

	macro openToAddResourceCustomField {
		Navigator.openWithAppendToBaseURL(
			baseURL = "${baseURL}",
			urlAppend = "group/control_panel/manage?p_p_id=com_liferay_expando_web_portlet_ExpandoPortlet&_com_liferay_expando_web_portlet_ExpandoPortlet_mvcPath=%2Fedit%2Fselect_field_type.jsp&_com_liferay_expando_web_portlet_ExpandoPortlet_modelResource=${modelResource}");
	}

	macro tearDownCP {
		for (var resourceName : list "Blogs Entry,Calendar Event,Document,Documents Folder,Knowledge Base Article,Knowledge Base Folder,Message Boards Category,Message Boards Message,Organization,Page,Role,Site,User,User Group,Web Content Article,Web Content Folder,Wiki Page") {
			ApplicationsMenu.gotoPortlet(
				category = "Configuration",
				panel = "Control Panel",
				portlet = "Custom Fields");

			AssertClick(
				key_resourceName = "${resourceName}",
				locator1 = "CustomFields#RESOURCE_TABLE_RESOURCE_LINK",
				value1 = "${resourceName}");

			if (IsElementNotPresent(locator1 = "Message#EMPTY_INFO")) {
				PortletEntry.selectAll();

				Click(locator1 = "Icon#DELETE");
			}

			AssertElementPresent(locator1 = "Message#EMPTY_INFO");
		}
	}

	macro viewCPIsNotPresent {
		var key_resourceName = "${resourceName}";

		AssertClick(
			locator1 = "CustomFields#RESOURCE_TABLE_RESOURCE_LINK",
			value1 = "${resourceName}");

		var key_customFieldName = "${customFieldName}";

		AssertElementNotPresent(locator1 = "CustomFieldsEditResource#CUSTOM_FIELDS_TABLE_NAME");
	}

	macro viewCPIsPresent {
		var key_resourceName = "${resourceName}";

		AssertClick(
			locator1 = "CustomFields#RESOURCE_TABLE_RESOURCE_LINK",
			value1 = "${resourceName}");

		var key_customFieldName = "${customFieldName}";

		AssertElementPresent(locator1 = "CustomFieldsEditResource#CUSTOM_FIELDS_TABLE_NAME");
	}

}