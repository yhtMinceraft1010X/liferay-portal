definition {

	macro _gotoPage {
		var key_pageName = "${pageName}";
		var count = "0";

		while (("${count}" != "3") && (IsElementNotPresent(locator1 = "Home#PAGE_ACTIVE"))) {
			AssertClick.assertPartialTextClickAt(
				locator1 = "Home#PAGE",
				value1 = "${pageName}");

			var count = ${count} + 1;
		}

		AssertVisible(locator1 = "Home#PAGE_ACTIVE");
	}

	macro _isLocalizedURL {
		var currentURL = Navigator.getCurrentURL();

		var portalUrl = PropsUtil.get("portal.url");
		var isLocalizedUrl = "false";

		if (contains("${currentUrl}", "${portalUrl}/${locale}")) {
			var isLocalizedUrl = "true";
		}

		return "${isLocalizedUrl}";
	}

	macro closeSpecificWindow {
		Navigator.selectSpecificWindow(windowTitle = "${closeWindowTitle}");

		Close.closeAndSelectWindow(locator1 = "title=${selectWindowTitle}");
	}

	macro closeWindow {
		Close.closeWindow();
	}

	macro ensureLocaleURL {
		var isLocalizedURL = Navigator._isLocalizedURL(locale = "${locale}");

		if ("${isLocalizedUrl}" == "false") {
			var currentURL = Navigator.getCurrentURL();

			var portalUrl = PropsUtil.get("portal.url");

			Navigator.openSpecificURL(url = StringUtil.replace("${currentUrl}", "${portalUrl}", "${portalUrl}/${locale}"));
		}
	}

	macro getCurrentURL {
		var currentUrl = selenium.getLocation();

		return "${currentUrl}";
	}

	macro gotoAccountSettings {
		UserBar.gotoDropdownItem(dropdownItem = "Account Settings");
	}

	macro gotoBack {
		Click(locator1 = "Icon#BACK");
	}

	macro gotoEditPageViaControlMenu {
		Click(locator1 = "ControlMenu#EDIT_COG");

		AssertTextEquals(
			locator1 = "Portlet#HEADER",
			value1 = "${pageName}");
	}

	macro gotoLoginPage {
		AssertClick(
			locator1 = "UserBar#USER_SIGN_IN",
			value1 = "Sign In");
	}

	macro gotoNavItem {
		var key_navItem = "${navItem}";

		AssertClick(
			locator1 = "NavBar#NAV_ITEM_LINK",
			value1 = "${navItem}");

		AssertElementPresent(locator1 = "NavBar#ACTIVE_NAV_ITEM_LINK");
	}

	macro gotoNavNested {
		var key_navNested = "${navNested}";

		AssertClick.assertPartialTextClickAt(
			locator1 = "NavNested#NAV_NESTED_LINK",
			value1 = "${navNested}");

		AssertElementPresent(locator1 = "NavNested#ACTIVE_NAV_NESTED_LINK");
	}

	macro gotoNavTab {
		var key_tab = "${navTab}";

		if (IsElementNotPresent(locator1 = "NavTab#ACTIVE_TAB_LINK")) {
			AssertClick.assertPartialTextClickAt(
				locator1 = "NavTab#TAB_LINK",
				value1 = "${navTab}");
		}

		AssertElementPresent(locator1 = "NavTab#ACTIVE_TAB_LINK");

		WaitForLiferayEvent.initializeLiferayEventLog();
	}

	macro gotoNavUnderline {
		var key_navUnderline = "${navUnderline}";

		AssertClick(
			locator1 = "NavUnderline#NAV_UNDERLINE_LINK",
			value1 = "${navUnderline}");

		AssertElementPresent(locator1 = "NavUnderline#ACTIVE_NAV_UNDERLINE_LINK");
	}

	macro gotoNotifications {
		UserBar.gotoDropdownItem(dropdownItem = "Notifications");
	}

	@summary = "Navigate to the page named '${pageName}'"
	macro gotoPage {
		Navigator.openURL(baseUrl = "${baseUrl}");

		Pause(locator1 = "3000");

		Navigator._gotoPage(pageName = "${pageName}");
	}

	macro gotoPersonalSite {
		UserBar.gotoDropdownItem(dropdownItem = "My Profile");
	}

	macro gotoPrivateSitePage {
		Navigator.openPrivateSiteURL(
			baseUrl = "${baseUrl}",
			siteName = "${siteName}");

		Navigator._gotoPage(pageName = "${pageName}");
	}

	@summary = "Navigate to the page named '${pageName}'"
	macro gotoPrivateStagedSitePage {
		Navigator.openPrivateStagedSiteURL(
			baseUrl = "${baseUrl}",
			siteNameUrl = "${siteName}");

		Navigator._gotoPage(pageName = "${pageName}");
	}

	macro gotoSidebarTab {
		var key_tabName = "${tabName}";

		if (IsElementNotPresent(locator1 = "Sidebar#ACTIVE_TAB")) {
			Click(locator1 = "Sidebar#TAB");
		}

		AssertElementPresent(locator1 = "Sidebar#ACTIVE_TAB");

		WaitForLiferayEvent.initializeLiferayEventLog();
	}

	macro gotoSitePage {
		Navigator.openSiteURL(
			baseUrl = "${baseUrl}",
			siteName = "${siteName}");

		Navigator._gotoPage(pageName = "${pageName}");
	}

	macro gotoSpecificURLPage {
		Navigator.openSpecificURL(url = "${url}");

		Navigator._gotoPage(pageName = "${pageName}");
	}

	@summary = "Navigate to the page named '${pageName}'"
	macro gotoStagedSitePage {
		Navigator.openStagingSiteURL(
			baseUrl = "${baseUrl}",
			defaultSite = "${defaultSite}",
			siteName = "${siteName}");

		Navigator._gotoPage(pageName = "${pageName}");
	}

	macro gotoStagedView {
		ProductMenuHelper.openProductMenu();

		Click(locator1 = "Link#STAGING");

		WaitForLiferayEvent.initializeLiferayEventLog();
	}

	macro openNodePort {
		Open(locator1 = "http://localhost:${nodePort}");
	}

	macro openPrivateSitePageURL {
		var siteNameUrl = StringUtil.replace("${siteName}", " ", "-");
		var siteNameUrl = StringUtil.lowerCase("${siteNameUrl}");
		var pageNameUrl = StringUtil.replace("${pageName}", " ", "-");
		var pageNameUrl = StringUtil.lowerCase("${pageNameUrl}");
		var baseUrl = "${baseUrl}";

		if (!(isSet(baseURL))) {
			var baseUrl = PropsUtil.get("portal.url");
		}

		Open(locator1 = "${baseUrl}/group/${siteNameUrl}/${pageNameUrl}");
	}

	macro openPrivateSiteURL {
		var siteNameUrl = StringUtil.replace("${siteName}", " ", "-");
		var siteNameUrl = StringUtil.lowerCase("${siteNameUrl}");
		var baseUrl = "${baseUrl}";

		if (!(isSet(baseURL))) {
			var baseUrl = PropsUtil.get("portal.url");
		}

		Open(locator1 = "${baseUrl}/group/${siteNameUrl}");
	}

	macro openPrivateStagedSiteURL {
		var siteNameUrl = StringUtil.replace("${siteNameUrl}", " ", "-");
		var siteNameUrl = StringUtil.lowerCase("${siteNameUrl}");
		var baseUrl = "${baseUrl}";

		if (!(isSet(baseURL))) {
			var baseUrl = PropsUtil.get("portal.url");
		}

		Open(locator1 = "${baseUrl}/group/${siteNameUrl}-staging");
	}

	macro openPrivateURL {
		var baseUrl = "${baseUrl}";

		if (!(isSet(baseURL))) {
			var baseUrl = PropsUtil.get("portal.url");
		}

		Open(locator1 = "${baseUrl}/group/guest");
	}

	macro openSitePage {
		var siteNameUrl = StringUtil.replace("${siteName}", " ", "-");
		var siteNameUrl = StringUtil.lowerCase("${siteNameUrl}");
		var baseUrl = "${baseUrl}";

		if (!(isSet(baseURL))) {
			var baseUrl = PropsUtil.get("portal.url");
		}

		var pageNameUrl = StringUtil.replace("${pageName}", " ", "-");
		var pageNameUrl = StringUtil.lowerCase("${pageNameUrl}");

		Open(locator1 = "${baseUrl}/web/${siteNameUrl}/${pageNameUrl}");
	}

	macro openSiteStagingConfiguration {
		var siteNameUrl = StringUtil.replace("${siteName}", " ", "-");
		var siteNameUrl = StringUtil.lowerCase("${siteNameUrl}");
		var baseUrl = "${baseUrl}";

		if (!(isSet(baseURL))) {
			var baseUrl = PropsUtil.get("portal.url");
		}

		Navigator.openWithAppendToBaseURL(urlAppend = "group/${siteNameUrl}-staging/~/control_panel/manage?p_p_id=com_liferay_staging_processes_web_portlet_StagingProcessesPortlet");
	}

	macro openSiteURL {
		var siteNameUrl = StringUtil.replace("${siteName}", " ", "-");
		var siteNameUrl = StringUtil.lowerCase("${siteNameUrl}");
		var baseUrl = "${baseUrl}";

		if (!(isSet(baseURL))) {
			var baseUrl = PropsUtil.get("portal.url");
		}

		Open(locator1 = "${baseUrl}/web/${siteNameUrl}");
	}

	macro openSpecificURL {
		Open(locator1 = "${url}");

		Alert.confirmSecurity();
	}

	macro openStagingSiteURL {
		var siteNameUrl = StringUtil.replace("${siteName}", " ", "-");
		var siteNameUrl = StringUtil.lowerCase("${siteNameUrl}");
		var baseUrl = "${baseUrl}";

		if (!(isSet(baseURL))) {
			var baseUrl = PropsUtil.get("portal.url");
		}

		if (isSet(defaultSite)) {
			Open(locator1 = "${baseUrl}/web/guest-staging");
		}
		else {
			Open(locator1 = "${baseUrl}/web/${siteNameUrl}-staging");
		}
	}

	macro openToGroupPagesPortlet {
		var plid = JSONLayout.getPlid(
			groupName = "${groupName}",
			layoutName = "${layoutName}",
			portalUrl = "${portalUrl}",
			site = "${site}");

		if ("${portlet}" == "General") {
			var screenNavigationEntryKey = "general";
		}

		if ("${portlet}" == "Open Graph") {
			var screenNavigationEntryKey = "open-graph";
		}

		if ("${portlet}" == "SEO") {
			var screenNavigationEntryKey = "seo";
		}

		var baseUrl = "${baseUrl}";

		if (!(isSet(baseURL))) {
			var baseUrl = PropsUtil.get("portal.url");
		}

		Open(locator1 = "${baseUrl}/group/${siteUrlKey}/~/control_panel/manage?p_p_id=com_liferay_layout_admin_web_portlet_GroupPagesPortlet&_com_liferay_layout_admin_web_portlet_GroupPagesPortlet_tabs1=pages&_com_liferay_layout_admin_web_portlet_GroupPagesPortlet_privateLayout=false&_com_liferay_layout_admin_web_portlet_GroupPagesPortlet_displayStyle=miller-columns&_com_liferay_layout_admin_web_portlet_GroupPagesPortlet_mvcRenderCommandName=%2Flayout_admin%2Fedit_layout&_com_liferay_layout_admin_web_portlet_GroupPagesPortlet_portletResource=com_liferay_layout_admin_web_portlet_GroupPagesPortlet&p_r_p_selPlid=${plid}&_com_liferay_layout_admin_web_portlet_GroupPagesPortlet_screenNavigationEntryKey=${screenNavigationEntryKey}");
	}

	macro openURL {
		WaitForSPARefresh();

		var baseUrl = "${baseUrl}";

		if (!(isSet(baseURL))) {
			var baseUrl = PropsUtil.get("portal.url");
		}

		Open(locator1 = "${baseUrl}/web/guest/home");
	}

	macro openWithAppendToBaseURL {
		if (!(isSet(baseURL))) {
			var baseUrl = PropsUtil.get("portal.url");
		}

		Open(locator1 = "${baseUrl}/${urlAppend}");
	}

	macro selectSpecificWindow {
		SelectWindow(locator1 = "title=${windowTitle}");
	}

	macro viewNoNavTab {
		AssertElementNotPresent(
			key_tab = "${navTab}",
			locator1 = "NavTab#TAB_LINK");

		AssertElementNotPresent(
			key_tab = "${navTab}",
			locator1 = "NavTab#ACTIVE_TAB_LINK");
	}

}