definition {

	macro addCollection {
		FragmentsAdminNavigator.gotoAddCollection();

		Collection.editCollection(
			collectionDescription = "${collectionDescription}",
			collectionName = "${collectionName}");

		Button.clickSave();
	}

	macro addFragment {
		LexiconEntry.gotoAdd();

		AssertTextEquals(
			locator1 = "Modal#HEADER",
			value1 = "Add Fragment");

		PortletEntry.inputName(name = "${fragmentName}");

		Button.clickSave();
	}

	macro addResource {
		LexiconEntry.gotoAdd();

		IFrame.selectConfigurationFrame();

		Pause(locator1 = "3000");

		AssertVisible(locator1 = "ItemSelector#SELECT_FILE_UPLOAD_IMAGE");

		UploadCommonFile.uploadCommonFileHiddenNoMouseOver(
			locator1 = "TextInput#FILE",
			value1 = "${resourceFile}");

		IFrame.selectConfigurationFrame();

		Button.clickAdd();
	}

	macro deleteEntry {
		LexiconCard.clickEllipsisItem(
			card = "${entry}",
			item = "Delete");

		AssertTextEquals(
			locator1 = "Modal#BODY",
			value1 = "Deleting a fragment is an action impossible to revert. It will be removed from the fragments set and it will not be possible to recover it.");

		Click(
			key_text = "Delete",
			locator1 = "Modal#MODAL_FOOTER_BUTTON");
	}

	macro exportCollection {
		Navigator.gotoNavNested(navNested = "${collectionName}");

		LexiconEntry.gotoEllipsisMenuItem(menuItem = "Export");
	}

	macro exportFragment {
		FragmentsAdminNavigator.gotoCollection(collectionName = "${collectionName}");

		FragmentsAdminNavigator.gotoEntryEllipsisItem(
			entry = "${fragmentName}",
			item = "Export");
	}

	macro exportMultipleCollections {
		for (var collectionName : list "${collectionNameList}") {
			Check(
				key_tableEntry = "${collectionName}",
				locator1 = "LexiconTable#TABLE_ENTRY_CHECKBOX");

			Pause(locator1 = "3000");
		}

		SelectFrameTop();

		Button.click(button = "Export");

		Alert.viewSuccessMessage();
	}

	macro importCollectionFromDependenciesFolder {
		FragmentsAdminNavigator.gotoImportCollection();

		UploadDependencyFile.uploadArchiveFile(fileName = "${collectionZipFile}");

		SelectFrameTop();

		Button.clickImport();

		SelectFrame.selectFrameNoLoading(locator1 = "IFrame#MODAL_IFRAME");

		Alert.viewSuccessMessage();

		SelectFrameTop();

		IFrame.closeFrame();
	}

	macro importCollectionFromTempFolder {
		FragmentsAdminNavigator.gotoImportCollection();

		var collectionsZipFileName = TestCase.getDownloadedTempFileName(fileNamePattern = "collections-*.zip");

		UploadTempFile(
			locator1 = "TextInput#FILE",
			value1 = "${collectionsZipFileName}");

		SelectFrameTop();

		Button.clickImport();

		SelectFrame.selectFrameNoLoading(locator1 = "IFrame#MODAL_IFRAME");

		Alert.viewSuccessMessage();

		SelectFrameTop();

		IFrame.closeFrame();
	}

	macro importFragmentFromTempFolder {
		WaitForLiferayEvent.initializeLiferayEventLog();

		LexiconEntry.gotoEllipsisMenuItem(menuItem = "Import");

		SelectFrame(locator1 = "IFrame#MODAL_BODY");

		var entriesZipFileName = TestCase.getDownloadedTempFileName(fileNamePattern = "entries-*.zip");

		UploadTempFile(
			locator1 = "TextInput#FILE",
			value1 = "${entriesZipFileName}");

		SelectFrameTop();

		Button.clickImport();

		SelectFrame.selectFrameNoLoading(locator1 = "IFrame#MODAL_IFRAME");

		Alert.viewSuccessMessage();

		SelectFrameTop();

		IFrame.closeFrame();
	}

	macro openFragmentsAdmin {
		Navigator.openWithAppendToBaseURL(urlAppend = "group/${siteURLKey}/~/control_panel/manage/-/fragments");
	}

	macro propagateChanges {
		if (isSet(itemNameList)) {
			for (var itemName : list "${itemNameList}") {
				Check(
					key_tableEntry = "${itemName}",
					locator1 = "LexiconTable#TABLE_ENTRY_CHECKBOX");
			}
		}
		else {
			PortletEntry.selectAll();
		}

		Click(
			key_text = "propagation",
			locator1 = "Icon#ANY");

		Alert.viewSuccessMessage();
	}

	macro selectCollection {
		SelectFrame(locator1 = "IFrame#MODAL_BODY");

		LexiconCard.clickCard(card = "${collectionName}");
	}

	macro viewEntryNotPresent {
		LexiconCard.viewCardNotPresent(card = "${entry}");
	}

	macro viewEntryPresent {
		LexiconCard.viewCardPresent(card = "${entry}");
	}

	macro viewEntryWorkflowStatus {
		LexiconCard.viewCardLabel(
			card = "${entry}",
			cardLabel = "${workflowStatus}");
	}

	macro viewSiteUsages {
		LexiconTable.viewTableEntryInfo(
			columnName = "usages",
			tableEntry = "${siteName}",
			tableEntryInfo = "${usages}");
	}

}