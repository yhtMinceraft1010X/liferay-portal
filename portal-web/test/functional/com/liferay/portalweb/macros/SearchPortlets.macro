definition {

	/*
		For searches that do not use the search portlet (e.g. searches within the product menu), use the macros within Search.macro instead.'Embedded' refers to the search bar in the page header whereas 'Widget' refers to the search bar within the page.
	 */

	macro _clickAddPortlet {
		Click.clickNoWaitForVisible(
			key_portletName = "${portletName}",
			locator1 = "Search#ADD_PORTLET_LINK");
	}

	macro addWidgets {
		if (IsNotVisible(locator1 = "ControlMenuAddPanel#CLOSE_ADD_PANEL")) {
			Click.clickAt(locator1 = "ControlMenu#ADD");
		}

		AssertElementPresent(locator1 = "ControlMenuAddPanel#SIDEBAR_HEADER");

		Navigator.gotoNavTab(navTab = "Widgets");

		Pause(locator1 = "5000");

		Type.sendKeysApplicationSearch(
			locator1 = "NavBar#APPLICATION_SEARCH_FIELD",
			value1 = "search");

		for (var portletName : list "${searchPortletList}") {
			SearchPortlets._clickAddPortlet(portletName = "${portletName}");

			var key_portletName = "${portletName}";

			AssertElementPresent(locator1 = "Portlet#LOADED");

			AssertElementPresent(locator1 = "Home#PORTLET");
		}
	}

	macro chooseEmbeddedScope {
		if (("${searchScope}" == "Everything") || ("${searchScope}" == "This Site")) {
			Select(
				locator1 = "Search#EMBEDDED_SEARCH_BAR_SCOPE_SELECT",
				value1 = "${searchScope}");
		}
		else {
			fail("Invalid search scope. Must be (Everything|This Site).");
		}
	}

	macro chooseSortOption {
		Click(locator1 = "Search#SORT_PORTLET_DROPDOWN");

		Click.clickNoMouseOver(
			locator1 = "Search#SORT_PORTLET_SORT_OPTION",
			sortOption = "${sortOption}");
	}

	macro chooseWidgetScope {
		if (("${searchScope}" == "Everything") || ("${searchScope}" == "This Site")) {
			Select(
				locator1 = "Search#SEARCH_BAR_WIDGET_SCOPE_SELECT",
				value1 = "${searchScope}");
		}
		else {
			fail("Invalid search scope. Must be (Everything|This Site).");
		}
	}

	macro configureCustomFilter {
		Portlet.gotoPortletOptions(
			portletName = "Custom Filter",
			portletOption = "Configuration");

		SelectFrame(locator1 = "IFrame#CONFIGURATION");

		if (isSet(customParameterName)) {
			Type(
				key_fieldLabel = "Custom Parameter Name",
				locator1 = "TextInput#GENERIC_TEXT_INPUT",
				value1 = "${customParameterName}");
		}

		if (isSet(federatedSearchKey)) {
			Type(
				key_fieldLabel = "Federated Search Key",
				locator1 = "TextInput#GENERIC_TEXT_INPUT",
				value1 = "${federatedSearchKey}");
		}

		if (isSet(filterField)) {
			Type(
				key_fieldLabel = "Filter Field",
				locator1 = "TextInput#GENERIC_TEXT_INPUT",
				value1 = "${filterField}");
		}

		if (isSet(filterQueryType)) {
			Select(
				key_fieldLabel = "Filter Query Type",
				locator1 = "Select#GENERIC_SELECT_FIELD",
				value1 = "${filterQueryType}");
		}

		if (isSet(filterValue)) {
			Type(
				key_fieldLabel = "Filter Value",
				locator1 = "TextInput#GENERIC_TEXT_INPUT",
				value1 = "${filterValue}");
		}

		if (isSet(occur)) {
			Select(
				key_fieldLabel = "Occur",
				locator1 = "Select#GENERIC_SELECT_FIELD",
				value1 = "${occur}");
		}

		PortletEntry.save();

		IFrame.closeFrame();
	}

	macro configureEmbeddedSearchBar {
		SearchPortlets.gotoEmbeddedOptions(portletOption = "Configuration");

		SelectFrame(locator1 = "IFrame#CONFIGURATION");

		if (isSet(searchScope)) {
			if (("${searchScope}" == "Everything") || ("${searchScope}" == "Let the User Choose") || ("${searchScope}" == "This Site")) {
				Select(
					locator1 = "Select#SCOPE_CONFIGURATION",
					value1 = "${searchScope}");
			}
			else {
				fail("Invalid search scope. Must be (Everything|Let the User Choose|This Site).");
			}
		}

		if (isSet(destinationPage)) {
			if (contains("${destinationPage}", " ")) {
				var destinationPage = StringUtil.replace("${destinationPage}", " ", "-");
			}

			var destinationPage = StringUtil.toLowerCase("${destinationPage}");

			Type(
				locator1 = "SearchConfiguration#DESTINATION_PAGE",
				value1 = "${destinationPage}");
		}

		PortletEntry.save();

		IFrame.closeFrame();
	}

	macro configureLowLevelSearchOptions {
		Portlet.gotoPortletOptions(
			portletName = "Low Level Search Options",
			portletOption = "Configuration");

		SelectFrame(locator1 = "IFrame#CONFIGURATION");

		if (isSet(connectionId)) {
			Select(
				key_fieldLabel = "Connection ID",
				locator1 = "Select#GENERIC_SELECT_FIELD",
				value1 = "${connectionId}");
		}

		if (isSet(federatedSearchKey)) {
			Type(
				key_fieldLabel = "Federated Search Key",
				locator1 = "TextInput#GENERIC_TEXT_INPUT",
				value1 = "${federatedSearchKey}");
		}

		if (isSet(indexes)) {
			Type(
				key_fieldLabel = "Indexes",
				locator1 = "TextInput#GENERIC_TEXT_INPUT",
				value1 = "${indexes}");
		}

		if (isSet(attributesKey)) {
			Type(
				locator1 = "TextInput#KEY",
				value1 = "${attributesKey}");

			Type(
				key_fieldLabel = "Value",
				locator1 = "TextInput#GENERIC_TEXT_INPUT",
				value1 = "${attributesValue}");
		}

		PortletEntry.save();

		IFrame.closeFrame();
	}

	macro configureSearchBarWidget {
		if (isSet(index)) {
			SearchPortlets.gotoWidgetOptionsSpecific(
				index = "${index}",
				portletOption = "Configuration");
		}
		else {
			SearchPortlets.gotoWidgetOptions(portletOption = "Configuration");
		}

		SelectFrame(locator1 = "IFrame#CONFIGURATION");

		if (isSet(keywordsParameterName)) {
			Type(
				locator1 = "SearchConfiguration#KEYWORDS_PARAMETER_NAME",
				value1 = "${keywordsParameterName}");
		}

		if (isSet(searchScope)) {
			if (("${searchScope}" == "Everything") || ("${searchScope}" == "Let the User Choose") || ("${searchScope}" == "This Site")) {
				Select(
					locator1 = "Select#SCOPE_CONFIGURATION",
					value1 = "${searchScope}");
			}
			else {
				fail("Invalid search scope. Must be (Everything|Let the User Choose|This Site).");
			}
		}

		if (isSet(scopeParameterName)) {
			Type(
				locator1 = "SearchConfiguration#SCOPE_PARAMETER_NAME",
				value1 = "${scopeParameterName}");
		}

		if (isSet(federatedSearchKey)) {
			Type(
				key_fieldLabel = "Federated Search Key",
				locator1 = "TextInput#GENERIC_TEXT_INPUT",
				value1 = "${federatedSearchKey}");
		}

		PortletEntry.save();

		IFrame.closeFrame();
	}

	macro configureSearchOptions {
		Portlet.gotoPortletOptions(
			portletName = "Search Options",
			portletOption = "Configuration");

		WaitForLiferayEvent.initializeLiferayEventLog();

		SelectFrameTop();

		SelectFrame(locator1 = "IFrame#CONFIGURATION");

		if ("${allowEmptySearches}" == "true") {
			Check(
				checkboxName = "Allow Empty Searches",
				locator1 = "Checkbox#ANY_CHECKBOX");
		}

		if ("${basicFacetSelection}" == "true") {
			Check(
				checkboxName = "Basic Facet Selection",
				locator1 = "Checkbox#ANY_CHECKBOX");
		}

		if (isSet(federatedSearchKey)) {
			Type(
				key_fieldLabel = "Federated Search Key",
				locator1 = "TextInput#GENERIC_TEXT_INPUT",
				value1 = "${federatedSearchKey}");
		}

		PortletEntry.save();

		IFrame.closeFrame();
	}

	macro configureSimilarResults {
		Portlet.gotoPortletOptions(
			portletName = "Similar Results",
			portletOption = "Configuration");

		SelectFrame(locator1 = "IFrame#CONFIGURATION");

		if (isSet(analyzer)) {
			Type(
				key_fieldLabel = "Analyzer",
				locator1 = "TextInput#GENERIC_TEXT_INPUT",
				value1 = "${analyzer}");
		}

		if (isSet(displayTemplate)) {
			Select(
				locator1 = "Select#DISPLAY_TEMPLATE",
				value1 = "${displayTemplate}");
		}

		if (isSet(fields)) {
			Type(
				key_fieldLabel = "Fields",
				locator1 = "TextInput#GENERIC_TEXT_INPUT",
				value1 = "${fields}");
		}

		if (isSet(linkBehavior)) {
			Select(
				locator1 = "Select#SELECT_ID",
				selectFieldId = "linkBehavior",
				value1 = "${linkBehavior}");
		}

		if (isSet(maximumDocumentFrequency)) {
			Type(
				key_fieldLabel = "Maximum Document Frequency",
				locator1 = "TextInput#GENERIC_NUMBER_INPUT",
				value1 = "${maximumDocumentFrequency}");
		}

		if (isSet(maximumItemDisplay)) {
			Type(
				key_maximumItems = "Maximum Item Display",
				locator1 = "Search#SIMILAR_RESULTS_MAXIMUM_ITEM_DISPLAY",
				value1 = "${maximumItemDisplay}");
		}

		if (isSet(maximumQueryTerms)) {
			Type(
				key_fieldLabel = "Maximum Query Terms",
				locator1 = "TextInput#GENERIC_NUMBER_INPUT",
				value1 = "${maximumQueryTerms}");
		}

		if (isSet(maximumWordLength)) {
			Type(
				key_fieldLabel = "Maximum Word Length",
				locator1 = "TextInput#GENERIC_NUMBER_INPUT",
				value1 = "${maximumWordLength}");
		}

		if (isSet(minimumDocumentFrequency)) {
			Type(
				key_fieldLabel = "Minimum Document Frequency",
				locator1 = "TextInput#GENERIC_NUMBER_INPUT",
				value1 = "${minimumDocumentFrequency}");
		}

		if (isSet(minimumShouldMatch)) {
			Type(
				key_fieldLabel = "Minimum Should Match",
				locator1 = "TextInput#GENERIC_TEXT_INPUT",
				value1 = "${minimumShouldMatch}");
		}

		if (isSet(minimumTermFrequency)) {
			Type(
				key_fieldLabel = "Minimum Term Frequency",
				locator1 = "TextInput#GENERIC_NUMBER_INPUT",
				value1 = "${minimumTermFrequency}");
		}

		if (isSet(minimumWordLength)) {
			Type(
				key_fieldLabel = "Minimum Word Length",
				locator1 = "TextInput#GENERIC_NUMBER_INPUT",
				value1 = "${minimumWordLength}");
		}

		if (isSet(searchScope)) {
			Select(
				locator1 = "Select#SCOPE_CONFIGURATION",
				value1 = "${searchScope}");
		}

		if (isSet(stopWords)) {
			Type(
				key_fieldLabel = "Stop Words",
				locator1 = "TextInput#GENERIC_TEXT_INPUT",
				value1 = "${stopWords}");
		}

		PortletEntry.save();

		IFrame.closeFrame();
	}

	macro configureWidgetScope {
		SearchPortlets.gotoWidgetOptions(portletOption = "Configuration");

		if (("${searchScope}" == "Everything") || ("${searchScope}" == "Let the User Choose") || ("${searchScope}" == "This Site")) {
			SelectFrame(locator1 = "IFrame#CONFIGURATION");

			Select(
				locator1 = "Select#SCOPE_CONFIGURATION",
				value1 = "${searchScope}");
		}
		else {
			fail("Invalid search scope. Must be (Everything|Let the User Choose|This Site).");
		}

		PortletEntry.save();

		IFrame.closeFrame();
	}

	macro getSearchTermURL {
		if (contains("${searchPage}", " ")) {
			var searchPage = StringUtil.replace("${searchPage}", " ", "-");
		}

		var searchPage = StringUtil.toLowerCase("${searchPage}");
		var portalURL = PropsUtil.get("portal.url");
		var searchURL = "${portalURL}/web/guest/${searchPage}?q=";
		var searchURL = "${searchURL}${searchTerm}";

		return "${searchURL}";
	}

	macro gotoConfigureSuggestionsOptions {
		MouseOver.mouseOverNotVisible(locator1 = "Search#SUGGESTION_WIDGET_ELLIPSIS_ICON");

		Click.waitForMenuToggleJSClick(locator1 = "Search#SUGGESTION_WIDGET_ELLIPSIS_ICON");

		MenuItem.click(menuItem = "Configuration");
	}

	macro gotoEmbeddedOptions {
		MouseOver.mouseOverNotVisible(locator1 = "Search#EMBEDDED_SEARCH_BAR_ELLIPSIS_ICON");

		Click(locator1 = "Search#EMBEDDED_SEARCH_BAR_ELLIPSIS_ICON");

		if (isSet(portletOption)) {
			var key_menuItem = "${portletOption}";

			Click(locator1 = "MenuItem#ANY_MENU_ITEM");
		}
	}

	macro gotoResultDetails {
		var key_searchAssetTitle = "${searchAssetTitle}";

		AssertClick(
			locator1 = "SearchResults#ASSET_ENTRY_TABLE_TITLE",
			value1 = "${searchAssetTitle}");
	}

	macro gotoWidgetOptions {
		MouseOver.mouseOverNotVisible(locator1 = "Search#SEARCH_BAR_WIDGET_ELLIPSIS_ICON");

		Click(locator1 = "Search#SEARCH_BAR_WIDGET_ELLIPSIS_ICON");

		if (isSet(portletOption)) {
			var key_menuItem = "${portletOption}";

			Click(locator1 = "MenuItem#ANY_MENU_ITEM");
		}
	}

	macro gotoWidgetOptionsSpecific {
		MouseOver.mouseOverNotVisible(
			key_index = "${index}",
			locator1 = "Search#SEARCH_BAR_WIDGET_ELLIPSIS_ICON_SPECIFIC");

		Click(
			key_index = "${index}",
			locator1 = "Search#SEARCH_BAR_WIDGET_ELLIPSIS_ICON_SPECIFIC");

		if (isSet(portletOption)) {
			var key_menuItem = "${portletOption}";

			Click(locator1 = "MenuItem#ANY_MENU_ITEM");
		}
	}

	macro searchEmbedded {
		AssertElementPresent(locator1 = "Search#EMBEDDED_SEARCH_BAR");

		Type(
			locator1 = "Search#EMBEDDED_SEARCH_BAR",
			value1 = "${searchTerm}");

		var browserType = PropsUtil.get("browser.type");

		if ("${browserType}" == "safari") {
			Click(locator1 = "Icon#BASIC_SEARCH");
		}
		else {
			KeyPress(
				locator1 = "Search#EMBEDDED_SEARCH_BAR",
				value1 = "\RETURN");
		}

		WaitForLiferayEvent.initializeLiferayEventLog();
	}

	macro searchWidget {
		Type(
			locator1 = "Search#SEARCH_BAR_WIDGET",
			value1 = "${searchTerm}");

		KeyPress(
			locator1 = "Search#SEARCH_BAR_WIDGET",
			value1 = "\RETURN");

		WaitForLiferayEvent.initializeLiferayEventLog();
	}

	macro setupSimilarResultsPage {
		JSONLayout.addPublicLayout(
			groupName = "Guest",
			layoutName = "Similar Results Page");

		JSONLayout.addWidgetToPublicLayout(
			column = "1",
			groupName = "Guest",
			layoutName = "Similar Results Page",
			widgetName = "Asset Publisher");

		JSONLayout.addWidgetToPublicLayout(
			column = "2",
			groupName = "Guest",
			layoutName = "Similar Results Page",
			widgetName = "Similar Results");

		Navigator.gotoPage(pageName = "Similar Results Page");
	}

	macro viewResultDetails {
		AssertTextEquals(
			locator1 = "SearchResults#ASSET_DETAILS_TITLE",
			value1 = "${searchAssetTitle}");
	}

	macro viewUserResultDetails {
		AssertTextEquals(
			locator1 = "Home#SITE_NAME",
			value1 = "${userName}");

		AssertTextEquals(
			locator1 = "Home#PAGE_1",
			value1 = "${pageName}");
	}

}