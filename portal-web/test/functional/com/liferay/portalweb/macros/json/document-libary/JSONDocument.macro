definition {

	@summary = "This calls the JSON WS API to add a Document entry comment"
	macro addComment {
		var fileEntryId = JSONDocument.getFileEntryId(
			dmDocumentTitle = "${dmDocumentTitle}",
			folderId = "${folderId}",
			groupName = "${groupName}",
			portalURL = "${baseURL}");

		var portalURL = JSONCompany.getPortalURL();

		if (!(isSet(parentCommentId))) {
			var parentCommentId = "0";
		}

		var curl = '''
			${portalURL}/o/headless-delivery/v1.0/documents/${fileEntryId}/comments \
			-H 'Content-Type: application/json' \
			-u 'test@liferay.com:test'
			-d '{
				"creator": {},
				"parentCommentId": "${parentCommentId}",
				"text": "${commentBody}"
			}'
		''';

		echo("## Adding a 'Comment' with the following info using JSONWS:");

		echo("## * commentBody: ${commentBody}");

		echo("## * fileEntryId: ${fileEntryId}");

		var messageId = JSONCurlUtil.post("${curl}");
	}

	@summary = "This calls the JSON WS API to add a Document entry"
	macro addFile {
		var portalURL = JSONCompany.getPortalURL();

		var repositoryId = JSONGroupAPI._getSiteIdByGroupKey(groupName = "${groupName}");

		if (isSet(folderName)) {
			Variables.assertDefined(parameterList = "${folderName},${groupName}");

			var folderId = JSONDocumentAPI._getFolderIdByFolderName(
				groupName = "${groupName}",
				name = "${folderName}");
		}
		else {
			var folderId = "0";
		}

		if (!(isSet(dmDocumentDescription))) {
			var dmDocumentDescription = "";
		}

		if (!(isSet(groupPermissions))) {
			var groupPermissions = "true";
		}

		if (!(isSet(guestPermissions))) {
			var guestPermissions = "true";
		}

		var serviceContext = JSONDocumentSetter.setServiceContext(
			addGroupPermissions = "${groupPermissions}",
			addGuestPermissions = "${guestPermissions}",
			assetCategoryIds = "${assetCategoryIds}",
			assetTagNames = "${assetTagNames}",
			serviceContext = "${serviceContext}",
			workflowAction = "${workflowAction}");

		var curl = '''
			${portalURL}/api/jsonws/dlapp/add-file-entry \
			-u test@liferay.com:test \
			-d repositoryId=${repositoryId} \
			-d folderId=${folderId} \
			-d sourceFileName= \
			-d mimeType= \
			-d title=${dmDocumentTitle} \
		 	-d description=${dmDocumentDescription} \
			-d changeLog= \
			-d file=@path_to_file \
			-d serviceContext=${serviceContext}
		''';

		echo("## Adding a 'File' with the following info using JSONWS:");

		echo("## * Title: ${dmDocumentTitle}");

		echo("## * Description: ${dmDocumentDescription}");

		var messageId = JSONCurlUtil.post("${curl}");
	}

	@summary = "This calls the JSON WS API to add a Document entry with uploaded file"
	macro addFileWithUploadedFile {
		Variables.assertDefined(parameterList = "${groupName},${mimeType},${sourceFileName}");

		var portalURL = JSONCompany.getPortalURL();

		var repositoryId = JSONGroupAPI._getSiteIdByGroupKey(groupName = "${groupName}");

		if (isSet(folderName)) {
			Variables.assertDefined(parameterList = "${folderName}");

			if (isSet(parentFolderName)) {
				var parentFolderId = JSONDocumentAPI._getFolderIdByFolderName(
					groupName = "${groupName}",
					name = "${parentFolderName}");
			}
			else {
				var parentFolderId = "0";
			}

			var folderId = JSONDocumentAPI._getFolderIdByFolderName(
				groupName = "${groupName}",
				name = "${folderName}",
				parentFolderId = "${parentFolderId}");
		}
		else {
			var folderId = "0";
		}

		var filePath = TestCase.getDependenciesDirPath(fileName = "${sourceFileName}");

		if (isSet(dmDocumentDescription)) {
			var description = "${dmDocumentDescription}";
		}
		else {
			var description = "";
		}

		if (isSet(fileName)) {
			var sourceFileName = "${fileName}";
		}

		if (!(isSet(expirationDate))) {
			var expirationDate = "2099-01-25 01:00";
		}

		if (!(isSet(groupPermissions))) {
			var groupPermissions = "true";
		}

		if (!(isSet(guestPermissions))) {
			var guestPermissions = "true";
		}

		if (isSet(reviewDate)) {
			var reviewDate = DateUtil.getDateOffsetByMinutes("${increaseMinutes}", "yyyy-MM-dd HH:mm");

			echo("## * reviewDate: ${reviewDate}");
		}

		var serviceContext = JSONDocumentSetter.setServiceContext(
			addGroupPermissions = "${groupPermissions}",
			addGuestPermissions = "${guestPermissions}",
			assetCategoryIds = "${assetCategoryIds}",
			assetTagNames = "${assetTagNames}",
			serviceContext = "${serviceContext}",
			workflowAction = "${workflowAction}");

		if (isSet(reviewDate)) {
			var curl = '''
				${portalURL}/api/jsonws/dlapp/add-file-entry \
				-u test@liferay.com:test \
				-F externalReferenceCode= \
				-F repositoryId=${repositoryId} \
				-F folderId=${folderId} \
				-F sourceFileName=${sourceFileName} \
				-F mimeType=${mimeType} \
				-F title=${dmDocumentTitle} \
			 	-F description=${description} \
				-F changeLog= \
				-F <CURL_DATA[expirationDate=${expirationDate}]CURL_DATA> \
				-F <CURL_DATA[reviewDate=${reviewDate}]CURL_DATA> \
				-F file=@${filePath} \
				-F serviceContext=${serviceContext}
			''';
		}
		else {
			var curl = '''
				${portalURL}/api/jsonws/dlapp/add-file-entry \
				-u test@liferay.com:test \
				-F externalReferenceCode= \
				-F repositoryId=${repositoryId} \
				-F folderId=${folderId} \
				-F sourceFileName=${sourceFileName} \
				-F mimeType=${mimeType} \
				-F title=${dmDocumentTitle} \
			 	-F description=${description} \
				-F changeLog= \
				-F file=@${filePath} \
				-F serviceContext=${serviceContext}
			''';
		}

		echo("## Adding a 'File' with the following info using JSONWS:");

		echo("## * Title: ${dmDocumentTitle}");

		echo("## * Description: ${description}");

		var fileEntryId = JSONCurlUtil.post("${curl}", "$..fileEntryId");

		return "${fileEntryId}";
	}

	@summary = "This calls the JSON WS API to delete a Document entry"
	macro deleteFile {
		var portalURL = JSONCompany.getPortalURL();

		if (!(isSet(site))) {
			var site = "true";
		}

		var groupId = JSONGroupAPI._getGroupIdByName(
			groupName = "${groupName}",
			site = "${site}");

		if (!(isSet(folderId))) {
			var folderId = "0";
		}
		else {
			var folderId = JSONDocumentAPI._getFolderIdByFolderName(
				groupName = "${groupName}",
				name = "${folderName}");
		}

		var curl = '''
			${portalURL}/api/jsonws/dlfileentry/delete-file-entry \
			-u test@liferay.com:test \
			-d groupId=${groupId} \
			-d folderId=${folderId} \
			-d title=${dmDocumentTitle}
		''';

		com.liferay.poshi.runner.util.JSONCurlUtil.post("${curl}");
	}

	macro getFileEntryId {
		if (!(isSet(portalURL))) {
			var portalURL = JSONCompany.getPortalURL();
		}

		if (!(isSet(site))) {
			var site = "true";
		}

		var groupId = JSONGroupAPI._getGroupIdByName(
			groupName = "${groupName}",
			portalURL = "${portalURL}",
			site = "${site}");

		if (!(isSet(folderId))) {
			var folderId = "0";
		}

		var curl = '''
			${portalURL}/api/jsonws/dlfileentry/get-file-entry \
			  -u test@liferay.com:test \
			  -d groupId=${groupId} \
			  -d folderId=${folderId} \
			  -d title=${dmDocumentTitle}
		''';
		var fileEntryId = JSONCurlUtil.post("${curl}", "$.[?(@['title'] == '${dmDocumentTitle}')]['fileEntryId']");

		return "${fileEntryId}";

		echo("## * File Entry ID: ${fileEntryId}");
	}

	macro getFileEntryUUID {
		if (!(isSet(portalURL))) {
			var portalURL = JSONCompany.getPortalURL();
		}

		if (!(isSet(site))) {
			var site = "true";
		}

		var groupId = JSONGroupAPI._getGroupIdByName(
			groupName = "${groupName}",
			portalURL = "${portalURL}",
			site = "${site}");

		if (!(isSet(folderId))) {
			var folderId = "0";
		}

		var curl = '''
			${portalURL}/api/jsonws/dlfileentry/get-file-entry \
			  -u test@liferay.com:test \
			  -d groupId=${groupId} \
			  -d folderId=${folderId} \
			  -d title=${dmDocumentTitle}
		''';
		var fileEntryUuid = JSONCurlUtil.post("${curl}", "$.[?(@['title'] == '${dmDocumentTitle}')]['uuid']");

		return "${fileEntryUuid}";
	}

	@summary = "This calls the JSON WS API to move a document to a folder"
	macro moveFileEntryToFolder {
		var fileEntryId = JSONDocument.getFileEntryId(
			dmDocumentTitle = "${dmDocumentTitle}",
			folderId = "${folderId}",
			groupName = "${groupName}",
			portalURL = "${baseURL}");

		var portalURL = JSONCompany.getPortalURL();

		var newFolderId = JSONDocumentAPI._getFolderIdByFolderName(
			groupName = "${groupName}",
			name = "${folderName}");

		var curl = '''
			${portalURL}/api/jsonws/dlapp/move-file-entry \
			-u test@liferay.com:test \
			-d fileEntryId=${fileEntryId} \
			-d newFolderId=${newFolderId}
		''';

		com.liferay.poshi.runner.util.JSONCurlUtil.post("${curl}");
	}

	@summary = "This calls the JSON WS API to move a document to a trash"
	macro moveToRecycleBin {
		var portalURL = JSONCompany.getPortalURL();

		if (isSet(folderName)) {
			var folderId = JSONDocumentAPI._getFolderIdByFolderName(
				groupName = "${groupName}",
				name = "${folderName}");
		}
		else {
			var folderId = "0";
		}

		var fileEntryId = JSONDocument.getFileEntryId(
			dmDocumentTitle = "${dmDocumentTitle}",
			folderId = "${folderId}",
			groupName = "${groupName}",
			portalURL = "${baseURL}");

		var portalURL = JSONCompany.getPortalURL();

		var curl = '''
			${portalURL}/api/jsonws/dltrash/move-file-entry-to-trash \
			-u test@liferay.com:test \
			-d fileEntryId=${fileEntryId}
		''';

		JSONCurlUtil.post("${curl}");
	}

	@summary = "This calls the JSON WS API to subscribe to a folder"
	macro subscribeToFolder {
		var folderId = JSONDocumentAPI._getFolderIdByFolderName(
			groupName = "${groupName}",
			name = "${dmFolderName}");

		if (!(isSet(portalURL))) {
			var portalURL = JSONCompany.getPortalURL();
		}

		if (!(isSet(site))) {
			var site = "true";
		}

		var groupId = JSONGroupAPI._getGroupIdByName(
			groupName = "${groupName}",
			portalURL = "${portalURL}",
			site = "${site}");

		var curl = '''
			${portalURL}/api/jsonws/dlapp/subscribe-folder \
			-u test@liferay.com:test \
			-d groupId=${groupId} \
			-d folderId=${folderId} \
		''';

		echo("## * groupId: ${groupId}");

		echo("## * folderId: ${folderId}");

		var messageId = JSONCurlUtil.post("${curl}");
	}

}