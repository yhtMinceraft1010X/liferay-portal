definition {

	macro _getFormInstanceId {
		Variables.assertDefined(parameterList = "${groupName},${formName}");

		var portalURL = JSONCompany.getPortalURL();

		var companyId = JSONCompany.getCompanyId();

		var groupId = JSONDDMFormSetter.setGroupId(groupName = "${groupName}");

		var curl = '''
			${portalURL}/api/jsonws/ddm.ddmforminstance/get-form-instances \
			  -u test@liferay.com:test \
			  -d companyId=${companyId} \
			  -d groupId=${groupId} \
			  -d "start=-1" \
			  -d "end=-1"
		''';

		var formInstanceId = JSONCurlUtil.post("${curl}","$.[?(@['nameCurrentValue'] == '${formName}')]['formInstanceId']");

		if ("${formInstanceId}" == "") {
			fail("FAIL. Cannot find formInstanceId.");
		}

		return "${formInstanceId}";
	}

	macro _getFormInstanceRecordId {
		var portalURL = JSONCompany.getPortalURL();

		var curl = '''
			${portalURL}/api/jsonws/ddm.ddmforminstancerecord/get-form-instance-records \
			  -u test@liferay.com:test \
			  -d ddmFormInstanceId=${formInstanceId}
		''';

		var formInstanceRecordId = JSONCurlUtil.post("${curl}","$.[?(@['formInstanceId'] == '${formInstanceId}')]['formInstanceRecordId']");

		if ("${formInstanceRecordId}" == "") {
			fail("FAIL. Cannot find formInstanceRecordId.");
		}

		return "${formInstanceRecordId}";
	}

}