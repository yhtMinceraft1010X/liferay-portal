definition {

	@summary = "This calls the JSON WS API to add a Blog entry comment"
	macro addComment {
		var entryId = JSONBlog.getBlogEntryId(
			entryTitle = "${entryTitle}",
			friendlyURL = "${friendlyURL}",
			groupName = "${groupName}");

		var portalURL = JSONCompany.getPortalURL();

		if (!(isSet(parentCommentId))) {
			var parentCommentId = "0";
		}

		if (!(isSet(userEmailAddress))) {
			var userEmailAddress = "test@liferay.com";
		}

		if (!(isSet(userPassword))) {
			var userPassword = "test";
		}

		var curl = '''
			${portalURL}/o/headless-delivery/v1.0/blog-postings/${entryId}/comments \
			-H 'Content-Type: application/json' \
			-u '${userEmailAddress}:${userPassword}'
			-d '{
				"creator": {},
				"parentCommentId": "${parentCommentId}",
				"text": "${commentBody}"
			}'
		''';

		echo("## Adding a 'Comment' with the following info using JSONWS:");

		echo("## * commentBody: ${commentBody}");

		echo("## * entryId: ${entryId}");

		var messageId = JSONCurlUtil.post("${curl}");
	}

	@summary = "This calls the JSON WS API to add a Blog entry"
	macro addEntry {
		Variables.assertDefined(parameterList = "${entryContent},${entryTitle}");

		var portalURL = JSONCompany.getPortalURL();

		var contentXml = JSONBlogSetter.setContentXML(content = "${entryContent}");

		if (!(isSet(entrySubtitle))) {
			var entrySubtitle = "";
		}

		if (!(isSet(entryAbstractDescription))) {
			var entryAbstractDescription = "";
		}

		if (!(isSet(guestPermissions))) {
			var guestPermissions = "true";
		}

		if (!(isSet(friendlyURL))) {
			var friendlyURL = "";
		}

		if (!(isSet(groupName))) {
			var groupName = "Guest";
		}

		if (!(isSet(userEmailAddress))) {
			var userEmailAddress = "test@liferay.com";
		}

		if (!(isSet(userPassword))) {
			var userPassword = "test";
		}

		var serviceContext = JSONBlogSetter.setServiceContext(
			addGuestPermissions = "${guestPermissions}",
			assetCategoryIds = "${assetCategoryIds}",
			assetTagNames = "${assetTagNames}",
			groupName = "${groupName}",
			serviceContext = "${serviceContext}",
			site = "${site}",
			workflowAction = "${workflowAction}");

		if (isSet(displayDate)) {
			var displayDateMinute = DateUtil.getFormattedCurrentDate("m");
			var displayDateHour = DateUtil.getFormattedCurrentDate("H");
			var displayDateDay = DateUtil.getCurrentDate();
			var displayDateYear = DateUtil.getCurrentYear();
			var displayDateMonth = DateUtil.getCurrentMonth();
			var displayDateMonth = ${displayDateMonth} - 1;

			if (isSet(increaseMinutes)) {
				var displayDateMinute = ${displayDateMinute} + ${increaseMinutes};
			}
		}
		else {
			var displayDateMinute = "01";
			var displayDateHour = "01";
			var displayDateDay = "01";
			var displayDateMonth = "01";
			var displayDateYear = "2004";
		}

		var curl = '''
			${portalURL}/api/jsonws/blogs.blogsentry/add-entry \
			-u ${userEmailAddress}:${userPassword} \
			-d externalReferenceCode= \
			-d title=${entryTitle} \
			-d subtitle=${entrySubtitle} \
			-d urlTitle=${friendlyURL} \
			-d description=${entryAbstractDescription} \
			-d content=${contentXml} \
			-d displayDateMonth=${displayDateMonth} \
			-d displayDateDay=${displayDateDay} \
			-d displayDateYear=${displayDateYear} \
			-d displayDateHour=${displayDateHour} \
			-d displayDateMinute=${displayDateMinute} \
			-d allowPingbacks=true \
			-d allowTrackbacks=false \
			-d trackbacks= \
			-d coverImageCaption= \
			-d -coverImageImageSelector= \
			-d -smallImageImageSelector= \
			-d serviceContext=${serviceContext}
		''';

		echo("## Adding a 'Blog' with the following info using JSONWS:");

		echo("## * Title: ${entryTitle}");

		echo("## * Content: ${entryContent}");

		var messageId = JSONCurlUtil.post("${curl}");
	}

	macro addEntryWithImage {
		Variables.assertDefined(parameterList = "${entryContent},${entryTitle},${groupName}");

		var portalURL = JSONCompany.getPortalURL();

		if (!(isSet(site))) {
			var site = "true";
		}

		var groupId = JSONGroupAPI._getGroupIdByName(
			grandParentGroupName = "${grandParentGroupName}",
			groupName = "${groupName}",
			parentGroupName = "${parentGroupName}",
			site = "${site}");

		if (!(isSet(userEmailAddress))) {
			var userEmailAddress = "test@liferay.com";
		}

		if (!(isSet(userPassword))) {
			var userPassword = "test";
		}

		if (!(isSet(coverImageId))) {
			var coverImageId = "0";
		}

		if (!(isSet(entrySubtitle))) {
			var entrySubtitle = "";
		}

		if (isSet(assetTagNames)) {
			var assetTagNames = '''"${assetTagNames}"''';
		}
		else {
			var assetTagNames = "";
		}

		if (isSet(assetCategoryIds)) {
			var assetCategoryIds = "${assetCategoryIds}";
		}
		else {
			var assetCategoryIds = "0";
		}

		if (isSet(viewableBy)) {
			var viewableBy = "${viewableBy}";
		}
		else {
			var viewableBy = "Anyone";
		}

		var curl = '''
				${portalURL}/o/headless-delivery/v1.0/sites/${groupId}/blog-postings \
				-H accept: application/json \
				-H Content-Type: application/json \
				-u ${userEmailAddress}:${userPassword} \
				-d {
					"alternativeHeadline": "${entrySubtitle}",
					"articleBody": "${entryContent}",
					"creator": {},
					"headline": "${entryTitle}",
					"image": {
					"caption": "",
					"imageId": ${coverImageId}
					},
					"keywords": [${assetTagNames}],
					"taxonomyCategoryIds": [${assetCategoryIds}],
					"viewableBy": "${viewableBy}"
				}
		''';

		echo("## Adding a 'Blog' with the following info:");

		echo("## * Title: ${entryTitle}");

		echo("## * Content: ${entryContent}");

		var entryId = JSONCurlUtil.post("${curl}", "$..entryId");

		return "${entryId}";
	}

	macro getBlogEntryId {
		if (!(isSet(site))) {
			var site = "true";
		}

		var groupId = JSONGroupAPI._getGroupIdByName(
			groupName = "${groupName}",
			site = "${site}");

		var portalURL = JSONCompany.getPortalURL();

		if (isSet(friendlyURL)) {
			var curl = '''
				${portalURL}/api/jsonws/blogs.blogsentry/get-entry \
			 	-u test@liferay.com:test \
			 	-d groupId=${groupId} \
			  	-d urlTitle=${friendlyURL}
				''';
		}
		else {
			var curl = '''
				${portalURL}/api/jsonws/blogs.blogsentry/get-entry \
			  	-u test@liferay.com:test \
			  	-d groupId=${groupId} \
			  	-d urlTitle=${entryTitle}
			''';
		}

		var entryId = JSONCurlUtil.post("${curl}", "$.[?(@['title'] == '${entryTitle}')]['entryId']");

		return "${entryId}";

		echo("## * Blogs ID: ${entryId}");
	}

	macro getBlogImageId {
		Variables.assertDefined(parameterList = "${groupName},${imageTitle}");

		if (!(isSet(site))) {
			var site = "true";
		}

		var portalURL = JSONCompany.getPortalURL();

		var groupId = JSONGroupAPI._getGroupIdByName(
			groupName = "${groupName}",
			site = "${site}");

		var curl = '''
			${portalURL}/o/headless-delivery/v1.0/sites/${groupId}/blog-posting-images \
			-H 'Content-Type: application/json' \
			-u 'test@liferay.com:test'
		''';
		var imageId = JSONCurlUtil.get("${curl}", "$..[?(@['title'] == '${imageTitle}')]['id']");

		return "${imageId}";
	}

	macro moveEntryToTrash {
		Variables.assertDefined(parameterList = "${entryTitle},${groupName}");

		var entryId = JSONBlog.getBlogEntryId(
			entryTitle = "${entryTitle}",
			friendlyURL = "${friendlyURL}",
			groupName = "${groupName}");

		var portalURL = JSONCompany.getPortalURL();

		if (!(isSet(userEmailAddress))) {
			var userEmailAddress = "test@liferay.com";
		}

		if (!(isSet(userPassword))) {
			var userPassword = "test";
		}

		var curl = '''
			${portalURL}/api/jsonws/blogs.blogsentry/move-entry-to-trash \
			-u ${userEmailAddress}:${userPassword} \
			-d entryId=${entryId} \
		''';
		var messageId = JSONCurlUtil.post("${curl}");
	}

	macro removeBlogImage {
		Variables.assertDefined(parameterList = "${groupName},${imageTitle}");

		var portalURL = JSONCompany.getPortalURL();

		var imageId = JSONBlog.getBlogImageId(
			groupName = "${groupName}",
			imageTitle = "${imageTitle}",
			site = "${site}");

		var curl = '''
			${portalURL}/o/headless-delivery/v1.0/blog-posting-images/${imageId} \
			-H 'Content-Type: application/json' \
			-u 'test@liferay.com:test'
		''';

		JSONCurlUtil.delete("${curl}");
	}

	@summary = "This calls the JSON WS API to update a blog entry"
	macro updateEntry {
		Variables.assertDefined(parameterList = "${entryTitle},${groupName}");

		var portalURL = JSONCompany.getPortalURL();

		if (!(isSet(site))) {
			var site = "true";
		}

		if (isSet(newEntryContent)) {
			var entryContent = "${newEntryContent}";
		}

		if (!(isSet(entrySubtitle))) {
			var entrySubtitle = "";
		}

		if (!(isSet(newFriendlyURL))) {
			var newFriendlyURL = "";
		}

		var contentXml = JSONBlogSetter.setContentXML(content = "${entryContent}");

		var entryId = JSONBlog.getBlogEntryId(
			entryTitle = "${entryTitle}",
			friendlyURL = "${friendlyURL}",
			groupName = "${groupName}",
			site = "${site}");

		var serviceContext = JSONBlogSetter.setServiceContext(
			assetCategoryIds = "${assetCategoryIds}",
			assetTagNames = "${assetTagNames}",
			groupName = "${groupName}",
			serviceContext = "${serviceContext}",
			site = "${site}",
			workflowAction = "${workflowAction}");

		if (isSet(newDisplayDate)) {
			var displayDateMinute = DateUtil.getFormattedCurrentDate("m");
			var displayDateHour = DateUtil.getFormattedCurrentDate("H");
			var displayDateDay = DateUtil.getCurrentDate();
			var displayDateYear = DateUtil.getCurrentYear();
			var displayDateMonth = DateUtil.getCurrentMonth();
			var displayDateMonth = ${displayDateMonth} - 1;

			if (isSet(increaseMinutes)) {
				var displayDateMinute = ${displayDateMinute} + ${increaseMinutes};
			}
		}
		else {
			var displayDateMinute = "01";
			var displayDateHour = "01";
			var displayDateDay = "01";
			var displayDateMonth = "01";
			var displayDateYear = "2004";
		}

		if (!(isSet(userEmailAddress))) {
			var userEmailAddress = "test@liferay.com";
		}

		if (!(isSet(userPassword))) {
			var userPassword = "test";
		}

		var curl = '''
			${portalURL}/api/jsonws/blogs.blogsentry/update-entry \
			-u ${userEmailAddress}:${userPassword} \
			-d entryId=${entryId} \
			-d title=${entryTitle} \
			-d subtitle=${entrySubtitle} \
			-d urlTitle=${newFriendlyURL} \
			-d description= \
			-d content=${contentXml} \
			-d displayDateMonth=${displayDateMonth} \
			-d displayDateDay=${displayDateDay} \
			-d displayDateYear=${displayDateYear} \
			-d displayDateHour=${displayDateHour} \
			-d displayDateMinute=${displayDateMinute} \
			-d allowPingbacks=true \
			-d allowTrackbacks=true \
			-d trackbacks= \
			-d coverImageCaption='' \
			-d -coverImageImageSelector= \
			-d -smallImageImageSelector= \
			-d serviceContext=${serviceContext}
		''';

		echo("## Updating a 'Blog' with the following info using JSONWS:");

		JSONCurlUtil.post("${curl}");
	}

	macro updateEntryCoverImage {
		Variables.assertDefined(parameterList = "${entryTitle},${entryContent},${groupName}");

		var portalURL = JSONCompany.getPortalURL();

		if (!(isSet(site))) {
			var site = "true";
		}

		var entryId = JSONBlog.getBlogEntryId(
			entryTitle = "${entryTitle}",
			friendlyURL = "${friendlyURL}",
			groupName = "${groupName}",
			site = "${site}");

		var groupId = JSONGroupAPI._getGroupIdByName(
			grandParentGroupName = "${grandParentGroupName}",
			groupName = "${groupName}",
			parentGroupName = "${parentGroupName}",
			site = "${site}");

		if (!(isSet(userEmailAddress))) {
			var userEmailAddress = "test@liferay.com";
		}

		if (!(isSet(userPassword))) {
			var userPassword = "test";
		}

		var curl = '''
				${portalURL}/o/headless-delivery/v1.0/blog-postings/${entryId} \
				-H accept: application/json \
				-H Content-Type: application/json \
				-u ${userEmailAddress}:${userPassword} \
				-d {
					"articleBody": "${entryContent}",
					"creator": {},
					"headline": "${entryTitle}",
					"image": {
					"caption": "",
					"imageId": ${coverImageId}
					},
				}
		''';

		JSONCurlUtil.put("${curl}");
	}

	macro uploadBlogImage {
		Variables.assertDefined(parameterList = "${groupName},${sourceFileName}");

		var portalURL = JSONCompany.getPortalURL();

		if (!(isSet(site))) {
			var site = "true";
		}

		var groupId = JSONGroupAPI._getGroupIdByName(
			grandParentGroupName = "${grandParentGroupName}",
			groupName = "${groupName}",
			parentGroupName = "${parentGroupName}",
			site = "${site}");

		var baseDir = PropsUtil.get("test.base.dir.name");
		var dependenciesDir = PropsUtil.get("test.dependencies.dir.name");
		var filePath = "${baseDir}${dependenciesDir}/${sourceFileName}";

		var curl = '''
			${portalURL}/o/headless-delivery/v1.0/sites/${groupId}/blog-posting-images \
			-H accept: application/json \
			-H Content-Type: multipart/form-data \
			-u test@liferay.com:test \
			-F file=@${filePath}
		''';
		var imageId = JSONCurlUtil.post("${curl}", "$..id");

		return "${imageId}";
	}

}