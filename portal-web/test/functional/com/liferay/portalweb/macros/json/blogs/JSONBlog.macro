definition {

	@summary = "This calls the JSON WS API to add a Blog entry comment"
	macro addComment {
		var entryId = JSONBlog.getBlogEntryId(
			friendlyURL = "${friendlyURL}",
			entryTitle = "${entryTitle}",
			groupName = "${groupName}");

		var portalURL = JSONCompany.getPortalURL();

		if (!(isSet(parentCommentId))) {
			var parentCommentId = "0";
		}

		var curl = '''
			${portalURL}/o/headless-delivery/v1.0/blog-postings/${entryId}/comments \
			-H 'Content-Type: application/json' \
			-u 'test@liferay.com:test'
			-d '{
				"creator": {},
				"parentCommentId": "${parentCommentId}",
				"text": "${commentBody}"
			}'
		''';

		echo("## Adding a 'Comment' with the following info using JSONWS:");

		echo("## * commentBody: ${commentBody}");

		echo("## * entryId: ${entryId}");

		var messageId = JSONCurlUtil.post("${curl}");
	}

	@summary = "This calls the JSON WS API to add a Blog entry"
	macro addEntry {
		Variables.assertDefined(parameterList = "${entryContent},${entryTitle}");

		var portalURL = JSONCompany.getPortalURL();

		var contentXML = JSONBlogSetter.setContentXML(content = "${entryContent}");

		if (!(isSet(entrySubtitle))) {
			var entrySubtitle = "";
		}

		if (isSet(groupName)) {
			var serviceContext = JSONBlogSetter.setServiceContext(
				assetCategoryIds = "${assetCategoryIds}",
				assetTagNames = "${assetTagNames}",
				groupName = "${groupName}",
				serviceContext = "${serviceContext}",
				site = "${site}",
				workflowAction = "${workflowAction}");
		}
		else {
			var serviceContext = JSONBlogSetter.setServiceContext(
				assetCategoryIds = "${assetCategoryIds}",
				assetTagNames = "${assetTagNames}",
				groupName = "Guest",
				serviceContext = "${serviceContext}",
				workflowAction = "${workflowAction}");
		}

		if (!(isSet(userEmailAddress))) {
			var userEmailAddress = "test@liferay.com";
		}

		if (!(isSet(userPassword))) {
			var userPassword = "test";
		}

		if (isSet(displayDate)) {
			var displayDateMinute = DateUtil.getFormattedCurrentDate("m");
			var displayDateHour = DateUtil.getCurrentHour();
			var displayDateDay = DateUtil.getCurrentDate();
			var displayDateYear = DateUtil.getCurrentYear();
			var displayDateMonth = DateUtil.getCurrentMonth();

			var displayDateMonth = ${displayDateMonth} - 1;

			if (isSet(increaseMinutes)) {
				var displayDateMinute = ${displayDateMinute} + ${increaseMinutes};
			}
		}
		else {
			var displayDateMinute = "01";
			var displayDateHour = "01";
			var displayDateDay = "01";
			var displayDateMonth = "01";
			var displayDateYear = "2004";
		}

		var curl = '''
			${portalURL}/api/jsonws/blogs.blogsentry/add-entry \
			-u ${userEmailAddress}:${userPassword} \
			-d title=${entryTitle} \
			-d subtitle=${entrySubtitle} \
			-d description= \
			-d content=${contentXML} \
			-d displayDateMonth=${displayDateMonth} \
			-d displayDateDay=${displayDateDay} \
			-d displayDateYear=${displayDateYear} \
			-d displayDateHour=${displayDateHour} \
			-d displayDateMinute=${displayDateMinute} \
			-d allowPingbacks=true \
			-d allowTrackbacks=true \
			-d trackbacks= \
			-d coverImageCaption='' \
			-d -coverImageImageSelector= \
			-d -smallImageImageSelector= \
			-d serviceContext=${serviceContext}
		''';

		echo("## Adding a 'Blog' with the following info using JSONWS:");

		echo("## * Title: ${entryTitle}");

		echo("## * Content: ${entryContent}");

		var messageId = JSONCurlUtil.post("${curl}");
	}

	macro getBlogEntryId {
		if (!(isSet(site))) {
			var site = "true";
		}

		var groupId = JSONGroupAPI._getGroupIdByName(groupName = "${groupName}", site = "${site}");

		var portalURL = JSONCompany.getPortalURL();

		if (isSet(friendlyURL)) {
			var curl = '''
				${portalURL}/api/jsonws/blogs.blogsentry/get-entry \
			 	-u test@liferay.com:test \
			 	-d groupId=${groupId} \
			  	-d urlTitle=${friendlyURL}
				''';
		}
		else {
			var curl = '''
				${portalURL}/api/jsonws/blogs.blogsentry/get-entry \
			  	-u test@liferay.com:test \
			  	-d groupId=${groupId} \
			  	-d urlTitle=${entryTitle}
			''';
		}

		var entryId = JSONCurlUtil.post("${curl}", "$.[?(@['title'] == '${entryTitle}')]['entryId']");

		return "${entryId}";

		echo("## * Blogs ID: ${entryId}");
	}

}