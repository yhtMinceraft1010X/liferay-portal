definition {

	macro addCP {
		LexiconEntry.gotoAdd();

		PortletEntry.inputName(name = "${informationTemplateName}");

		if (isSet(itemType)) {
			Select(
				key_fieldLabel = "Item Type",
				locator1 = "Select#GENERIC_SELECT_FIELD",
				value1 = "${itemType}");
		}

		if (isSet(itemSubtype)) {
			Select(
				key_fieldLabel = "Item Subtype",
				locator1 = "Select#GENERIC_SELECT_FIELD",
				value1 = "${itemSubtype}");
		}

		PortletEntry.save();
	}

	macro addDescription {
		WaitForVisible(locator1 = "Sidebar#PANEL_OPEN");

		Navigator.gotoSidebarTab(tabName = "Properties");

		PortletEntry.inputDescription(description = "${informationTemplateDescription}");
	}

	macro addField {
		WaitForVisible(locator1 = "Sidebar#PANEL_OPEN");

		Navigator.gotoSidebarTab(tabName = "Elements");

		Click(
			key_panelTitle = "${panelTitle}",
			key_variableName = "${fieldName}",
			locator1 = "DDMEditTemplate#WEB_CONTENT_SCRIPT_VARIABLE");
	}

	macro copyCP {
		InformationTemplates.gotoInformationTemplateEllipsisItem(
			informationTemplateName = "${informationTemplateName}",
			item = "Copy");

		if (isSet(informationTemplateCopyName)) {
			PortletEntry.inputName(name = "${informationTemplateCopyName}");
		}

		if (isSet(description)) {
			PortletEntry.inputDescription(description = "${description}");
		}

		Click(locator1 = "Button#COPY");

		Alert.viewSuccessMessage();
	}

	macro deleteCP {
		InformationTemplates.gotoInformationTemplateEllipsisItem(
			informationTemplateName = "${informationTemplateName}",
			item = "Delete");

		AssertConfirm(value1 = "Are you sure you want to delete this? It will be deleted immediately.");

		Alert.viewSuccessMessage();
	}

	macro editName {
		PortletEntry.inputName(name = "${informationTemplateName}");
	}

	macro gotoInformationTemplateEllipsisItem {
		Click(
			key_tableEntry = "${informationTemplateName}",
			locator1 = "LexiconTable#TABLE_ENTRY_ELLIPSIS");

		MenuItem.clickNoError(menuItem = "${item}");
	}

	macro importScript {
		WaitForVisible(locator1 = "Sidebar#PANEL_OPEN");

		Click(locator1 = "Icon#HEADER_VERTICAL_ELLIPSIS");

		var testBaseDirName = PropsUtil.get("test.base.dir.name");
		var testDependenciesDirName = PropsUtil.get("test.dependencies.dir.name");
		var filePath = "${testBaseDirName}tests/enduser/wem/informationtemplate/${testDependenciesDirName}/${scriptFile}";
		var filePath = FileUtil.fixFilePath("${filePath}");

		UploadFile(
			inputFieldId = "importScript",
			locator1 = "TextInput#INPUT_ID",
			value1 = "${filePath}");
	}

	macro inputScript {
		Type.typeCodeMirrorEditorNoError(
			locator1 = "DDMEditTemplate#CODE_MIRROR_EDITOR",
			value1 = "${scriptContent}");
	}

	macro openInformationTemplatesAdmin {
		Navigator.openWithAppendToBaseURL(urlAppend = "group/${siteURLKey}/~/control_panel/manage?p_p_id=com_liferay_template_web_internal_portlet_TemplatePortlet&p_p_lifecycle=0&p_p_state=maximized");
	}

	macro viewDescription {
		WaitForVisible(locator1 = "Sidebar#PANEL_OPEN");

		Navigator.gotoSidebarTab(tabName = "Properties");

		AssertTextEquals(
			locator1 = "TextArea#DESCRIPTION",
			value1 = "${informationTemplateDescription}");
	}

	macro viewScriptContent {
		AssertTextEquals.assertPartialText(
			locator1 = "DDMEditTemplate#CODE_MIRROR_EDITOR_CONTENT",
			value1 = "${scriptContent}");
	}

	macro viewTemplate {
		AssertTextEquals(
			key_columnName = "name",
			key_tableEntry = "${informationTemplateName}",
			locator1 = "LexiconTable#TABLE_ENTRY_INFO",
			value1 = "${informationTemplateName}");

		if (isSet(itemType)) {
			AssertTextEquals(
				key_columnName = "item-type",
				key_tableEntry = "${informationTemplateName}",
				locator1 = "LexiconTable#TABLE_ENTRY_INFO",
				value1 = "${itemType}");
		}

		if (isSet(itemSubtype)) {
			AssertTextEquals(
				key_columnName = "item-subtype",
				key_tableEntry = "${informationTemplateName}",
				locator1 = "LexiconTable#TABLE_ENTRY_INFO",
				value1 = "${itemSubtype}");
		}
	}

	macro viewTemplateNotPresent {
		AssertElementNotPresent(
			key_tableEntry = "${informationTemplateName}",
			locator1 = "LexiconTable#TABLE_ENTRY");
	}

}