definition {

	var dmDocumentTypeName = "File Upload";

	macro gotoAddCategoriesPG {
		for (var key_dmDocumentTitle : list "${dmDocumentTitleList}") {
			Check(locator1 = "DocumentsAndMedia#DESCRIPTIVE_LIST_DOCUMENT_CHECKBOX");
		}

		AssertElementPresent.assertVisible(locator1 = "Icon#EDIT_CATEGORIES");

		Click(locator1 = "Icon#EDIT_CATEGORIES");
	}

	macro gotoAddDocumentTypeCP {
		LexiconEntry.gotoAdd();

		var key_dmDocumentTypeName = "${dmDocumentTypeName}";

		if ("${dmDocumentTypeName}" == "External Video Shortcut") {
			AssertVisible(
				key_text = "video",
				locator1 = "Icon#ANY");
		}

		if ("${dmDocumentTypeName}" == "Google Drive Shortcut") {
			AssertVisible(
				key_text = "google-drive",
				locator1 = "Icon#ANY");
		}

		MenuItem.click(menuItem = "${dmDocumentTypeName}");
	}

	macro gotoAddDocumentTypePG {
		if (isSet(contentPage)) {
			LexiconEntry.gotoAdd();
		}
		else {
			Portlet.clickPortletPlusIconPG(portletName = "Documents and Media");
		}

		var key_dmDocumentTypeName = "${dmDocumentTypeName}";

		MenuItem.click(menuItem = "${dmDocumentTypeName}");
	}

	macro gotoAddRepositoryCP {
		AssertVisible(locator1 = "Button#PLUS");

		LexiconEntry.gotoAdd();

		MenuItem.click(menuItem = "Repository");
	}

	macro gotoAddRepositoryPG {
		Portlet.clickPortletPlusIconPG(portletName = "Documents and Media");

		MenuItem.click(menuItem = "Repository");
	}

	macro gotoCheckedOutDocument {
		var key_dmDocumentTitle = "${dmDocumentTitle}";

		AssertClick(
			locator1 = "DocumentsAndMedia#DESCRIPTIVE_LIST_DOCUMENT_TITLE",
			value1 = "${dmCheckedOutDocumentTitle}");
	}

	macro gotoDocumentCP {
		LexiconEntry.gotoEntry(rowEntry = "${dmDocumentTitle}");
	}

	macro gotoDocumentDetails {
		DMDocument.expandInfo();

		AssertClick(
			locator1 = "NavBar#DETAILS",
			value1 = "Details");
	}

	macro gotoDocumentPermissionsCP {
		LexiconEntry.gotoEntryMenuItem(
			menuItem = "Permissions",
			rowEntry = "${dmDocumentTitle}");

		SelectFrame(locator1 = "IFrame#MODAL_BODY");
	}

	macro gotoDocumentPG {
		var key_dmDocumentTitle = "${dmDocumentTitle}";

		AssertClick(
			locator1 = "DocumentsAndMedia#DESCRIPTIVE_LIST_DOCUMENT_TITLE",
			value1 = "${dmDocumentTitle}");
	}

	macro gotoDocumentsAndMedia {
		Navigator.gotoNavItem(navItem = "Documents and Media");
	}

	macro gotoDocumentType {
		Navigator.gotoNavItem(navItem = "Document Types");
	}

	macro gotoDocumentVersion {
		DMDocument.expandInfo();

		var key_tab = "Versions";

		AssertClick(
			locator1 = "NavTab#TAB_LINK",
			value1 = "Versions");
	}

	macro gotoEditDocumentPG {
		var key_dmDocumentTitle = "${dmDocumentTitle}";

		AssertClick(
			locator1 = "DocumentsAndMedia#DESCRIPTIVE_LIST_DOCUMENT_TITLE",
			value1 = "${dmDocumentTitle}");

		Click(locator1 = "Button#EDIT");
	}

	macro gotoEditDocumentType {
		DMDocumentType.selectDocumentTypeFrame();

		var key_dmDocumentTypeName = "${dmDocumentTypeName}";

		LexiconEntry.openEntryMenu(rowEntry = "${dmDocumentTypeName}");

		MenuItem.click(menuItem = "Edit");
	}

	macro gotoEditImage {
		LexiconEntry.openEntryMenu(rowEntry = "${dmDocumentTitle}");

		MenuItem.click(menuItem = "Edit Image");
	}

	macro gotoFolder {
		var key_dmFolderName = "${dmFolderName}";

		AssertClick(
			locator1 = "DocumentsAndMedia#DESCRIPTIVE_LIST_FOLDER_TITLE",
			value1 = "${dmFolderName}");

		AssertVisible(locator1 = "DocumentsAndMedia#BREADCRUMB_FOLDER_NAME");
	}

	macro gotoFolderPermissions {
		var key_dmFolderName = "${dmFolderName}";

		Click(locator1 = "DocumentsAndMedia#DESCRIPTIVE_LIST_VERTICAL_ELLIPSIS");

		MenuItem.click(menuItem = "Permissions");

		SelectFrame(locator1 = "IFrame#MODAL_BODY");
	}

	macro gotoFolderViaBreadcrumb {
		var key_dmFolderName = "${dmFolderName}";

		AssertClick.assertTextClickAt(locator1 = "DocumentsAndMedia#BREADCRUMB_FOLDER_NAME");
	}

	macro gotoHomeCP {
		AssertClick.assertTextClickAt(
			locator1 = "DocumentsAndMedia#BREADCRUMB_HOME",
			value1 = "Home");
	}

	macro gotoMetadataSets {
		Navigator.gotoNavItem(navItem = "Metadata Sets");
	}

	macro gotoRelatedAssetPG {
		var key_dmDocumentTitle = "${dmDocumentTitle}";

		AssertClick(
			locator1 = "DocumentsAndMedia#DESCRIPTIVE_LIST_DOCUMENT_TITLE",
			value1 = "${dmDocumentTitle}");

		var key_assetTitle = "${key_assetTitle}";

		AssertTextEquals(
			locator1 = "DocumentsAndMediaDocument#DOCUMENT_INFO_TITLE",
			value1 = "${dmDocumentTitle}");

		DMDocument.expandInfo();

		AssertTextEquals(
			locator1 = "DocumentsAndMediaDocument#DOCUMENT_INFO_RELATED_ASSETS_HEADER",
			value1 = "Related Assets");

		AssertTextEquals(
			locator1 = "DocumentsAndMediaDocument#DOCUMENT_INFO_RELATED_ASSETS_TITLE",
			value1 = "${assetTitle}");

		AssertClick(
			locator1 = "DocumentsAndMediaDocument#DOCUMENT_INFO_RELATED_ASSETS_TITLE",
			value1 = "${assetTitle}");
	}

	macro gotoRepository {
		var key_dmFolderName = "${dmRepositoryName}";

		AssertClick(
			locator1 = "DocumentsAndMedia#DESCRIPTIVE_LIST_FOLDER_TITLE",
			value1 = "${dmRepositoryName}");

		if (isSet(guest)) {
			AssertElementPresent(locator1 = "MicrosoftLogin#LOGIN_EMAIL_FIELD");
		}
		else {
			AssertVisible(locator1 = "DocumentsAndMedia#BREADCRUMB_FOLDER_NAME");
		}
	}

	macro gotoShare {
		var key_dmDocumentTitle = "${dmDocumentTitle}";

		AssertElementPresent(locator1 = "DocumentsAndMedia#DESCRIPTIVE_LIST_DOCUMENT_TITLE");

		AssertClick(
			locator1 = "DocumentsAndMedia#DESCRIPTIVE_LIST_DOCUMENT_TITLE",
			value1 = "${dmDocumentTitle}");

		DMDocument.viewDocumentTitleCmdPG(dmDocumentTitle = "${dmDocumentTitle}");

		WaitForLiferayEvent.initializeLiferayEventLog();

		var key_text = "Share";

		AssertClick(
			locator1 = "Button#SHARE_SIDENAV",
			value1 = "Share");
	}

	macro gotoShareInfoPanel {
		var key_dmDocumentTitle = "${dmDocumentTitle}";

		AssertClick(
			locator1 = "DocumentsAndMedia#DESCRIPTIVE_LIST_DOCUMENT_TITLE",
			value1 = "${dmDocumentTitle}");

		DMDocument.viewDocumentTitleCmdPG(dmDocumentTitle = "${dmDocumentTitle}");

		Click.clickAt(locator1 = "Icon#INFO");

		AssertElementPresent(locator1 = "Button#SHARE_SIDEBAR");

		Click(locator1 = "Button#SHARE_SIDEBAR");
	}

	macro gotoShareViaSharedContent {
		var key_contentTitle = "${dmDocumentTitle}";

		Click(
			locator1 = "Sharing#SHARED_CONTENT_CONTENT_OPTIONS",
			value1 = "${contentTitle}");

		MenuItem.click(menuItem = "Share");
	}

	macro gotoURLDetail {
		DMDocument.expandInfo();

		Click(locator1 = "DocumentsAndMediaDocument#DETAILS_URL");
	}

	macro openDocumentsAndMediaAdmin {
		Navigator.openWithAppendToBaseURL(
			baseURL = "${baseURL}",
			urlAppend = "group/${siteURLKey}/~/control_panel/manage?p_p_id=com_liferay_document_library_web_portlet_DLAdminPortlet");
	}

	macro openDocumentTypesAdmin {
		Navigator.openWithAppendToBaseURL(
			baseURL = "${baseURL}",
			urlAppend = "group/${siteURLKey}/~/control_panel/manage?p_p_id=com_liferay_document_library_web_portlet_DLAdminPortlet&_com_liferay_document_library_web_portlet_DLAdminPortlet_navigation=file_entry_types");
	}

	macro openMetadataSetsAdmin {
		Navigator.openWithAppendToBaseURL(
			baseURL = "${baseURL}",
			urlAppend = "group/${siteURLKey}/~/control_panel/manage?p_p_id=com_liferay_document_library_web_portlet_DLAdminPortlet&_com_liferay_document_library_web_portlet_DLAdminPortlet_navigation=file_entry_metadata_sets");
	}

	// You can use this navigation when you need to access the configuration quickly without checking the UI.

	macro openToAddDocumentType {
		Navigator.openWithAppendToBaseURL(
			baseURL = "${baseURL}",
			urlAppend = "group/${siteURLKey}/~/control_panel/manage?p_p_id=com_liferay_document_library_web_portlet_DLAdminPortlet&_com_liferay_document_library_web_portlet_DLAdminPortlet_mvcRenderCommandName=%2Fdocument_library%2Fedit_file_entry_type&_com_liferay_document_library_web_portlet_DLAdminPortlet_redirect=%2Fgroup%2Fguest%2F%7E%2Fcontrol_panel%2Fmanage%3Fp_p_id%3Dcom_liferay_document_library_web_portlet_DLAdminPortlet%26p_p_lifecycle%3D0%26p_p_state%3Dmaximized%26p_p_mode%3Dview%26_com_liferay_document_library_web_portlet_DLAdminPortlet_navigation%3Dfile_entry_types");
	}

	macro openToAddEntry {
		var groupId = JSONGroupAPI._getGroupIdByName(
			groupName = "${groupName}",
			site = "true");

		if (isSet(documentTypeName)) {
			if (("${documentTypeName}" == "External Video Shortcut") || ("${documentTypeName}" == "Google Drive Shortcut")) {
				var typeId = JSONDocumentAPI._getTypeIdByTypeName(
					documentTypeName = "${documentTypeName}",
					groupName = "Global");
			}
			else {
				var typeId = JSONDocumentAPI._getTypeIdByTypeName(
					documentTypeName = "${documentTypeName}",
					groupName = "${groupName}");
			}
		}
		else {
			var typeId = "0";
		}

		if (isSet(folderName)) {
			var folderId = JSONDocumentAPI._getFolderIdByFolderName(
				groupName = "${groupName}",
				name = "${folderName}");
		}
		else {
			var folderId = "0";
		}

		// Entries accessed in site pages will be redirected back to the page after publishing. Those accessed in admin will stay in admin after publishing.

		if (isSet(mediaGallery)) {
			Navigator.openWithAppendToBaseURL(urlAppend = "group/${siteURLKey}/~/control_panel/manage?p_p_id=com_liferay_document_library_web_portlet_DLAdminPortlet&_com_liferay_document_library_web_portlet_DLAdminPortlet_mvcRenderCommandName=%2Fdocument_library%2Fedit_file_entry&_com_liferay_document_library_web_portlet_DLAdminPortlet_cmd=add&_com_liferay_document_library_web_portlet_DLAdminPortlet_redirect=%2Fweb%2F${siteURLKey}%2F${layoutName}&_com_liferay_document_library_web_portlet_DLAdminPortlet_portletResource=com_liferay_document_library_web_portlet_IGDisplayPortlet_INSTANCE_Nxd1lYxcrZQ7&_com_liferay_document_library_web_portlet_DLAdminPortlet_fileEntryTypeId=${typeId}&_com_liferay_document_library_web_portlet_DLAdminPortlet_folderId=${folderId}&_com_liferay_document_library_web_portlet_DLAdminPortlet_repositoryId=${groupId}");
		}
		else {
			Navigator.openWithAppendToBaseURL(urlAppend = "group/${siteURLKey}/~/control_panel/manage?p_p_id=com_liferay_document_library_web_portlet_DLAdminPortlet&_com_liferay_document_library_web_portlet_DLAdminPortlet_mvcRenderCommandName=%2Fdocument_library%2Fedit_file_entry&_com_liferay_document_library_web_portlet_DLAdminPortlet_cmd=add&_com_liferay_document_library_web_portlet_DLAdminPortlet_redirect=%2Fgroup%2F${siteURLKey}%2F%7E%2Fcontrol_panel%2Fmanage%3Fp_p_id%3Dcom_liferay_document_library_web_portlet_DLAdminPortlet%26p_p_lifecycle%3D0%26p_p_state%3Dmaximized%26p_v_l_s_g_id%3D${groupId}&_com_liferay_document_library_web_portlet_DLAdminPortlet_fileEntryTypeId=${typeId}&_com_liferay_document_library_web_portlet_DLAdminPortlet_folderId=${folderId}&_com_liferay_document_library_web_portlet_DLAdminPortlet_portletResource=com_liferay_document_library_web_portlet_DLAdminPortlet&_com_liferay_document_library_web_portlet_DLAdminPortlet_repositoryId=${groupId}");
		}
	}

	macro openToConfigInSystemSettings {
		var baseURL = "${baseURL}";

		if (!(isSet(baseURL))) {
			var baseURL = PropsUtil.get("portal.url");
		}

		if ("${portlet}" == "AWS Translator") {
			var portletId = "com.liferay.translation.translator.aws.internal.configuration.AWSTranslatorConfiguration";
		}

		if ("${portlet}" == "Azure Translator") {
			var portletId = "com.liferay.translation.translator.azure.internal.configuration.AzureTranslatorConfiguration";
		}

		if ("${portlet}" == "Documents and Media Size Limits") {
			var portletId = "com.liferay.document.library.internal.configuration.DLSizeLimitConfiguration";
		}

		if ("${portlet}" == "File Entries") {
			var portletId = "com.liferay.document.library.configuration.DLFileEntryConfiguration";
		}

		if ("${portlet}" == "FFMPEG Audio Converter") {
			var portletId = "com.liferay.document.library.preview.audio.internal.configuration.DLAudioFFMPEGAudioConverterConfiguration";
		}

		if ("${portlet}" == "FFMPEG Video Converter") {
			var portletId = "com.liferay.document.library.video.internal.configuration.DLVideoFFMPEGVideoConverterConfiguration";
		}

		if ("${portlet}" == "Google Cloud Translator") {
			var portletId = "com.liferay.translation.translator.google.cloud.internal.configuration.GoogleCloudTranslatorConfiguration";
		}

		if ("${portlet}" == "Google Drive") {
			var portletId = "com.liferay.document.library.google.drive.configuration.DLGoogleDriveCompanyConfiguration";
		}

		if ("${portlet}" == "One Drive") {
			var portletId = "com.liferay.document.library.opener.onedrive.web.internal.configuration.DLOneDriveCompanyConfiguration";
		}

		if ("${portlet}" == "Service") {
			var portletId = "com.liferay.document.library.configuration.DLConfiguration";
		}

		if ("${portlet}" == "Cache Control") {
			var portletId = "com.liferay.document.library.web.internal.configuration.CacheControlConfiguration";
		}

		Open(locator1 = "${baseURL}/group/control_panel/manage?p_p_id=com_liferay_configuration_admin_web_portlet_SystemSettingsPortlet&_com_liferay_configuration_admin_web_portlet_SystemSettingsPortlet_factoryPid=${portletId}&_com_liferay_configuration_admin_web_portlet_SystemSettingsPortlet_mvcRenderCommandName=%2Fconfiguration_admin%2Fedit_configuration");
	}

	macro openToEditEntryInSite {
		if (isSet(folderName)) {
			Variables.assertDefined(parameterList = "${folderName},${groupName}");

			var folderId = JSONDocumentAPI._getFolderIdByFolderName(
				groupName = "${groupName}",
				name = "${folderName}");
		}

		var fileEntryId = JSONDocument.getFileEntryId(
			dmDocumentTitle = "${dmDocumentTitle}",
			folderId = "${folderId}",
			groupName = "${groupName}",
			site = "${site}");

		Navigator.openWithAppendToBaseURL(urlAppend = "group/${siteURLKey}/~/control_panel/manage?p_p_id=com_liferay_document_library_web_portlet_DLAdminPortlet&_com_liferay_document_library_web_portlet_DLAdminPortlet_mvcRenderCommandName=%2Fdocument_library%2Fedit_file_entry&_com_liferay_document_library_web_portlet_DLAdminPortlet_fileEntryId=${fileEntryId}");
	}

	macro openToEntryInAdmin {
		var fileEntryId = JSONDocument.getFileEntryId(
			dmDocumentTitle = "${dmDocumentTitle}",
			folderId = "${folderId}",
			groupName = "${groupName}",
			portalURL = "${baseURL}",
			site = "${site}");

		Navigator.openWithAppendToBaseURL(
			baseURL = "${baseURL}",
			urlAppend = "group/${siteURLKey}/~/control_panel/manage?p_p_id=com_liferay_document_library_web_portlet_DLAdminPortlet&_com_liferay_document_library_web_portlet_DLAdminPortlet_mvcRenderCommandName=%2Fdocument_library%2Fview_file_entry&_com_liferay_document_library_web_portlet_DLAdminPortlet_fileEntryId=${fileEntryId}");
	}

	macro openToFolderInAdmin {
		var folderId = JSONDocumentAPI._getFolderIdByFolderName(
			groupName = "${groupName}",
			name = "${dmFolderName}");

		Navigator.openWithAppendToBaseURL(urlAppend = "group/${siteURLKey}/~/control_panel/manage?p_p_id=com_liferay_document_library_web_portlet_DLAdminPortlet&_com_liferay_document_library_web_portlet_DLAdminPortlet_mvcRenderCommandName=%2Fdocument_library%2Fview_folder&_com_liferay_document_library_web_portlet_DLAdminPortlet_folderId=${folderId}");
	}

}