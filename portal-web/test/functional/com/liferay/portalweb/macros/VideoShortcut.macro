definition {

	macro addCP {
		if (!(isSet(skipNavigation))) {
			if (isSet(localizedDocumentTypeName)) {
				DMNavigator.gotoAddDocumentTypeCP(dmDocumentTypeName = "${localizedDocumentTypeName}");
			}
			else {
				DMNavigator.gotoAddDocumentTypeCP(dmDocumentTypeName = "External Video Shortcut");
			}
		}

		if (isSet(localizedVideoURL)) {
			Type(
				key_text = "${localizedVideoURL}",
				locator1 = "TextInput#ANY",
				value1 = "${videoURL}");
		}
		else {
			Type(
				key_text = "Video URL",
				locator1 = "TextInput#ANY",
				value1 = "${videoURL}");
		}

		Pause(locator1 = "5000");

		if (isSet(videoShortcutPreview)) {
			AssertVisible(
				key_videoShortcutPreview = "${videoShortcutPreview}",
				locator1 = "DocumentsAndMediaDocument#VIDEO_SHORTCUT_PREVIEW");
		}

		if (isSet(extractedVideoShortcutTitle)) {
			var titleText = selenium.getElementValue("TextInput#TITLE");

			if ("${extractedVideoShortcutTitle}" == "${titleText}") {
				echo("This is a correct title.");
			}
			else {
				fail("FAIL!");
			}
		}

		if (isSet(videoShortcutTitle)) {
			PortletEntry.inputTitle(title = "${videoShortcutTitle}");
		}

		if (isSet(videoShortcutDescription)) {
			PortletEntry.inputDescription(description = "${videoShortcutDescription}");
		}

		if (isSet(categoryName)) {
			var key_categoryName = "${categoryName}";

			Panel.expandPanel(panel = "Categorization");

			AssetCategorization.addCategory(
				categoryName = "${categoryName}",
				vocabularyName = "${vocabularyName}");
		}

		if (isSet(tagNameList)) {
			Panel.expandPanel(panel = "Categorization");

			for (var tagName : list "${tagNameList}") {
				AssetCategorization.addTag(tagName = "${tagName}");
			}
		}

		if (isSet(localizedPublish)) {
			Button.click(button = "${localizedPublish}");

			Alert.viewSuccessMessage();
		}
		else {
			PortletEntry.publish();
		}
	}

	macro addPG {
		DMNavigator.gotoAddDocumentTypePG(
			contentPage = "${contentPage}",
			dmDocumentTypeName = "External Video Shortcut");

		Type(
			key_text = "Video URL",
			locator1 = "TextInput#ANY",
			value1 = "${videoURL}");

		Pause(locator1 = "5000");

		if (isSet(videoShortcutPreview)) {
			AssertVisible(
				key_videoShortcutPreview = "${videoShortcutPreview}",
				locator1 = "DocumentsAndMediaDocument#VIDEO_SHORTCUT_PREVIEW");
		}

		if (isSet(extractedVideoShortcutTitle)) {
			var titleText = selenium.getElementValue("TextInput#TITLE");

			if ("${extractedVideoShortcutTitle}" == "${titleText}") {
				echo("This is a correct title.");
			}
			else {
				fail("FAIL!");
			}
		}

		if (isSet(videoShortcutTitle)) {
			PortletEntry.inputTitle(title = "${videoShortcutTitle}");
		}

		if (isSet(videoShortcutDescription)) {
			PortletEntry.inputDescription(description = "${videoShortcutDescription}");
		}

		AssertClick(
			locator1 = "Button#PUBLISH",
			value1 = "Publish");
	}

	macro editCP {
		LexiconEntry.gotoEntryMenuItem(
			menuItem = "Edit",
			rowEntry = "${videoShortcutTitle}");

		if (isSet(videoShortcutPreviousTitle)) {
			AssertTextEquals(
				locator1 = "TextInput#TITLE",
				value1 = "${videoShortcutPreviousTitle}");
		}

		if (isSet(videoShortcutPreviousDescription)) {
			AssertTextEquals(
				locator1 = "TextArea#DESCRIPTION",
				value1 = "${videoShortcutPreviousDescription}");
		}

		Type(
			key_text = "Video URL",
			locator1 = "TextInput#ANY",
			value1 = "${videoURLEdit}");

		Pause(locator1 = "5000");

		if (isSet(videoShortcutPreview)) {
			AssertVisible(
				key_videoShortcutPreview = "${videoShortcutPreview}",
				locator1 = "DocumentsAndMediaDocument#VIDEO_SHORTCUT_PREVIEW");
		}

		if (isSet(videoShortcutTitleEdit)) {
			PortletEntry.inputTitle(title = "${videoShortcutTitleEdit}");
		}

		if (isSet(videoShortcutDescriptionEdit)) {
			PortletEntry.inputDescription(description = "${videoShortcutDescriptionEdit}");
		}

		PortletEntry.publish();
	}

	macro validateURL {
		Type(
			key_text = "Video URL",
			locator1 = "TextInput#ANY",
			value1 = "${invalidURL}");

		Pause(locator1 = "5000");

		if (isSet(youtube)) {
			SelectFrame.selectFrameNoLoading(locator1 = "IFrame#EXTERNAL_VIDEO_IFRAME");

			Click(locator1 = "Button#YOUTUBE_PLAY");

			AssertTextEquals.assertPartialText(
				locator1 = "Message#ERROR_INVALID_YOUTUBE_VIDEO",
				value1 = "An error occurred. Please try again later.");
		}
		else if (isSet(facebook)) {
			SelectFrame.selectFrameNoLoading(locator1 = "IFrame#EXTERNAL_VIDEO_IFRAME");

			AssertTextEquals.assertPartialText(
				locator1 = "Message#ERROR_INVALID_FACEBOOK_VIDEO",
				value1 = "Video Unavailable");
		}
		else if (isSet(vimeo)) {
			SelectFrame.selectFrameNoLoading(locator1 = "IFrame#EXTERNAL_VIDEO_IFRAME");

			AssertTextEquals.assertPartialText(
				locator1 = "Message#ERROR_INVALID_VIMEO_VIDEO",
				value1 = "The embed code for this video is not valid.");
		}
		else {
			AssertTextEquals(
				locator1 = "Message#INFO_EXTERNAL_VIDEO_PREVIEW",
				value1 = "Sorry, this platform is not supported.");
		}
	}

	macro viewCP {
		LexiconEntry.changeDisplayStyle(displayStyle = "list");

		LexiconEntry.gotoEntry(rowEntry = "${videoShortcutTitle}");

		AssertTextEquals(
			key_dmDocumentTitle = "${videoShortcutTitle}",
			locator1 = "DocumentsAndMediaDocument#DOCUMENT_INFO_TITLE",
			value1 = "${videoShortcutTitle}");
	}

	macro viewPG {
		LexiconEntry.changeDisplayStyle(displayStyle = "list");

		AssertClick(
			key_dmDocumentTitle = "${videoShortcutTitle}",
			locator1 = "DocumentsAndMedia#DESCRIPTIVE_LIST_DOCUMENT_TITLE",
			value1 = "${videoShortcutTitle}");

		DMDocument.expandInfo();

		DMDocument.viewDocumentTitleCmdPG(dmDocumentTitle = "${videoShortcutTitle}");

		if (isSet(videoShortcutDescription)) {
			var key_dmDocumentDescription = "${videoShortcutDescription}";

			ScrollWebElementIntoView(locator1 = "DocumentsAndMediaDocument#DOCUMENT_INFO_DESCRIPTION");

			AssertTextEquals(
				locator1 = "DocumentsAndMediaDocument#DOCUMENT_INFO_DESCRIPTION",
				value1 = "${videoShortcutDescription}");
		}
	}

	macro viewPlayedVideo {
		SelectFrame.selectFrameNoLoading(locator1 = "IFrame#EXTERNAL_VIDEO_IFRAME");

		Pause(locator1 = "5000");

		if (IsElementPresent(locator1 = "Button#TWITCH_PLAY")) {
			Click(locator1 = "Button#TWITCH_PLAY");

			AssertElementPresent(locator1 = "DocumentsAndMediaDocument#TWITCH_VIDEO_INACTIVE");
		}
		else {
			AssertElementNotPresent(locator1 = "DocumentsAndMediaDocument#TWITCH_VIDEO_INACTIVE");
		}

		SelectFrameTop(value1 = "relative=top");
	}

}