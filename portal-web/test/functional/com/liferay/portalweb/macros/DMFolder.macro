definition {

	var assetType = "Documents Folder";

	macro addCP {
		var key_dmFolderName = "${dmFolderName}";

		LexiconEntry.gotoAdd();

		AssertClick.assertTextClickAt(
			key_menuItem = "Folder",
			locator1 = "MenuItem#ANY_MENU_ITEM",
			value1 = "Folder");

		Pause(locator1 = "1000");

		PortletEntry.inputName(name = "${dmFolderName}");

		if (isSet(dmFolderDescription)) {
			PortletEntry.inputDescription(description = "${dmFolderDescription}");
		}

		Button.clickSave();

		if ("${dmFolder}" == "Duplicate") {
			Alert.viewRequestFailedToComplete();

			AssertTextEquals.assertPartialText(
				locator1 = "Message#ERROR_1",
				value1 = "Please enter a unique folder name.");
		}
		else {
			Alert.viewSuccessMessage();
		}

		if (isSet(siteName)) {
			Navigator.openSiteURL(siteName = "${siteName}");

			ProductMenu.gotoPortlet(
				category = "Content & Data",
				portlet = "Documents and Media",
				site = "${siteName}");
		}

		DMFolder.viewEntryPresent(dmFolderName = "${dmFolderName}");
	}

	macro addPG {
		if (isSet(contentPage)) {
			LexiconEntry.gotoAdd();
		}
		else {
			Portlet.clickPortletPlusIconPG(portletName = "Documents and Media");
		}

		AssertClick.assertTextClickAt(
			key_menuItem = "Folder",
			locator1 = "MenuItem#ANY_MENU_ITEM",
			value1 = "Folder");

		Pause(locator1 = "1000");

		PortletEntry.inputName(name = "${dmFolderName}");

		PortletEntry.inputDescription(description = "${dmFolderDescription}");

		if (isSet(viewableBy)) {
			Panel.expandPanel(panel = "Permissions");

			Select(
				locator1 = "Select#PERMISSIONS",
				value1 = "${viewableBy}");

			if (isSet(permissionDefinitionKeyList)) {
				PermissionsInline.addOrRemoveEntryPermissionsFromAllRoles(
					addOrRemoveViewPermission = "${addOrRemoveViewPermission}",
					key_roleTitle = "${roleTitle}",
					permissionDefinitionKeyList = "${permissionDefinitionKeyList}");
			}
		}

		Button.clickSave();

		if ("${dmFolder}" == "Duplicate") {
			Alert.viewRequestFailedToComplete();

			AssertTextEquals.assertPartialText(
				locator1 = "Message#ERROR_1",
				value1 = "Please enter a unique folder name.");
		}
		else if ("${dmFolder}" == "Invalid") {
			Alert.viewRequestFailedToComplete();

			AssertTextEquals.assertPartialText(
				locator1 = "Message#ERROR_1",
				value1 = "The folder name cannot contain the following invalid characters: \ // : * ? &quot; < > | ../ /...");
		}
		else {
			Alert.viewSuccessMessage();
		}
	}

	macro addPGViaMG {
		Portlet.clickPortletPlusIconPG(portletName = "Media Gallery");

		AssertClick.assertTextClickAt(
			key_menuItem = "Folder",
			locator1 = "MenuItem#ANY_MENU_ITEM",
			value1 = "Folder");

		Pause(locator1 = "1000");

		PortletEntry.inputName(name = "${mgFolderName}");

		PortletEntry.inputDescription(description = "${mgFolderDescription}");

		PortletEntry.save();

		var key_mgFolderName = "${mgFolderName}";

		DMFolder.viewEntryPresent(dmFolderName = "${mgFolderName}");
	}

	macro cannotViewCP {
		var key_dmFolderName = "${dmFolderName}";

		LexiconCard.viewCardNotPresent(card = "${dmFolderName}");
	}

	macro configureWorkflowCP {
		if ("${dmFolderName}" == "homeFolder") {
			Portlet.gotoPortletOptions(portletOption = "Edit");
		}
		else {
			LexiconEntry.gotoEntryMenuItem(
				menuItem = "Edit",
				rowEntry = "${dmFolderName}");

			if (IsElementNotPresent.isNotVisible(locator1 = "Radio#WORKFLOW_PARENT_FOLDER")) {
				Click(locator1 = "DocumentsAndMediaEditFolder#WORKFLOW_DOCUMENT_TYPE_RESTRICTIONS_TOGGLE");
			}
		}

		if ("${workflowRestriction}" == "Parent Folder") {
			Click(locator1 = "Radio#WORKFLOW_PARENT_FOLDER");

			Button.clickSave();
		}
		else if ("${workflowRestriction}" == "Specific Restrictions") {
			Click(locator1 = "Radio#WORKFLOW_SPECIFIC_RESTRICTIONS");

			AssertClick(
				locator1 = "DocumentsAndMediaEditFolder#WORKFLOW_SPECIFIC_RESTRICTIONS_SELECT_DOCUMENT_TYPE_BUTTON",
				value1 = "Select Document Type");

			SelectFrame(value1 = "relative=top");

			SelectFrame(locator1 = "DocumentsAndMediaSelectDocument#SELECT_DOCUMENT_IFRAME");

			var key_dmDocumentTypeName = "${dmDocumentTypeName}";

			AssertClick(
				locator1 = "DocumentsAndMediaSelectDocument#DOCUMENTS_TABLE_CHOOSE",
				value1 = "Choose");

			SelectFrameTop(value1 = "relative=top");

			Select(
				locator1 = "DocumentsAndMediaEditFolder#WORKFLOW_SPECIFIC_RESTRICTIONS_TYPE_WORKFLOW_SELECT",
				value1 = "${workflowDefinition}");

			AssertClickNoError(
				key_text = "Save",
				locator1 = "Button#ANY",
				value1 = "Save");

			AssertConfirm(value1 = "You are making changes to the document types. If a document in this folder or its subfolders is currently in draft or pending workflow states, you may lose some or all of its metadata. A new version will be created for all other documents. Are you sure you wish to continue?");
		}
		else if ("${workflowRestriction}" == "Default Workflow") {
			if (!(isSet(homeFolder))) {
				Click(locator1 = "Radio#WORKFLOW_DEFAULT_WORKFLOW");
			}

			Select(
				locator1 = "Select#WORKFLOW_DEFAULT",
				value1 = "${workflowDefinition}");

			PortletEntry.save();
		}
	}

	macro deleteCP {
		var key_dmFolderName = "${dmFolderName}";

		Click(locator1 = "DocumentsAndMedia#DESCRIPTIVE_LIST_VERTICAL_ELLIPSIS");

		if (isSet(recycleBinDisabled)) {
			MenuItem.clickNoError(menuItem = "Delete");

			AssertConfirm(value1 = "Are you sure you want to delete this? It will be deleted immediately.");

			Alert.viewSuccessMessage();
		}
		else {
			PortletEntry.clickMoveToRecycleBin();

			VerifyElementPresent(locator1 = "Message#SUCCESS");
		}
	}

	macro deletePG {
		var key_dmFolderName = "${dmFolderName}";

		AssertTextEquals(
			locator1 = "DocumentsAndMedia#DESCRIPTIVE_LIST_FOLDER_TITLE",
			value1 = "${dmFolderName}");

		Check(locator1 = "DocumentsAndMedia#DESCRIPTIVE_LIST_FOLDER_CHECKBOX");

		Click(
			key_text = "trash",
			locator1 = "ManagementBar#ANY_ICON");

		VerifyElementPresent(locator1 = "Message#SUCCESS");

		AssertElementNotPresent(locator1 = "DocumentsAndMedia#DESCRIPTIVE_LIST_FOLDER_ICON");
	}

	macro editCP {
		var key_dmFolderName = "${dmFolderName}";

		DMFolder.viewEntryPresent(dmFolderName = "${dmFolderName}");

		AssertElementPresent(locator1 = "DocumentsAndMedia#DESCRIPTIVE_LIST_VERTICAL_ELLIPSIS");

		Click(locator1 = "DocumentsAndMedia#DESCRIPTIVE_LIST_VERTICAL_ELLIPSIS");

		MenuItem.click(menuItem = "Edit");

		PortletEntry.inputName(name = "${dmFolderNameEdit}");

		PortletEntry.inputDescription(description = "${dmFolderDescriptionEdit}");

		PortletEntry.save();

		DMFolder.viewEntryPresent(dmFolderName = "${dmFolderName}");
	}

	macro editPG {
		var key_dmFolderName = "${dmFolderName}";

		AssertTextEquals(
			locator1 = "DocumentsAndMedia#DESCRIPTIVE_LIST_FOLDER_TITLE",
			value1 = "${dmFolderName}");

		Click(locator1 = "DocumentsAndMedia#DESCRIPTIVE_LIST_VERTICAL_ELLIPSIS");

		MenuItem.click(menuItem = "Edit");

		PortletEntry.inputName(name = "${dmFolderNameEdit}");

		PortletEntry.inputDescription(description = "${dmFolderDescriptionEdit}");

		Button.clickSave();

		var key_dmFolderNameEdit = "${dmFolderNameEdit}";

		Alert.viewSuccessMessage();

		AssertTextEquals.assertPartialText(
			locator1 = "DocumentsAndMedia#DESCRIPTIVE_LIST_FOLDER_EDITED_TITLE",
			value1 = "${dmFolderNameEdit}");
	}

	macro editPGViaMG {
		LexiconEntry.gotoEllipsisMenuItem(menuItem = "Edit");

		PortletEntry.inputName(name = "${mgFolderNameEdit}");

		PortletEntry.inputDescription(description = "${mgFolderDescriptionEdit}");

		PortletEntry.save();

		var key_mgFolderName = "${mgFolderName}";

		DMFolder.viewEntryPresent(dmFolderName = "${mgFolderNameEdit}");
	}

	macro gotoEditCP {
		var key_dmFolderName = "${folderName}";

		DMFolder.viewEntryPresent(dmFolderName = "${folderName}");

		Click(locator1 = "DocumentsAndMedia#DESCRIPTIVE_LIST_VERTICAL_ELLIPSIS");

		MenuItem.click(menuItem = "Edit");
	}

	macro gotoEntry {
		LexiconCard.clickCard(card = "${dmFolderName}");
	}

	macro gotoPGViaMG {
		var key_mgFolderName = "${mgFolderName}";

		DMFolder.gotoEntry(dmFolderName = "${mgFolderName}");

		AssertTextEquals(
			locator1 = "MediaGallery#BREADCRUMB",
			value1 = "${mgFolderName}");
	}

	macro moveToFolderCP {
		LexiconEntry.changeDisplayStyle(displayStyle = "list");

		var key_dmFolderName = "${dmFolderName}";

		AssertElementPresent(locator1 = "DocumentsAndMedia#DESCRIPTIVE_LIST_FOLDER_TITLE");

		Click(
			locator1 = "DocumentsAndMedia#DESCRIPTIVE_LIST_VERTICAL_ELLIPSIS",
			value1 = "${dmFolderName2}");

		MenuItem.click(menuItem = "Move");

		SelectFrame.selectFrameNoLoading(locator1 = "DocumentsAndMediaSelectFolder#SELECT_FOLDER_IFRAME");

		if ("${moveSubfolder}" == "true") {
			AssertClick(
				key_breadcrumbName = "Home",
				locator1 = "BreadcrumbPortlet#BREADCRUMB_FOLDER",
				value1 = "Home");

			AssertClick(
				key_text = "Select This Folder",
				locator1 = "Button#ANY",
				value1 = "Select This Folder");

			SelectFrame(locator1 = "relative=top");

			if ("${moveFailure}" == "true") {
				AssertTextEquals.assertPartialText(
					locator1 = "Message#ERROR",
					value1 = "Error:The folder you selected already has an entry with this name. Please select a different folder.");
			}
			else {
				Alert.viewSuccessMessage();
			}
		}
		else {
			var key_dmFolderName = "${dmFolderName1}";

			AssertClick(
				locator1 = "DocumentsAndMediaSelectFolder#SELECT_BUTTON",
				value1 = "Select");

			SelectFrame(locator1 = "relative=top");

			Alert.viewSuccessMessage();

			AssertElementNotPresent(
				key_dmFolderName = "${dmFolderName2}",
				locator1 = "DocumentsAndMedia#DESCRIPTIVE_LIST_FOLDER_TITLE");

			Click(
				key_dmFolderName = "${dmFolderName1}",
				locator1 = "DocumentsAndMedia#DESCRIPTIVE_LIST_FOLDER_TITLE",
				value1 = "${dmFolderName1}");

			AssertElementPresent(
				key_dmFolderName = "${dmFolderName2}",
				locator1 = "DocumentsAndMedia#DESCRIPTIVE_LIST_FOLDER_TITLE");
		}
	}

	macro moveToRecycleBinCP {
		var key_dmFolderName = "${dmFolderName}";

		DMFolder.viewEntryPresent(dmFolderName = "${dmFolderName}");

		LexiconCard.checkCardCheckbox(card = "${dmFolderName}");

		Click(locator1 = "DocumentsAndMedia#DESCRIPTIVE_LIST_VERTICAL_ELLIPSIS");

		PortletEntry.clickMoveToRecycleBin();

		VerifyElementPresent(locator1 = "Message#SUCCESS");

		AssertTextEquals(
			locator1 = "Message#EMPTY_INFO",
			value1 = "There are no documents or media files in this folder.");
	}

	macro removePermissions {
		DMFolder.viewFolderPermissionsPG(
			dmFolder = "${dmFolder}",
			key_roleTitle = "${roleTitle}");

		var key_roleTitle = "${roleTitle}";

		for (var permissionName : list "${permissionNameList}") {
			Uncheck(locator1 = "Permissions#CONTENT_PERMISSIONS_${permissionName}_CHECKBOX");
		}

		PortletEntry.save();
	}

	macro subscribePG {
		Click(locator1 = "Icon#INFO");

		Click(locator1 = "Icon#SUBSCRIBE");

		Alert.viewSuccessMessage();
	}

	macro tearDownCP {
		Navigator.openURL();

		ProductMenu.gotoPortlet(
			category = "Content & Data",
			portlet = "Documents and Media",
			site = "${site}");

		if (IsElementNotPresent(locator1 = "Message#EMPTY_INFO")) {
			Click.clickNoMouseOverNoWaitForVisible(locator1 = "DocumentsAndMedia#TOOLBAR_SELECT_ALL_CHECKBOX");

			Click(locator1 = "DocumentsAndMedia#TOOLBAR_ACTIONS");

			MenuItem.viewPresent(menuItem = "Delete");

			PortletEntry.clickMoveToRecycleBin();
		}

		JSONRecycleBin.emptyRecycleBin();
	}

	macro tearDownPGViaMG {
		Navigator.gotoPage(pageName = "${pageName}");

		while (IsElementPresent(locator1 = "MediaGallery#ENTRIES_FOLDER")) {
			Click(locator1 = "MediaGallery#ENTRIES_FOLDER");

			AssertClick(
				locator1 = "MediaGallery#ENTRY_RECYCLE_BIN_ICON",
				value1 = "Delete");

			VerifyElementPresent(locator1 = "Message#SUCCESS");
		}

		JSONRecycleBin.emptyRecycleBin();
	}

	macro viewCP {
		var key_dmFolderName = "${dmFolderName}";

		if (IsElementPresent(key_displayStyle = "list", locator1 = "Icon#CURRENT_DISPLAY_STYLE_ACTIVE")) {
			AssertElementPresent(locator1 = "DocumentsAndMedia#DESCRIPTIVE_LIST_FOLDER_ICON");

			AssertTextEquals(
				locator1 = "DocumentsAndMedia#DESCRIPTIVE_LIST_FOLDER_TITLE",
				value1 = "${dmFolderName}");
		}
		else {
			DMFolder.viewEntryPresent(dmFolderName = "${dmFolderName}");
		}

		if ("${specificNameFolder}" == "true") {
			var rowEntry = "${dmFolderName}";

			AssertClick(
				locator1 = "DocumentsAndMedia#DESCRIPTIVE_LIST_FOLDER_TITLE",
				value1 = "${dmFolderName}");
		}
		else {
			LexiconEntry.gotoEntry(
				displayStyle = "${displayStyle}",
				rowEntry = "${dmFolderName}");
		}

		AssertTextEquals(
			locator1 = "DocumentsAndMedia#BREADCRUMB_FOLDER_NAME",
			value1 = "${dmFolderName}");
	}

	macro viewDefaultCP {
		AssertElementNotPresent(locator1 = "DocumentsAndMedia#ICON_DOCUMENT_GENERIC");

		AssertTextEquals(
			locator1 = "Message#EMPTY_INFO",
			value1 = "There are no documents or media files in this folder.");
	}

	macro viewEntryPresent {
		LexiconCard.viewCardPresent(card = "${dmFolderName}");
	}

	macro viewFolderPermissionsPG {
		var key_dmFolderName = "${dmFolder}";
		var key_roleTitle = StringUtil.lowerCase("${key_roleTitle}");

		LexiconEntry.changeDisplayStyle(displayStyle = "list");

		Click(locator1 = "DocumentsAndMedia#DESCRIPTIVE_LIST_VERTICAL_ELLIPSIS");

		MenuItem.click(menuItem = "Permissions");

		IFrame.selectPermissionsFrame();
	}

	macro viewNoSubscribe {
		Click(locator1 = "Icon#INFO");

		AssertElementNotPresent(locator1 = "Icon#SUBSCRIBE");
	}

	macro viewPG {
		var key_dmFolderName = "${dmFolderName}";

		AssertTextEquals.assertPartialText(
			locator1 = "DocumentsAndMedia#BREADCRUMB_FOLDER_NAME",
			value1 = "${dmFolderName}");
	}

	macro viewPGViaMG {
		var key_mgFolderName = "${mgFolderName}";

		DMFolder.gotoEntry(dmFolderName = "${mgFolderName}");

		AssertTextEquals(
			locator1 = "MediaGallery#FOLDER_HEADER_TITLE",
			value1 = "${mgFolderName}");
	}

}