definition {

	@summary = "Change site default language in Site Settings."
	macro changeDefaultLanguage {
		ProductMenu.gotoPortlet(
			category = "Configuration",
			portlet = "Site Settings");

		Click(
			key_itemName = "Localization",
			locator1 = "ListGroupItem#ITEM_TEXT");

		Click(
			key_fieldLabel = "Define a custom default language",
			locator1 = "SystemSettings#LABEL");

		Select(
			locator1 = "Select#SITE_DEFAULT_LANGUAGE",
			value1 = "${defaultPortalLanguage}");

		Button.clickSave();
	}

	macro gotoHomePage {
		ProductMenuHelper.openProductMenu();

		Click(locator1 = "ProductMenu#HOME_PAGE_LINK");
	}

	@summary = "Navigate to the Live site"
	macro gotoLiveSite {
		if (IsElementNotPresent(locator1 = "ProductMenu#TOGGLE")) {
			ApplicationsMenu.gotoSite(site = "${site}");
		}

		ProductMenuHelper.openProductMenu();

		AssertClick(
			locator1 = "ProductMenu#LIVE_LINK",
			value1 = "Live");
	}

	@summary = "Navigate to Product Menu > Sites > Sites Modal > ${site}"
	macro gotoLocalizedSite {
		var siteName = TestCase.getSiteName(siteName = "${siteName}");

		if (IsElementNotPresent(locator1 = "ProductMenu#TOGGLE")) {
			ApplicationsMenu.gotoSite(site = "${siteName}");
		}

		ProductMenuHelper.openProductMenu();

		WaitForLiferayEvent.initializeLiferayEventLog();

		ProductMenuHelper.openWorkspace();

		SelectFrame(locator1 = "IFrame#MODAL_BODY");

		Navigator.gotoNavItem(navItem = "${navItem}");

		LexiconEntry.changeDisplayStyle(displayStyle = "${displayStyle}");

		SiteSelector._searchSite(site = "${site}");

		LexiconEntry.gotoEntry(rowEntry = "${site}");

		SelectFrameTop();

		ProductMenuHelper.openProductMenu();

		AssertTextEquals.assertPartialText(
			locator1 = "ProductMenu#PRODUCT_MENU_PANEL_SITE_ADMINISTRATION_SITE_NAME",
			value1 = "${site}");
	}

	@summary = "Navigate to Product Menu > ${category} > ${portlet}"
	macro gotoPortlet {
		if (IsElementNotPresent(locator1 = "ProductMenu#TOGGLE")) {
			ApplicationsMenu.gotoSite(site = "${site}");
		}

		ProductMenuHelper.openProductMenu();

		ProductMenuHelper.expandCategory(category = "${category}");

		ProductMenuHelper.gotoPortlet(portlet = "${portlet}");
	}

	@summary = "Navigate to Product Menu > ${panel} > ${category} > ${portlet}"
	macro gotoPortletWhenDisableApplicationsMenu {
		ProductMenuHelper.expandPanel(panel = "${panel}");

		ProductMenuHelper.expandCategory(category = "${category}");

		ProductMenuHelper.gotoPortlet(portlet = "${portlet}");
	}

	@summary = "Navigate to Product Menu > ${category} > ${scope} > ${portlet}"
	macro gotoScopedPortlet {
		ProductMenuHelper.openProductMenu();

		ProductMenuHelper.expandCategory(category = "${category}");

		ProductMenuHelper.selectScope(scope = "${scope}");

		ProductMenuHelper.gotoPortlet(portlet = "${portlet}");
	}

	@summary = "Navigate to Product Menu > Sites > Sites Modal > ${site}"
	macro gotoSite {
		if (IsElementNotPresent(locator1 = "ProductMenu#TOGGLE")) {
			ApplicationsMenu.gotoSite(site = "${site}");
		}

		ProductMenuHelper.openProductMenu();

		ProductMenuHelper.openWorkspace();

		SelectFrame(locator1 = "IFrame#MODAL_BODY");

		Pause(locator1 = "3000");

		Navigator.gotoNavItem(navItem = "All Sites");

		SiteSelector.selectSite(site = "${site}");

		ProductMenuHelper.openProductMenu();

		AssertTextEquals.assertPartialText(
			locator1 = "ProductMenu#PRODUCT_MENU_PANEL_SITE_ADMINISTRATION_SITE_NAME",
			value1 = "${site}");
	}

	@summary = "Navigate to the Staging site"
	macro gotoStagingSite {
		if (IsElementNotPresent(locator1 = "ProductMenu#TOGGLE")) {
			ApplicationsMenu.gotoSite(site = "${site}");
		}

		if (IsElementNotPresent(locator1 = "ProductMenu#STAGING_SELECTED")) {
			ProductMenuHelper.openProductMenu();

			AssertClick(
				locator1 = "ProductMenu#STAGING_LINK",
				value1 = "Staging");
		}

		AssertVisible(locator1 = "ProductMenu#STAGING_SELECTED");
	}

	@summary = "View ${site} is not available in the product menu."
	macro viewNoSite {
		var siteName = TestCase.getSiteName(siteName = "${siteName}");

		if (IsElementNotPresent(locator1 = "ProductMenu#TOGGLE")) {
			ApplicationsMenu.gotoSite(site = "${siteName}");
		}

		ProductMenuHelper.openProductMenu();

		if ((IsElementPresent(locator1 = "ProductMenu#BACK_ARROW")) && (IsVisible(locator1 = "ProductMenu#BACK_ARROW"))) {
			Click(locator1 = "ProductMenu#BACK_ARROW");
		}

		ProductMenuHelper.viewNoSite(site = "${site}");
	}

}