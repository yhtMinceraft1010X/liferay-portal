definition {

	macro addCP {
		LexiconEntry.gotoAdd();

		AssertVisible(locator1 = "Sidebar#FORM_BUILDER");

		if (isSet(structureName)) {
			WebContentStructures.addName(structureName = "${structureName}");
		}

		if (isSet(structureKey)) {
			if (IsElementNotPresent(key_tabName = "Properties", locator1 = "Sidebar#ACTIVE_TAB")) {
				Navigator.gotoSidebarTab(tabName = "Properties");
			}

			PortletEntry.inputStructureKeyTextInput(structureKey = "${structureKey}");
		}

		if (isSet(structureDescription)) {
			if (IsElementNotPresent(key_tabName = "Properties", locator1 = "Sidebar#ACTIVE_TAB")) {
				Navigator.gotoSidebarTab(tabName = "Properties");
			}

			PortletEntry.inputDescriptionTextInput(description = "${structureDescription}");
		}
	}

	macro addFavoriteStructure {
		AssertTextEquals.assertPartialText(
			locator1 = "Message#INFO_1",
			value1 = "You can add as many as 7 favorites in your quick menu.");

		Click(
			key_rowEntry = "${structureName}",
			locator1 = "Icon#ADD_FAVORITE");

		Alert.viewSuccessMessage();
	}

	macro addLocalization {
		Click(locator1 = "Button#LOCALIZATION_GENERIC");

		if (IsElementNotPresent(key_locale = "${locale}", locator1 = "Translation#LOCALIZATION_NAME")) {
			WebContentStructures.addTranslations(
				localeList = "${locale}",
				localizedDone = "${localizedDone}",
				localizedManageTranslations = "${localizedManageTranslations}");
		}
		else {
			Click(locator1 = "Button#LOCALIZATION_GENERIC");
		}

		PortletEntry.changeLocale(locale = "${locale}");

		WebContentStructures.addName(structureName = "${localization1Name}");

		if (isSet(locale2)) {
			Click(locator1 = "Button#LOCALIZATION_GENERIC");

			if (IsElementNotPresent(key_locale = "${locale2}", locator1 = "Translation#LOCALIZATION_NAME")) {
				WebContentStructures.addTranslations(
					localeList = "${locale2}",
					localizedDone = "${localizedDone}",
					localizedManageTranslations = "${localizedManageTranslations}");
			}
			else {
				Click(locator1 = "Button#LOCALIZATION_GENERIC");
			}

			PortletEntry.changeLocale(locale = "${locale2}");

			WebContentStructures.addName(structureName = "${localization2Name}");
		}
	}

	macro addName {
		Type.clickAtType(
			locator1 = "TextInput#NAME",
			value1 = "${structureName}");
	}

	macro addNullCP {
		LexiconEntry.gotoAdd();

		AssertElementNotPresent(locator1 = "TextInput#REQUIRED_ALERT");

		Button.clickSave();

		AssertElementPresent(locator1 = "TextInput#REQUIRED_ALERT");

		SelectFrame(value1 = "relative=top");
	}

	macro addTranslations {
		if (isSet(localizedManageTranslations)) {
			var manageTranslations = "${localizedManageTranslations}";
		}
		else {
			var manageTranslations = "Manage Translations";
		}

		if (IsElementNotPresent(key_menuItem = "${manageTranslations}", locator1 = "MenuItem#ANY_MENU_ITEM")) {
			Click(locator1 = "Button#LOCALIZATION_GENERIC");
		}

		MenuItem.click(menuItem = "${manageTranslations}");

		WaitForElementPresent(locator1 = "Modal#BODY");

		for (var locale : list "${localeList}") {
			Click(locator1 = "Modal#PLUS");

			MenuItem.click(menuItem = "${locale}");

			AssertElementPresent(
				key_tableEntry = "${locale}",
				locator1 = "LexiconTable#TABLE_ENTRY");
		}

		Click(locator1 = "Modal#PLUS");

		if (isSet(localizedDone)) {
			Click(
				key_text = "${localizedDone}",
				locator1 = "Modal#MODAL_FOOTER_BUTTON");
		}
		else {
			Click(
				key_text = "Done",
				locator1 = "Modal#MODAL_FOOTER_BUTTON");
		}
	}

	macro copyCP {
		var key_ddlDataDefinitionName = "${structureName}";

		Click(locator1 = "DDMSelectStructure#DDM_STRUCTURE_ELLIPSIS");

		MenuItem.click(menuItem = "Copy");

		if (isSet(structureCopyName)) {
			PortletEntry.inputName(name = "${structureCopyName}");
		}

		if (isSet(structureCopyLocalName)) {
			PortletEntry.changeLocale(locale = "${locale}");

			PortletEntry.inputName(name = "${structureCopyLocalName}");
		}

		if ("${copyTemplates}" == "true") {
			Check(
				checkboxName = "Copy Templates",
				locator1 = "Checkbox#ANY_CHECKBOX");
		}

		AssertClick(
			locator1 = "Button#COPY",
			value1 = "Copy");

		VerifyElementPresent(locator1 = "Message#SUCCESS");
	}

	macro deleteCP {
		Navigator.gotoNavItem(navItem = "Structures");

		var key_ddlDataDefinitionName = "${structureName}";

		Click(locator1 = "DDMSelectStructure#DDM_STRUCTURE_ELLIPSIS");

		MenuItem.clickNoError(menuItem = "Delete");

		AssertConfirm(value1 = "Are you sure you want to delete this? It will be deleted immediately.");

		if ("${webContentHasReference}" == "true") {
			Alert.viewRequestFailedToComplete();

			AssertTextEquals.assertPartialText(
				locator1 = "Message#ERROR_1",
				value1 = "The structure cannot be deleted because it is required by one or more structure links.");
		}
		else {
			Alert.viewSuccessMessage();
		}
	}

	macro editStructureDefaultValuesCP {
		LexiconEntry.gotoEntryMenuItem(
			menuItem = "Edit Default Values",
			rowEntry = "${structureName}");
	}

	macro openWebContentStructuresAdmin {
		Navigator.openWithAppendToBaseURL(urlAppend = "group/${siteURLKey}/~/control_panel/manage?p_p_id=com_liferay_journal_web_portlet_JournalPortlet&_com_liferay_journal_web_portlet_JournalPortlet_mvcPath=%2Fview_ddm_structures.jsp");
	}

	macro resetStructureDefaultValuesCP {
		ClickNoError(locator1 = "Button#RESET_VALUES");

		if (isSet(cancel)) {
			Confirm.dismissAlert();

			if (isSet(templateName)) {
				Panel.expandPanel(panel = "Default Template");

				AssertTextEquals(
					inputFieldId = "ddmTemplateName",
					locator1 = "TextInput#INPUT_ID",
					value1 = "${templateName}");
			}

			if (isSet(assetTitle)) {
				var key_assetTitle = "${assetTitle}";

				AssertTextEquals.assertPartialText(
					locator1 = "AssetRelatedAssets#RELATED_ASSETS_TABLE_TITLE",
					value1 = "${assetTitle}");
			}
		}
		else {
			AssertConfirm(value1 = "Are you sure you want to reset the default values?");

			Alert.viewSuccessMessage();

			if (isSet(templateName)) {
				Panel.expandPanel(panel = "Default Template");

				AssertTextEquals(
					inputFieldId = "ddmTemplateName",
					locator1 = "TextInput#INPUT_ID",
					value1 = "${templateName}");
			}

			if (isSet(assetTitle)) {
				var key_assetTitle = "${assetTitle}";

				AssertElementNotPresent(locator1 = "AssetRelatedAssets#RELATED_ASSETS_TABLE_TYPE");

				AssertElementNotPresent(locator1 = "AssetRelatedAssets#RELATED_ASSETS_TABLE_TITLE");

				AssertElementNotPresent(locator1 = "AssetRelatedAssets#RELATED_ASSETS_TABLE_SCOPE");

				AssertElementNotPresent(locator1 = "AssetRelatedAssets#RELATED_ASSETS_TABLE_REMOVE_LINK");
			}
		}
	}

	macro saveCP {
		DDLDataDefinition.save(publicationsEnabled = "${publicationsEnabled}");

		if (isSet(addRoleViewPermissions)) {
			LexiconEntry.gotoEntryMenuItem(
				menuItem = "Permissions",
				rowEntry = "${structureName}");

			SelectFrame(locator1 = "IFrame#MODAL_BODY");

			PermissionsInline.addOrRemoveViewPermissionFromAllRoles(
				addOrRemoveViewPermission = "Add",
				roleList = "${addRoleViewPermissions}");

			IFrame.selectTopFrame();
		}
	}

	macro tearDownCP {
		Navigator.openURL();

		ProductMenu.gotoPortlet(
			category = "Content & Data",
			portlet = "Web Content");

		Navigator.gotoNavItem(navItem = "Structures");

		Search.searchCP(searchTerm = "2");

		while (IsElementNotPresent(locator1 = "Message#EMPTY_INFO")) {
			PortletEntry.selectAll();

			ClickNoError(locator1 = "Icon#DELETE");

			AssertConfirm(value1 = "Are you sure you want to delete this? It will be deleted immediately.");

			Alert.viewSuccessMessage();
		}

		Search.searchCP(searchTerm = "Structure");

		while (IsElementNotPresent(locator1 = "Message#EMPTY_INFO")) {
			PortletEntry.selectAll();

			ClickNoError(locator1 = "Icon#DELETE");

			AssertConfirm.assertConfirmationNoWait(value1 = "Are you sure you want to delete this? It will be deleted immediately.");

			Alert.viewSuccessMessage();
		}

		Portlet.viewEmptyMessage(message = "There are no results.");
	}

	macro viewCP {
		var key_ddlDataDefinitionName = "${structureName}";

		if (isSet(structureName)) {
			AssertTextEquals(
				locator1 = "DDMSelectStructure#DDM_STRUCTURE_TABLE_NAME",
				value1 = "${structureName}");
		}

		if (isSet(structureDescription)) {
			AssertTextEquals(
				locator1 = "DDMSelectStructure#DDM_STRUCTURE_TABLE_DESCRIPTION",
				value1 = "${structureDescription}");
		}

		SelectFrame(value1 = "relative=top");
	}

	macro viewFavoriteStructureViaIFrame {
		AssertTextEquals(
			locator1 = "WC#ADD_TO_FAVORITES",
			value1 = "Add to Favorites (${indexNumber})");

		ScrollBy(value1 = "0, 50");

		AssertElementPresent(
			key_rowEntry = "${structureName}",
			locator1 = "Icon#REMOVE_FAVORITE");
	}

	macro viewFavoriteStructureViaPlusButton {
		LexiconEntry.gotoAdd();

		MenuItem.viewPresent(menuItem = "More");

		MenuItem.viewPresent(menuItem = "Favorites");

		MenuItem.viewPresent(menuItem = "${structureName}");
	}

	macro viewNoFavoriteStructure {
		AssertTextEquals(
			locator1 = "WC#ADD_TO_FAVORITES",
			value1 = "Add to Favorites (0)");
	}

	macro viewScopesViaMoreIFrame {
		AssertTextEquals(
			key_tableEntry = "${structureName}",
			key_tableEntryText = "${scopeName}",
			locator1 = "LexiconTable#TABLE_ENTRY_TEXT",
			value1 = "${scopeName}");
	}

	macro viewStructuresViaMoreIFrame {
		AssertTextEquals(
			key_rowEntry = "${structureName}",
			locator1 = "ContentRow#ENTRY_CONTENT_ENTRY_NAME_LINK",
			value1 = "${structureName}");
	}

	macro viewTableEntryCP {
		var key_ddlDataDefinitionName = "${structureName}";

		AssertTextEquals(
			locator1 = "DDMSelectStructure#DDM_STRUCTURE_TABLE_NAME",
			value1 = "${structureName}");

		AssertTextEquals(
			locator1 = "DDMSelectStructure#DDM_STRUCTURE_TABLE_DESCRIPTION",
			value1 = "${structureDescription}");
	}

}