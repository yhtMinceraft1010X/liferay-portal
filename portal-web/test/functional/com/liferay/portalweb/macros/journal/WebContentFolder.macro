definition {

	macro addCP {
		var key_webContentTitle = "${folderName}";

		LexiconEntry.gotoAdd();

		if (IsElementPresent(key_menuItem = "Folder", locator1 = "MenuItem#ANY_MENU_ITEM")) {
			MenuItem.click(menuItem = "Folder");
		}

		PortletEntry.inputName(name = "${folderName}");

		PortletEntry.inputDescription(description = "${folderDescription}");

		PortletEntry.save();

		LexiconEntry.changeDisplayStyle(displayStyle = "list");

		AssertElementPresent.pauseAssertVisible(locator1 = "WC#ENTRY_LIST_THUMBNAIL");

		AssertTextEquals.assertPartialText(
			locator1 = "WC#ENTRY_LIST_TITLE",
			value1 = "${folderName}");
	}

	macro addSubfolderCP {
		var key_webContentTitle = "${folderName}";

		LexiconEntry.changeDisplayStyle(displayStyle = "list");

		Click(
			locator1 = "WC#ENTRY_LIST_TITLE",
			value1 = "${folderName}");

		AssertTextEquals(
			locator1 = "Portlet#HEADER",
			value1 = "Web Content");

		LexiconEntry.gotoAdd();

		if (IsElementPresent(key_menuItem = "Subfolder", locator1 = "MenuItem#ANY_MENU_ITEM")) {
			MenuItem.click(menuItem = "Subfolder");
		}

		PortletEntry.inputName(name = "${subfolderName}");

		PortletEntry.inputDescription(description = "${subfolderDescription}");

		Button.clickSave();

		var key_webContentTitle = "${subfolderName}";

		Alert.viewSuccessMessage();

		AssertElementPresent.pauseAssertVisible(locator1 = "WC#ENTRY_LIST_THUMBNAIL");

		AssertTextEquals.assertPartialText(
			locator1 = "WC#ENTRY_LIST_TITLE",
			value1 = "${subfolderName}");
	}

	macro checkSubfolderPermissions {
		var key_webContentTitle = "${folderName}";

		LexiconEntry.changeDisplayStyle(displayStyle = "list");

		Click(
			locator1 = "WC#ENTRY_LIST_TITLE",
			value1 = "${folderName}");

		AssertTextEquals(
			locator1 = "Portlet#HEADER",
			value1 = "Web Content");

		MouseOver(locator1 = "Button#PLUS");

		if ("${subfolder}" == "false") {
			MenuItem.viewNotPresent(menuItem = "Subfolder");
		}
		else {
			MenuItem.viewPresent(menuItem = "Subfolder");
		}
	}

	macro configureWorkflowCP {
		WebContentFolder.gotoEditCP(
			folderName = "${folderName}",
			webContentTitle = "${folderName}");

		if (IsElementNotPresent.isNotVisible(locator1 = "Radio#WORKFLOW_PARENT_FOLDER")) {
			Click(locator1 = "WCEditFolder#WORKFLOW_STRUCTURE_RESTRICTIONS_TOGGLE");
		}

		if ("${workflowRestriction}" == "Parent Folder") {
			Click(locator1 = "Radio#WORKFLOW_PARENT_FOLDER");
		}
		else if ("${workflowRestriction}" == "Specific Restrictions") {
			Click(locator1 = "Radio#WORKFLOW_SPECIFIC_RESTRICTIONS");

			AssertClick(
				locator1 = "WCEditFolder#WORKFLOW_SPECIFIC_RESTRICTIONS_CHOOSE_STRUCTURE_BUTTON",
				value1 = "Choose Structure");

			SelectFrame(value1 = "relative=top");

			SelectFrame(locator1 = "IFrame#MODAL_BODY");

			var key_ddlDataDefinitionName = "${structureName}";

			AssertClickNoError(
				locator1 = "DDMSelectStructure#DDM_STRUCTURE_TABLE_NAME_LINK",
				value1 = "${structureName}");

			SelectFrame(value1 = "relative=top");

			Select(
				locator1 = "WCEditFolder#WORKFLOW_SPECIFIC_RESTRICTIONS_STRUCTURE_WORKFLOW_SELECT",
				value1 = "${workflowDefinition}");
		}
		else if ("${workflowRestriction}" == "Default Workflow") {
			Click(locator1 = "Radio#WORKFLOW_DEFAULT_WORKFLOW");

			Select(
				locator1 = "Select#WORKFLOW_DEFAULT",
				value1 = "${workflowDefinition}");
		}

		PortletEntry.save();
	}

	macro gotoCP {
		LexiconEntry.changeDisplayStyle(displayStyle = "list");

		var key_webContentTitle = "${webContentTitle}";

		AssertTextEquals.assertPartialText(
			locator1 = "WC#ENTRY_LIST_TITLE",
			value1 = "${folderName}");

		AssertClick(
			locator1 = "WC#ENTRY_LIST_TITLE",
			value1 = "${folderName}");

		AssertTextEquals(
			locator1 = "Portlet#HEADER",
			value1 = "Web Content");
	}

	macro gotoEditCP {
		var key_webContentTitle = "${folderName}";

		LexiconEntry.changeDisplayStyle(displayStyle = "list");

		AssertTextEquals.assertPartialText(
			locator1 = "WC#ENTRY_LIST_TITLE",
			value1 = "${webContentTitle}");

		Click(locator1 = "WC#ENTRY_LIST_MENULIST_ICON");

		MenuItem.click(menuItem = "Edit");
	}

	macro moveToRecycleBinCP {
		LexiconEntry.changeDisplayStyle(displayStyle = "list");

		var key_webContentTitle = "${folderName}";

		AssertTextEquals.assertPartialText(
			locator1 = "WC#ENTRY_LIST_TITLE",
			value1 = "${folderName}");

		WebContentNavigator.gotoEntryMenuItem(
			entry = "${folderName}",
			menuItem = "Delete");

		VerifyElementPresent(locator1 = "Message#SUCCESS");
	}

	macro selectFolderTreeNode {
		Portlet.expandTree();

		Click(
			key_nodeName = "${nodeName}",
			locator1 = "Treeview#NODE_ITEM");
	}

	macro viewFolderWorkflow {
		WebContentFolder.gotoEditCP(
			folderName = "${folderName}",
			webContentTitle = "${folderName}");

		var key_workflowResourceValue = "${workflowResourceValue}";

		AssertTextEquals(
			locator1 = "WCEditFolder#WORKFLOW_FOLDER_WORKFLOW",
			value1 = "${workflowResourceValue}");
	}

}