definition {

	@summary = "Select a content type and subtype when adding a display page. These locators should not be needed to be reused."
	macro _selectMappingTypes {
		if (isSet(contentType)) {
			Select(
				locator1 = "//div[contains(.,'Content Type')]/select",
				value1 = "${contentType}");
		}

		if (isSet(subType)) {
			Select(
				locator1 = "//div[contains(.,'Subtype')]/select",
				value1 = "${subType}");
		}
	}

	macro addDisplayPage {
		LexiconEntry.gotoAdd();

		if (isSet(masterLayoutName)) {
			LexiconCard.clickCard(card = "${masterLayoutName}");
		}
		else {
			LexiconCard.clickCard(card = "Blank");
		}

		PortletEntry.inputName(name = "${displayPageName}");

		DisplayPageTemplatesAdmin._selectMappingTypes(
			contentType = "${contentType}",
			subType = "${subType}");

		Button.clickSave();

		PageEditor.assertFragmentSidebarLoaded();
	}

	macro deleteDisplayPage {
		LexiconCard.clickEllipsisItem(
			card = "${displayPageName}",
			item = "Delete");

		if ("${defaultDisplayPageTemplate}" == "true") {
			AssertTextEquals(
				locator1 = "Modal#BODY",
				value1 = "Are you sure you want to delete the default display page template? All related display pages will be deleted.");
		}
		else {
			AssertTextEquals(
				locator1 = "Modal#BODY",
				value1 = "Are you sure you want to delete this? It will be deleted immediately.");
		}

		Click(
			key_text = "Delete",
			locator1 = "Modal#MODAL_FOOTER_BUTTON");
	}

	macro gotoDisplayPage {
		LexiconCard.clickCardTitleLink(card = "${displayPageName}");

		PageEditor.assertFragmentSidebarLoaded();
	}

	macro gotoDisplayPageEllipsisItem {
		Pause(locator = "1000");

		LexiconCard.clickEllipsisItem(
			card = "${displayPageName}",
			item = "${item}");
	}

	macro importDisplayPageTemplateFromTempFolder {
		Click.waitForMenuToggleJSClick(locator1 = "Icon#HEADER_VERTICAL_ELLIPSIS");

		MenuItem.click(menuItem = "Import");

		SelectFrame(locator1 = "IFrame#MODAL_BODY");

		var displayPageTemplateZipFileName = TestCase.getDownloadedTempFileName(fileNamePattern = "display-page-template-*.zip");

		UploadTempFile(
			locator1 = "TextInput#FILE",
			value1 = "${displayPageTemplateZipFileName}");

		Button.clickImport();

		Alert.viewSuccessMessage();

		IFrame.closeFrame();
	}

	macro markDisplayPageAsDefault {
		DisplayPageTemplatesAdmin.gotoDisplayPageEllipsisItem(
			displayPageName = "${displayPageName}",
			item = "Mark as Default");

		LexiconCard.viewCardSticker(
			card = "${displayPageName}",
			icon = "check-circle");
	}

	macro openDisplayPagesAdmin {
		Navigator.openWithAppendToBaseURL(urlAppend = "group/${siteURLKey}/~/control_panel/manage?p_p_id=com_liferay_layout_page_template_admin_web_portlet_LayoutPageTemplatesPortlet&_com_liferay_layout_page_template_admin_web_portlet_LayoutPageTemplatesPortlet_tabs1=display-page-templates");
	}

	macro renameDisplayPage {
		DisplayPageTemplatesAdmin.gotoDisplayPageEllipsisItem(
			displayPageName = "${displayPageName}",
			item = "Rename");

		PortletEntry.inputName(name = "${newDisplayPageName}");

		Modal.save();
	}

	macro viewDisplayPageNotPresent {
		LexiconCard.viewCardNotPresent(card = "${displayPageName}");
	}

	macro viewDisplayPagePresent {
		LexiconCard.viewCardPresent(card = "${displayPageName}");
	}

	macro viewPGViaAP {
		var key_assetContent = "${webContentContent}";
		var key_assetTitle = "${webContentTitle}";

		AssertTextEquals(
			locator1 = "AP#ASSET_ABSTRACT_TITLE",
			value1 = "${webContentTitle}");

		AssertTextEquals.assertPartialText(
			locator1 = "AP#ASSET_ABSTRACT_DESCRIPTION",
			value1 = "${webContentContent}");

		AssertClick(
			locator1 = "AP#ASSET_ABSTRACT_TITLE",
			value1 = "${webContentTitle}");

		for (var content : list "${contentList}") {
			AssertElementPresent(
				key_text = "${content}",
				locator1 = "Fragment#EDITABLE_FIELD_CONTENT");
		}
	}

}