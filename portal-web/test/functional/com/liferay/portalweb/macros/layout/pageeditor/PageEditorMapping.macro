definition {

	macro assertAssetTypeUnselectable {
		PageEditor.gotoTab(tabName = "Mapping");

		AssertElementNotPresent(
			key_fieldLabel = "Item",
			key_text = "plus",
			locator1 = "Icon#ANY_ITEM_SELECTOR");
	}

	macro gotoEditableFieldMapping {
		if (!(isSet(index))) {
			var index = "1";
		}

		PageEditor.clickEditableField(
			contributed = "${contributed}",
			fragmentName = "${fragmentName}",
			id = "${id}",
			index = "${index}");

		if (IsElementPresent(key_sidebarButton = "Comments", locator1 = "PageEditor#FRAGMENT_SIDEBAR_TITLE")) {
			PageEditor.gotoTab(tabName = "Browser");
		}

		if ((contains("${id}", "image")) || (contains("${id}", "img"))) {
			Navigator.gotoNavTab(navTab = "Image Source");

			if (!(IsSelectedLabel(key_fieldLabel = "Source Selection", locator1 = "Select#GENERIC_SELECT_FIELD", value1 = "Mapping"))) {
				Select(
					key_fieldLabel = "Source Selection",
					locator1 = "Select#GENERIC_SELECT_FIELD",
					value1 = "Mapping");

				AssertSelectedLabel(
					key_fieldLabel = "Source Selection",
					locator1 = "Select#GENERIC_SELECT_FIELD",
					value1 = "Mapping");
			}
		}
		else {
			Navigator.gotoNavTab(navTab = "Mapping");
		}
	}

	macro mapEditableFieldToAsset {
		if (IsTextEqual(key_fieldLabel = "Item", locator1 = "PageEditor#ITEM_SELECTOR_CONTENT_INPUT", value1 = "")) {
			Click(
				key_fieldLabel = "Item",
				key_text = "plus",
				locator1 = "Icon#ANY_ITEM_SELECTOR");
		}
		else {
			Click(
				key_fieldLabel = "Item",
				key_text = "change",
				locator1 = "Icon#ANY_ITEM_SELECTOR");
		}

		if (IsElementPresent(key_menuItem = "Select Item...",locator1 = "MenuItem#ANY_MENU_ITEM")) {
			if (isSet(contentItem)) {
				MenuItem.click(menuItem = "${contentItem}");
			}
			else {
				MenuItem.click(menuItem = "Select Item...");
			}
		}

		SelectFrame(locator1 = "IFrame#MODAL_BODY");

		NavItem.click(navItem = "${navItem}");

		if (isSet(uploadFileName)) {
			AssertElementPresent(locator1 = "ItemSelector#SELECT_FILE_UPLOAD_IMAGE");

			UploadCommonFile.uploadCommonFileHiddenNoMouseOver(
				locator1 = "TextInput#FILE",
				value1 = "${uploadFileName}");

			Pause(locator1 = "5000");

			AssertElementPresent.pauseAssertVisible(
				locator1 = "ItemSelector#ADD_BUTTON",
				value1 = "Add");

			AssertClick(
				locator1 = "ItemSelector#ADD_BUTTON",
				value1 = "Add");
		}
		else if ("${navItem}" == "Web Content") {
			LexiconEntry.changeDisplayStyle(displayStyle = "cards");

			Click(
				key_card = "${entryTitle}",
				locator1 = "Card#CARD_TITLE");
		}
		else {
			Click(
				key_card = "${entryTitle}",
				locator1 = "Card#CARD_TITLE");
		}

		AssertTextEquals(
			key_fieldLabel = "Item",
			locator1 = "PageEditor#ITEM_SELECTOR_CONTENT_INPUT",
			value1 = "${entryTitle}");

		Pause(locator1 = "3000");
	}

	macro mapEditableFieldToCollectionItems {
		Click(locator1 = "PageEditor#CONTENT_PAGE_DISABLED_HEADER");

		Click.clickNoWaitForVisible(
			key_colPosition = "1",
			key_element = "//div[contains(@class,'page-editor')]//li[contains(.,'${fragmentName}')]",
			key_fragmentName = "Collection Display",
			key_rowPosition = "1",
			locator1 = "PageEditor#COLLECTION_CELL_NESTED_ELEMENT");

		Click(
			key_colPosition = "1",
			key_element = "//div[contains(@class,'page-editor__topper__bar') and contains(.,'${fragmentName}')]//following-sibling::div[contains(@class,'page-editor__topper__content')]//*[contains(@data-lfr-editable-id,'${id}')]",
			key_fragmentName = "Collection Display",
			key_rowPosition = "1",
			locator1 = "PageEditor#COLLECTION_CELL_NESTED_ELEMENT");

		if ((contains("${id}", "image")) || (contains("${id}", "img"))) {
			Navigator.gotoNavTab(navTab = "Image Source");

			if (!(IsSelectedLabel(key_fieldLabel = "Source Selection", locator1 = "Select#GENERIC_SELECT_FIELD", value1 = "Mapping"))) {
				Select(
					key_fieldLabel = "Source Selection",
					locator1 = "Select#GENERIC_SELECT_FIELD",
					value1 = "Mapping");

				AssertSelectedLabel(
					key_fieldLabel = "Source Selection",
					locator1 = "Select#GENERIC_SELECT_FIELD",
					value1 = "Mapping");
			}
		}
		else {
			Navigator.gotoNavTab(navTab = "Mapping");
		}

		Select(
			key_fieldLabel = "Field",
			locator1 = "Select#GENERIC_SELECT_FIELD",
			value1 = "${field}");

		PageEditor.waitForAutoSave();

		AssertSelectedLabel(
			key_fieldLabel = "Field",
			locator1 = "Select#GENERIC_SELECT_FIELD",
			value1 = "${field}");
	}

	macro mapEditableTextToField {
		PageEditorMapping.gotoEditableFieldMapping(
			contributed = "${contributed}",
			fragmentName = "${fragmentName}",
			id = "${id}",
			index = "${index}");

		if (isSet(source)) {
			Select(
				key_fieldLabel = "Source",
				locator1 = "Select#GENERIC_SELECT_FIELD",
				value1 = "${source}");
		}

		Pause(locator1 = "3000");

		Select(
			key_fieldLabel = "Field",
			locator1 = "Select#GENERIC_SELECT_FIELD",
			value1 = "${field}");

		Pause(locator1 = "3000");

		PageEditor.waitForAutoSave();
	}

}