definition {

	macro _selectContent {
		if (IsTextEqual(key_fieldLabel = "Item", locator1 = "PageEditor#ITEM_SELECTOR_CONTENT_INPUT", value1 = "")) {
			Click(
				key_fieldLabel = "Item",
				key_text = "plus",
				locator1 = "Icon#ANY_ITEM_SELECTOR");
		}
		else {
			Click(
				key_fieldLabel = "Item",
				key_text = "change",
				locator1 = "Icon#ANY_ITEM_SELECTOR");
		}

		if (IsElementPresent(key_menuItem = "Select Item...",locator1 = "MenuItem#ANY_MENU_ITEM")) {
			MenuItem.click(menuItem = "Select Item...");
		}

		SelectFrame(locator1 = "IFrame#MODAL_BODY");

		NavItem.click(navItem = "${navItem}");

		LexiconEntry.changeDisplayStyle(displayStyle = "cards");

		Click(
			key_card = "${entryTitle}",
			locator1 = "Card#CARD_TITLE");

		Pause(locator1 = "3000");
	}

	macro _selectField {
		Select(
			locator1 = "PageEditorEditableLink#FIELD",
			value1 = "${field}");
	}

	macro gotoEditableFieldLink {
		PageEditor.clickEditableField(
			contributed = "${contributed}",
			fragmentName = "${fragmentName}",
			id = "${id}");

		Navigator.gotoNavTab(navTab = "Link");
	}

	macro mapEditableFieldToPage {
		PageEditorEditableLink.gotoEditableFieldLink(
			fragmentName = "${fragmentName}",
			id = "${id}");

		Select(
			key_fieldLabel = "Link",
			locator1 = "Select#GENERIC_SELECT_FIELD",
			value1 = "Page");

		if (IsTextEqual(key_fieldLabel = "Page", locator1 = "PageEditor#ITEM_SELECTOR_CONTENT_INPUT", value1 = "")) {
			Click(
				key_fieldLabel = "Page",
				key_text = "plus",
				locator1 = "Icon#ANY_ITEM_SELECTOR");
		}
		else {
			Click(
				key_fieldLabel = "Page",
				key_text = "change",
				locator1 = "Icon#ANY_ITEM_SELECTOR");
		}

		SelectFrame(locator1 = "IFrame#MODAL_BODY");

		if (isSet(navItem)) {
			Navigator.gotoNavItem(navItem = "${navItem}");
		}

		if (isSet(keywords)) {
			Type(
				locator1 = "ItemSelector#SEARCH_FIELD",
				value1 = "${keywords}");
		}
		else {
			Portlet.expandTree();
		}

		AssertClick.assertPartialTextClickAt(
			key_nodeName = "${pageName}",
			locator1 = "Treeview#NODE_UNSELECTED",
			value1 = "${pageName}");

		SelectFrameTop();

		PageEditor.waitForAutoSave();
	}

	macro mapURLToAsset {
		Select(
			key_fieldLabel = "Link",
			locator1 = "Select#GENERIC_SELECT_FIELD",
			value1 = "Mapped URL");

		if (isSet(entryTitle)) {
			PageEditorEditableLink._selectContent(
				entryTitle = "${entryTitle}",
				navItem = "${navItem}");
		}

		if (isSet(source)) {
			Select(
				key_fieldLabel = "Source",
				locator1 = "Select#GENERIC_SELECT_FIELD",
				value1 = "${source}");
		}

		PageEditorEditableLink._selectField(field = "${field}");
	}

	macro updateURL {
		Variables.assertDefined(parameterList = "${url}");

		Type(
			key_text = "URL",
			locator1 = "TextInput#ANY",
			value1 = "${url}");

		Click(
			key_sidebarButton = "Browser",
			locator1 = "PageEditor#FRAGMENT_SIDEBAR_TITLE");

		if ("${target}" == "Blank") {
			while (IsNotChecked.isNotCheckedNotVisible(checkboxName = "Open in a new tab.", locator1 = "Checkbox#ANY_CHECKBOX")) {
				Click.clickAtNotVisible(
					checkboxName = "Open in a new tab.",
					locator1 = "Checkbox#ANY_CHECKBOX");
			}

			Pause(locator1 = "3000");
		}
		else if ("${target}" == "Self") {
			while (IsChecked.isNotCheckedNotVisible(checkboxName = "Open in a new tab.", locator1 = "Checkbox#ANY_CHECKBOX")) {
				Click.clickAtNotVisible(
					checkboxName = "Open in a new tab.",
					locator1 = "Checkbox#ANY_CHECKBOX");
			}

			Pause(locator1 = "3000");
		}

		PageEditor.waitForAutoSave();
	}

}