definition {

	macro addMasters {
		LexiconEntry.gotoAdd();

		PortletEntry.inputName(name = "${masterLayoutName}");

		Button.clickSave();

		PageEditor.assertFragmentSidebarLoaded();
	}

	macro deleteMasters {
		MastersPageTemplatesAdmin.gotoMastersEllipsisItem(
			item = "Delete",
			masterLayoutName = "${masterLayoutName}");

		AssertTextEquals(
			locator1 = "Modal#BODY",
			value1 = "Are you sure you want to delete this? It will be deleted immediately.");

		Click(
			key_text = "Delete",
			locator1 = "Modal#MODAL_FOOTER_BUTTON");

		Alert.viewSuccessMessage();
	}

	macro gotoMasters {
		LexiconCard.clickCardTitleLink(card = "${masterLayoutName}");

		PageEditor.assertFragmentSidebarLoaded();
	}

	macro gotoMastersEllipsisItem {
		Click.waitForMenuToggleJSClick(
			key_card = "${masterLayoutName}",
			locator1 = "Card#CARD_ELLIPSIS");

		MenuItem.click(menuItem = "${item}");
	}

	macro importMastersFromTempFolder {
		Click.waitForMenuToggleJSClick(locator1 = "Icon#HEADER_VERTICAL_ELLIPSIS");

		MenuItem.click(menuItem = "Import");

		SelectFrame(locator1 = "IFrame#MODAL_BODY");

		var mastersZipFileName = TestCase.getDownloadedTempFileName(fileNamePattern = "master-page-*.zip");

		UploadTempFile(
			locator1 = "TextInput#FILE",
			value1 = "${mastersZipFileName}");

		Button.clickImport();

		Alert.viewSuccessMessage();

		IFrame.closeFrame();
	}

	macro openMastersAdmin {
		Navigator.openWithAppendToBaseURL(urlAppend = "group/${siteURLKey}/~/control_panel/manage?p_p_id=com_liferay_layout_page_template_admin_web_portlet_LayoutPageTemplatesPortlet&_com_liferay_layout_page_template_admin_web_portlet_LayoutPageTemplatesPortlet_tabs1=master-layouts");
	}

	macro selectMasters {
		LexiconCard.viewCardTitle(card = "${masterLayoutName}");

		Check.checkNotVisible(
			key_card = "${masterLayoutName}",
			locator1 = "Card#CARD_CHECKBOX");
	}

	macro viewMastersPresent {
		LexiconCard.viewCardPresent(card = "${masterLayoutName}");
	}

}