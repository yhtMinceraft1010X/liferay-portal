@component-name = "portal-security"
definition {

	property portal.release = "true";
	property portal.upstream = "true";
	property testray.main.component.name = "Login";

	setUp {
		TestCase.setUpPortalInstance();

		User.firstLoginPG();
	}

	@description = "This is a use case for LPS-124898 TC-6: Verify Refresh Token Offset is not existing in the Instance Settings."
	@priority = "4"
	@uitest
	test AssertTokenRefreshOffsetCannotBeSetAtInstanceLevel {
		property test.name.skip.portal.instance = "OpenIDConnect#AssertTokenRefreshOffsetCannotBeSetAtInstanceLevel";

		ApplicationsMenu.gotoPortlet(
			category = "Configuration",
			panel = "Control Panel",
			portlet = "Instance Settings");

		SystemSettings.gotoConfiguration(
			configurationCategory = "SSO",
			configurationName = "OpenID Connect",
			configurationScope = "Virtual Instance Scope");

		AssertElementNotPresent(locator1 = "OpenIDConnect#TOKEN_REFRESH_OFFSET");
	}

	@description = "This is a use case for LPS-124900 TC-4: Users should be logged out when they don't have a valid access token because a misconfigured refresh token lifetime at Portal. "
	@priority = "4"
	test AssertUserIsLoggedOutWithHigherRefreshToken {
		property test.name.skip.portal.instance = "OpenIDConnect#AssertUserIsLoggedOutWithHigherRefreshToken";

		task ("Startup Keycloak for test") {
			AntCommand(
				locator1 = "build-test-keycloak.xml",
				value1 = "prepare-selenium-keycloak-server");
		}

		task ("Add the OSGI configuration file") {
			OSGiConfig.copyOSGiConfigFile(
				OSGiConfigFileBaseDir = "test/functional/com/liferay/portalweb/tests/coreinfrastructure/security/openidconnect/dependencies",
				OSGiConfigFileName = "com.liferay.portal.security.sso.openid.connect.internal.configuration.OpenIdConnectProviderConfiguration~default.config");
		}

		task ("Enable OpenID Connect login and set Portal's refresh token life time higher than OIDC provider's") {
			OpenIDConnect.enableOpenIdConnect();

			OpenIDConnect.setTokenRefreshOffset(expirationTime = "120");
		}

		User.logoutPG();

		task ("Verify that the User is logged out due his token lifespan is over at OIDC provider") {
			User.loginUserWithOpenIDConnectEnabledPG(
				userEmailAddress = "user1",
				userPassword = "test");

			Pause(locator1 = "70000");

			MyAccount.openMyAccountAdmin();

			User.viewLoggedOutPG();
		}
	}

	@description = "This is a use case for LPS-124898 TC-1: Verify Instance admin can set the token refresh offset via System settings."
	@priority = "4"
	@uitest
	test TokenRefreshOffsetCanBeSetAtSystemLevel {
		property test.name.skip.portal.instance = "OpenIDConnect#TokenRefreshOffsetCanBeSetAtSystemLevel";

		task ("Enable and set the OpenID connect token refresh offset time") {
			OpenIDConnect.enableOpenIdConnect();

			OpenIDConnect.setTokenRefreshOffset(expirationTime = "60");
		}
	}

	@description = "This is a use case for LPS-124898 TC-5: Verify Token Refresh Offset cannot be saved with a value under 30 second. Blocked by LPS-138193"
	@priority = "4"
	test VerifyTokenRefreshOffsetMinimum {
		property osgi.module.configuration.file.names = "com.liferay.portal.security.sso.openid.connect.internal.configuration.OpenIdConnectProviderConfiguration~default.config";
		property portal.upstream = "quarantine";
		property test.name.skip.portal.instance = "OpenIDConnect#VerifyTokenRefreshOffsetMinimum";

		OpenIDConnect.enableOpenIdConnect();

		OpenIDConnect.setTokenRefreshOffset(expirationTime = "20");

		WaitForConsoleTextPresent(value1 = "Token refresh offset needs to be at least 30 seconds");

		AssertTextEquals(
			locator1 = "OpenIDConnect#TOKEN_REFRESH_OFFSET",
			value1 = "30");
	}

	@description = "This is a use case for LPS-124900 TC-2: User is still logged in after the first access token's lifetime is over and they receive a new access token in the background."
	@priority = "5"
	test VerifyUserIsStillLoggedInAfterAccessTokenExpiresAndRenew {
		property test.name.skip.portal.instance = "OpenIDConnect#VerifyUserIsStillLoggedInAfterAccessTokenExpiresAndRenew";

		task ("Startup Keycloak for test") {
			AntCommand(
				locator1 = "build-test-keycloak.xml",
				value1 = "prepare-selenium-keycloak-server");
		}

		task ("Add the OSGI configuration file") {
			OSGiConfig.copyOSGiConfigFile(
				OSGiConfigFileBaseDir = "test/functional/com/liferay/portalweb/tests/coreinfrastructure/security/openidconnect/dependencies",
				OSGiConfigFileName = "com.liferay.portal.security.sso.openid.connect.internal.configuration.OpenIdConnectProviderConfiguration~default.config");
		}

		task ("Enable OpenID connect") {
			OpenIDConnect.enableOpenIdConnect();
		}

		User.logoutPG();

		task ("Verify that the User stay logged in after a new access token is generated") {
			User.loginUserWithOpenIDConnectEnabledPG(
				userEmailAddress = "user1",
				userPassword = "test");

			Pause(locator1 = "65000");

			Refresh();

			User.viewLoggedInPG();
		}
	}

}