@component-name = "portal-security"
definition {

	property portal.release = "true";
	property portal.upstream = "true";
	property testray.main.component.name = "AntiSamy";

	setUp {
		TestCase.setUpPortalInstance();

		User.firstLoginPG();
	}

	tearDown {
		var testPortalInstance = PropsUtil.get("test.portal.instance");

		if ("${testPortalInstance}" == "true") {
			PortalInstances.tearDownCP();
		}
		else {
			WikiPage.tearDownCP();

			PagesAdmin.tearDownCP();
		}
	}

	@description = "LPS-112581 TC-4: AntiSamy configuration XML should be configurable through the Portal."
	@priority = "5"
	test AntiSamyConfigXMLShouldBeConfigurableInPortal {
		property antisamy.enabled = "true";
		property portal.acceptance = "true";
		property test.name.skip.portal.instance = "AntiSamy#AntiSamyConfigXMLShouldBeConfigurableInPortal";

		task ("Verify that AntiSamy module has been started") {
			SAMLNavigation.viewConsoleValue(consoleValue = "STARTED com.liferay.portal.security.antisamy");
		}

		task ("Edit the configuration file url of Knowledge Base Article") {
			SystemSettings.openSystemSettingsAdmin();

			SystemSettings.gotoConfiguration(
				configurationCategory = "Security Tools",
				configurationName = "AntiSamy Sanitizer by Class Name",
				configurationScope = "System Scope");

			LexiconEntry.gotoEntryMenuItem(
				menuItem = "Edit",
				rowEntry = "com.liferay.knowledge.base.model.KBArticle");

			SystemSettings.editTextAreaSetting(
				settingName = "Configuration File URL",
				settingValue = "/META-INF/knowledge-base-sanitizer-configuration.xml");

			SystemSettings.saveConfiguration();
		}

		task ("Assert that configuration could be changed, but there is no .xml file") {
			AssertElementNotPresent(locator1 = "Message#SUCCESS");
		}
	}

	@description = "LPS-112581 TC-3: Knowledge base specific AntiSamy rule should work and sanitize dynamic parameter from KB article."
	@priority = "5"
	test ModelSpecificAntiSamyRulesShouldWorkForModel {
		property antisamy.enabled = "true";
		property portal.acceptance = "true";
		property test.name.skip.portal.instance = "AntiSamy#ModelSpecificAntiSamyRulesShouldWorkForModel";

		task ("Verify that AntiSamy module has been started") {
			SAMLNavigation.viewConsoleValue(consoleValue = "STARTED com.liferay.portal.security.antisamy");
		}

		task ("Add KB article with dynamic attribute and view it as guest") {
			KBAdmin.openKBAdmin(siteURLKey = "guest");

			KBArticle.addCP(
				kbArticleAddViaSource = "true",
				kbArticleContentSource = '''<p data-test="test">Hello Liferay</p>''',
				kbArticleTitle = "Test AntiSamy");

			KBAdmin.openKBAdmin(siteURLKey = "guest");

			KBArticle.viewCP(
				kbArticleContent = "Hello Liferay",
				kbArticleTitle = "Test AntiSamy");
		}

		task ("Save KB article as Draft") {
			KBAdmin.openKBAdmin(siteURLKey = "guest");

			KBArticle.editCP(
				kbArticleTitle = "Test AntiSamy",
				saveAsDraft = "true");
		}

		task ("Assert that dynamic attribute has been sanitized") {
			CKEditor.assertSourceValue(sourceValue = "<p>Hello Liferay</p>");
		}
	}

	@description = "LRQA-42763 TC-1: Javascript injected into Web Content Display page should be sanitized by AntiSamy."
	@priority = "4"
	test ScriptInWebContentDisplayPortletShouldBeSanitized {
		task ("Add public widget page") {
			JSONLayout.addPublicLayout(
				groupName = "Guest",
				layoutName = "Web Content Display Page");
		}

		task ("Add Web Content Display portlet and insert JS as content") {
			JSONLayout.addWidgetToPublicLayout(
				groupName = "Guest",
				layoutName = "Web Content Display Page",
				widgetName = "Web Content Display");

			var webContentContent = '''
					<p id='demo'>FAIL</p>

					<script type='text/javascript'>
						document.getElementById('demo').innerHTML = 'PASS';
					</script>
				''';

			Navigator.gotoPage(pageName = "Web Content Display Page");

			WebContentDisplayPortlet.add(
				webContentContent = "${webContentContent}",
				webContentTitle = "WC WebContent Title");
		}

		task ("Assert that script is sanitized so page content remains unchanged") {
			WebContentDisplayPortlet.viewContent(
				webContentContent = "p id='demo'>FAIL</p>",
				webContentTitle = "WC WebContent Title");
		}
	}

	@description = "LRQA-4988 TC-1: Javascript injected into Wiki portlet should be sanitized by AntiSamy."
	@priority = "4"
	test ScriptInWikiPortletShouldBeSanitized {
		task ("Add public widget page") {
			JSONLayout.addPublicLayout(
				groupName = "Guest",
				layoutName = "AntiSamy Test Page");
		}

		task ("Add Wiki portlet and insert JS as content") {
			JSONLayout.addWidgetToPublicLayout(
				groupName = "Guest",
				layoutName = "AntiSamy Test Page",
				widgetName = "Wiki");

			var wikiPageContent = '''
					<p id='demo'>PASS</p>

					<script type='text/javascript'>
						document.getElementById('demo').innerHTML = 'FAIL';
					</script>
				''';

			Navigator.gotoPage(pageName = "AntiSamy Test Page");

			WikiPage.addFrontPagePG(
				htmlSource = "true",
				wikiPageContent = "${wikiPageContent}");
		}

		task ("Assert that script is sanitized so page content remains unchanged") {
			WikiPage.viewPG(
				wikiPageContent = "PASS",
				wikiPageTitle = "FrontPage");
		}
	}

}