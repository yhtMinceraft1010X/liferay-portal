definition {

	property portal.release = "true";
	property portal.upstream = "true";
	property testray.component.names = "Remote Apps";
	property testray.main.component.name = "Remote Apps";

	setUp {
		TestCase.setUpPortalInstance();

		User.firstLoginPG();
	}

	tearDown {
		var testPortalInstance = PropsUtil.get("test.portal.instance");

		if ("${testPortalInstance}" == "true") {
			PortalInstances.tearDownCP();
		}
	}

	@description = "LPS-147198. Verify remote app entry workflow can be configured at instance level"
	@priority = "5"
	test CanConfigureWorkflowInstanceLevel {
		property portal.acceptance = "true";

		task ("Navigate to workflow configuration") {
			ApplicationsMenu.gotoPortlet(
				category = "Workflow",
				panel = "Applications",
				portlet = "Process Builder");

			Navigator.gotoNavItem(navItem = "Configuration");
		}

		task ("Search for Remote App Entry") {
			Search.searchCP(searchTerm = "Remote App Entry");
		}

		task ("Assert Remote App Entry is listed") {
			AssertElementPresent(
				key_fieldName = "Remote App Entry",
				key_order = "1",
				locator1 = "WorkflowConfiguration#PROCESS_BUILDER_TABLE_FIELD_NAME");

			AssertElementPresent(
				key_workflowResourceValue = "No Workflow",
				locator1 = "WorkflowConfiguration#RESOURCE_TABLE_SELECT");
		}
	}

	@description = "LPS-147200. Verify pending remote app portlet cannot be added to page"
	@priority = "4"
	test PendingRemoteAppCannotBeAddedToPage {
		property portal.acceptance = "true";

		task ("Apply workflow approval process to remote apps") {
			RemoteAppsWorkflow.applyWorkflow();
		}

		task ("Add user with permissions to create remote app") {
			Permissions.setUpRegRoleLoginUserCP(
				roleTitle = "New Regular Role",
				userEmailAddress = "userea@liferay.com",
				userFirstName = "userfn",
				userLastName = "userln",
				userScreenName = "usersn");

			RemoteAppsWorkflow.addUserPermissions();
		}

		task ("Create remote app as user with permissions") {
			User.logoutAndLoginPG(
				userLoginEmailAddress = "userea@liferay.com",
				userLoginFullName = "userfn userln");

			RemoteApps.goToRemoteAppsPortlet();

			RemoteAppsWorkflow.addApp(
				entryName = "Test Remote App",
				entryURL = "http://www.liferay.com");
		}

		task ("Verify remote app pending status as admin") {
			User.logoutAndLoginPG(
				userLoginEmailAddress = "test@liferay.com",
				userLoginFullName = "Test Test");

			RemoteApps.goToRemoteAppsPortlet();

			RemoteAppsWorkflow.assertTableStatus(
				entryName = "Test Remote App",
				entryStatus = "Pending");
		}

		task ("Create content page") {
			JSONLayout.addPublicLayout(
				groupName = "Guest",
				layoutName = "Test Page",
				type = "content");

			ContentPagesNavigator.openEditContentPage(
				pageName = "Test Page",
				siteName = "Guest");
		}

		task ("Assert remote app portlet cannot be added to page") {
			Type.sendKeysApplicationSearch(
				locator1 = "PageEditor#FRAGMENTS_AND_WIDGETS_SEARCH_FIELD",
				value1 = "Test Remote App");

			AssertElementPresent(
				key_infoMessage = "There are no fragments or widgets on this page.",
				locator1 = "Message#INFO_SPECIFIC");
		}
	}

}