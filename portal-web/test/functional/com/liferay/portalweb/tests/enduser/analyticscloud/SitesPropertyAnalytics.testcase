@component-name = "portal-analytics-cloud"
definition {

	property portal.release = "false";
	property portal.upstream = "true";
	property property.group = "analytics.cloud.profile";
	property testray.main.component.name = "Analytics Cloud";

	setUp {
		task ("Copy the token from AC") {
			ACUtils.launchAC();

			ACSettings.takeTokenFromAnalyticsCloud();
		}

		task ("Set up instance and sign in DXP") {
			TestCase.setUpPortalInstance();

			ACUtils.launchDXP();
		}

		task ("Add a new site with a public widget page") {
			ACUtils.addSiteAndPage();
		}

		task ("Connect the DXP to AC") {
			ACDXPSettings.connectDXPtoAnalyticsCloud(siteName = "Site Name");
		}
	}

	tearDown {
		var testPortalInstance = PropsUtil.get("test.portal.instance");

		ACUtils.tearDownDXP();

		if ("${testPortalInstance}" == "true") {
			PortalInstances.tearDownCP();
		}

		ACUtils.tearDownAC();
	}

	@description = "Feature ID: Legacy | Automation ID: LRAC-8330 | Test Summary: Assert Chart ticks for pages Visitor Behavior standardized"
	@ignore = "true"
	@priority = "5"
	test AssertChartTicksPagesVisitorBehaviorStandardized {

		// AC Refactor: data doesn't appear on time
		// AC Refactor ticket: LRAC-8994

		task ("View the site page as Test Test") {
			ACUtils.navigateToSitePage(
				pageName = "AC Page",
				siteName = "Site Name");
		}

		task ("Close sessions") {
			ACUtils.closeAllSessionsAndWait();
		}

		task ("Switch to new property in AC") {
			ACUtils.launchAC();

			ACProperties.switchProperty(propertyName = "${assignedPropertyName}");
		}

		task ("Navigate to Pages tab in Sites dashboard") {
			ACNavigation.goToSites();

			ACNavigation.switchTab(tabName = "Pages");
		}

		task ("Select the Last 24 hours in time period") {
			ACTimeFilter.clickTimeFilterButton();

			ACTimeFilter.setLast24Hours();
		}

		task ("Navigate to page report entry") {
			ACNavigation.openItem(itemName = "AC Page - Site Name - Liferay DXP");
		}

		task ("View the time period is Last 24 hours in Visitors Behavior chart") {
			ACTimeFilter.assertCurrentTimeFilterRange(
				cardName = "Visitors Behavior",
				timeFilterRange = "Last 24 hours");
		}

		task ("View the format of recharts xAxis ticks is h tt") {
			var time = ACCards.getAxisTick(
				axis = "xAxis",
				cardName = "Visitors Behavior");

			ACUtils.assertTimePattern(
				format = "h tt",
				time = "${time}");
		}

		task ("Select the Last 90 days time period in site metrics chart") {
			ACTimeFilter.clickTimeFilterButton(cardName = "Visitors Behavior");

			ACTimeFilter.setLast90Days();
		}

		task ("View the format of recharts xAxis ticks is h tt") {
			var date = ACCards.getAxisTick(
				axis = "xAxis",
				cardName = "Visitors Behavior");

			ACUtils.assertDatePattern(
				date = "${date}",
				format = "MMM d");
		}
	}

	@description = "Feature ID: Legacy | Automation ID: LRAC-8329 | Test Summary: Assert Chart ticks for Site Metrics standardized"
	@priority = "3"
	test AssertChartTicksSiteMetricsStandardized {
		task ("Switch to new property in AC") {
			ACUtils.launchAC();

			ACProperties.switchProperty(propertyName = "${assignedPropertyName}");
		}

		task ("Navigate to the Overview tab on Sites dashboard") {
			ACNavigation.goToSites();

			ACNavigation.switchTab(tabName = "Overview");
		}

		task ("View the time period is Last 30 days in site metrics chart") {
			ACTimeFilter.assertCurrentTimeFilterRange(
				cardName = "${assignedPropertyName} Activities",
				timeFilterRange = "Last 30 days");
		}

		task ("View the interval is D in site metrics chart") {
			ACCards.viewSelectedInterval(
				cardName = "${assignedPropertyName} Activities",
				interval = "D");
		}

		task ("View the format of recharts xAxis ticks is MMM d") {
			var date = ACCards.getAxisTick(
				axis = "xAxis",
				cardName = "${assignedPropertyName} Activities");

			ACUtils.assertDatePattern(
				date = "${date}",
				format = "MMM d");
		}

		task ("Select the W in interval selector") {
			ACCards.selectInterval(
				cardName = "${assignedPropertyName} Activities",
				interval = "W");
		}

		task ("View the format of recharts xAxis ticks is MMM d - MMM d or MMM d - d") {
			var date = ACCards.getAxisTick(
				axis = "xAxis",
				cardName = "${assignedPropertyName} Activities");

			var dateLength = StringUtil.length("${date}");

			if (("${dateLength}" == "9") || ("${dateLength}" == "10") || ("${dateLength}" == "11")) {
				ACUtils.assertDatePattern(
					date = "${date}",
					format = "MMM d - d");
			}
			else {
				ACUtils.assertDatePattern(
					date = "${date}",
					format = "MMM d - MMM d");
			}
		}

		task ("Select the M in interval selector") {
			ACCards.selectInterval(
				cardName = "${assignedPropertyName} Activities",
				interval = "M");
		}

		task ("View the format of recharts xAxis ticks is MMM") {
			var date = ACCards.getAxisTick(
				axis = "xAxis",
				cardName = "${assignedPropertyName} Activities");

			ACUtils.assertDatePattern(
				date = "${date}",
				format = "MMM");
		}

		task ("Select the Last 24 hours time period in site metrics chart") {
			ACTimeFilter.clickTimeFilterButton(cardName = "${assignedPropertyName} Activities");

			ACTimeFilter.setLast24Hours();
		}

		task ("View the interval option buttons are disabled") {
			for (var interval : list "D,W,M") {
				AssertVisible(
					key_text = "${interval}",
					locator1 = "Button#ANY_DISABLED");
			}
		}
	}

	@description = "Feature ID: Legacy | Automation ID: LRAC-8331 | Test Summary: Assert Site Metrics select Sessions/Visitor card tab"
	@priority = "3"
	test AssertSiteMetricsSelectSessionsPerVisitorCardTab {
		task ("Switch to new property in AC") {
			ACUtils.launchAC();

			ACProperties.switchProperty(propertyName = "${assignedPropertyName}");
		}

		task ("Navigate to the Overview tab on Sites dashboard") {
			ACNavigation.goToSites();

			ACNavigation.switchTab(tabName = "Overview");
		}

		task ("Navigate to the Sessions/Visitor tab") {
			ACNavigation.switchTabInCard(tabName = "Sessions/Visitor");
		}

		task ("View the Sessions/Visitor tab is active") {
			ACNavigation.viewActiveSwitchTabInCard(tabName = "Sessions/Visitor");
		}
	}

	@description = "Feature ID: Legacy | Automation ID: LRAC-8332 | Test Summary: Assert Site Metrics select Visitors card tab"
	@priority = "3"
	test AssertSiteMetricsSelectUniqueVisitorsCardTab {
		task ("Switch to new property in AC") {
			ACUtils.launchAC();

			ACProperties.switchProperty(propertyName = "${assignedPropertyName}");
		}

		task ("Navigate to the Overview tab on Sites dashboard") {
			ACNavigation.goToSites();

			ACNavigation.switchTab(tabName = "Overview");
		}

		task ("Navigate to the Unique Visitors tab") {
			ACNavigation.switchTabInCard(tabName = "Unique Visitors");
		}

		task ("View the Unique Visitors tab is active") {
			ACNavigation.viewActiveSwitchTabInCard(tabName = "Unique Visitors");
		}
	}

}