@component-name = "portal-wcm"
definition {

	property portal.release = "true";
	property portal.upstream = "true";
	property testray.main.component.name = "Content and Application Templates";

	setUp {
		task ("Set up instance and sign in") {
			TestCase.setUpPortalInstance();

			User.firstLoginPG();
		}

		task ("Add a site") {
			JSONGroup.addGroup(groupName = "Test Site Name");
		}

		task ("Enable local live with page versioning enabled") {
			JSONStaging.enableLocalStaging(
				branchingPrivate = "true",
				branchingPublic = "true",
				groupName = "Test Site Name");
		}
	}

	tearDown {
		var testPortalInstance = PropsUtil.get("test.portal.instance");

		if ("${testPortalInstance}" == "true") {
			PortalInstances.tearDownCP();
		}
		else {
			JSONGroup.deleteGroupByName(groupName = "Test Site Name");
		}
	}

	@description = "This is a test for LPS-130051. The user could apply widget template to Navigation Menu on content page when enable local live with page versioning."
	@priority = "4"
	test ApplyWidgetTemplateToNavigationMenuOnContentPage {
		task ("Add a content page") {
			Staging.openStagingAdmin(siteURLKey = "test-site-name-staging");

			JSONLayout.addPublicLayout(
				groupName = "Test Site Name (Staging)",
				layoutName = "Test Page Name",
				site = "false",
				type = "content");
		}

		task ("Add a Navigation Menu widget to content page") {
			ContentPagesNavigator.openEditContentPage(
				pageName = "Test Page Name",
				siteName = "Test Site Name Staging");

			PageEditor.addWidget(portletName = "Navigation Menu");
		}

		task ("Select the List Menu as display template in Navigation Menu") {
			NavigationMenusWidget.gotoNavigationMenuWidgetConfig();

			NavigationMenusWidget.selectDisplayTemplate(templateName = "List Menu");

			Portlet.saveAndCloseConfig();

			PageEditor.clickPublish();
		}

		task ("View the List Menu is applied to Navigation Menu at view mode in staging site") {
			ContentPagesNavigator.openViewContentPage(
				pageName = "Test Page Name",
				siteName = "Test Site Name Staging");

			AssertVisible(
				key_itemName = "Test Page Name",
				locator1 = "NavigationMenusWidget#LIST_MENU_ANY_ITEM");
		}

		task ("Publish to Live") {
			Staging.markAsReadyForPublication();

			Staging.gotoPublishToLive();

			Staging.publishToLive();
		}

		task ("View the List Menu is applied to Navigation Menu at view mode in live site") {
			ContentPagesNavigator.openViewContentPage(
				pageName = "Test Page Name",
				siteName = "Test Site Name");

			AssertVisible(
				key_itemName = "Test Page Name",
				locator1 = "NavigationMenusWidget#LIST_MENU_ANY_ITEM");
		}
	}

}