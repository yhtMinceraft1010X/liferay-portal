@component-name = "portal-wcm"
definition {

	property portal.release = "true";
	property portal.upstream = "true";
	property testray.main.component.name = "Style Books";

	setUp {
		task ("Set up instance and sign in") {
			TestCase.setUpPortalInstance();

			User.firstLoginPG();
		}

		task ("Add a site via JSON") {
			JSONGroup.addGroup(groupName = "Test Site Name");
		}
	}

	tearDown {
		var testPortalInstance = PropsUtil.get("test.portal.instance");

		if ("${testPortalInstance}" == "true") {
			PortalInstances.tearDownCP();
		}
		else {
			JSONGroup.deleteGroupByName(groupName = "Test Site Name");
		}
	}

	@description = "This is a test for LPS-121979 and LPS-120005. Change the configuration of page won't break the connection with its style book."
	@priority = "5"
	test ChangeConfigurationOfPageBasedOnStyleBook {
		property portal.acceptance = "true";
		property testray.component.names = "Frontend Token";

		task ("Add a style book") {
			StyleBooksAdmin.openStyleBooksAdmin(siteURLKey = "test-site-name");

			StyleBooksAdmin.addStyleBooks(styleBookName = "Test Style Book Name");
		}

		task ("Edit the style book and Publish it") {
			StyleBooks.selectForm(formName = "Typography");

			StyleBooks.expandSection(sectionName = "Font Family");

			Type(
				key_fieldLabel = "Font Family Base",
				locator1 = "TextInput#GENERIC_TEXT_INPUT",
				value1 = "'Courier New'");

			StyleBooks.clickManagementBar();

			StyleBooks.waitForAutoSave();

			StyleBooks.clickPublish();
		}

		task ("Add a content page") {
			JSONLayout.addPublicLayout(
				groupName = "Test Site Name",
				layoutName = "Test Page Name",
				type = "content");
		}

		task ("Add a Paragraph fragment to page") {
			ContentPagesNavigator.openEditContentPage(
				pageName = "Test Page Name",
				siteName = "Test Site Name");

			PageEditor.addFragment(
				collectionName = "Basic Components",
				fragmentName = "Paragraph");
		}

		task ("Change to the new style book via Page Design Options panel") {
			PageEditor.gotoTab(tabName = "Page Design Options");

			Navigator.gotoNavTab(navTab = "Style Book");

			AssertElementPresent(
				key_card = "Styles from Theme",
				locator1 = "Card#CARD_SELECTED");

			StyleBooks.selectStyleBookViaPageDesignOptions(styleBookName = "Test Style Book Name");

			var fontFamily = '''"Courier New"''';

			AssertCssValue(
				fragmentName = "paragraph",
				id = "element-text",
				index = "1",
				locator1 = "Fragment#CONTRIBUTED_FRAGMENT_TEXT",
				locator2 = "font-family",
				value1 = "${fontFamily}");

			PageEditor.clickPublish();
		}

		task ("Assert the style book applied to page in view mode") {
			ContentPagesNavigator.openViewContentPage(
				pageName = "Test Page Name",
				siteName = "Test Site Name");

			AssertCssValue(
				fragmentName = "paragraph",
				id = "element-text",
				index = "1",
				locator1 = "Fragment#CONTRIBUTED_FRAGMENT_TEXT",
				locator2 = "font-family",
				value1 = "${fontFamily}");
		}

		task ("Modify the page name") {
			Navigator.gotoEditPageViaControlMenu(pageName = "Test Page Name");

			PagesAdmin.editName(pageNameEdit = "New Test Page Name");
		}

		task ("Assert the style book still applied to page in view mode") {
			Navigator.gotoBack();

			AssertCssValue(
				fragmentName = "paragraph",
				id = "element-text",
				index = "1",
				locator1 = "Fragment#CONTRIBUTED_FRAGMENT_TEXT",
				locator2 = "font-family",
				value1 = "${fontFamily}");
		}
	}

	@description = "This is a test for LPS-116106. Page Creator can change style book of content page via Look and Feel settings."
	@priority = "4"
	test ChangeStyleBookOfContentPageViaLookAndFeel {
		task ("Add a style book") {
			StyleBooksAdmin.openStyleBooksAdmin(siteURLKey = "test-site-name");

			StyleBooksAdmin.addStyleBooks(styleBookName = "Test Style Book Name");
		}

		task ("Edit the style book and Publish it") {
			StyleBooks.selectForm(formName = "Buttons");

			StyleBooks.expandSection(sectionName = "Button Outline Secondary");

			Type(
				key_fieldLabel = "Color",
				key_groupLabel = "Button Outline Secondary",
				locator1 = "TextInput#GROUPED_TEXT_INPUT",
				value1 = "#00FF00");

			StyleBooks.clickManagementBar();

			StyleBooks.waitForAutoSave();

			StyleBooks.selectForm(formName = "Typography");

			StyleBooks.expandSection(sectionName = "Headings");

			Type(
				key_fieldLabel = "Heading 5 Font Size",
				locator1 = "TextInput#GENERIC_TEXT_INPUT",
				value1 = "2rem");

			StyleBooks.clickManagementBar();

			StyleBooks.waitForAutoSave();

			StyleBooks.clickPublish();
		}

		task ("Add a content page") {
			JSONLayout.addPublicLayout(
				groupName = "Test Site Name",
				layoutName = "Test Page Name",
				type = "content");
		}

		task ("Add a Heading fragment to page") {
			ContentPagesNavigator.openEditContentPage(
				pageName = "Test Page Name",
				siteName = "Test Site Name");

			PageEditor.addFragment(
				collectionName = "Basic Components",
				fragmentName = "Heading");
		}

		task ("Configure the Heading Level of Heading as H5") {
			PageEditor.gotoConfiguration(fragmentName = "Heading");

			PageEditor.configureFragment(headingLevel = "H5");

			PageEditor.viewFragmentStyle(
				fragmentName = "heading",
				headingLevel = "h5");
		}

		task ("Add a Button fragment below the Heading") {
			PageEditor.addFragment(
				collectionName = "Basic Components",
				fragmentName = "Button",
				targetFragmentName = "Heading");
		}

		task ("Configure the Type of Heading as Button Outline Secondary") {
			PageEditor.gotoConfiguration(fragmentName = "Button");

			PageEditor.configureFragment(type = "Outline Secondary");

			PageEditor.viewFragmentStyle(
				fragmentName = "button",
				type = "outline-secondary");
		}

		task ("Change to the new style book via Look and Feel") {
			PageEditor.gotoTab(tabName = "Page Design Options");

			Click(locator1 = "PageEditor#MORE_LINK");

			AssertTextEquals(
				key_styleBookName = "Styles from Theme",
				locator1 = "PagesAdmin#STYLE_BOOK_NAME",
				value1 = "Styles from Theme");

			AssertClick(
				key_text = "Change Style Book",
				locator1 = "Button#ANY",
				value1 = "Change Style Book");

			StyleBooks.selectStyleBook(styleBookName = "Test Style Book Name");

			AssertTextEquals(
				key_styleBookName = "Test Style Book Name",
				locator1 = "PagesAdmin#STYLE_BOOK_NAME",
				value1 = "Test Style Book Name");

			Button.clickSave();

			Alert.viewSuccessMessage();

			Navigator.gotoBack();

			AssertCssValue(
				fragmentName = "button",
				id = "link",
				index = "1",
				locator1 = "Fragment#CONTRIBUTED_FRAGMENT_TEXT",
				locator2 = "color",
				value1 = "rgba(0, 255, 0, 1)");

			AssertCssValue(
				fragmentName = "heading",
				id = "element-text",
				index = "1",
				locator1 = "Fragment#CONTRIBUTED_FRAGMENT_TEXT",
				locator2 = "font-size",
				value1 = "32px");

			PageEditor.clickPublish();
		}

		task ("Assert the new style book is applied to content page in view mode") {
			ContentPagesNavigator.openViewContentPage(
				pageName = "Test Page Name",
				siteName = "Test Site Name");

			AssertCssValue(
				key_content = "Go Somewhere",
				key_element = "a",
				key_id = "link",
				key_type = "link",
				locator1 = "StyleBookEditor#CONTRIBUTED_FRAGMENT_INLINE_CONTENT",
				locator2 = "color",
				value1 = "rgba(0, 255, 0, 1)");

			AssertCssValue(
				key_content = "Heading Example",
				key_element = "h5",
				key_id = "element-text",
				key_type = "text",
				locator1 = "StyleBookEditor#CONTRIBUTED_FRAGMENT_INLINE_CONTENT",
				locator2 = "font-size",
				value1 = "32px");
		}

		task ("Change to default style book via Look and Feel") {
			ContentPages.gotoPageEditor();

			PageEditor.gotoTab(tabName = "Page Design Options");

			Click(locator1 = "PageEditor#MORE_LINK");

			AssertTextEquals(
				key_styleBookName = "Test Style Book Name",
				locator1 = "PagesAdmin#STYLE_BOOK_NAME",
				value1 = "Test Style Book Name");

			AssertClick(
				key_text = "Change Style Book",
				locator1 = "Button#ANY",
				value1 = "Change Style Book");

			StyleBooks.selectStyleBook(styleBookName = "Styles from Theme");

			AssertTextEquals(
				key_styleBookName = "Styles from Theme",
				locator1 = "PagesAdmin#STYLE_BOOK_NAME",
				value1 = "Styles from Theme");

			Button.clickSave();

			Alert.viewSuccessMessage();

			Navigator.gotoBack();

			AssertCssValue(
				fragmentName = "button",
				id = "link",
				index = "1",
				locator1 = "Fragment#CONTRIBUTED_FRAGMENT_TEXT",
				locator2 = "color",
				value1 = "rgba(107, 108, 126, 1)");

			AssertCssValue(
				fragmentName = "heading",
				id = "element-text",
				index = "1",
				locator1 = "Fragment#CONTRIBUTED_FRAGMENT_TEXT",
				locator2 = "font-size",
				value1 = "14px");

			PageEditor.clickPublish();
		}

		task ("Assert the default style book is applied to content page in view mode") {
			ContentPagesNavigator.openViewContentPage(
				pageName = "Test Page Name",
				siteName = "Test Site Name");

			AssertCssValue(
				key_content = "Go Somewhere",
				key_element = "a",
				key_id = "link",
				key_type = "link",
				locator1 = "StyleBookEditor#CONTRIBUTED_FRAGMENT_INLINE_CONTENT",
				locator2 = "color",
				value1 = "rgba(107, 108, 126, 1)");

			AssertCssValue(
				key_content = "Heading Example",
				key_element = "h5",
				key_id = "element-text",
				key_type = "text",
				locator1 = "StyleBookEditor#CONTRIBUTED_FRAGMENT_INLINE_CONTENT",
				locator2 = "font-size",
				value1 = "14px");
		}
	}

	@description = "This is a test for LPS-116106. Page Creator can change style book of widget page via Look and Feel settings."
	@priority = "4"
	test ChangeStyleBookOfWidgetPageViaLookAndFeel {
		task ("Add a style book") {
			StyleBooksAdmin.openStyleBooksAdmin(siteURLKey = "test-site-name");

			StyleBooksAdmin.addStyleBooks(styleBookName = "Test Style Book Name");
		}

		task ("Edit the style book and Publish it") {
			StyleBooks.selectForm(formName = "Buttons");

			StyleBooks.expandSection(sectionName = "Button Primary");

			Type(
				key_fieldLabel = "Background Color",
				key_groupLabel = "Button Primary",
				locator1 = "TextInput#GROUPED_TEXT_INPUT",
				value1 = "#00FF00");

			StyleBooks.clickManagementBar();

			StyleBooks.waitForAutoSave();

			Pause(locator1 = "3000");

			StyleBooks.expandSection(sectionName = "Button Secondary");

			Type(
				key_fieldLabel = "Color",
				key_groupLabel = "Button Secondary",
				locator1 = "TextInput#GROUPED_TEXT_INPUT",
				value1 = "#FF0000");

			StyleBooks.clickManagementBar();

			StyleBooks.waitForAutoSave();

			StyleBooks.clickPublish();
		}

		task ("Add a widget page") {
			JSONLayout.addPublicLayout(
				groupName = "Test Site Name",
				layoutName = "Test Page Name");
		}

		task ("Add a Blogs portlet to page") {
			JSONLayout.addWidgetToPublicLayout(
				groupName = "Test Site Name",
				layoutName = "Test Page Name",
				widgetName = "Blogs");
		}

		task ("Change to the new style book via Look and Feel") {
			Navigator.gotoSitePage(
				pageName = "Test Page Name",
				siteName = "Test Site Name");

			Click(locator1 = "ControlMenu#EDIT_COG");

			Navigator.gotoNavTab(navTab = "Look and Feel");

			AssertTextEquals(
				key_styleBookName = "Styles from Theme",
				locator1 = "PagesAdmin#STYLE_BOOK_NAME",
				value1 = "Styles from Theme");

			AssertClick(
				key_text = "Change Style Book",
				locator1 = "Button#ANY",
				value1 = "Change Style Book");

			StyleBooks.selectStyleBook(styleBookName = "Test Style Book Name");

			AssertTextEquals(
				key_styleBookName = "Test Style Book Name",
				locator1 = "PagesAdmin#STYLE_BOOK_NAME",
				value1 = "Test Style Book Name");

			Button.clickSave();

			Alert.viewSuccessMessage();

			Navigator.gotoBack();

			AssertCssValue(
				key_text = "New Entry",
				locator1 = "Button#ANY",
				locator2 = "background-color",
				value1 = "rgba(0, 255, 0, 1)");

			AssertCssValue(
				key_text = "Subscribe",
				locator1 = "Button#ANY",
				locator2 = "color",
				value1 = "rgba(255, 0, 0, 1)");
		}

		task ("Change to default style book via Look and Feel") {
			Click(locator1 = "ControlMenu#EDIT_COG");

			Navigator.gotoNavTab(navTab = "Look and Feel");

			AssertTextEquals(
				key_styleBookName = "Test Style Book Name",
				locator1 = "PagesAdmin#STYLE_BOOK_NAME",
				value1 = "Test Style Book Name");

			AssertClick(
				key_text = "Change Style Book",
				locator1 = "Button#ANY",
				value1 = "Change Style Book");

			StyleBooks.selectStyleBook(styleBookName = "Styles from Theme");

			AssertTextEquals(
				key_styleBookName = "Styles from Theme",
				locator1 = "PagesAdmin#STYLE_BOOK_NAME",
				value1 = "Styles from Theme");

			Button.clickSave();

			Alert.viewSuccessMessage();

			Navigator.gotoBack();

			AssertCssValue(
				key_text = "New Entry",
				locator1 = "Button#ANY",
				locator2 = "background-color",
				value1 = "rgba(11, 95, 255, 1)");

			AssertCssValue(
				key_text = "Subscribe",
				locator1 = "Button#ANY",
				locator2 = "color",
				value1 = "rgba(107, 108, 126, 1)");
		}
	}

	@description = "This is a test for LPS-142078. The user could mark a new style book as default."
	@priority = "4"
	test MarkNewStyleBookAsDefault {
		task ("Add a style book") {
			JSONStylebooks.addStyleBookEntry(
				groupName = "Test Site Name",
				styleBookEntryName = "Test Style Book Name");
		}

		task ("Navigate to the Style Books admin") {
			StyleBooksAdmin.openStyleBooksAdmin(siteURLKey = "test-site-name");
		}

		task ("View the Styles from Theme is marked as default") {
			LexiconCard.viewCardSticker(
				card = "Styles from Theme",
				icon = "check-circle");
		}

		task ("Mark the new style book as default") {
			StyleBooksAdmin.markStyleBookAsDefault(
				confirmMessage = "Do you want to replace Styles from Theme for Test Style Book Name as the default style book?",
				styleBookName = "Test Style Book Name");
		}
	}

	@description = "This is a test for LPS-140834. The user could select style book in master page when use public pages theme."
	@priority = "4"
	test SelectStyleBookInMasterPageWhenUsePublicPagesTheme {
		property portal.release = "false";

		task ("Add a public page and change it's theme to Minium") {
			JSONLayout.addPublicLayout(
				groupName = "Test Site Name",
				layoutName = "Test Public Page");

			PagesAdmin.openPagesAdmin(siteURLKey = "test-site-name");

			PagesAdmin.changeSiteCurrentTheme(
				privacy = "Public Pages",
				themeName = "Minium");
		}

		task ("Add a private page and change it's theme to Speedwell") {
			JSONLayout.addPrivateLayout(
				groupName = "Test Site Name",
				layoutName = "Test Private Page");

			PagesAdmin.openPagesAdmin(siteURLKey = "test-site-name");

			PagesAdmin.changeSiteCurrentTheme(
				privacy = "Private Pages",
				themeName = "Speedwell");
		}

		task ("Add a style book") {
			StyleBooksAdmin.openStyleBooksAdmin(siteURLKey = "test-site-name");

			StyleBooksAdmin.addStyleBooks(styleBookName = "Test Style Book Name");
		}

		task ("Add a custom master page") {
			JSONLayoutpagetemplate.addMasterPageTemplateEntry(
				groupName = "Test Site Name",
				masterPageTemplateEntryName = "Custom Masters Page Template");
		}

		task ("Change master page's theme to Minium") {
			MastersPageTemplatesAdmin.openMastersAdmin(siteURLKey = "test-site-name");

			MastersPageTemplatesAdmin.gotoMasters(masterLayoutName = "Custom Masters Page Template");

			PageEditor.gotoTab(tabName = "Page Design Options");

			Click(locator1 = "PageEditor#MORE_LINK");

			Theme.configureLookAndFeelSpecificLook();

			Theme.changeCurrentTheme(themeName = "Minium");

			PortletEntry.save();

			Navigator.gotoBack();
		}

		task ("Select style book via Page Design Options panel") {
			PageEditor.gotoTab(tabName = "Page Design Options");

			Navigator.gotoNavTab(navTab = "Style Book");
		}
	}

	@description = "This is a test for LPS-142078. The user could view the default style book in Style Books admin but cannot delete and edit it."
	@priority = "5"
	test ViewDefaultStyleBook {
		property portal.acceptance = "true";

		task ("Navigate to the Style Books admin") {
			StyleBooksAdmin.openStyleBooksAdmin(siteURLKey = "test-site-name");
		}

		task ("View the Styles from Theme is shown") {
			StyleBooksAdmin.viewStyleBooksPresent(styleBookName = "Styles from Theme");
		}

		task ("View cannot edit or delete the Styles form Theme") {
			AssertElementNotPresent(
				key_card = "Blank",
				locator1 = "Card#CARD_ELLIPSIS");

			AssertElementNotPresent(
				key_card = "Blank",
				locator1 = "Card#CARD_CHECKBOX");

			AssertElementNotPresent(
				key_card = "Blank",
				locator1 = "Card#CARD_TITLE_LINK");
		}
	}

}