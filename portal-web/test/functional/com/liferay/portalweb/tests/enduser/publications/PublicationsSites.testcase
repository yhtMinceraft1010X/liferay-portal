@component-name = "portal-publications"
definition {

	property app.server.types = "jboss,tomcat,weblogic,websphere,wildfly";
	property database.types = "db2,hypersonic,mariadb,mysql,oracle,postgresql,sqlserver";
	property portal.release = "true";
	property portal.upstream = "true";
	property testray.main.component.name = "Publications";

	setUp {
		TestCase.setUpPortalInstance();

		User.firstLoginPG();

		JSONPublications.enablePublications();
	}

	tearDown {
		var testPortalInstance = PropsUtil.get("test.portal.instance");

		if ("${testPortalInstance}" == "true") {
			PortalInstances.tearDownCP();
		}
		else {
			Publications.disablePublications();
		}
	}

	@priority = "4"
	test PublishPrivateChildSite {
		JSONPublications.addPublication(publicationName = "Publication Name");

		JSONPublications.selectPublication(publicationName = "Publication Name");

		JSONGroup.addGroup(groupName = "Site Name");

		Site.openSitesAdmin();

		Site.addBlankCP(
			membershipType = "Private",
			parentSiteName = "Site Name",
			siteName = "Child Site Name");

		Site.openSitesAdmin();

		LexiconTable.clickEllipsisItem(
			item = "View Child Sites",
			tableEntry = "Site Name");

		Site.viewCP(
			siteName = "Child Site Name",
			siteType = "Private");

		ProductMenu.gotoPortlet(
			category = "Site Builder",
			portlet = "Pages",
			site = "Child Site Name");

		PagesAdmin.addPublicPage(pageName = "Child Site Page");

		PublicationsNavigator.gotoReviewChanges();

		PublicationsChanges.viewChanges(
			changeCategory = "Site",
			changeSite = "Site Name",
			changeTitle = "Child Site Name",
			changeType = "Added",
			changeUser = "Test Test");

		JSONPublications.publishPublication(publicationName = "Publication Name");

		Publications.viewPublicationPublished(publicationName = "Publication Name");

		Navigator.openSitePage(
			pageName = "Child Site Page",
			siteName = "Child Site Name");

		AssertTextNotPresent(value1 = "The requested resource could not be found.");
	}

	@priority = "4"
	test PublishSiteCreatedInPublication {
		JSONPublications.addPublication(publicationName = "Publication Name");

		JSONPublications.selectPublication(publicationName = "Publication Name");

		JSONGroup.addGroup(groupName = "Site Name");

		JSONLayout.addPublicLayout(
			groupName = "Site Name",
			layoutName = "Site Page");

		Site.openSitesAdmin();

		Site.viewCP(siteName = "Site Name");

		PublicationsNavigator.gotoReviewChanges();

		PublicationsChanges.viewChanges(
			changeCategory = "Site",
			changeSite = "Global",
			changeTitle = "Site Name",
			changeType = "Added",
			changeUser = "Test Test");

		JSONPublications.publishPublication(publicationName = "Publication Name");

		Publications.viewPublicationPublished(publicationName = "Publication Name");

		Navigator.openSitePage(
			pageName = "Site Page",
			siteName = "Site Name");

		AssertTextNotPresent(value1 = "The requested resource could not be found.");
	}

}