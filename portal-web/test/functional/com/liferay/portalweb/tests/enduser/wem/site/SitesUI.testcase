@component-name = "portal-wcm"
definition {

	property portal.release = "true";
	property portal.upstream = "true";
	property testray.component.names = "Site Administration";
	property testray.main.component.name = "Site Administration";

	setUp {
		TestCase.setUpPortalInstance();

		User.firstLoginPG();
	}

	tearDown {
		var testPortalInstance = PropsUtil.get("test.portal.instance");

		if ("${testPortalInstance}" == "true") {
			PortalInstances.tearDownCP();
		}
		else {
			Site.tearDownCP();
		}
	}

	@description = "This is a test for LPS-112992. The user could add a child site in Sites admin."
	@priority = "5"
	test AddChildSite {
		property portal.acceptance = "true";
		property testray.component.names = "Training";

		task ("Add a site via JSON") {
			JSONGroup.addGroup(groupName = "Site Name");
		}

		task ("Add a child site") {
			ApplicationsMenu.gotoPortlet(
				category = "Sites",
				panel = "Control Panel",
				portlet = "Sites");

			Site.addBlankCP(
				parentSiteName = "Site Name",
				siteName = "Child SiteName");
		}

		task ("View child site added") {
			ApplicationsMenu.gotoPortlet(
				category = "Sites",
				panel = "Control Panel",
				portlet = "Sites");

			Site.viewChildCP(
				childSiteName = "Child SiteName",
				siteName = "Site Name");
		}
	}

	@description = "Able to add multiple sites basing on different site template."
	@priority = "4"
	@uitest
	test AddSitesMultiple {
		task ("Add a blank site") {
			Site.openSitesAdmin();

			Site.addBlankCP(siteName = "Site1 Name");
		}

		task ("View blank site added") {
			Site.openSitesAdmin();

			Site.viewCP(siteName = "Site1 Name");
		}

		task ("Add a site tempalte and add a site basing on site template") {
			Site.openSitesAdmin();

			SiteTemplates.addCP(siteTemplateName = "Site Template Name");

			Site.openSitesAdmin();

			Site.addCP(
				siteName = "Site2 Name",
				siteTemplateName = "Site Template Name",
				siteType = "Site Template");
		}

		task ("View site added") {
			Site.openSitesAdmin();

			Site.viewCP(siteName = "Site2 Name");
		}

		task ("Add a site basing on Minium site template") {
			Site.openSitesAdmin();

			Site.addSiteTemplateCP(
				siteName = "Site3 Name",
				siteTemplateName = "Minium");
		}

		task ("View site added") {
			Site.openSitesAdmin();

			Site.viewCP(siteName = "Site3 Name");
		}
	}

	@description = "This is a test for LPS-149293. The user could cancel the site creation."
	@priority = "4"
	test CancelSiteCreation {
		task ("Navigate to the Sites admin") {
			Site.openSitesAdmin();
		}

		task ("Cancel site creation after typing name") {
			LexiconEntry.gotoAdd();

			LexiconCard.clickCard(card = "Blank");

			SelectFrame(locator1 = "IFrame#MODAL_BODY");

			PortletEntry.inputName(name = "Test Site Name");

			Button.clickCancel();
		}

		task ("View the new site is not created") {
			Site.openSitesAdmin();

			LexiconTable.viewTableEntryNotPresent(tableEntry = "Test Site Name");
		}
	}

	@description = "Able to delete site."
	@priority = "5"
	@uitest
	test DeleteSite {
		property portal.acceptance = "true";

		task ("Add a blank site") {
			Site.openSitesAdmin();

			Site.addBlankCP(siteName = "Site Name");
		}

		task ("Assert able to delete the site") {
			Site.openSitesAdmin();

			Site.deleteCP(siteName = "Site Name");
		}
	}

}