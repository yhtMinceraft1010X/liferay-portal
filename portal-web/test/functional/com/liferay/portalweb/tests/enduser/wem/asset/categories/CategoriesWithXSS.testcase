@component-name = "portal-wcm"
definition {

	property portal.release = "true";
	property portal.upstream = "true";
	property testray.main.component.name = "Categories";

	setUp {
		task ("Set up instance and sign in") {
			TestCase.setUpPortalInstance();

			User.firstLoginPG();
		}

		task ("Add a site") {
			JSONGroup.addGroup(groupName = "Test Site Name");
		}
	}

	tearDown {
		var testPortalInstance = PropsUtil.get("test.portal.instance");

		if ("${testPortalInstance}" == "true") {
			PortalInstances.tearDownCP();
		}
		else {
			JSONGroup.deleteGroupByName(groupName = "Test Site Name");
		}
	}

	@description = "This is a test for LPS-121449. No alert shown when add a vocabulary in site with XSS name."
	@priority = "4"
	@uitest
	test AddVocabularyInSiteWithXSSName {
		var siteName = '''<script>alert(123)</script>''';

		task ("Add a site with XSS name") {
			ApplicationsMenu.gotoPortlet(
				category = "Sites",
				panel = "Control Panel",
				portlet = "Sites");

			Site.addBlankCP(siteName = "${siteName}");
		}

		task ("Add a vocabulary") {
			ProductMenu.gotoPortlet(
				category = "Categorization",
				portlet = "Categories");

			Vocabulary.addCP(vocabularyName = "Vocabulary Name");
		}

		task ("View the alert not shown") {
			AssertAlertNotPresent();
		}
	}

	@description = "This is a test for LPS-121450. No alert shown when add a vocabulary in site where existing structure and document type have XSS name."
	@priority = "4"
	@uitest
	test AddVocabularyWhenExistingStructureAndDocumentTypeWithXSSName {
		var documentName = '''<script>alert(123)</script>''';

		var structureName = '''<script>alert(456)</script>''';

		task ("Add a document type with XSS name") {
			DMNavigator.openDocumentsAndMediaAdmin(siteURLKey = "test-site-name");

			DMNavigator.gotoDocumentType();

			DMDocumentType.add(
				dmDocumentTypeFieldNames = "Text",
				dmDocumentTypeName = "${documentName}");
		}

		task ("View the XSS is not escaped") {
			LexiconEntry.viewEntryName(rowEntry = "${documentName}");
		}

		task ("Add a web content structure with XSS name") {
			WebContentNavigator.openWebContentAdmin(siteURLKey = "test-site-name");

			NavItem.gotoStructures();

			WebContentStructures.addCP(structureName = "${structureName}");

			DataEngine.addField(
				fieldFieldLabel = "Text",
				fieldName = "Text");

			WebContentStructures.saveCP();
		}

		task ("View the XSS is not escaped") {
			WebContentStructures.viewCP(structureName = "${structureName}");
		}

		task ("View no alert shown when create a vocabulary") {
			Category.openCategoriesAdmin(siteURLKey = "test-site-name");

			Vocabulary.gotoAddVocabulary();

			AssertAlertNotPresent();
		}
	}

	@description = "This is a test for LPS-132705. The user could select a category with XSS name in Account Settings."
	@priority = "4"
	@uitest
	test SelectCategoryWithXSSNameInAccountSettings {
		task ("Add a global category with XSS name in Topic") {
			Category.openCategoriesAdmin(siteURLKey = "global");

			Category.addCP(
				categoryName = '''><img src=x onerror=alert()>''',
				vocabularyName = "Topic");
		}

		task ("Navigate to Account Settings") {
			UserBar.gotoDropdownItem(dropdownItem = "Account Settings");
		}

		task ("Select the new category") {
			Click(locator1 = "Button#SELECT_CATEGORIES");

			AssetCategorization.selectCategory(categoryName = '''><img src=x onerror=alert()>''');
		}

		task ("View the selected category in Account Settings") {
			AssertVisible(
				key_categoryName = '''><img src=x onerror=alert()>''',
				locator1 = "AssetCategorization#CATEGORY");
		}

		task ("Save change") {
			PortletEntry.save();
		}

		task ("Refresh the page") {
			Refresh();
		}

		task ("View the alert not shown") {
			AssertAlertNotPresent();
		}
	}

	@description = "This is a test for LPS-123823. The user can select a category with XSS name on the page settings."
	@priority = "4"
	@uitest
	test SelectCategoryWithXSSNameInPageConfiguration {
		var categoryName = '''><img src=xss onerror=alert(1)>''';

		task ("Add a global category with XSS name in Topic") {
			Category.openCategoriesAdmin(siteURLKey = "global");

			Category.addCP(
				categoryName = "${categoryName}",
				vocabularyName = "Topic");
		}

		task ("Add a widget page") {
			JSONLayout.addPublicLayout(
				groupName = "Test Site Name",
				layoutName = "Test Page Name");
		}

		task ("Select the category in page configuration") {
			SitePages.openPagesAdmin(siteURLKey = "test-site-name");

			SitePages.gotoPageMenuItem(
				menuItem = "Configure",
				pageName = "Test Page Name");

			Click(
				key_vocabularyName = "Topic",
				locator1 = "AssetCategorization#CATEGORY_SELECT");

			AssetCategorization.selectCategory(categoryName = "${categoryName}");
		}

		task ("View the alert not shown") {
			AssertAlertNotPresent();
		}
	}

}