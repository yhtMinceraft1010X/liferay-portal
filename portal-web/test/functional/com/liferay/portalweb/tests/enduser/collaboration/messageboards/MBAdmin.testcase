@component-name = "portal-lima"
definition {

	property portal.acceptance = "true";
	property portal.release = "true";
	property portal.upstream = "true";
	property testray.main.component.name = "Message Boards";

	setUp {
		TestCase.setUpPortalInstance();

		User.firstLoginPG();
	}

	tearDown {
		var testPortalInstance = PropsUtil.get("test.portal.instance");

		if ("${testPortalInstance}" == "true") {
			PortalInstances.tearDownCP();
		}
		else {
			MessageboardsCategory.tearDownCP();

			MessageboardsThread.tearDownCP();

			MessageboardsPortlet.tearDownConfigurationGeneralCP();
		}
	}

	@priority = "5"
	@refactordone
	@uitest
	test CanAddCategory {
		property portal.acceptance = "true";

		MessageBoardsAdmin.openMessageBoardsAdmin(siteURLKey = "guest");

		MessageboardsCategory.addCP(categoryName = "MB Category Name");

		MessageboardsCategory.viewCP(
			categoryCount = "0 Categories",
			categoryName = "MB Category Name",
			threadCount = "0");
	}

	@priority = "4"
	@refactordone
	test CanAddSubcategory {
		JSONMBMessage.addCategory(
			categoryDescription = "MB Category Description",
			categoryName = "MB Category Name",
			userEmailAddress = "test@liferay.com");

		var parentCategoryId = JSONMBMessage.getCategoryIdByName(
			categoryName = "MB Category Name",
			groupName = "Guest");

		JSONMBMessage.addCategory(
			categoryDescription = "MB Subcategory Description",
			categoryName = "MB Subcategory Name",
			groupName = "Guest",
			parentCategoryId = "${parentCategoryId}",
			userEmailAddress = "test@liferay.com");

		MessageboardsCategory.openToCategory(
			categoryName = "MB Category Name",
			groupName = "Guest",
			siteURLKey = "guest");

		MessageboardsCategory.viewCP(
			categoryCount = "0 Categories",
			categoryName = "MB Subcategory Name",
			threadCount = "0");
	}

	@priority = "5"
	@refactordone
	@uitest
	test CanAddThread {
		property app.server.types = "jboss,tcserver,tomcat,weblogic,websphere,wildfly";
		property database.types = "db2,hypersonic,mariadb,mysql,oracle,postgresql,sqlserver,sybase";
		property environment.acceptance = "true";
		property portal.acceptance = "true";

		MessageBoardsAdmin.openMessageBoardsAdmin(siteURLKey = "guest");

		MessageboardsThread.addCP(
			threadBody = "MB Thread Message Body",
			threadSubject = "MB Thread Message Subject");

		MessageBoardsAdmin.openMessageBoardsAdmin(siteURLKey = "guest");

		MessageboardsThread.viewCP(
			replyCount = "0",
			threadBody = "MB Thread Message Body",
			threadSubject = "MB Thread Message Subject");
	}

	@description = "This is a test for LPS-136921. It checks that the ban length and expiration of a user can be changed."
	@priority = "3"
	test CanChangeBanLengthAndExpiration {
		property custom.properties = "jsonws.web.service.paths.excludes=${line.separator}message.boards.expire.ban.interval=0${line.separator}company.default.time.zone=America/Los_Angeles";
		property portal.acceptance = "false";

		MessageboardsThread.updateCheckInterval(fieldValue = "1");

		JSONLayout.addPublicLayout(
			groupName = "Guest",
			layoutName = "Message Boards Page");

		JSONLayout.addWidgetToPublicLayout(
			column = "1",
			groupName = "Guest",
			layoutName = "Message Boards Page",
			widgetName = "Message Boards");

		JSONUser.addUser(
			userEmailAddress = "userea1@liferay.com",
			userFirstName = "userfn1",
			userLastName = "userln1",
			userScreenName = "usersn1");

		JSONUser.setFirstPassword(
			agreeToTermsAndAnswerReminderQuery = "true",
			requireReset = "false",
			userEmailAddress = "userea1@liferay.com");

		PermissionsInline.gotoMessageBoardsPG(pageName = "Message Boards Page");

		for (var permissionDefinitionKey : list "INLINE_PERMISSIONS_REPLY_TO_MESSAGE_CHECKBOX,INLINE_PERMISSIONS_VIEW_CHECKBOX") {
			Role.definePermissionPG(
				permissionDefinitionKey = "${permissionDefinitionKey}",
				roleTitle = "User");
		}

		JSONMBMessage.addMessage(
			groupName = "Guest",
			threadBody = "MB Thread Message Body",
			threadSubject = "MB Thread Message Subject");

		User.logoutAndLoginPG(
			userLoginEmailAddress = "userea1@liferay.com",
			userLoginFullName = "userfn1 userln1");

		Navigator.gotoPage(pageName = "Message Boards Page");

		MessageboardsThread.replyPG(
			threadReplyBody = "MB Thread1 Reply Body",
			threadSubject = "MB Thread Message Subject");

		User.logoutAndLoginPG(
			userLoginEmailAddress = "test@liferay.com",
			userLoginFullName = "Test Test");

		MessageboardsThread.openToThread(
			groupName = "Guest",
			layoutName = "message-boards-page",
			siteURLKey = "guest",
			threadSubject = "MB Thread Message Subject");

		MessageboardsThread.banUser(
			threadReplyBody = "MB Thread1 Reply Body",
			userName = "userfn1 userln1");

		MessageboardsPortlet.viewBannedUserCP(
			disableAutoExpire = "true",
			siteURLKey = "Guest",
			userScreenName = "userfn1 userln1");
	}

	@priority = "4"
	@refactordone
	test CanDeleteCategories {
		for (var num : list "1,2,3") {
			JSONMBMessage.addCategory(
				categoryDescription = "MB Category Description",
				categoryName = "MB Category${num} Name",
				userEmailAddress = "test@liferay.com");
		}

		MessageBoardsAdmin.openMessageBoardsAdmin(siteURLKey = "guest");

		MessageboardsCategory.deleteCP();

		MessageboardsCategory.viewDefault();
	}

	@priority = "5"
	@refactordone
	test CanEditThreadSubjectAndBody {
		JSONMBMessage.addMessage(
			groupName = "Guest",
			threadBody = "MB Thread Message Body",
			threadSubject = "MB Thread Message Subject");

		JSONMBMessage.updateMessage(
			groupName = "Guest",
			threadBodyEdit = "MB Thread Message Body Edit",
			threadSubject = "MB Thread Message Subject",
			threadSubjectEdit = "MB Thread Message Subject Edit");

		MessageBoardsAdmin.openMessageBoardsAdmin(siteURLKey = "guest");

		MessageboardsThread.viewCP(
			replyCount = "0",
			threadBody = "MB Thread Message Body Edit",
			threadSubject = "MB Thread Message Subject Edit");
	}

	@description = "This is a test for LPS-136918. It checks that a category can be exported."
	@priority = "3"
	test CanExportCategory {
		JSONMBMessage.addCategory(
			categoryDescription = "MB Category Description",
			categoryName = "MB Category Name",
			userEmailAddress = "test@liferay.com");

		MessageBoardsAdmin.openMessageBoardsAdmin(siteURLKey = "guest");

		LAR.exportPortlet(larFileName = "MB_portlet.lar");

		MessageboardsThread.assertDownloadedFileNameFromTempFolder(exportedFileName = "MB_portlet");
	}

	@description = "This is a test for LPS-136917. It checks that a category can be imported."
	@priority = "3"
	test CanImportCategory {
		MessageBoardsAdmin.openMessageBoardsAdmin(siteURLKey = "guest");

		LAR.importPortlet(
			initializeLiferayEvent = "true",
			larFileName = "message-boards-category.lar",
			uploadFrom = "dependencies");

		MessageBoardsAdmin.openMessageBoardsAdmin(siteURLKey = "guest");

		MessageboardsCategory.viewCP(
			categoryCount = "0 Categories",
			categoryName = "MB Category Name",
			threadCount = "0");
	}

	@description = "This is a test for LPS-136923. It checks that a user can be unbanned."
	@priority = "3"
	test CanUnbanUser {
		property portal.acceptance = "false";

		JSONLayout.addPublicLayout(
			groupName = "Guest",
			layoutName = "Message Boards Page");

		JSONLayout.addWidgetToPublicLayout(
			column = "1",
			groupName = "Guest",
			layoutName = "Message Boards Page",
			widgetName = "Message Boards");

		JSONUser.addUser(
			userEmailAddress = "userea@liferay.com",
			userFirstName = "userfn",
			userLastName = "userln",
			userScreenName = "usersn");

		JSONUser.setFirstPassword(
			agreeToTermsAndAnswerReminderQuery = "true",
			requireReset = "false",
			userEmailAddress = "userea@liferay.com");

		PermissionsInline.gotoMessageBoardsPG(pageName = "Message Boards Page");

		for (var permissionDefinitionKey : list "INLINE_PERMISSIONS_REPLY_TO_MESSAGE_CHECKBOX,INLINE_PERMISSIONS_VIEW_CHECKBOX") {
			Role.definePermissionPG(
				permissionDefinitionKey = "${permissionDefinitionKey}",
				roleTitle = "User");
		}

		JSONMBMessage.addMessage(
			groupName = "Guest",
			threadBody = "MB Thread Message Body",
			threadSubject = "MB Thread Message Subject");

		User.logoutAndLoginPG(
			userLoginEmailAddress = "userea@liferay.com",
			userLoginFullName = "userfn userln");

		Navigator.gotoPage(pageName = "Message Boards Page");

		MessageboardsThread.replyPG(
			threadReplyBody = "MB Thread Reply Body",
			threadSubject = "MB Thread Message Subject");

		User.logoutAndLoginPG(
			userLoginEmailAddress = "test@liferay.com",
			userLoginFullName = "Test Test");

		MessageboardsThread.openToThread(
			groupName = "Guest",
			layoutName = "message-boards-page",
			siteURLKey = "guest",
			threadSubject = "MB Thread Message Subject");

		MessageboardsThread.banUser(
			threadReplyBody = "MB Thread Reply Body",
			userName = "userfn userln");

		MessageboardsThread.unbanUser(
			siteURLKey = "guest",
			userScreenName = "userfn userln");

		MessageboardsThread.viewNoBannedUsers();
	}

	@description = "This is a test for LPS-82508. It ensures that the number of threads and categories in a message board reflects the whole hierarchy."
	@ignore = "Test Stub"
	@priority = "5"
	test CanViewCountsFromRootCategory {
		property portal.acceptance = "false";

		// TODO LPS-148587 CanViewCountsFromRootCategory pending implementation

	}

}