@component-name = "portal-wcm"
definition {

	property portal.release = "true";
	property portal.upstream = "true";
	property testray.main.component.name = "Site Pages Administration";

	setUp {
		TestCase.setUpPortalInstance();

		User.firstLoginPG();

		JSONGroup.addGroup(groupName = "Test Site Name");
	}

	tearDown {
		var testPortalInstance = PropsUtil.get("test.portal.instance");

		if ("${testPortalInstance}" == "true") {
			PortalInstances.tearDownCP();
		}
		else {
			JSONGroup.deleteGroupByName(groupName = "Test Site Name");
		}
	}

	@priority = "3"
	test AddPageAndChildPageTranslations {
		property test.name.skip.portal.instance = "LocalizationWithSitePages#AddPageAndChildPageTranslations";
		property testray.component.names = "Localization,Site Administration,Site Localization,Site Pages Administration";

		var portalURL = PropsUtil.get("portal.url");

		JSONLayout.addPublicLayout(
			groupName = "Test Site Name",
			layoutName = "Home");

		SitePages.openPagesAdmin(siteURLKey = "test-site-name");

		SitePages.gotoPageMenuItem(
			menuItem = "Configure",
			pageName = "Home");

		Page.addPageTranslations(
			page = "Home",
			pageTranslation = "es_ES",
			pageTranslationName = "Casa");

		Navigator.openSiteURL(siteName = "Test Site Name");

		Page.viewTranslationsPG(
			defaultLanguageLocalizationMessage = "Display the page in English (United States).",
			pageName = "Casa",
			preferredLanguageLocalizationMessage = "Establecer español (España) como su idioma preferido.",
			sitePageURL = "${portalURL}/es/web/test-site-name/home",
			translationLanguageLocalizationMessage = "This page is displayed in Spanish (Spain).");

		Navigator.openSiteURL(siteName = "Test Site Name");

		Page.localizedPG(pageLocale = "Display the page in English (United States)");

		JSONLayout.addPublicLayout(
			groupName = "Test Site Name",
			layoutName = "Blog Page",
			parentLayoutName = "Home");

		SitePages.openPagesAdmin(siteURLKey = "test-site-name");

		SitePages.gotoPageMenuItem(
			menuItem = "Configure",
			pageName = "Blog Page",
			parentPagesHierarchy = "Home");

		Page.addPageTranslations(
			page = "Blog Page",
			pageTranslation = "es_ES",
			pageTranslationName = "Blog Página");

		Navigator.openSiteURL(siteName = "Test Site Name");

		Page.viewTranslationsPG(
			childPageName = "Blog Página",
			defaultLanguageLocalizationMessage = "Display the page in English (United States).",
			preferredLanguageLocalizationMessage = "Establecer español (España) como su idioma preferido.",
			sitePageURL = "${portalURL}/es/web/test-site-name/blog-page",
			translationLanguageLocalizationMessage = "This page is displayed in Spanish (Spain).");
	}

	@description = "Users can preview localized draft for a content page."
	@priority = "3"
	test PreviewLocalizedDraftContentPage {
		task ("Add a content page") {
			JSONLayout.addPublicLayout(
				groupName = "Test Site Name",
				layoutName = "Test Page Name",
				type = "content");
		}

		task ("Add a Button fragment to page") {
			ContentPagesNavigator.openEditContentPage(
				pageName = "Test Page Name",
				siteName = "Test Site Name");

			PageEditor.addFragment(
				collectionName = "Basic Components",
				fragmentName = "Button");
		}

		task ("Change the locale to ca-ES") {
			PageEditor.changeLocale(
				currentLocale = "en-US",
				locale = "ca-ES");
		}

		task ("Add the localized inline text") {
			PageEditor.editFragmentText(
				fragmentName = "Button",
				id = "link",
				text = "Aneu a algun lloc");
		}

		task ("Navigate to the Pages admin") {
			SitePages.openPagesAdmin(siteURLKey = "test-site-name");
		}

		task ("Change the user language to ca-ES") {
			Navigator.ensureLocaleURL(locale = "ca");
		}

		task ("Preview the localized draft content page in a new tab") {
			SitePages.gotoPageMenuItem(
				menuItem = "Previsualitza l'esborrany",
				pageName = "Test Page Name");

			SelectWindow.selectPopUp(locator1 = "null");

			ControlMenu.viewHeaderTitle(headerTitle = "Test Page Name");

			ContentPages.viewFragmentText(
				fragmentName = "button",
				id = "link",
				text = "Aneu a algun lloc");
		}
	}

}