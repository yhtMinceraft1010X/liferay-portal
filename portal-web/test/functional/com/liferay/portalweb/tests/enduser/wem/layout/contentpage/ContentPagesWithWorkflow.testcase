@component-name = "portal-wcm"
definition {

	property custom.properties = "jsonws.web.service.paths.excludes=";
	property portal.release = "true";
	property portal.upstream = "true";
	property testray.main.component.name = "Site Pages Administration";

	setUp {
		task ("Set up instance and sign in") {
			TestCase.setUpPortalInstance();

			User.firstLoginPG();
		}

		task ("Add a site") {
			JSONGroup.addGroup(groupName = "Test Site Name");
		}

		task ("Add a new user") {
			JSONUser.addUser(
				userEmailAddress = "userea@liferay.com",
				userFirstName = "userfn",
				userLastName = "userln",
				userScreenName = "usersn");

			JSONUser.setFirstPassword(
				agreeToTermsAndAnswerReminderQuery = "true",
				requireReset = "false",
				userEmailAddress = "userea@liferay.com");
		}

		task ("Assign the new user to site") {
			JSONGroup.assignUserToGroup(
				groupName = "Test Site Name",
				userEmailAddress = "userea@liferay.com");
		}

		task ("Add a new site role") {
			JSONRole.addSiteRole(roleTitle = "Page Author");
		}

		task ("Define the permissions of new site role") {
			for (var resourceAction : list "UPDATE,UPDATE_LAYOUT_CONTENT,VIEW") {
				Permissions.definePermissionViaJSONAPI(
					resourceAction = "${resourceAction}",
					resourceName = "com.liferay.portal.kernel.model.Layout",
					roleTitle = "Page Author",
					roleType = "site");
			}

			for (var resourceAction : list "ACCESS_IN_CONTROL_PANEL,VIEW") {
				Permissions.definePermissionViaJSONAPI(
					resourceAction = "${resourceAction}",
					resourceName = "com_liferay_layout_admin_web_portlet_GroupPagesPortlet",
					roleTitle = "Page Author",
					roleType = "site");
			}

			Permissions.definePermissionViaJSONAPI(
				resourceAction = "VIEW",
				resourceName = "com.liferay.layout.seo",
				roleTitle = "Page Author",
				roleType = "site");

			Permissions.definePermissionViaJSONAPI(
				resourceAction = "VIEW_SITE_ADMINISTRATION",
				resourceName = "com.liferay.portal.kernel.model.Group",
				roleTitle = "Page Author",
				roleType = "site");
		}

		task ("Assign site role to new user") {
			JSONGroup.assignGroupRoleToUser(
				groupName = "Test Site Name",
				roleTitle = "Page Author",
				userEmailAddress = "userea@liferay.com");
		}

		task ("Add a content page with Heading fragment") {
			JSONLayout.addPublicLayout(
				groupName = "Test Site Name",
				layoutName = "Test Page Name",
				type = "content");

			ContentPagesNavigator.openEditContentPage(
				pageName = "Test Page Name",
				siteName = "Test Site Name");

			PageEditor.addFragment(
				collectionName = "Basic Components",
				fragmentName = "Heading");

			PageEditor.clickPublish();
		}

		task ("Enable Single Approver for Content Page") {
			Workflow.openWorkflowAdmin(siteURLKey = "test-site-name");

			Workflow.configureWorkflow(
				workflowDefinition = "Single Approver",
				workflowResourceValue = "Content Page");
		}

		task ("Sign in as new user") {
			User.logoutAndLoginPG(userLoginEmailAddress = "userea@liferay.com");
		}

		task ("Edit inline text of Heading fragment then submit for application") {
			Navigator.gotoSitePage(
				pageName = "Test Page Name",
				siteName = "Test Site Name");

			ContentPages.gotoPageEditor();

			PageEditor.editFragmentText(
				fragmentName = "Heading",
				id = "text",
				text = "New editable fragment text");

			PageEditor.submitForPublication();
		}

		task ("View pending status is shown") {
			PagesAdmin.openPagesAdmin(siteURLKey = "Test Site Name");

			PagesAdmin.viewPageStatus(
				index = "2",
				pageName = "Test Page Name",
				status = "Pending");
		}
	}

	tearDown {
		var testPortalInstance = PropsUtil.get("test.portal.instance");

		if ("${testPortalInstance}" == "true") {
			PortalInstances.tearDownCP();
		}
		else {
			User.logoutAndLoginPG(userLoginEmailAddress = "test@liferay.com");

			JSONUser.tearDownNonAdminUsers();

			JSONRole.deleteRole(roleTitle = "Page Author");

			JSONGroup.deleteGroupByName(groupName = "Test Site Name");
		}
	}

	@description = "This is a test for LPS-98384. Assert the modification won't be shown in content page for guests until approve."
	@priority = "3"
	test CannotViewModificationUntilApproveAsGuest {
		task ("View guests can not view modification before approve") {
			User.logoutPG();

			Navigator.gotoSitePage(
				pageName = "Test Page Name",
				siteName = "Test Site Name");

			ContentPages.viewFragmentText(
				fragmentName = "heading",
				id = "element-text",
				text = "Heading Example");
		}

		task ("Approve latest modification") {
			User.loginPG(userEmailAddress = "test@liferay.com");

			UserBar.gotoDropdownItem(dropdownItem = "My Workflow Tasks");

			Workflow.assignToMeTaskByActions(
				workflowAssetTitle = "Test Page Name",
				workflowAssetType = "Content Page",
				workflowTask = "Review");

			Workflow.approveTaskByActions(
				workflowAssetTitle = "Test Page Name",
				workflowAssetType = "Content Page",
				workflowTask = "Review");
		}

		task ("View pending status is not shown") {
			PagesAdmin.openPagesAdmin(siteURLKey = "Test Site Name");

			AssertElementNotPresent(
				index = "2",
				key_itemName = "Test Page Name",
				locator1 = "PagesAdmin#LIST_GROUP_ITEM_PAGE_STATUS");
		}

		task ("View modification is shown to guest after approve") {
			User.logoutPG();

			Navigator.gotoSitePage(
				pageName = "Test Page Name",
				siteName = "Test Site Name");

			ContentPages.viewFragmentText(
				fragmentName = "heading",
				id = "element-text",
				text = "New editable fragment text");
		}
	}

	@description = "This is a test for LPS-98384. The page author can edit content page after reject or approve."
	@priority = "3"
	test EditContentPageAfterRejectOrApproveAsPageAuthor {
		task ("Reviewer rejects the request of modification") {
			User.logoutAndLoginPG(userLoginEmailAddress = "test@liferay.com");

			UserBar.gotoDropdownItem(dropdownItem = "My Workflow Tasks");

			Workflow.assignToMeTaskByActions(
				workflowAssetTitle = "Test Page Name",
				workflowAssetType = "Content Page",
				workflowTask = "Review");

			Workflow.rejectTaskByActions(
				workflowAssetTitle = "Test Page Name",
				workflowAssetType = "Content Page",
				workflowTask = "Review");
		}

		task ("View page author can edit page at view mode after rejected") {
			User.logoutAndLoginPG(userLoginEmailAddress = "userea@liferay.com");

			Navigator.gotoSitePage(
				pageName = "Test Page Name",
				siteName = "Test Site Name");

			ContentPages.viewFragmentText(
				fragmentName = "heading",
				id = "element-text",
				text = "Heading Example");

			AssertElementPresent(locator1 = "Icon#EDIT_PENCIL");
		}

		task ("View pending status is shown") {
			PagesAdmin.openPagesAdmin(siteURLKey = "Test Site Name");

			PagesAdmin.viewPageStatus(
				index = "2",
				pageName = "Test Page Name",
				status = "Pending");
		}

		task ("View edit action is shown in Pages admin") {
			PagesAdmin.openPageEllipsisMenu(pageName = "Test Page Name");

			MenuItem.viewVisible(menuItem = "Edit");
		}

		task ("Edit Heading fragment") {
			MenuItem.click(menuItem = "Edit");

			PageEditor.editFragmentText(
				fragmentName = "Heading",
				id = "text",
				text = "Modified text");
		}

		task ("View page author can preview the modification") {
			PagesAdmin.openPagesAdmin(siteURLKey = "Test Site Name");

			PagesAdmin.viewPageStatus(
				index = "2",
				pageName = "Test Page Name",
				status = "Pending");

			PagesAdmin.openPageEllipsisMenu(pageName = "Test Page Name");

			MenuItem.viewVisible(menuItem = "Preview");

			MenuItem.click(menuItem = "Preview");

			ContentPages.viewFragmentText(
				fragmentName = "heading",
				id = "element-text",
				text = "Modified text");
		}

		task ("Resubmit the request after modification") {
			UserBar.gotoDropdownItem(dropdownItem = "My Workflow Tasks");

			Workflow.resubmitTaskByActions(
				workflowAssetTitle = "Test Page Name",
				workflowAssetType = "Content Page",
				workflowTask = "Update");
		}

		task ("View reviewer can preview the modification") {
			User.logoutAndLoginPG(userLoginEmailAddress = "test@liferay.com");

			PagesAdmin.openPagesAdmin(siteURLKey = "Test Site Name");

			PagesAdmin.viewPageStatus(
				index = "2",
				pageName = "Test Page Name",
				status = "Pending");

			PagesAdmin.openPageEllipsisMenu(pageName = "Test Page Name");

			MenuItem.viewVisible(menuItem = "Preview");

			MenuItem.click(menuItem = "Preview");

			ContentPages.viewFragmentText(
				fragmentName = "heading",
				id = "element-text",
				text = "Modified text");
		}

		task ("Reviewer approve the request of modification") {
			UserBar.gotoDropdownItem(dropdownItem = "My Workflow Tasks");

			Workflow.assignToMeTaskByActions(
				workflowAssetTitle = "Test Page Name",
				workflowAssetType = "Content Page",
				workflowTask = "Review");

			Workflow.approveTaskByActions(
				workflowAssetTitle = "Test Page Name",
				workflowAssetType = "Content Page",
				workflowTask = "Review");
		}

		task ("View peding status is not shown as page author") {
			User.logoutAndLoginPG(userLoginEmailAddress = "userea@liferay.com");

			PagesAdmin.openPagesAdmin(siteURLKey = "Test Site Name");

			AssertElementNotPresent(
				index = "2",
				key_itemName = "Test Page Name",
				locator1 = "PagesAdmin#LIST_GROUP_ITEM_PAGE_STATUS");
		}

		task ("View preview action is not shown and edit is shown in Pages Admin") {
			PagesAdmin.openPageEllipsisMenu(pageName = "Test Page Name");

			MenuItem.viewNotPresent(menuItem = "Preview");

			MenuItem.viewVisible(menuItem = "Edit");
		}

		task ("View page author can edit after approve") {
			ContentPagesNavigator.openViewContentPage(
				pageName = "Test Page Name",
				siteName = "Test Site Name");

			AssertElementPresent(locator1 = "Icon#EDIT_PENCIL");

			ContentPages.gotoPageEditor();

			PageEditor.viewFragmentEditableText(
				editableText = "Modified text",
				fragmentName = "Heading",
				id = "element-text");

			PageEditor.editFragmentText(
				fragmentName = "Heading",
				id = "text",
				text = "New modified text");

			PageEditor.submitForPublication();
		}
	}

	@description = "This is a test for LPS-98384. The reviewer can edit the content page and preview the modification after submit."
	@priority = "5"
	test PreviewModificationAndEditAfterSubmitAsReviewer {
		property portal.acceptance = "true";

		task ("View reviewer can preview modification of content page") {
			User.logoutAndLoginPG(userLoginEmailAddress = "test@liferay.com");

			PagesAdmin.openPagesAdmin(siteURLKey = "Test Site Name");

			PagesAdmin.viewPageStatus(
				index = "2",
				pageName = "Test Page Name",
				status = "Pending");

			PagesAdmin.openPageEllipsisMenu(pageName = "Test Page Name");

			MenuItem.viewVisible(menuItem = "Preview");

			MenuItem.click(menuItem = "Preview");

			ContentPages.viewFragmentText(
				fragmentName = "heading",
				id = "element-text",
				text = "New editable fragment text");
		}

		task ("View reviewer can edit after submit") {
			AssertVisible(locator1 = "Icon#EDIT_PENCIL");

			PagesAdmin.openPagesAdmin(siteURLKey = "Test Site Name");

			PagesAdmin.openPageEllipsisMenu(pageName = "Test Page Name");

			MenuItem.viewVisible(menuItem = "Edit");

			MenuItem.click(menuItem = "Edit");

			PageEditor.editFragmentText(
				fragmentName = "Heading",
				id = "text",
				text = "Modified text");

			PagesAdmin.openPagesAdmin(siteURLKey = "Test Site Name");

			PagesAdmin.viewPageStatus(
				index = "2",
				pageName = "Test Page Name",
				status = "Pending");

			PagesAdmin.openPageEllipsisMenu(pageName = "Test Page Name");

			MenuItem.click(menuItem = "Preview");

			ContentPages.viewFragmentText(
				fragmentName = "heading",
				id = "element-text",
				text = "Modified text");
		}
	}

	@description = "This is a test for LPS-98384. The page author can not edit again after submit but can preview the modification."
	@priority = "5"
	test PreviewModificationButCannotEditAfterSubmitAsPageAuthor {
		property portal.acceptance = "true";

		task ("View the Heading fragment still displays original text for public") {
			Navigator.gotoSitePage(
				pageName = "Test Page Name",
				siteName = "Test Site Name");

			ContentPages.viewFragmentText(
				fragmentName = "heading",
				id = "element-text",
				text = "Heading Example");
		}

		task ("View page author can not edit again after submit") {
			AssertElementNotPresent(locator1 = "Icon#EDIT_PENCIL");

			PagesAdmin.openPagesAdmin(siteURLKey = "Test Site Name");

			PagesAdmin.viewPageStatus(
				index = "2",
				pageName = "Test Page Name",
				status = "Pending");

			PagesAdmin.openPageEllipsisMenu(pageName = "Test Page Name");

			MenuItem.viewNotPresent(menuItem = "Edit");
		}

		task ("View page author can preview modification of content page") {
			MenuItem.viewVisible(menuItem = "Preview");

			MenuItem.click(menuItem = "Preview");

			AssertElementNotPresent(locator1 = "Icon#EDIT_PENCIL");

			ContentPages.viewFragmentText(
				fragmentName = "heading",
				id = "element-text",
				text = "New editable fragment text");
		}
	}

}