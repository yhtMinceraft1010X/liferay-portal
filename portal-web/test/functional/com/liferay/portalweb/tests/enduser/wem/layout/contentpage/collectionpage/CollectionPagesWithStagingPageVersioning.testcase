@component-name = "portal-wcm"
definition {

	property portal.release = "true";
	property portal.upstream = "true";
	property testray.main.component.name = "Content Pages";

	setUp {
		task ("Set up instance and sign in") {
			TestCase.setUpPortalInstance();

			User.firstLoginPG();
		}

		task ("Add a site") {
			JSONGroup.addGroup(groupName = "Test Site Name");
		}
	}

	tearDown {
		var testPortalInstance = PropsUtil.get("test.portal.instance");

		if ("${testPortalInstance}" == "true") {
			PortalInstances.tearDownCP();
		}
		else {
			JSONGroup.deleteGroupByName(groupName = "Test Site Name");
		}
	}

	@description = "This is a test case for LPS-139864. Can enable local live in a site with different types of collection page."
	@priority = "4"
	test ActivateStagingPageVersioning {
		task ("Add a manual collection for all types") {
			JSONAssetlist.addManualAssetListEntryWithItemType(
				groupName = "Test Site Name",
				itemType = "All Types",
				title = "Manual Collection");
		}

		task ("Add a dynamic collection for all types") {
			JSONAssetlist.addDynamicAssetListEntryWithItemType(
				groupName = "Test Site Name",
				itemType = "All Types",
				title = "Dynamic Collection");
		}

		task ("Add the first collection page based on manual collection") {
			SitePages.openPagesAdmin(siteURLKey = "test-site-name");

			ContentPages.addPublicCollectionPage(
				collectionName = "Manual Collection",
				pageName = "First Collection Page Name",
				tabName = "Collections");
		}

		task ("Publish the first collection page") {
			PageEditor.clickPublish();
		}

		task ("Add the second collection page based on dynamic collection") {
			ContentPages.addPublicCollectionPage(
				collectionName = "Dynamic Collection",
				pageName = "Second Collection Page Name",
				tabName = "Collections");
		}

		task ("Add a Button fragment above the Container then publish") {
			PageEditor.addFragment(
				collectionName = "Basic Components",
				dropTarget = "Header",
				fragmentName = "Button",
				targetFragmentName = "Container");

			PageEditor.clickPublish();
		}

		task ("Add the third collection page based on collection provider") {
			ContentPages.addPublicCollectionPage(
				collectionName = "Most Viewed Assets",
				pageName = "Third Collection Page Name",
				tabName = "Collection Providers");
		}

		task ("Add a HTML fragment above the Container without publish") {
			PageEditor.addFragment(
				collectionName = "Basic Components",
				dropTarget = "Header",
				fragmentName = "HTML",
				targetFragmentName = "Container");
		}

		task ("Activate local live staging") {
			Staging.openStagingAdmin(siteURLKey = "test-site-name");

			Staging.activateStagingCP(
				pageVersioningEnabled = "true",
				siteName = "Test Site Name");
		}

		task ("View the workflow message is not shown in staging site") {
			for (var text : list "First,Second") {
				ContentPagesNavigator.openViewContentPage(
					pageName = "${text} Collection Page Name",
					siteName = "Test Site Name Staging");

				AssertElementNotPresent(locator1 = "Staging#STAGING_MENU_READY_FOR_PUBLICATION");
			}
		}

		task ("View the Heading fragment is shown in live site") {
			ContentPagesNavigator.openViewContentPage(
				pageName = "First Collection Page Name",
				siteName = "Test Site Name");

			ContentPages.viewFragmentText(
				fragmentName = "heading",
				id = "element-text",
				text = "Manual Collection");
		}

		task ("View the Button and Heading fragments are shown in live site") {
			ContentPagesNavigator.openViewContentPage(
				pageName = "Second Collection Page Name",
				siteName = "Test Site Name");

			ContentPages.viewFragmentText(
				fragmentName = "button",
				id = "link",
				text = "Go Somewhere");

			ContentPages.viewFragmentText(
				fragmentName = "heading",
				id = "element-text",
				text = "Dynamic Collection");
		}

		task ("Assert View action of the third collection page is disabled in live site") {
			SitePages.openPagesAdmin(siteURLKey = "test-site-name");

			SitePages.clickPageMenu(pageName = "Third Collection Page Name");

			AssertVisible(
				key_menuItem = "View",
				locator1 = "MenuItem#DISABLED_MENU_ITEM");
		}
	}

}