@component-name = "portal-web-forms-and-data-lists"
definition {

	property custom.properties = "minifier.enabled=true";
	property portal.release = "true";
	property portal.upstream = "true";
	property testray.main.component.name = "Forms";

	setUp {
		TestCase.setUpPortalInstance();

		User.firstLoginUI();
	}

	tearDown {
		var testPortalInstance = PropsUtil.get("test.portal.instance");

		if ("${testPortalInstance}" == "true") {
			PortalInstances.tearDownCP();
		}
		else {
			Navigator.openURL();

			ProductMenu.gotoPortlet(
				category = "Content & Data",
				portlet = "Forms");

			FormsAdmin.teardownForms();

			PagesAdmin.tearDownCP();
		}
	}

	@description = "LPS-64798 - Verify that adding the Paragraph Field does not prevent page navigation"
	@priority = "4"
	test CanBeAddedWithoutAffectingNavigation {
		property portal.acceptance = "true";
		property test.name.skip.portal.instance = "FormsParagraphField#CanBeAddedWithoutAffectingNavigation";
		property testray.component.names = "Training";

		FormsAdmin.addForm();

		Form.editName();

		Form.editDescription();

		Form.editPageTitle(
			formPageName = "Page 1",
			formTitleNumber = "1");

		Form.gotoAddField(
			fieldPositionNumber = "0",
			fieldType = "Paragraph",
			formPageNumber = "0");

		var fieldParagraphField = FormFields.getFieldName();

		FormFields.editParagraphBody(content = "Paragraph body text.");

		Form.closeSidebar();

		Form.addTextField(
			fieldPositionNumber = "1",
			formPageNumber = "0");

		var fieldTextField = FormFields.getFieldName();

		Form.editFieldLabelMetal(fieldValue = "SingleLineText");

		Form.closeSidebar();

		Form.addTextField(
			fieldPositionNumber = "2",
			formPageNumber = "0");

		var fieldMultilineTextField = FormFields.getFieldName();

		Form.editFieldLabelMetal(fieldValue = "MultilineText");

		FormFields.selectTextFieldLineOption(textFieldLineOption = "Multiple Lines");

		FormFields.enableSwitch(fieldName = "Required Field");

		Form.closeSidebar();

		Form.gotoAddField(
			fieldPositionNumber = "3",
			fieldType = "Select from List",
			formPageNumber = "0");

		var fieldSelectFromListField = FormFields.getFieldName();

		Form.editFieldLabelMetal(fieldValue = "Select");

		FormFields.editOptionsRow(
			optionName = "Select1",
			optionValue = "Select 1");

		FormFields.editOptionsRow(
			optionName = "Select2",
			optionValue = "Select 2",
			rowNumber = "2");

		Form.closeSidebar();

		Form.gotoAddField(
			fieldPositionNumber = "4",
			fieldType = "Single Selection",
			formPageNumber = "0");

		var fieldSingleSelectionField = FormFields.getFieldName();

		Form.editFieldLabelMetal(fieldValue = "Radio");

		FormFields.editOptionsRow(
			optionName = "Option1",
			optionValue = "Option 1");

		FormFields.editOptionsRow(
			optionName = "Option2",
			optionValue = "Option 2",
			rowNumber = "2");

		FormFields.enableSwitch(fieldName = "Required Field");

		Form.closeSidebar();

		Form.gotoAddField(
			fieldPositionNumber = "5",
			fieldType = "Date",
			formPageNumber = "0");

		var fieldDateField = FormFields.getFieldName();

		Form.closeSidebar();

		Form.gotoAddField(
			fieldPositionNumber = "6",
			fieldType = "Multiple Selection",
			formPageNumber = "0");

		var fieldMultipleSelectionField = FormFields.getFieldName();

		Form.editFieldLabelMetal(fieldValue = "Checkbox");

		FormFields.editOptionsRow(
			optionName = "Checkbox1",
			optionValue = "Checkbox 1",
			rowNumber = "1");

		FormFields.editOptionsRow(
			optionName = "Checkbox2",
			optionValue = "Checkbox 2",
			rowNumber = "2");

		FormFields.enableSwitch(fieldName = "Required Field");

		Form.closeSidebar();

		Form.addPage(formAddNewPageNumber = "1");

		Form.editPageTitle(
			formPageName = "Page 2",
			formTitleNumber = "2");

		Form.gotoAddField(
			fieldPositionNumber = "0",
			fieldType = "Grid",
			formPageNumber = "1");

		var fieldGridField = FormFields.getFieldName();

		Form.editFieldLabelMetal(fieldValue = "Grid Field Survey");

		for (var optionNumber : list "1,2,3") {
			FormFields.editGridOptions(
				gridField = "Row",
				optionValue = "Option ${optionNumber}",
				rowNumber = "${optionNumber}");
		}

		for (var optionNumber : list "1,2,3") {
			FormFields.editGridOptions(
				gridField = "Column",
				optionValue = "Category ${optionNumber}",
				rowNumber = "${optionNumber}");
		}

		Form.closeSidebar();

		Form.addPage(formAddNewPageNumber = "2");

		Form.editPageTitle(
			formPageName = "Page 3",
			formTitleNumber = "3");

		Form.gotoAddField(
			fieldPositionNumber = "0",
			fieldType = "Upload",
			formPageNumber = "2");

		var fieldUploadField = FormFields.getFieldName();

		Form.editFieldLabelMetal(
			fieldPositionNumber = "0",
			fieldValue = "Upload Field");

		Form.closeSidebar();

		Form.gotoAddField(
			fieldPositionNumber = "1",
			fieldType = "Numeric",
			formPageNumber = "2");

		var fieldNumericField = FormFields.getFieldName();

		Form.editFieldLabelMetal(fieldValue = "Value");

		Form.closeSidebar();

		Form.gotoAddField(
			fieldPositionNumber = "2",
			fieldType = "Color",
			formPageNumber = "2");

		var colorFieldName = FormFields.getFieldName();

		Form.closeSidebar();

		Form.save();

		ProductMenu.gotoPortlet(
			category = "Site Builder",
			portlet = "Pages");

		PagesAdmin.addPublicPage(pageName = "Form Page");

		Navigator.gotoPage(pageName = "Form Page");

		Portlet.addPG(
			pageName = "Form Page",
			portletName = "Form");

		FormPortletNavigator.gotoSelectForm();

		FormPortletConfiguration.selectForm();

		PortletEntry.save();

		Navigator.gotoPage(pageName = "Form Page");

		FormPortlet.waitFormRenderer(text = "Next");

		FormFields.viewParagraphField(
			fieldName = "${fieldParagraphField}",
			fieldTitle = "Paragraph",
			fieldValue = "Paragraph body text.");

		FormFields.editText(
			fieldName = "${fieldTextField}",
			fieldValue = "Single Line Text");

		FormFields.editTextMultilineOnPages(
			fieldName = "${fieldMultilineTextField}",
			fieldValue = "Multiline Text");

		FormFields.selectSelectOption(
			fieldName = "${fieldSelectFromListField}",
			selectOption = "Select 1");

		FormFields.selectRadioOption(
			fieldName = "${fieldSingleSelectionField}",
			radioOption = "Option 1");

		FormFields.editDate(
			fieldName = "${fieldDateField}",
			fieldValue = "05/02/2016");

		FormFields.selectCheckboxOption(
			checkboxOption = "Checkbox 1",
			fieldName = "${fieldMultipleSelectionField}");

		FormPortlet.nextPage(
			formPageCurrent = "Page 1",
			formPageNext = "Page 2");

		for (var optionNumber : list "1,2,3") {
			FormFields.selectGridRadioOption(
				categoryColumnName = "Category ${optionNumber}",
				fieldName = "${fieldGridField}",
				optionNumber = "${optionNumber}",
				optionRowName = "Option ${optionNumber}");
		}

		FormPortlet.nextPage(
			formPageCurrent = "Page 2",
			formPageNext = "Page 3");

		FormFields.editColor(
			colorSwatchItem = "2BA676",
			fieldLabel = "Color");

		FormFields.viewUploadField(
			fieldLabel = "Upload Field",
			fieldName = "${fieldUploadField}");

		ItemSelector.gotoItemSelectorViaSelectButton();

		ItemSelector.uploadFile(
			navTab = "Documents and Media",
			uploadFileName = "Document_1.jpg");

		FormPortlet.submitSuccessfully();

		Navigator.openURL();

		ProductMenu.gotoPortlet(
			category = "Content & Data",
			portlet = "Forms");

		FormsAdminNavigator.gotoViewEntries();

		FormsAdmin.viewFormTableEntry(
			fieldLabel = "Status",
			fieldValue = "Approved");

		FormsAdminNavigator.gotoView();

		FormFields.viewParagraphField(
			fieldName = "${fieldParagraphField}",
			fieldTitle = "Paragraph",
			fieldValue = "Paragraph body text.");

		FormsAdmin.viewTextEntry(
			fieldLabel = "SingleLineText",
			fieldName = "${fieldTextField}",
			fieldValue = "Single Line Text");

		FormsAdmin.viewMultilineTextEntry(
			fieldLabel = "MultilineText",
			fieldName = "${fieldMultilineTextField}",
			fieldValue = "Multiline Text");

		FormsAdmin.viewSelectEntry(
			fieldLabel = "Select",
			fieldName = "${fieldSelectFromListField}",
			selectOption = "Select 1");

		FormsAdmin.viewRadioEntry(
			fieldLabel = "Radio",
			fieldName = "${fieldSingleSelectionField}",
			fieldValue = "Option 1");

		FormsAdmin.viewDateEntry(
			fieldLabel = "Date",
			fieldName = "${fieldDateField}",
			fieldValue = "05/02/2016");

		FormsAdmin.viewCheckboxEntry(
			fieldName = "${fieldMultipleSelectionField}",
			fieldValue = "Checkbox 1");

		FormPortlet.nextPage(
			formPageCurrent = "Page 1",
			formPageNext = "Page 2");

		for (var optionNumber : list "1:1,2:2,3:3") {
			var columnNumber = StringUtil.extractLast("${optionNumber}", ":");
			var rowNumber = StringUtil.extractFirst("${optionNumber}", ":");

			FormFields.assertGridOptionChecked(
				columnValue = "Category ${rowNumber}",
				rowValue = "Option ${columnNumber}");
		}

		FormPortlet.nextPage(
			formPageCurrent = "Page 2",
			formPageNext = "Page 3");

		FormsAdmin.viewUploadFile(
			fieldLabel = "Upload Field",
			fieldName = "${fieldUploadField}",
			uploadFileName = "Document_1.jpg");

		FormFields.viewTextField(
			fieldLabel = "Value",
			fieldName = "${fieldNumericField}");

		FormsAdmin.viewColorEntry(
			fieldLabel = "Color",
			fieldName = "${colorFieldName}",
			fieldValue = "2BA676");
	}

	@description = "Verify that a Paragraph Field can be created"
	@priority = "5"
	test CanBeCreated {
		property portal.acceptance = "true";

		FormsAdmin.addForm();

		Form.editName(formName = "Form 1");

		Form.gotoAddField(
			fieldPositionNumber = "0",
			fieldType = "Paragraph");

		FormFields.editParagraphBody(content = "Test");

		Form.save();

		Click(locator1 = "Icon#BACK");

		FormsAdminNavigator.gotoEditFormByName(formName = "Form 1");

		AssertElementPresent(
			key_fieldName = "Paragraph",
			locator1 = "FormFields#PARAGRAPH_BODY_TEXT_CONTENT",
			value1 = "Test");
	}

	@description = "Verify that a Paragraph Field can be deleted"
	@priority = "5"
	test CanBeDeleted {
		property portal.acceptance = "true";

		FormsAdmin.addForm();

		Form.editName(formName = "Form 1");

		Form.gotoAddField(
			fieldPositionNumber = "0",
			fieldType = "Paragraph");

		FormFields.editParagraphBody(content = "Test");

		Form.closeSidebar();

		Form.addTextField(fieldPositionNumber = "1");

		Form.closeSidebar();

		FormsAdminNavigator.gotoDeleteField(
			fieldLabel = "Paragraph",
			fieldName = "Paragraph");

		Form.save();

		FormsAdminNavigator.gotoBack();

		FormsAdminNavigator.gotoEditFormByName(formName = "Form 1");

		AssertElementNotPresent(
			key_fieldName = "Paragraph",
			locator1 = "FormFields#PARAGRAPH_BODY_TEXT_CONTENT");
	}

	@description = "Verify that a Paragraph Field can be duplicated"
	@priority = "5"
	test CanBeDuplicated {
		property portal.acceptance = "true";

		FormsAdmin.addForm();

		Form.editName(formName = "Form 1");

		Form.gotoAddField(
			fieldPositionNumber = "0",
			fieldType = "Paragraph");

		FormFields.editParagraphBody(content = "Test");

		Click(
			key_name = "Paragraph",
			locator1 = "FormFields#FIELD_TARGET_LABEL");

		Click(locator1 = "Icon#VERTICAL_ELLIPSIS_BUTTON");

		Click(
			key_selectOption = "Duplicate",
			locator1 = "FormViewBuilder#SELECT_FIELD_OPTIONS_LIST");

		Form.save();

		FormsAdminNavigator.gotoBack();

		FormsAdminNavigator.gotoEditFormByName(formName = "Form 1");

		AssertElementPresent(
			key_bodyText = "Test",
			key_fieldName = "Paragraph",
			locator1 = "FormFields#FIELD_TARGET_LABEL_AND_BODY_TEXT");

		AssertElementPresent(
			key_bodyText = "Test",
			key_fieldName = "Copy of Paragraph",
			locator1 = "FormFields#FIELD_TARGET_LABEL_AND_BODY_TEXT");

		Click(
			key_bodyText = "Test",
			key_fieldName = "Paragraph",
			locator1 = "FormFields#FIELD_TARGET_LABEL_AND_BODY_TEXT");

		Form.gotoAdvancedTab();

		var reff = selenium.getAttribute("//div[contains(@data-field-name,'fieldReference')]/input@value");

		echo("Creating new Option complete with Values ${reff}...");

		Click(
			key_bodyText = "Test",
			key_fieldName = "Copy of Paragraph",
			locator1 = "FormFields#FIELD_TARGET_LABEL_AND_BODY_TEXT");

		AssertElementNotPresent(
			key_fieldName = "",
			key_fieldValue = "${reff}",
			locator1 = "Translation#TARGET_LANGUAGE_FIELD");
	}

	@description = "Verify that a Paragraph Field can be edited"
	@priority = "5"
	test CanBeEdited {
		property portal.acceptance = "true";

		FormsAdmin.addForm();

		Form.editName(formName = "Form 1");

		Form.gotoAddField(
			fieldPositionNumber = "0",
			fieldType = "Paragraph");

		FormFields.editParagraphBody(content = "Test");

		Form.save();

		FormsAdminNavigator.gotoBack();

		FormsAdminNavigator.gotoEditFormByName(formName = "Form 1");

		FormsAdminNavigator.gotoEditField(fieldName = "Paragraph");

		FormFields.editParagraphBody(content = "Paragraph body text.");

		Form.save();

		FormsAdminNavigator.gotoBack();

		FormsAdminNavigator.gotoEditFormByName(formName = "Form 1");

		AssertElementPresent(
			key_fieldName = "Paragraph",
			locator1 = "FormFields#PARAGRAPH_BODY_TEXT_CONTENT",
			value1 = "Paragraph body text.");
	}

	@description = "Verify that a Form can be submitted with the Paragraph Field"
	@priority = "5"
	test CanBeSubmitted {
		FormsAdmin.addForm();

		Form.editName(formName = "Form 1");

		Form.gotoAddField(
			fieldPositionNumber = "0",
			fieldType = "Paragraph");

		FormFields.editParagraphBody(content = "Test");

		Form.save();

		Form.publishForm();

		FormsAdminNavigator.gotoPublishCopyURLLink();

		var publishedFormPublicURL = Form.getPublishedFormPublicURL(publishedFormPublicURL = "${publishedFormPublicURL}");

		Navigator.openSpecificURL(url = "${publishedFormPublicURL}");

		AssertElementPresent(
			key_bodyName = "Test",
			locator1 = "FormFields#PARAGRAPH_SPECIFIC_FIELD_BY_BODY");

		Click(
			key_text = "Submit",
			locator1 = "Button#ANY");

		AssertElementPresent(locator1 = "Message#SUCCESS");

		Navigator.openURL();

		ProductMenu.gotoPortlet(
			category = "Content & Data",
			portlet = "Forms");

		FormsAdminNavigator.gotoViewEntries();

		FormsAdminNavigator.gotoView();

		AssertElementPresent(
			key_bodyName = "Test",
			locator1 = "FormFields#PARAGRAPH_SPECIFIC_FIELD_BY_BODY");
	}

	@description = "Verify that a Table can be deleted in the Body Text by highlighting the table and pressing backspace"
	@priority = "4"
	test CanDeleteATableUsingBackspace {
		FormsAdmin.addForm();

		Form.editName(formName = "Form 1");

		Form.gotoAddField(
			fieldPositionNumber = "0",
			fieldType = "Paragraph");

		FormFields.addTableInBodyParagraph();

		Form.save();

		FormFields.deleteTextWithBackspaceInBodyParagraph();

		FormFields.viewTableNotPresent();

		FormFields.editParagraphBody(content = "test");

		Form.save();

		ProductMenu.gotoPortlet(
			category = "Content & Data",
			portlet = "Forms");

		FormsAdminNavigator.gotoEditFormByName(formName = "Form 1");

		FormFields.viewParagraphField(
			fieldName = "Paragraph",
			fieldTitle = "Paragraph",
			fieldValue = "Test");

		FormFields.viewTableNotPresent();
	}

	@description = "Verify that a Table can be deleted in the Body Text using the right click menu"
	@ignore = "Test Stub"
	@priority = "4"
	test CanDeleteATableUsingRightClick {
		property portal.acceptance = "false";

		// TODO LPS-133010 CanDeleteATableUsingRightClick pending implementation

	}

	@description = "Verify that the Formatting Style can be changed using the predefined style options"
	@priority = "4"
	test CanEditFormattingStyle {
		property test.name.skip.portal.instance = "FormsParagraphField#CanEditFormattingStyle";

		FormsAdmin.addForm();

		Form.editName(formName = "Form 1");

		Form.gotoAddField(
			fieldPositionNumber = "0",
			fieldType = "Paragraph");

		FormFields.editParagraphBody(content = "Test");

		Click(
			key_titleName = "Formatting Styles",
			locator1 = "FormFields#TOOLBAR_ANY_ITEM_BUTTON");

		SelectFrame.selectFrameNoLoading(locator1 = "FormFields#PARAGRAPH_FORMATTING_STYLES");

		Click.clickAt(
			key_titleName = "Heading 2",
			locator1 = "FormFields#TOOLBAR_ANY_ITEM_BUTTON",
			value1 = "Heading 2");

		SelectFrame.selectSecondFrame();

		Form.save();

		Alert.viewSuccessMessageText(successMessage = "Your request completed successfully.");

		AssertElementPresent(
			key_formattingStyle = "h2",
			locator1 = "FormFields#PARAGRAPH_TEXT_VALUE_STYLE",
			value = "Test");

		FormsAdminNavigator.gotoEditField(fieldName = "Paragraph");

		Click(
			key_titleName = "Formatting Styles",
			locator1 = "FormFields#TOOLBAR_ANY_ITEM_BUTTON");

		SelectFrame.selectFrameNoLoading(locator1 = "FormFields#PARAGRAPH_FORMATTING_STYLES");

		Click.clickAt(
			key_titleName = "Preformatted Text",
			locator1 = "FormFields#TOOLBAR_ANY_ITEM_BUTTON",
			value1 = "Preformatted Text");

		SelectFrameTop();

		FormPortlet.waitFormRenderer(text = "Save");

		Form.save();

		Alert.viewSuccessMessageText(successMessage = "Your request completed successfully.");

		FormsAdminNavigator.gotoBack();

		FormsAdminNavigator.gotoEditFormByName(formName = "Form 1");

		AssertElementPresent(
			key_formattingStyle = "pre",
			locator1 = "FormFields#PARAGRAPH_TEXT_VALUE_STYLE",
			value = "Test");
	}

	@description = "Verify that a Form cannot be saved or published without adding anything in the Body Text"
	@priority = "5"
	test CannotBeSavedOrPublishedWithoutBodyText {
		property portal.acceptance = "true";

		FormsAdmin.addForm();

		Form.editName(formName = "Form 1");

		Form.gotoAddField(
			fieldPositionNumber = "0",
			fieldType = "Paragraph");

		AssertElementPresent(
			key_fieldFieldLabel = "Body Text",
			locator1 = "FormFields#FIELD_REQUIRED");

		Click(locator1 = "Button#SAVE");

		Alert.viewErrorMessage(errorMessage = "Setting (Body Text) of field Paragraph is invalid.");

		FormsAdmin.addForm();

		Form.editName(formName = "Form 1");

		Form.gotoAddField(
			fieldPositionNumber = "0",
			fieldType = "Paragraph");

		Button.clickPublish();

		Alert.viewErrorMessage(errorMessage = "Setting (Body Text) of field Paragraph is invalid.");

		FormsAdmin.addForm();

		Form.editName(formName = "Form 1");

		Form.gotoAddField(
			fieldPositionNumber = "0",
			fieldType = "Paragraph");

		FormFields.editParagraphBody(content = "Test");

		Click(locator1 = "Button#SAVE");

		Alert.viewSuccessMessageText(successMessage = "Your request completed successfully.");

		Refresh();

		Button.clickPublish();

		Alert.viewSuccessMessageText(successMessage = "The form was published successfully.");
	}

	@description = "Verify that a link to a URL can be added to the Body Text"
	@priority = "5"
	test CanSupportALinkToAURL {
		property test.name.skip.portal.instance = "FormsParagraphField#CanSupportALinkToAURL";

		FormsAdmin.addForm();

		Form.editName(formName = "Form 1");

		Form.gotoAddField(
			fieldPositionNumber = "0",
			fieldType = "Paragraph");

		FormFields.addParagraphLinkToAURL(paragraphLink = "www.liferay.com");

		Form.save();

		FormsAdminNavigator.gotoBack();

		FormsAdminNavigator.gotoEditFormByName(formName = "Form 1");

		FormFields.viewParagraphField(
			fieldTitle = "Paragraph",
			url = "http://www.liferay.com");
	}

	@description = "Verify that Bulleted Lists can be added to the Body Text"
	@priority = "5"
	test CanSupportBulletedLists {
		property test.name.skip.portal.instance = "FormsParagraphField#CanSupportBulletedLists";

		FormsAdmin.addForm();

		Form.editName(formName = "Form 1");

		Form.gotoAddField(
			fieldPositionNumber = "0",
			fieldType = "Paragraph");

		CKEditor.addSourceContent(content = "<p>Test A</p><p>Test B</p><p>Test C</p>");

		Click(locator1 = "CKEditor#TOOLBAR_SOURCE_BUTTON");

		SelectFrame.selectFrameNoLoading(locator1 = "CKEditor#BODY_FIELD_IFRAME");

		SelectFieldText(locator1 = "CKEditor#BODY");

		SelectFrame(value1 = "relative=top");

		Click(
			key_titleName = "Insert/Remove Bulleted List",
			locator1 = "FormFields#TOOLBAR_ANY_ITEM_BUTTON");

		Form.save();

		AssertElementPresent(locator1 = "Message#SUCCESS");

		ProductMenu.gotoPortlet(
			category = "Content & Data",
			portlet = "Forms");

		Click(
			key_formName = "Form 1",
			locator1 = "FormsAdmin#FORMS_TABLE_NAME");

		AssertElementPresent(
			key_fieldLabel = "Paragraph",
			locator1 = "FormFields#LABEL");

		for (var liContent : list "Test A,Test B,Test C") {
			AssertElementPresent(
				key_itemName = "${liContent}",
				key_labelName = "Paragraph",
				locator1 = "FormFields#PARAGRAPH_CONTENT_UNORDERED_LIST");
		}
	}

	@description = "Verify that the Body Text supports HTML"
	@priority = "5"
	test CanSupportHTML {
		FormsAdmin.addForm();

		Form.editName(formName = "Form 1");

		Form.gotoAddField(
			fieldPositionNumber = "0",
			fieldType = "Paragraph");

		CKEditor.addSourceContent(content = "<h1>Paragraph Test</h1>");

		FormFields.viewSourceElement(
			elementName = "h1",
			value = "Paragraph Test");

		Form.save();

		Form.publishForm();

		FormsAdminNavigator.gotoPublishedForm();

		FormFields.viewParagraphField(
			fieldTitle = "Paragraph",
			fieldValue = "Paragraph Test");

		FormFields.viewParagraphContentNotEquals(fieldValue = "<h1>Paragraph Test</h1>");

		FormPortlet.submitSuccessfullyAndViewSuccessPage();
	}

	@description = "Verify that an Image can be uploaded to the Body Text"
	@priority = "5"
	test CanSupportImages {
		property test.name.skip.portal.instance = "FormsParagraphField#CanSupportImages";

		FormsAdmin.addForm();

		Form.editName(formName = "Form 1");

		Form.gotoAddField(
			fieldPositionNumber = "0",
			fieldType = "Paragraph");

		Click(
			key_titleName = "Image",
			locator1 = "FormFields#TOOLBAR_ANY_ITEM_BUTTON");

		ItemSelector.uploadFile(
			navTab = "Documents and Media",
			uploadFileName = "Document_1.jpg");

		Form.save();

		Alert.viewSuccessMessageText(successMessage = "Your request completed successfully.");

		FormsAdminNavigator.gotoBack();

		FormsAdminNavigator.gotoEditFormByName(formName = "Form 1");

		AssertElementPresent(
			key_imageName = "Document_1",
			locator1 = "AdaptiveMedia#IMAGE_VARIANT_SOURCE_URL");
	}

	@description = "Verify that Numbered Lists can be added to the Body Text"
	@priority = "5"
	test CanSupportNumberedLists {
		property test.name.skip.portal.instance = "FormsParagraphField#CanSupportNumberedLists";

		FormsAdmin.addForm();

		Form.editName(formName = "Form 1");

		Form.gotoAddField(
			fieldPositionNumber = "0",
			fieldType = "Paragraph");

		CKEditor.addSourceContent(content = "<p>Test A</p><p>Test B</p><p>Test C</p>");

		Click(locator1 = "CKEditor#TOOLBAR_SOURCE_BUTTON");

		SelectFrame.selectFrameNoLoading(locator1 = "CKEditor#BODY_FIELD_IFRAME");

		SelectFieldText(locator1 = "CKEditor#BODY");

		SelectFrame(value1 = "relative=top");

		Click(
			key_titleName = "Insert/Remove Numbered List",
			locator1 = "FormFields#TOOLBAR_ANY_ITEM_BUTTON");

		Form.save();

		AssertElementPresent(locator1 = "Message#SUCCESS");

		ProductMenu.gotoPortlet(
			category = "Content & Data",
			portlet = "Forms");

		Click(
			key_formName = "Form 1",
			locator1 = "FormsAdmin#FORMS_TABLE_NAME");

		AssertElementPresent(
			key_fieldLabel = "Paragraph",
			locator1 = "FormFields#LABEL");

		for (var liContent : list "Test A,Test B,Test C") {
			AssertElementPresent(
				key_itemName = "${liContent}",
				key_labelName = "Paragraph",
				locator1 = "FormFields#PARAGRAPH_CONTENT_ORDERED_LIST");
		}
	}

	@description = "Verify that a Table can be created in the Body Text"
	@priority = "4"
	test CanSupportTables {
		property test.name.skip.portal.instance = "FormsParagraphField#CanSupportTables";

		FormsAdmin.addForm();

		Form.editName(formName = "Form 1");

		Form.gotoAddField(
			fieldPositionNumber = "0",
			fieldType = "Paragraph");

		Click(
			key_titleName = "Table",
			locator1 = "FormFields#TOOLBAR_ANY_ITEM_BUTTON");

		Click(locator1 = "CKEditor#OK_BUTTON");

		Form.save();

		AssertElementPresent(locator1 = "Message#SUCCESS");

		Click(locator1 = "Icon#BACK");

		FormsAdminNavigator.gotoEditFormByName(formName = "Form 1");

		AssertElementPresent(
			key_fieldLabel = "Paragraph",
			locator1 = "FormFields#LABEL");

		AssertElementPresent(
			key_fieldName = "liferay-ddm-form-field-paragraph-text",
			locator1 = "FormFields#PARAGRAPH_BODY_CONTENT_TABLE");
	}

	@description = "Verify that a Video can be uploaded to the Body Text"
	@priority = "5"
	test CanSupportVideos {
		property test.name.skip.portal.instance = "FormsParagraphField#CanSupportVideos";

		FormsAdmin.addForm();

		Form.editName(formName = "Form 1");

		Form.gotoAddField(
			fieldPositionNumber = "0",
			fieldType = "Paragraph");

		Click(
			key_titleName = "Video",
			locator1 = "FormFields#TOOLBAR_ANY_ITEM_BUTTON");

		var key_navTab = "URL";

		SelectFrame(locator1 = "ItemSelector#ITEM_SELECTOR_IFRAME");

		AssertClick(
			locator1 = "ItemSelector#NAVIGATION_SPECIFIC_TAB",
			value1 = "URL");

		AssertElementPresent(locator1 = "ItemSelector#URL_LABEL");

		AssertElementNotPresent(locator1 = "IFrame#EXTERNAL_VIDEO_IFRAME");

		Type(
			locator1 = "TextInput#VIDEO_URL",
			value1 = "https://www.youtube.com/watch?v=2EPZxIC5ogU");

		Button.clickAdd();

		SelectFrameTop();

		FormPortlet.waitFormRenderer(text = "Save");

		Form.save();

		Alert.viewSuccessMessageText(successMessage = "Your request completed successfully.");

		FormsAdminNavigator.gotoBack();

		FormsAdminNavigator.gotoEditFormByName(formName = "Form 1");

		AssertElementPresent(locator1 = "YoutubePortlet#VIDEO_IFRAME");
	}

	@description = "Verify that inputs to the Body Text can be undone and redone"
	@priority = "5"
	test CanUndoAndRedo {
		property test.name.skip.portal.instance = "FormsParagraphField#CanUndoAndRedo";

		FormsAdmin.addForm();

		Form.editName(formName = "Form 1");

		Form.gotoAddField(
			fieldPositionNumber = "0",
			fieldType = "Paragraph");

		FormFields.editParagraphBody(content = "Test");

		Form.save();

		FormsAdminNavigator.gotoEditField(fieldName = "Paragraph");

		FormFields.editParagraphBody(content = "Test Body Text");

		Click(
			key_titleName = "Undo",
			locator1 = "FormFields#TOOLBAR_ANY_ITEM_BUTTON");

		AssertElementPresent(
			key_fieldName = "Paragraph",
			locator1 = "FormFields#PARAGRAPH_BODY_TEXT_CONTENT",
			value1 = "Test");

		Click(
			key_titleName = "Redo",
			locator1 = "FormFields#TOOLBAR_ANY_ITEM_BUTTON");

		AssertElementPresent(
			key_fieldName = "Paragraph",
			locator1 = "FormFields#PARAGRAPH_BODY_TEXT_CONTENT",
			value1 = "Test Body Text");
	}

	@description = "Verify that changes to a Field can be canceled"
	@ignore = "Test Stub"
	@priority = "5"
	test ChangesCanBeCanceled {
		property portal.acceptance = "false";

		// TODO LPS-131225 ChangesCanBeCanceled pending implementation

	}

	@description = "Verify that the Field Reference can be Edited"
	@priority = "5"
	test ReferenceCanBeEdited {
		FormsAdmin.addForm();

		Form.editName(formName = "Form 1");

		Form.gotoAddField(
			fieldPositionNumber = "0",
			fieldType = "Paragraph");

		FormFields.editParagraphBody(content = "Test");

		Form.gotoAdvancedTab();

		Form.editFieldReferenceMetal(fieldValue = "Paragraph1234");

		Form.save();

		FormsAdminNavigator.gotoBack();

		FormsAdminNavigator.gotoEditFormByName(formName = "Form 1");

		FormFields.viewFieldReference(
			fieldName = "Paragraph",
			key_fieldReferenceValue = "Paragraph1234");
	}

	@description = "Verify that text in the Body Text can be Bolded, Italicized and Underlined"
	@priority = "5"
	test TextCanBeBoldedItalicizedAndUnderlined {
		property test.name.skip.portal.instance = "FormsParagraphField#TextCanBeBoldedItalicizedAndUnderlined";

		FormsAdmin.addForm();

		Form.editName(formName = "Form 1");

		Form.gotoAddField(
			fieldPositionNumber = "0",
			fieldType = "Paragraph");

		FormFields.editTextBodyToolBar(content = "Test");

		FormFields.toggleCKEditorContentStyle(optionTool = "Bold");

		WaitForElementPresent(
			key_textName = "Test",
			locator1 = "FormFields#PARAGRAPH_ASSERT_BODY_TEXT_CONTENT_BOLD");

		Form.save();

		Click(
			key_textName = "Test",
			locator1 = "FormFields#PARAGRAPH_ASSERT_BODY_TEXT_CONTENT_BOLD");

		FormFields.toggleCKEditorContentStyle(optionTool = "Bold");

		FormFields.toggleCKEditorContentStyle(optionTool = "Italic");

		WaitForElementPresent(
			key_textName = "Test",
			locator1 = "FormFields#PARAGRAPH_ASSERT_BODY_TEXT_CONTENT_ITALIC");

		Form.save();

		Click(
			key_textName = "Test",
			locator1 = "FormFields#PARAGRAPH_ASSERT_BODY_TEXT_CONTENT_ITALIC");

		FormFields.toggleCKEditorContentStyle(optionTool = "Italic");

		FormFields.toggleCKEditorContentStyle(optionTool = "Underline");

		WaitForElementPresent(
			key_textName = "Test",
			locator1 = "FormFields#PARAGRAPH_ASSERT_BODY_TEXT_CONTENT_UNDERLINE");

		Form.save();
	}

	@description = "Verify that the Field Title can be edited"
	@priority = "5"
	test TitleCanBeEdited {
		FormsAdmin.addForm();

		Form.editName(formName = "Form 1");

		Form.gotoAddField(
			fieldPositionNumber = "0",
			fieldType = "Paragraph");

		FormFields.editParagraphBody(content = "Test");

		Form.editFieldLabelMetal(fieldValue = "Test Paragraph");

		Form.save();

		Click(locator1 = "Icon#BACK");

		FormsAdminNavigator.gotoEditFormByName(formName = "Form 1");

		FormFields.viewParagraphField(
			fieldName = "Paragraph",
			fieldTitle = "Test Paragraph",
			fieldValue = "Test");
	}

	@description = "Verify that the Field Type can be changed"
	@ignore = "Test Stub"
	@priority = "5"
	test TypeCanBeChanged {
		property portal.acceptance = "false";

		// TODO LPS-131232 TypeCanBeChanged pending implementation

	}

}