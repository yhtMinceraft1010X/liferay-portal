@component-name = "portal-analytics-cloud"
definition {

	property portal.release = "false";
	property portal.upstream = "true";
	property property.group = "analytics.cloud.profile";
	property testray.main.component.name = "Analytics Cloud";

	setUp {
		task ("Copy the token from AC") {
			ACUtils.launchAC();

			ACSettings.takeTokenFromAnalyticsCloud();
		}

		task ("Set up instance and sign in DXP") {
			TestCase.setUpPortalInstance();

			ACUtils.launchDXP();
		}

		task ("Add a new site with a public widget page") {
			ACUtils.addSiteAndPage();
		}

		task ("Add specific pages and web contents to Site as interest topics") {
			var pageNameList = "sports,technology,science";

			for (var layoutName : list "${pageNameList}") {
				ACUtils.addPage(layoutName = "${layoutName}");

				ACUtils.createWCAndAddToPage(
					layoutName = "${layoutName}",
					webContentTitle = "Web Content AC Title ${layoutName}");
			}
		}

		task ("Connect the DXP to AC") {
			ACDXPSettings.connectDXPtoAnalyticsCloud(siteName = "Site Name");
		}

		task ("Add a new user") {
			JSONUser.addUser(
				userEmailAddress = "ac@liferay.com",
				userFirstName = "ac",
				userLastName = "ac",
				userScreenName = "ac");

			JSONUser.setFirstPassword(
				agreeToTermsAndAnswerReminderQuery = "true",
				requireReset = "false",
				userEmailAddress = "ac@liferay.com");
		}

		task ("Sync the Contact Data") {
			ACDXPSettings.syncNewContactData();
		}
	}

	tearDown {
		var testPortalInstance = PropsUtil.get("test.portal.instance");

		ACUtils.tearDownDXP();

		if ("${testPortalInstance}" == "true") {
			PortalInstances.tearDownCP();
		}
		else {
			JSONGroup.deleteGroupByName(groupName = "Site Name");
		}

		JSONUser.tearDownNonAdminUsers();

		ACUtils.tearDownAC();
	}

	@description = "Feature ID: Legacy | Automation ID: LRAC-8501 | Test Summary: Interest can be aggregated from multiple pages"
	@priority = "4"
	test InterestCanBeAggregatedFromMultiplePages {
		property portal.upstream = "quarantine";

		// AC Refactor: Interests in the segment take time to appear.
		// AC Refactor ticket: LRAC-9168

		var usernameList = "ac";

		var pageNameList = "sports,technology,science";

		task ("Get site assigned property name") {
			var propertyName = ACDXPSettings.getAssignedPropertyName(siteName = "Site Name");
		}

		task ("View new pages in site as new user") {
			User.logoutAndLoginPG(
				userLoginEmailAddress = "${usernameList}@liferay.com",
				userLoginFullName = "${usernameList} ${usernameList}");

			for (var pageName : list "${pageNameList}") {
				ACUtils.navigateToSitePage(
					pageName = "${pageName}",
					siteName = "Site Name");
			}
		}

		task ("Switch to new property in AC") {
			ACUtils.closeAllSessionsAndWait();

			ACUtils.launchAC();

			ACProperties.switchProperty(propertyName = "${propertyName}");
		}

		task ("Add a new static segment") {
			ACNavigation.goToSegments();

			ACSegments.createStaticSegment();
		}

		task ("Type the segment name") {
			ACSegments.nameSegment(segmentName = "Static Segment Test");
		}

		task ("Add the new user as member") {
			ACSegments.addStaticMember(tableMember = "${usernameList} ${usernameList}");
		}

		task ("Create the segment") {
			ACSegments.saveSegment();
		}

		// AC Refactor: Interests in the segment take time to appear.

		task ("View various segment interests under the top interests card") {
			ACInterests.viewInterest(interestTopic = "site");

			ACInterests.viewInterest(interestTopic = "sports");

			ACInterests.viewInterest(interestTopic = "science");

			ACInterests.viewInterest(interestTopic = "technology");
		}
	}

	@description = "Feature ID: Legacy | Automation ID: LRAC-8502 | Test Summary: Search the Details of a Segment's Interest"
	@priority = "3"
	test SearchDetailsOfSegmentsInterest {
		property portal.upstream = "quarantine";

		// AC Refactor: Test was written in a local to UAT test environment where data took hours to process and will need to be revisited.
		// AC Refactor ticket: LRAC-9169

		var usernameList = "user1,user2,user3,user4,user5";

		task ("Add 5 more new users") {
			for (var name : list "${usernameList}") {
				JSONUser.addUser(
					userEmailAddress = "${name}@liferay.com",
					userFirstName = "${name}",
					userLastName = "${name}",
					userScreenName = "${name}");

				JSONUser.setFirstPassword(
					agreeToTermsAndAnswerReminderQuery = "true",
					requireReset = "false",
					userEmailAddress = "${name}@liferay.com");
			}
		}

		task ("Sync the Contact Data") {
			ACDXPSettings.syncNewContactData();
		}

		task ("Get site assigned property name") {
			var propertyName = ACDXPSettings.getAssignedPropertyName(siteName = "Site Name");
		}

		task ("View the blogs page as different users") {
			ACUtils.navigateToSitePageWithUser(
				actionType = "View Blog",
				documentTitleList = "Blogs AC Title",
				emailList = "userea@liferay.com,user1@liferay.com",
				pageName = "Blogs Page",
				siteName = "Site Name");

			ACUtils.closeAllSessions();
		}

		task ("View the documents page as different users") {
			ACUtils.navigateToSitePageWithUser(
				actionType = "View DM",
				documentTitleList = "DM AC Title",
				emailList = "user2@liferay.com,user3@liferay.com",
				pageName = "Documents Page",
				siteName = "Site Name");

			ACUtils.closeAllSessions();
		}

		task ("View the web content page as different users") {
			ACUtils.navigateToSitePageWithUser(
				emailList = "user4@liferay.com,user5@liferay.com",
				pageName = "Web Content Page",
				siteName = "Site Name");

			ACUtils.closeAllSessions();
		}

		task ("Switch to new property in AC") {
			ACUtils.launchAC();

			ACProperties.switchProperty(propertyName = "${propertyName}");
		}

		task ("Add a new static segment") {
			ACNavigation.goToSegments();

			ACSegments.createStaticSegment();
		}

		task ("Type the segment name") {
			ACSegments.nameSegment(segmentName = "Static Segment Test");
		}

		task ("Select the 5 items in Add Members modal") {
			Click(locator1 = "ACSegments#ADD_MEMBERS_BUTTON");

			ACUtils.changePagination(itemsPerPage = "5");
		}

		task ("Add the user1 user1 as member") {
			Click(
				key_TableMember = "user1 user1",
				locator1 = "ACSegments#TABLE_CHECKBOX");

			Click(locator1 = "ACSegments#ADD_BUTTON");
		}

		task ("Select the 5 items in Add Members modal") {
			Click(locator1 = "ACSegments#ADD_MEMBERS_BUTTON");

			ACUtils.changePagination(itemsPerPage = "5");
		}

		task ("Navigate to page 2") {
			ACUtils.checkAnyPage(pageNumber = "2");
		}

		task ("Add the userfn userln as member") {
			Click(
				key_TableMember = "userfn userln",
				locator1 = "ACSegments#TABLE_CHECKBOX");

			Click(locator1 = "ACSegments#ADD_BUTTON");
		}

		task ("Create the segment") {
			ACSegments.saveSegment();
		}

		task ("Go to a segment interest details page") {
			ACInterests.clickInterest(interestTopic = "liferay dxp");
		}

		task ("Search Individual tab") {
			ACUtils.searchBar(searchItem = "user");
		}

		task ("View individuals") {
			ACInterests.viewInterestUser(userName = "userfn userln");

			ACInterests.viewInterestUser(userName = "user1 user1");

			ACInterests.viewInterestUser(userName = "user2 user2");

			ACInterests.viewInterestUser(userName = "user3 user3");
		}

		task ("Search Individual tab") {
			ACInterests.switchTab(tabName = "Active Pages");
		}

		task ("Search Active Pages tab") {
			ACUtils.searchBar(searchItem = "DXP");
		}

		task ("View pages") {
			ACInterests.viewInterestPage(pageName = "Blogs AC Title - Site Name - Liferay DXP");

			ACInterests.viewInterestPage(pageName = "Documents Page - Site Name - Liferay DXP");

			ACInterests.viewInterestPage(pageName = "Documents Page - Site Name - Liferay DXP");
		}
	}

	@description = "Feature ID: Legacy | Automation ID: LRAC-8500 | Test Summary: List of interests appears as bar graphs with percentage of interested members"
	@priority = "5"
	test ViewListInterestsAppearsBarGraphsWithPercentage {
		property portal.upstream = "quarantine";

		// AC Refactor: Test was written in a local to UAT test environment where data took hours to process and will need to be revisited.
		// AC Refactor ticket: LRAC-9167

		var usernameList = "user1,user2,user3,user4,user5";

		task ("Add 5 more new users") {
			for (var name : list "${usernameList}") {
				JSONUser.addUser(
					userEmailAddress = "${name}@liferay.com",
					userFirstName = "${name}",
					userLastName = "${name}",
					userScreenName = "${name}");

				JSONUser.setFirstPassword(
					agreeToTermsAndAnswerReminderQuery = "true",
					requireReset = "false",
					userEmailAddress = "${name}@liferay.com");
			}
		}

		task ("Sync the Contact Data") {
			ACDXPSettings.syncNewContactData();
		}

		task ("Get site assigned property name") {
			var propertyName = ACDXPSettings.getAssignedPropertyName(siteName = "Site Name");
		}

		task ("View the blogs page as different users") {
			ACUtils.navigateToSitePageWithUser(
				actionType = "View Blog",
				documentTitleList = "Blogs AC Title",
				emailList = "userea@liferay.com,user1@liferay.com",
				pageName = "Blogs Page",
				siteName = "Site Name");

			ACUtils.closeAllSessions();
		}

		task ("View the documents page as different users") {
			ACUtils.navigateToSitePageWithUser(
				actionType = "View DM",
				documentTitleList = "DM AC Title",
				emailList = "user2@liferay.com,user3@liferay.com",
				pageName = "Documents Page",
				siteName = "Site Name");

			ACUtils.closeAllSessions();
		}

		task ("View the web content page as different users") {
			ACUtils.navigateToSitePageWithUser(
				emailList = "user4@liferay.com,user5@liferay.com",
				pageName = "Web Content Page",
				siteName = "Site Name");

			ACUtils.closeAllSessions();
		}

		task ("Switch to new property in AC") {
			ACUtils.launchAC();

			ACProperties.switchProperty(propertyName = "${propertyName}");
		}

		task ("Add a new static segment") {
			ACNavigation.goToSegments();

			ACSegments.createStaticSegment();
		}

		task ("Type the segment name") {
			ACSegments.nameSegment(segmentName = "Static Segment Test");
		}

		task ("Select the 5 items in Add Members modal") {
			Click(locator1 = "ACSegments#ADD_MEMBERS_BUTTON");

			ACUtils.changePagination(itemsPerPage = "5");
		}

		task ("Add the user1 user1 as member") {
			Click(
				key_TableMember = "user1 user1",
				locator1 = "ACSegments#TABLE_CHECKBOX");

			Click(locator1 = "ACSegments#ADD_BUTTON");
		}

		task ("Select the 5 items in Add Members modal") {
			Click(locator1 = "ACSegments#ADD_MEMBERS_BUTTON");

			ACUtils.changePagination(itemsPerPage = "5");
		}

		task ("Navigate to page 2") {
			ACUtils.checkAnyPage(pageNumber = "2");
		}

		task ("Add the userfn userln as member") {
			Click(
				key_TableMember = "userfn userln",
				locator1 = "ACSegments#TABLE_CHECKBOX");

			Click(locator1 = "ACSegments#ADD_BUTTON");
		}

		task ("Create the segment") {
			ACSegments.saveSegment();
		}

		task ("View various segment top interest numbers under the top interests card") {
			ACInterests.viewInterest(
				interestTopic = "liferay dxp",
				membersCount = "4",
				membersPercentage = "66.67%");

			ACInterests.viewInterest(
				interestTopic = "site",
				membersCount = "4",
				membersPercentage = "66.67%");

			ACInterests.viewInterest(
				interestTopic = "ac",
				membersCount = "2",
				membersPercentage = "33.33%");

			ACInterests.viewInterest(
				interestTopic = "ac title",
				membersCount = "2",
				membersPercentage = "33.33%");

			ACInterests.viewInterest(
				interestTopic = "blog test",
				membersCount = "2",
				membersPercentage = "33.33%");
		}
	}

}