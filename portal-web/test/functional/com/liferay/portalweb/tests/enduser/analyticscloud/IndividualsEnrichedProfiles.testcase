@component-name = "portal-analytics-cloud"
definition {

	property analytics.cloud.release = "true";
	property analytics.cloud.upstream = "true";
	property portal.release = "false";
	property portal.upstream = "true";
	property property.group = "analytics.cloud.profile";
	property testray.main.component.name = "Analytics Cloud";

	setUp {
		task ("Copy the token from AC") {
			ACUtils.launchAC();

			ACSettings.takeTokenFromAnalyticsCloud();
		}

		task ("Set up instance and sign in DXP") {
			TestCase.setUpPortalInstance();

			ACUtils.launchDXP();
		}

		task ("Add a new site with a public widget page, then add a blog") {
			ACUtils.addSiteAndPage();

			ACUtils.createBlogsAndAddToPage();
		}

		task ("Connect the DXP to AC") {
			ACDXPSettings.connectDXPtoAnalyticsCloud(siteName = "Site Name");
		}
	}

	tearDown {
		var testPortalInstance = PropsUtil.get("test.portal.instance");

		ACUtils.tearDownDXP();

		if ("${testPortalInstance}" == "true") {
			PortalInstances.tearDownCP();
		}
		else {
			JSONGroup.deleteGroupByName(groupName = "Site Name");
		}

		JSONUser.tearDownNonAdminUsers();

		ACUtils.tearDownAC();
	}

	@description = "Feature ID: Legacy | Automation ID: LRAC-8911 | Test Summary: Enriched profiles increases by 1 when an anonymous individual converted to a known individual"
	@priority = "5"
	test IncreaseByOneWhenAnonymousIndividualConvertedToKnownIndividual {
		property analytics.cloud.upstream = "false";
		property portal.acceptance = "false";
		property portal.release = "false";

		task ("Add a new user") {
			JSONUser.addUser(
				userEmailAddress = "ac@liferay.com",
				userFirstName = "ac",
				userLastName = "ac",
				userScreenName = "ac");

			JSONUser.setFirstPassword(
				agreeToTermsAndAnswerReminderQuery = "true",
				requireReset = "false",
				userEmailAddress = "ac@liferay.com");
		}

		task ("View the blog as a new user") {
			ACUtils.navigateToSitePageWithUser(
				actionType = "View Blog",
				documentTitleList = "Blogs AC Title",
				emailList = "ac@liferay.com",
				pageName = "AC Page",
				siteName = "Site Name");
		}

		task ("Close Sessions") {
			ACUtils.closeAllSessionsAndWait();
		}

		task ("Switch to new property in AC") {
			ACUtils.launchAC();

			ACProperties.switchProperty(propertyName = "${assignedPropertyName}");
		}

		task ("Navigate to the Overview on the Individuals dashboard") {
			ACNavigation.goToIndividuals();

			ACIndividualsDashboard.goToOverviewTab();
		}

		task ("View the number of Total Individuals, Known, Anonymous and Enriched Profiles") {
			ACCards.viewTotalCount(
				cardName = "Total Individuals",
				count = "1");

			ACCards.viewTotalCount(
				cardName = "Known",
				count = "0");

			ACCards.viewTotalCount(
				cardName = "Anonymous",
				count = "1");

			ACCards.viewTotalCount(
				cardName = "Enriched Profiles",
				count = "0 Profiles");
		}

		task ("Sync the new user and view the blog") {
			ACUtils.launchDXP();

			ACDXPSettings.syncNewContactData();

			User.logoutAndLoginPG(
				userLoginEmailAddress = "ac@liferay.com",
				userLoginFullName = "ac ac");

			ACUtils.navigateToSitePage(
				actionType = "View Blog",
				documentTitleList = "Blogs AC Title",
				pageName = "AC Page",
				siteName = "Site Name");
		}

		task ("Close Sessions") {
			ACUtils.closeAllSessionsAndWait();
		}

		task ("Switch to new property in AC") {
			ACUtils.launchAC();

			ACProperties.switchProperty(propertyName = "${assignedPropertyName}");
		}

		task ("Navigate to the Overview on the Individuals dashboard") {
			ACNavigation.goToIndividuals();

			ACIndividualsDashboard.goToOverviewTab();
		}

		task ("View the number of Total Individuals, Known and Anonymous") {
			ACCards.viewTotalCount(
				cardName = "Total Individuals",
				count = "1");

			ACCards.viewTotalCount(
				cardName = "Known",
				count = "1");

			ACCards.viewTotalCount(
				cardName = "Anonymous",
				count = "0");

			ACCards.viewTotalCount(
				cardName = "Enriched Profiles",
				count = "1 Profiles");
		}
	}

}