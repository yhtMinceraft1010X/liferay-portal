@component-name = "portal-analytics-cloud"
definition {

	property custom.properties = "jsonws.web.service.paths.excludes=";
	property portal.release = "false";
	property portal.upstream = "true";
	property property.group = "analytics.cloud.profile";
	property testray.main.component.name = "Analytics Cloud";

	setUp {
		task ("Copy the token from AC") {
			ACUtils.launchAC();

			ACSettings.takeTokenFromAnalyticsCloud();
		}

		task ("Set up instance and sign in DXP") {
			TestCase.setUpPortalInstance();

			ACUtils.launchDXP();
		}

		task ("Add a new site with a public widget page") {
			ACUtils.addSiteAndPage();
		}

		task ("Add a Web Content Display to page and display the custom event") {
			ACCustomEvents.createWCWithCustomEventAndAddToPage();
		}

		task ("Add a new user") {
			JSONUser.addUser(
				userEmailAddress = "ac@liferay.com",
				userFirstName = "ac",
				userLastName = "ac",
				userScreenName = "ac");

			JSONUser.setFirstPassword(
				agreeToTermsAndAnswerReminderQuery = "true",
				requireReset = "false",
				userEmailAddress = "ac@liferay.com");
		}

		task ("Connect the DXP to AC") {
			ACDXPSettings.connectDXPtoAnalyticsCloud(siteName = "Site Name");
		}

		task ("Sign in as new user") {
			User.logoutAndLoginPG(
				userLoginEmailAddress = "ac@liferay.com",
				userLoginFullName = "ac ac");
		}

		task ("View the site page") {
			ACUtils.navigateToSitePage(
				pageName = "AC Page",
				siteName = "Site Name");
		}
	}

	tearDown {
		var testPortalInstance = PropsUtil.get("test.portal.instance");

		ACUtils.tearDownDXP();

		if ("${testPortalInstance}" == "true") {
			PortalInstances.tearDownCP();
		}
		else {
			JSONGroup.deleteGroupByName(groupName = "Site Name");
		}

		JSONUser.tearDownNonAdminUsers();

		ACUtils.tearDownAC();
	}

	@description = "Feature ID: LRAC-7858 | Automation ID: LRAC-10212 | Test Summary: Global attributes in settings"
	@priority = "5"
	test AssertGlobalAttributesInSettings {
		var globalAttributesList = "canonicalUrl,pageDescription,pageKeywords,pageTitle,referrer,url";

		task ("Fill fields and create custom event") {
			var customEventName = ACCustomEvents.generateCustomEventName();

			ACCustomEvents.createCustomEvent(
				customEventName = "${customEventName}",
				timeToSendCustomEvent = "1");
		}

		task ("Close sessions") {
			ACUtils.closeAllSessionsAndWait();
		}

		task ("Switch the property in AC") {
			ACUtils.launchAC();

			ACProperties.switchProperty(propertyName = "${assignedPropertyName}");
		}

		task ("Navigation to global attributes tab") {
			ACNavigation.goToSettings();

			ACSettings.goToDefinitions();

			ACSettings.goToEventAttributes();

			ACNavigation.openItem(itemName = "Global Attributes");

			ACNavigation.viewActiveSwitchTab(tabName = "Global Attributes");
		}

		task ("View the default attributes") {
			ACUtils.viewNameList(nameList = "${globalAttributesList}");
		}
	}

	@description = "Feature ID: LRAC-7858 | Automation ID: LRAC-10213 | Test Summary: Global attributes data shows sample data and last accessed"
	@priority = "5"
	test AssertSampleDataAndLastAccessInGlobalAttributes {
		var globalAttributeName = "pageTitle";
		var dataSource = StringUtil.extractFirst("${assignedPropertyName}", " ");

		task ("Fill fields and create custom event") {
			var customEventName = ACCustomEvents.generateCustomEventName();

			ACCustomEvents.createCustomEvent(
				customEventName = "${customEventName}",
				timeToSendCustomEvent = "1");
		}

		task ("Close sessions") {
			ACUtils.closeAllSessionsAndWait();
		}

		task ("Switch the property in AC") {
			ACUtils.launchAC();

			ACProperties.switchProperty(propertyName = "${assignedPropertyName}");
		}

		task ("Navigation to global attributes tab") {
			ACNavigation.goToSettings();

			ACSettings.goToDefinitions();

			ACSettings.goToEventAttributes();

			ACNavigation.openItem(itemName = "Global Attributes");
		}

		task ("Select a global attribute") {
			ACUtils.searchBar(searchItem = "${globalAttributeName}");

			var sampleRawDataValue = ACCustomEvents.getSampleRawData(attributeName = "${globalAttributeName}");

			ACNavigation.openItem(itemName = "${globalAttributeName}");
		}

		task ("View the sample data") {
			ACCustomEvents.viewDataAttribute(attributeValueList = "${sampleRawDataValue}");

			ACCustomEvents.viewDataAttributeInOrder(
				attributeValue = "AC Page - Site Name - ${dataSource}",
				index = "1");
		}
	}

	@description = "Feature ID: LRAC-4265 | Automation ID: LRAC-10215 | Test Summary: Edit global attribute description"
	@priority = "5"
	test EditGlobalAttributeDescription {
		var globalAttributeName = "pageTitle";

		task ("Fill fields and create custom event") {
			var customEventName = ACCustomEvents.generateCustomEventName();

			ACCustomEvents.createCustomEvent(
				customEventName = "${customEventName}",
				timeToSendCustomEvent = "1");
		}

		task ("Close sessions") {
			ACUtils.closeAllSessionsAndWait();
		}

		task ("Switch the property in AC") {
			ACUtils.launchAC();

			ACProperties.switchProperty(propertyName = "${assignedPropertyName}");
		}

		task ("Navigation to global attributes tab") {
			ACNavigation.goToSettings();

			ACSettings.goToDefinitions();

			ACSettings.goToEventAttributes();

			ACNavigation.openItem(itemName = "Global Attributes");
		}

		task ("Edit the global attribute") {
			ACNavigation.openItem(itemName = "${globalAttributeName}");

			ACUtils.clickAnyButton(button = "Edit");
		}

		task ("Change the description of the global attribute") {
			ACCustomEvents.changeCustomEventDisplayNameOrDescription(newDescription = "${globalAttributeName} Description");
		}

		task ("Back to Event Attributes and Navigation to global attributes tab") {
			ACNavigation.backNavigation(pageName = "Event Attributes");

			ACNavigation.openItem(itemName = "Global Attributes");
		}

		task ("Check if the description has been changed") {
			ACCustomEvents.viewDisplayNameOrDescriptionList(nameValue = "${globalAttributeName} Description");
		}
	}

	@description = "Feature ID: LRAC-7858 | Automation ID: LRAC-10214 | Test Summary: Edit global attributes name"
	@priority = "5"
	test RenameGlobalAttribute {
		var globalAttributeName = "pageTitle";

		task ("Fill fields and create custom event") {
			var customEventName = ACCustomEvents.generateCustomEventName();

			ACCustomEvents.createCustomEvent(
				customEventName = "${customEventName}",
				timeToSendCustomEvent = "1");
		}

		task ("Close sessions") {
			ACUtils.closeAllSessionsAndWait();
		}

		task ("Switch the property in AC") {
			ACUtils.launchAC();

			ACProperties.switchProperty(propertyName = "${assignedPropertyName}");
		}

		task ("Navigation to global attributes tab") {
			ACNavigation.goToSettings();

			ACSettings.goToDefinitions();

			ACSettings.goToEventAttributes();

			ACNavigation.openItem(itemName = "Global Attributes");
		}

		task ("Edit the global attribute") {
			ACNavigation.openItem(itemName = "${globalAttributeName}");

			ACUtils.clickAnyButton(button = "Edit");
		}

		task ("Change the display name of the global attribute") {
			ACCustomEvents.changeCustomEventDisplayNameOrDescription(newDisplayName = "${globalAttributeName} Display Name");
		}

		task ("Back to Event Attributes and Navigation to global attributes tab") {
			ACNavigation.backNavigation(pageName = "Event Attributes");

			ACNavigation.openItem(itemName = "Global Attributes");
		}

		task ("Check if the display name has been changed") {
			ACCustomEvents.viewDisplayNameOrDescriptionList(nameValue = "${globalAttributeName} Display Name");
		}
	}

	@description = "Feature ID: LRAC-4265 | Automation ID: LRAC-10216 | Test Summary: Edit global attribute name with a name already in use"
	@priority = "4"
	test RenameGlobalAttributeWithNameAlreadyInUse {
		var globalAttributeName1 = "url";
		var globalAttributeName2 = "referrer";
		var globalAttributeName3 = "canonicalUrl";

		task ("Fill fields and create custom event") {
			var customEventName = ACCustomEvents.generateCustomEventName();

			ACCustomEvents.createCustomEvent(
				customEventName = "${customEventName}",
				timeToSendCustomEvent = "1");
		}

		task ("Close sessions") {
			ACUtils.closeAllSessionsAndWait();
		}

		task ("Switch the property in AC") {
			ACUtils.launchAC();

			ACProperties.switchProperty(propertyName = "${assignedPropertyName}");
		}

		task ("Navigation to global attributes tab") {
			ACNavigation.goToSettings();

			ACSettings.goToDefinitions();

			ACSettings.goToEventAttributes();

			ACNavigation.openItem(itemName = "Global Attributes");
		}

		task ("Edit the global attribute") {
			ACNavigation.openItem(itemName = "${globalAttributeName1}");

			ACUtils.clickAnyButton(button = "Edit");
		}

		task ("Change the display name of the global attribute") {
			ACCustomEvents.changeCustomEventDisplayNameOrDescription(newDisplayName = "${globalAttributeName1} Display Name");
		}

		task ("Back to Event Attributes and Navigation to global attributes tab") {
			ACNavigation.backNavigation(pageName = "Event Attributes");

			ACNavigation.openItem(itemName = "Global Attributes");
		}

		task ("Edit the global attribute") {
			ACNavigation.openItem(itemName = "${globalAttributeName2}");

			ACUtils.clickAnyButton(button = "Edit");
		}

		task ("Rename to an existing display name") {
			ACCustomEvents.changeCustomEventDisplayNameOrDescription(newDisplayName = "${globalAttributeName3}");
		}

		task ("Check that the display name cannot be renamed to an existing one") {
			ACCustomEvents.viewInvalidCustomEventEdition(invalidMessage = "An attribute already exists with that display name. Please enter a different display name.");
		}

		task ("Rename to an existing display name") {
			ACCustomEvents.changeCustomEventDisplayNameOrDescription(newDisplayName = "${globalAttributeName1} Display Name");
		}

		task ("Check that the display name cannot be renamed to an existing one") {
			ACCustomEvents.viewInvalidCustomEventEdition(invalidMessage = "An attribute already exists with that display name. Please enter a different display name.");
		}
	}

	@description = "Feature ID: LRAC-4265 | Automation ID: LRAC-10217 | Test Summary: Search for global attribute"
	@ignore = "true"
	@priority = "4"
	test SearchForGlobalAttribute {

		// AC Story ticket: LRAC-9248

		var globalAttributesList = "canonicalUrl,pageDescription,pageTitle";

		task ("Fill fields and create custom event") {
			var customEventName = ACCustomEvents.generateCustomEventName();

			ACCustomEvents.createCustomEvent(
				attributesName = "${attributesNameList}",
				customEventName = "${customEventName}",
				timeToSendCustomEvent = "1");
		}

		task ("Close sessions") {
			ACUtils.closeAllSessionsAndWait();
		}

		task ("Switch the property in AC") {
			ACUtils.launchAC();

			ACProperties.switchProperty(propertyName = "${assignedPropertyName}");
		}

		task ("Navigation to global attributes tab") {
			ACNavigation.goToSettings();

			ACSettings.goToDefinitions();

			ACSettings.goToEventAttributes();

			ACNavigation.openItem(itemName = "Global Attributes");
		}

		task ("Search for the name of the global attributes and verify that it was found") {
			for (var globalAttribute : list "${globalAttributesList}") {
				ACUtils.searchBar(searchItem = "${globalAttribute}");

				ACUtils.viewNameList(nameList = "${globalAttribute}");
			}
		}

		task ("Search for the name of attribute that doesn't exist and verify that it didn't find anything") {
			ACUtils.searchBar(searchItem = "acqa");

			// This test is being ignored due to a history that makes changes to empty state messages.

			ACUtils.viewNoResultsMessage(noResultMessage = "There are no global attributes found.");
		}
	}

}