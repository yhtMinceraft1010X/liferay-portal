@component-name = "portal-analytics-cloud"
definition {

	property custom.properties = "jsonws.web.service.paths.excludes=";
	property portal.release = "false";
	property portal.upstream = "true";
	property property.group = "analytics.cloud.profile";
	property testray.main.component.name = "Analytics Cloud";

	setUp {
		task ("Copy the token from AC") {
			ACUtils.launchAC();

			ACSettings.takeTokenFromAnalyticsCloud();
		}

		task ("Set up instance and sign in DXP") {
			TestCase.setUpPortalInstance();

			ACUtils.launchDXP();
		}

		task ("Add a new site with a public widget page") {
			ACUtils.addSiteAndPage();
		}

		task ("Add a Web Content Display to page and display the custom event") {
			ACCustomEvents.createWCWithCustomEventAndAddToPage();
		}

		task ("Add a new user") {
			JSONUser.addUser(
				userEmailAddress = "ac@liferay.com",
				userFirstName = "ac",
				userLastName = "ac",
				userScreenName = "ac");

			JSONUser.setFirstPassword(
				agreeToTermsAndAnswerReminderQuery = "true",
				requireReset = "false",
				userEmailAddress = "ac@liferay.com");
		}

		task ("Connect the DXP to AC") {
			ACDXPSettings.connectDXPtoAnalyticsCloud(siteName = "Site Name");
		}

		task ("Sign in as new user") {
			User.logoutAndLoginPG(
				userLoginEmailAddress = "ac@liferay.com",
				userLoginFullName = "ac ac");
		}

		task ("View the site page") {
			ACUtils.navigateToSitePage(
				pageName = "AC Page",
				siteName = "Site Name");
		}
	}

	tearDown {
		var testPortalInstance = PropsUtil.get("test.portal.instance");

		ACUtils.tearDownDXP();

		if ("${testPortalInstance}" == "true") {
			PortalInstances.tearDownCP();
		}
		else {
			JSONGroup.deleteGroupByName(groupName = "Site Name");
		}

		JSONUser.tearDownNonAdminUsers();

		ACUtils.tearDownAC();
	}

	@description = "Feature ID: LRAC-7346 | Automation ID: LRAC-10195 | Test Summary: Unblock single custom event and unblock custom events in bulk"
	@ignore = "true"
	@priority = "5"
	test CanUnblockCustomEvent {

		// AC Story ticket: LRAC-9248

		task ("Fill fields and create 3 custom events") {
			var customEventName1 = ACCustomEvents.generateCustomEventName();

			ACCustomEvents.createCustomEvent(
				customEventName = "${customEventName1}",
				timeToSendCustomEvent = "1");

			var customEventName2 = ACCustomEvents.generateCustomEventName();

			ACCustomEvents.createCustomEvent(
				customEventName = "${customEventName2}",
				timeToSendCustomEvent = "1");

			var customEventName3 = ACCustomEvents.generateCustomEventName();

			ACCustomEvents.createCustomEvent(
				customEventName = "${customEventName3}",
				timeToSendCustomEvent = "1");
		}

		task ("Close sessions") {
			ACUtils.closeAllSessionsAndWait();
		}

		task ("Switch the property in AC and go to event analysis") {
			ACUtils.launchAC();

			ACProperties.switchProperty(propertyName = "${assignedPropertyName}");
		}

		task ("Navigation to custom events tab") {
			ACNavigation.goToSettings();

			ACSettings.goToDefinitions();

			ACSettings.goToEvents();

			ACNavigation.openItem(itemName = "Custom Events");
		}

		task ("Block all custom events") {
			ACCustomEvents.blockAllCustomEvents();
		}

		task ("Navigation to block custom event list") {
			ACCustomEvents.openBlockCustomEventList();
		}

		task ("Unblock custom event") {
			ACCustomEvents.unblockCustomEvent(customEventName = "${customEventName1}");
		}

		task ("Search for the name of the custom event that was unblocked in block list") {
			ACUtils.searchBar(searchItem = "${customEventName1}");

			// This test is being ignored due to a history that makes changes to empty state messages.

			ACUtils.viewNoResultsMessage(noResultMessage = "There are no items found.");
		}

		task ("Navigation to custom events tab") {
			ACNavigation.backNavigation(pageName = "Events");

			ACNavigation.openItem(itemName = "Custom Events");
		}

		task ("Search for the name of the custom event that was unblocked in custom event list") {
			ACUtils.searchBar(searchItem = "${customEventName1}");

			ACUtils.viewNameList(nameList = "${customEventName1}");
		}

		task ("Navigation to block custom event list") {
			ACCustomEvents.openBlockCustomEventList();
		}

		task ("Unblock all custom events") {
			ACCustomEvents.unblockAllCustomEvents();

			ACUtils.viewNoResultsMessage(noResultMessage = "No blocked events to report.");
		}

		task ("Navigation to custom events tab") {
			ACNavigation.backNavigation(pageName = "Events");

			ACNavigation.openItem(itemName = "Custom Events");
		}

		task ("Search for the name of the custom event that was unblocked in custom event list") {
			for (var customEventName : list "${customEventName2},${customEventName3}") {
				ACUtils.searchBar(searchItem = "${customEventName}");

				ACUtils.viewNameList(nameList = "${customEventName}");
			}
		}
	}

	@description = "Feature ID: LRAC-7346 | Automation ID: LRAC-10194 | Test Summary: Blocked event is hidden in the Event Analysis events lists and Unblocked event is visible in the Event Analysis events lists"
	@priority = "4"
	test ViewEventIsVisibleOrNotInEventAnalysisWhenBlockedOrUnblocked {
		task ("Fill fields and create custom event") {
			var customEventName = ACCustomEvents.generateCustomEventName();

			ACCustomEvents.createCustomEvent(
				customEventName = "${customEventName}",
				timeToSendCustomEvent = "1");
		}

		task ("Close sessions") {
			ACUtils.closeAllSessionsAndWait();
		}

		task ("Switch the property in AC and go to event analysis") {
			ACUtils.launchAC();

			ACProperties.switchProperty(propertyName = "${assignedPropertyName}");

			ACNavigation.goToEventAnalysis();
		}

		task ("Click to add the custom event and check if it is present") {
			ACEventAnalysis.clickToAddEventButton();

			ACEventAnalysis.searchEventAnalysis(searchItem = "${customEventName}");

			ACEventAnalysis.viewEventAnalysisList(eventList = "${customEventName}");
		}

		task ("Navigation to custom events tab") {
			ACNavigation.goToSettings();

			ACSettings.goToDefinitions();

			ACSettings.goToEvents();

			ACNavigation.openItem(itemName = "Custom Events");
		}

		task ("Block custom event") {
			ACCustomEvents.blockCustomEvent(customEventName = "${customEventName}");
		}

		task ("Navigation to event analysis") {
			ACNavigation.exitSettings();

			ACNavigation.goToEventAnalysis();
		}

		task ("Click to add the custom event and check if it is not present") {
			ACEventAnalysis.clickToAddEventButton();

			ACEventAnalysis.searchEventAnalysis(searchItem = "${customEventName}");

			ACEventAnalysis.viewEventAnalysisListNotPresent(eventList = "${customEventName}");
		}

		task ("Navigation to custom events tab") {
			ACNavigation.goToSettings();

			ACSettings.goToDefinitions();

			ACSettings.goToEvents();

			ACNavigation.openItem(itemName = "Custom Events");
		}

		task ("Navigation to block custom event list") {
			ACCustomEvents.openBlockCustomEventList();
		}

		task ("Unblock custom event") {
			ACCustomEvents.unblockCustomEvent(customEventName = "${customEventName}");
		}

		task ("Navigation to event analysis") {
			ACNavigation.exitSettings();

			ACNavigation.goToEventAnalysis();
		}

		task ("Click to add the custom event and check if it is present") {
			ACEventAnalysis.clickToAddEventButton();

			ACEventAnalysis.searchEventAnalysis(searchItem = "${customEventName}");

			ACEventAnalysis.viewEventAnalysisList(eventList = "${customEventName}");
		}
	}

}