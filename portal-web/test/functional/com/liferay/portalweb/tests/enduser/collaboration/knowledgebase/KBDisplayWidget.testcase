@component-name = "portal-knowledge-base"
definition {

	property portal.release = "true";
	property portal.upstream = "true";
	property testray.main.component.name = "Knowledge Base";

	setUp {
		TestCase.setUpPortalInstance();

		User.firstLoginPG();

		JSONLayout.addPublicLayout(
			groupName = "Guest",
			layoutName = "Knowledge Base Display Page");

		JSONLayout.addWidgetToPublicLayout(
			column = "1",
			groupName = "Guest",
			layoutName = "Knowledge Base Display Page",
			widgetName = "Knowledge Base Display");
	}

	tearDown {
		var testPortalInstance = PropsUtil.get("test.portal.instance");

		if ("${testPortalInstance}" == "true") {
			PortalInstances.tearDownCP();
		}
		else {
			Workflow.tearDownSiteWorkflow();

			BlogsEntry.tearDownCP();

			KBArticle.tearDownCP();

			PagesAdmin.tearDownCP();
		}
	}

	@priority = "4"
	@refactordone
	@uitest
	test CanAddChildArticle {
		JSONKnowledgeBase.addkBArticle(
			groupName = "Guest",
			kbArticleContent = "Knowledge Base Article Content",
			kbArticleTitle = "Knowledge Base Article Title");

		Navigator.gotoPage(pageName = "Knowledge Base Display Page");

		KBArticle.addChildPGViaKBDisplay(
			kbArticleContent = "Child Article Content",
			kbArticleTitle = "Child Article Title");

		Refresh();

		KBArticle.viewChildArticlePG(
			kbArticleTitle = "Knowledge Base Article Title",
			kbChildArticleContent = "Child Article Content",
			kbChildArticleTitle = "Child Article Title");
	}

	@priority = "4"
	@refactorneeded
	test CanAddMultipleSuggestions {
		KBAdmin.openKBAdmin(siteURLKey = "guest");

		for (var articleCount : list "1,2") {
			KBArticle.addCP(
				kbArticleContent = "Knowledge Base Article${articleCount} Content",
				kbArticleTitle = "Knowledge Base Article${articleCount} Title");
		}

		Navigator.gotoPage(pageName = "Knowledge Base Display Page");

		KBArticle.gotoKBArticleViaKBDisplayHierarchyNavigationPG(kbArticleTitle = "Knowledge Base Article1 Title");

		for (var suggestionCount : list "1,2,3") {
			KBArticleSuggestion.addPG(
				kbArticleTitle = "Knowledge Base Article1 Title",
				kbSuggestionBody = "Knowledge Base Article1 Suggestion${suggestionCount}");
		}

		Navigator.gotoPage(pageName = "Knowledge Base Display Page");

		AssertTextEquals.assertPartialText(
			locator1 = "KnowledgeBase#LINK_THERE_ARE_SUGGESTIONS",
			value1 = "There are 3 suggestions. (3 Pending)");

		for (var suggestionCount : list "1,2,3") {
			KBArticleSuggestion.viewPG(
				kbSuggestionBody = "Knowledge Base Article1 Suggestion${suggestionCount}",
				kbSuggestionUserName = "Test Test",
				status = "New");
		}
	}

	@priority = "4"
	@refactordone
	test CanCancelAddingChildArticle {
		JSONKnowledgeBase.addkBArticle(
			groupName = "Guest",
			kbArticleContent = "Knowledge Base Article Content",
			kbArticleTitle = "Knowledge Base Article Title");

		Navigator.gotoPage(pageName = "Knowledge Base Display Page");

		KBArticle.addChildPGViaKBDisplay(
			cancel = "true",
			kbArticleContent = "Child Article Content",
			kbArticleTitle = "Child Article Title");

		Navigator.gotoPage(pageName = "Knowledge Base Display Page");

		KBArticle.viewPG(
			kbArticleContent = "Knowledge Base Article Content",
			kbArticleTitle = "Knowledge Base Article Title");

		AssertElementNotPresent(locator1 = "KnowledgeBase#HIERARCHY_NAVIGATION_CHILD");
	}

	@priority = "4"
	@refactordone
	test CanDeleteArticle {
		JSONKnowledgeBase.addkBArticle(
			groupName = "Guest",
			kbArticleContent = "Knowledge Base Article Content",
			kbArticleTitle = "Knowledge Base Article Title");

		Navigator.gotoPage(pageName = "Knowledge Base Display Page");

		KBArticle.deletePG(kbArticleTitle = "Knowledge Base Article Title");

		KBArticle.viewDefaultViaKBDisplayPG();
	}

	@priority = "4"
	@refactordone
	test CanDeleteChildArticle {
		JSONKnowledgeBase.addkBArticle(
			groupName = "Guest",
			kbArticleContent = "Knowledge Base Article Content",
			kbArticleTitle = "Knowledge Base Article Title");

		JSONKnowledgeBase.addkBChildArticle(
			groupName = "Guest",
			kbArticleTitle = "Knowledge Base Article Title",
			kbChildArticleContent = "Child Article Content",
			kbChildArticleTitle = "Child Article Title");

		Navigator.gotoPage(pageName = "Knowledge Base Display Page");

		KBArticle.viewChildArticlePG(
			kbArticleTitle = "Knowledge Base Article Title",
			kbChildArticleContent = "Child Article Content",
			kbChildArticleTitle = "Child Article Title");

		KBArticle.deletePG();
	}

	@priority = "5"
	@refactordone
	test CanEditArticle {
		JSONKnowledgeBase.addkBArticle(
			groupName = "Guest",
			kbArticleContent = "Knowledge Base Article Content",
			kbArticleTitle = "Knowledge Base Article Title");

		Navigator.gotoPage(pageName = "Knowledge Base Display Page");

		KBArticle.editPGViaKBDisplay(
			kbArticleContentEdit = "Knowledge Base Article Content Edit",
			kbArticleTitle = "Knowledge Base Article Title",
			kbArticleTitleEdit = "Knowledge Base Article Title Edit");

		Navigator.gotoPage(pageName = "Knowledge Base Display Page");

		KBArticle.viewViaKBDisplayPG(
			kbArticleContent = "Knowledge Base Article Content Edit",
			kbArticleTitle = "Knowledge Base Article Title Edit");
	}

	@priority = "4"
	@refactorneeded
	test CanEditChildArticle {
		JSONKnowledgeBase.addkBArticle(
			groupName = "Guest",
			kbArticleContent = "Knowledge Base Article Parent Content 1",
			kbArticleTitle = "Knowledge Base Article Parent Title 1");

		JSONKnowledgeBase.addkBChildArticle(
			groupName = "Guest",
			kbArticleTitle = "Knowledge Base Article Parent Title 1",
			kbChildArticleContent = "Knowledge Base Article Parent Title 1 Child Content 1",
			kbChildArticleTitle = "Knowledge Base Article Parent Title 1 Child 1");

		Navigator.gotoPage(pageName = "Knowledge Base Display Page");

		KBArticle.gotoKBChildArticleViaKBDisplayHierarchyNavigationPG(
			kbArticleTitle = "Knowledge Base Article Parent Title 1",
			kbChildArticleTitle = "Knowledge Base Article Parent Title 1 Child 1");

		KBArticle.editPGViaKBDisplay(
			kbArticleContentEdit = "Knowledge Base Child Article Content Edited 1",
			kbArticleTitleEdit = "Knowledge Base Article Parent Title 1 Child 1");

		Navigator.gotoPage(pageName = "Knowledge Base Display Page");

		KBArticle.viewParentArticleHierarchyViaKBDisplayPG(kbArticleTitle = "Knowledge Base Article Parent Title");

		KBArticle.viewChildArticleHierarchyViaKBDisplayPG(kbArticleTitle = "Knowledge Base Article Parent Title 1");
	}

	@priority = "4"
	@refactorneeded
	test CanEditDisplaySettings {
		JSONKnowledgeBase.addkBArticle(
			groupName = "Guest",
			kbArticleContent = "Knowledge Base Article Content",
			kbArticleTitle = "Knowledge Base Article Title");

		Navigator.gotoPage(pageName = "Knowledge Base Display Page");

		KBArticle.viewDefaultKBDisplaySettingsPG();

		KBArticle.configureKBDisplaySettingsPG(
			history = "false",
			print = "false",
			subscribe = "false");

		Navigator.gotoPage(pageName = "Knowledge Base Display Page");

		KBArticle.viewKBDisplayPG(
			history = "false",
			print = "false",
			subscribe = "false");
	}

	@description = "This is a test for LPS-94613. It checks that a user cannot select the draft article from the folder in KB display widget."
	@ignore = "Test Stub"
	@priority = "4"
	test CannotSelectDraftArticleFromFolder {

		// TODO LPS-148977 CannotSelectDraftArticleFromFolder pending implementation

	}

	@priority = "3"
	@refactordone
	test CannotViewDuplicateBreadcrumbsWhenAddingParentArticle {
		JSONKnowledgeBase.addkBArticle(
			groupName = "Guest",
			kbArticleContent = "Knowledge Base Article Content Parent",
			kbArticleTitle = "Knowledge Base Article Title Parent");

		JSONKnowledgeBase.addkBArticle(
			groupName = "Guest",
			kbArticleContent = "Knowledge Base Article Content Child",
			kbArticleTitle = "Knowledge Base Article Title Child");

		Navigator.gotoPage(pageName = "Knowledge Base Display Page");

		KBArticle.gotoKBArticleViaKBDisplayHierarchyNavigationPG(kbArticleTitle = "Knowledge Base Article Title Child");

		KBArticle.moveArticleViaKBDisplayPG(kbArticleTitle = "Knowledge Base Article Title Child");

		SelectFrame(locator1 = "IFrame#MODAL_BODY");

		AssertElementPresent(locator1 = "KnowledgeBaseSelectParent#BREADCRUMB_HOME");

		AssertElementNotPresent(locator1 = "//ul[contains(@class,'breadcrumb')]//a[contains(.,'Home')]/following-sibling::ul[contains(@class,'breadcrumb')]");
	}

	@priority = "3"
	@refactordone
	test CanSaveChildArticleAsDraft {
		JSONKnowledgeBase.addkBArticle(
			groupName = "Guest",
			kbArticleContent = "Knowledge Base Article Content Parent",
			kbArticleTitle = "Knowledge Base Article Title Parent");

		Navigator.gotoPage(pageName = "Knowledge Base Display Page");

		KBArticle.addChildPGViaKBDisplay(
			kbArticleContent = "Knowledge Base Child Article Content",
			kbArticleTitle = "Knowledge Base Child Article Title",
			saveAsDraft = "true");

		Navigator.gotoPage(pageName = "Knowledge Base Display Page");

		AssertElementNotPresent(locator1 = "KnowledgeBase#HIERARCHY_NAVIGATION_CHILD");

		KBAdmin.openKBAdmin(siteURLKey = "guest");

		KBArticle.gotoChildArticleDescriptiveDetails(kbArticleTitle = "Knowledge Base Article Title Parent");

		KBArticle.viewCP(
			kbArticleContent = "Knowledge Base Child Article Content",
			kbArticleTitle = "Knowledge Base Child Article Title",
			kbStatus = "Draft");
	}

	@priority = "4"
	@refactordone
	test CanSelectParentArticle {
		JSONKnowledgeBase.addkBArticle(
			groupName = "Guest",
			kbArticleContent = "Knowledge Base Article Parent Content 1",
			kbArticleTitle = "Knowledge Base Article Parent Title 1");

		JSONKnowledgeBase.addkBArticle(
			groupName = "Guest",
			kbArticleContent = "Knowledge Base Article Parent Content 1 Child 1",
			kbArticleTitle = "Knowledge Base Article Parent Title 1 Child 1");

		Navigator.gotoPage(pageName = "Knowledge Base Display Page");

		KBArticle.gotoKBArticleViaKBDisplayHierarchyNavigationPG(kbArticleTitle = "Knowledge Base Article Parent Title 1 Child 1");

		KBArticle.moveArticleViaKBDisplayPG(kbArticleTitle = "Knowledge Base Article Parent Title 1 Child 1");

		KBArticle.selectParentViaKBDisplayPG(kbArticleTitle = "Knowledge Base Article Parent Title 1");

		KBArticle.viewParentArticleHierarchyViaKBDisplayPG(kbArticleTitle = "Knowledge Base Article Parent Title");

		KBArticle.viewChildArticleHierarchyViaKBDisplayPG(kbArticleTitle = "Knowledge Base Article Parent Title 1");
	}

	@priority = "4"
	@refactordone
	test CanViewArticlesInFolder {
		JSONKnowledgeBase.addkBFolder(
			groupName = "Guest",
			kbFolderDescription = "KB Folder 1 Description",
			kbFolderName = "KB Folder");

		JSONKnowledgeBase.addkBArticle(
			folderName = "KB Folder",
			groupName = "Guest",
			kbArticleContent = "Knowledge Base Article Content 1",
			kbArticleTitle = "Knowledge Base Article Title 1");

		JSONKnowledgeBase.addkBArticle(
			folderName = "KB Folder",
			groupName = "Guest",
			kbArticleContent = "Knowledge Base Article Content 2",
			kbArticleTitle = "Knowledge Base Article Title 2");

		Navigator.gotoPage(pageName = "Knowledge Base Display Page");

		KBArticle.viewViaKBDisplayPG(
			kbArticleContent = "Knowledge Base Article Content 1",
			kbArticleTitle = "Knowledge Base Article Title 1");

		KBArticle.gotoKBArticleViaKBDisplayHierarchyNavigationPG(kbArticleTitle = "Knowledge Base Article Title 2");

		KBArticle.viewViaKBDisplayPG(
			kbArticleContent = "Knowledge Base Article Content 2",
			kbArticleTitle = "Knowledge Base Article Title 2");
	}

	@priority = "3"
	@refactorneeded
	test CanViewArticlesWithDifferentSiteScopes {
		JSONLayout.addPublicLayout(
			groupName = "Guest",
			layoutName = "Knowledge Base Display Page 2");

		JSONLayout.addWidgetToPublicLayout(
			column = "1",
			groupName = "Guest",
			layoutName = "Knowledge Base Display Page 2",
			widgetName = "Knowledge Base Display");

		Navigator.gotoPage(pageName = "Knowledge Base Display Page");

		Portlet.selectScopePG(
			portletName = "Knowledge Base Display",
			scope = "Knowledge Base Display Page");

		Navigator.gotoPage(pageName = "Knowledge Base Display Page 2");

		Portlet.selectScopePG(
			portletName = "Knowledge Base Display",
			scope = "Knowledge Base Display Page 2");

		Navigator.openURL();

		ProductMenu.gotoScopedPortlet(
			category = "Content & Data",
			portlet = "Knowledge Base",
			scope = "Knowledge Base Display Page");

		KBArticle.addCP(
			kbArticleContent = "Knowledge Base Article Content",
			kbArticleTitle = "Knowledge Base Article Title for Page");

		Navigator.openURL();

		ProductMenu.gotoScopedPortlet(
			category = "Content & Data",
			portlet = "Knowledge Base",
			scope = "Knowledge Base Display Page 2");

		KBArticle.addCP(
			kbArticleContent = "Knowledge Base Article Content",
			kbArticleTitle = "Knowledge Base Article Title for Page 2");

		Navigator.gotoPage(pageName = "Knowledge Base Display Page");

		KBArticle.viewPG(
			kbArticleContent = "Knowledge Base Article Content",
			kbArticleTitle = "Knowledge Base Article Title for Page");

		Navigator.gotoPage(pageName = "Knowledge Base Display Page 2");

		KBArticle.viewPG(
			kbArticleContent = "Knowledge Base Article Content",
			kbArticleTitle = "Knowledge Base Article Title for Page 2");
	}

	@priority = "4"
	@refactorneeded
	test CanViewArticleViaDefaultFriendlyURL {
		JSONKnowledgeBase.addkBArticle(
			groupName = "Guest",
			kbArticleContent = "Knowledge Base Article Content",
			kbArticleTitle = "Knowledge Base Article Title");

		JSONKnowledgeBase.addkBArticle(
			groupName = "Guest",
			kbArticleContent = "Knowledge Base Article2 Content",
			kbArticleTitle = "Knowledge Base Article2 Title");

		Navigator.gotoPage(pageName = "Knowledge Base Display Page");

		var kbArticleFriendlyURL = selenium.getAttribute("//div[@class='kbarticle-navigation']/ul/li/a[contains(.,'Knowledge Base Article2 Title')]@href");

		Navigator.openSpecificURL(url = "${kbArticleFriendlyURL}");

		KBArticle.viewViaKBDisplayPG(
			kbArticleContent = "Knowledge Base Article2 Content",
			kbArticleTitle = "Knowledge Base Article2 Title");
	}

	@priority = "3"
	@refactorneeded
	test CanViewArticleViaNavigationLinks {
		JSONKnowledgeBase.addkBArticle(
			groupName = "Guest",
			kbArticleContent = "Knowledge Base Article Content 1",
			kbArticleTitle = "Knowledge Base Article Title 1");

		JSONKnowledgeBase.addkBArticle(
			groupName = "Guest",
			kbArticleContent = "Knowledge Base Article Content 2",
			kbArticleTitle = "Knowledge Base Article Title 2");

		JSONKnowledgeBase.addkBArticle(
			groupName = "Guest",
			kbArticleContent = "Knowledge Base Article Content 3",
			kbArticleTitle = "Knowledge Base Article Title 3");

		Navigator.gotoPage(pageName = "Knowledge Base Display Page");

		KBArticle.viewViaKBDisplayPG(
			kbArticleContent = "Knowledge Base Article Content 1",
			kbArticleTitle = "Knowledge Base Article Title 1");

		KBArticle.viewNavigationLinksPG(
			next = "true",
			previous = "false");

		Click(locator1 = "KnowledgeBaseArticle#NEXT");

		KBArticle.viewViaKBDisplayPG(
			kbArticleContent = "Knowledge Base Article Content 2",
			kbArticleTitle = "Knowledge Base Article Title 2");

		KBArticle.viewNavigationLinksPG(
			next = "true",
			previous = "true");

		Click(locator1 = "KnowledgeBaseArticle#NEXT");

		KBArticle.viewViaKBDisplayPG(
			kbArticleContent = "Knowledge Base Article Content 3",
			kbArticleTitle = "Knowledge Base Article Title 3");

		KBArticle.viewNavigationLinksPG(
			next = "false",
			previous = "true");

		Click(locator1 = "KnowledgeBaseArticle#PREVIOUS");

		KBArticle.viewViaKBDisplayPG(
			kbArticleContent = "Knowledge Base Article Content 2",
			kbArticleTitle = "Knowledge Base Article Title 2");

		KBArticle.viewNavigationLinksPG(
			next = "true",
			previous = "true");

		Click(locator1 = "KnowledgeBaseArticle#PREVIOUS");

		KBArticle.viewViaKBDisplayPG(
			kbArticleContent = "Knowledge Base Article Content 1",
			kbArticleTitle = "Knowledge Base Article Title 1");

		KBArticle.viewNavigationLinksPG(
			next = "true",
			previous = "false");
	}

	@priority = "4"
	@refactorneeded
	test CanViewArticleViaUserDefinedFriendlyURL {
		KBAdmin.openKBAdmin(siteURLKey = "guest");

		KBArticle.addCP(
			kbArticleContent = "Knowledge Base Article Content",
			kbArticleTitle = "Knowledge Base Article Title");

		KBArticle.addCP(
			kbArticleContent = "Knowledge Base Article2 Content",
			kbArticleFriendlyURL = "Friendly Universal Resource Locator",
			kbArticleTitle = "Knowledge Base Article2 Title");

		Navigator.gotoPage(pageName = "Knowledge Base Display Page");

		var kbAricleFriendlyURL = selenium.getAttribute("//div[@class='kbarticle-navigation']/ul/li/a[contains(.,'Knowledge Base Article2 Title')]@href");

		Navigator.gotoPage(pageName = "Home");

		Navigator.openSpecificURL(url = "${kbAricleFriendlyURL}");

		KBArticle.viewViaKBDisplayPG(
			kbArticleContent = "Knowledge Base Article2 Content",
			kbArticleTitle = "Knowledge Base Article2 Title");
	}

	@priority = "4"
	@refactordone
	test CanViewChildArticleViaNextLink {
		JSONKnowledgeBase.addkBArticle(
			groupName = "Guest",
			kbArticleContent = "Knowledge Base Article Parent Content 1",
			kbArticleTitle = "Knowledge Base Article Parent Title 1");

		JSONKnowledgeBase.addkBChildArticle(
			groupName = "Guest",
			kbArticleTitle = "Knowledge Base Article Parent Title 1",
			kbChildArticleContent = "This is a child article. Lengthy articles are no longer summarized on the KB Display portlet, and the Read More link has been deprecated. To access a child article, one must click on the parent article and then click on the child page link found either: under the article navigation, or under the parent article entry.",
			kbChildArticleTitle = "Knowledge Base Article Parent Title 1 Child 1");

		Navigator.gotoPage(pageName = "Knowledge Base Display Page");

		KBArticle.viewParentArticleHierarchyViaKBDisplayPG(kbArticleTitle = "Knowledge Base Article Parent Title");

		KBArticle.viewViaNextLinkPG(
			kbArticleContent = "This is a child article. Lengthy articles are no longer summarized on the KB Display portlet, and the Read More link has been deprecated. To access a child article, one must click on the parent article and then click on the child page link found either: under the article navigation, or under the parent article entry.",
			kbArticleTitle = "Knowledge Base Article Parent Title 1 Child 1");
	}

	@description = "This test covers LPS-91303. It ensures that the content of a selected root folder is viewable."
	@priority = "3"
	@refactordone
	test CanViewContentOfConfiguredRootFolder {
		JSONKnowledgeBase.addkBFolder(
			groupName = "Guest",
			kbFolderDescription = "KB Folder 1 Description",
			kbFolderName = "KB Folder 1");

		JSONKnowledgeBase.addkBArticle(
			groupName = "Guest",
			kbArticleContent = "Knowledge Base Home Article",
			kbArticleTitle = "Knowledge Base Home Article");

		JSONKnowledgeBase.addkBArticle(
			folderName = "KB Folder 1",
			groupName = "Guest",
			kbArticleContent = "Knowledge Base Article Folder Content 1",
			kbArticleTitle = "Knowledge Base Article Folder Title 1");

		Navigator.gotoPage(pageName = "Knowledge Base Display Page");

		KBFolder.gotoViaKBDisplayPG(kbFolderName = "KB Folder 1");

		KBArticle.viewViaKBDisplayPG(
			kbArticleContent = "Knowledge Base Article Folder Content 1",
			kbArticleTitle = "Knowledge Base Article Folder Title 1");
	}

	@priority = "4"
	@refactordone
	test CanViewCustomFriendlyURL {
		var portalURL = PropsUtil.get("portal.url");

		for (var num : list "1,2") {
			JSONKnowledgeBase.addkBArticle(
				groupName = "Guest",
				kbArticleContent = "KB Article Content${num}",
				kbArticleTitle = "KB Article Title${num}",
				urlTitle = "custom-friendly-url${num}");
		}

		Navigator.gotoPage(pageName = "Knowledge Base Display Page");

		for (var num : list "1,2") {
			KBArticle.gotoViaKBDisplayPG(kbArticleTitle = "KB Article Title${num}");

			AssertLocation(value1 = "${portalURL}/web/guest/knowledge-base-display-page/-/knowledge_base/custom-friendly-url${num}");
		}
	}

	@priority = "4"
	@refactordone
	test CanViewDefaultFriendlyURL {
		var portalURL = PropsUtil.get("portal.url");

		for (var num : list "1,2") {
			JSONKnowledgeBase.addkBArticle(
				groupName = "Guest",
				kbArticleContent = "KB Article Content${num}",
				kbArticleTitle = "KB Article Title${num}");
		}

		Navigator.gotoPage(pageName = "Knowledge Base Display Page");

		for (var num : list "1,2") {
			KBArticle.gotoViaKBDisplayPG(kbArticleTitle = "KB Article Title${num}");

			AssertLocation(value1 = "${portalURL}/web/guest/knowledge-base-display-page/-/knowledge_base/kb-article-title${num}");
		}
	}

	@priority = "4"
	@refactordone
	test CanViewEditedKBArticleTitleHistory {
		JSONKnowledgeBase.addkBArticle(
			groupName = "Guest",
			kbArticleContent = "KB Article Content",
			kbArticleTitle = "KB Article Title");

		KBAdmin.openKBAdmin(siteURLKey = "guest");

		KBArticle.editCP(
			kbArticleContentEdit = "KB Article Content Edit",
			kbArticleTitle = "KB Article Title",
			kbArticleTitleEdit = "KB Article Title Edit");

		Navigator.gotoPage(pageName = "Knowledge Base Display Page");

		KBArticle.viewArticleHistoryPG(
			kbArticleContent = "KB Article Content Edit",
			kbArticleContentAddedDiff = "Edit",
			kbArticleVersionNew = "2",
			kbArticleVersionOld = "1",
			viewNoXmlInTitle = "true");
	}

	@priority = "4"
	@refactordone
	test CanViewEmptyState {
		Navigator.gotoPage(pageName = "Knowledge Base Display Page");

		KBArticle.viewDefaultViaKBDisplayPG();
	}

	@priority = "4"
	@refactordone
	test CanViewSuggestionsWorkflow {
		JSONKnowledgeBase.addkBArticle(
			groupName = "Guest",
			kbArticleContent = "Knowledge Base Article1 Content",
			kbArticleTitle = "Knowledge Base Article1 Title");

		Navigator.gotoPage(pageName = "Knowledge Base Display Page");

		KBArticleSuggestion.addPG(
			kbArticleTitle = "Knowledge Base Article1 Title",
			kbSuggestionBody = "Knowledge Base Article1 Suggestion");

		KBArticleSuggestion.gotoSuggestionStatusNew(
			kbSuggestionBody = "Knowledge Base Article1 Suggestion",
			suggestionCount = "1");

		KBArticleSuggestion.viewPG(
			kbSuggestionBody = "Knowledge Base Article1 Suggestion",
			kbSuggestionUserName = "Test Test",
			status = "New");

		KBArticleSuggestion.moveToInProgressStatus(
			kbArticleTitle = "Knowledge Base Article1 Title",
			kbSuggestionBody = "Knowledge Base Article1 Suggestion");

		KBArticleSuggestion.viewPG(
			kbSuggestionBody = "Knowledge Base Article1 Suggestion",
			kbSuggestionUserName = "Test Test",
			status = "In Progress");

		KBArticleSuggestion.moveToResolvedStatus(
			kbArticleTitle = "Knowledge Base Article1 Title",
			kbSuggestionBody = "Knowledge Base Article1 Suggestion");

		KBArticleSuggestion.viewPG(
			kbSuggestionBody = "Knowledge Base Article1 Suggestion",
			kbSuggestionUserName = "Test Test",
			status = "Resolved");
	}

	@priority = "3"
	@refactorneeded
	test JavascriptIsEscapedFromArticleTitle {
		KBAdmin.openKBAdmin(siteURLKey = "guest");

		KBArticle.addCP(
			kbArticleContent = "Knowledge Base Article Content",
			kbArticleFriendlyURL = "Title",
			kbArticleTitle = '''><img onerror="alert('xss')" src="xx" /><''',
			specialCharacters = "true");

		KBAdmin.openKBAdmin(siteURLKey = "guest");

		KBArticle.addCP(
			kbArticleContent = "Knowledge Base Article Content",
			kbArticleTitle = "Knowledge Base Article Title");

		Navigator.gotoPage(pageName = "Knowledge Base Display Page");

		AssertAlertNotPresent();

		KBArticle.viewViaKBDisplayPG(
			kbArticleContent = "Knowledge Base Article Content",
			kbArticleTitle = '''><img onerror="alert('xss')" src="xx" /><''');
	}

	@priority = "4"
	@refactorneeded
	test ViewImportedKBArticlesInHierarchyWithMarkdownFormat {
		KBAdmin.openKBAdmin(siteURLKey = "guest");

		KBArticle.importCP(
			applyNumericalPrefix = "true",
			kbZipFile = "KBArticlesWithMarkdown.zip");

		Navigator.gotoPage(pageName = "Knowledge Base Display Page");

		KBArticle.gotoKBArticleViaKBDisplayHierarchyNavigationPG(kbArticleTitle = "Knowledge Base Article Parent Title 1");

		KBArticle.viewParentArticleHierarchyViaKBDisplayPG(kbArticleTitle = "Knowledge Base Article Parent Title");

		KBArticle.viewChildArticleHierarchyViaKBDisplayPG(kbArticleTitle = "Knowledge Base Article Parent Title 1");

		KBArticle.viewChildArticleHierarchyViaKBDisplayPG(kbArticleTitle = "Knowledge Base Article Parent Title 2");

		KBArticle.viewChildArticleHierarchyViaKBDisplayPG(kbArticleTitle = "Knowledge Base Article Parent Title 3");

		KBArticle.viewChildArticleHierarchyViaKBDisplayPG(kbArticleTitle = "Knowledge Base Article Parent Title 4");

		KBArticle.viewChildArticleHierarchyViaKBDisplayPG(kbArticleTitle = "Knowledge Base Article Parent Title 5");

		KBArticle.gotoKBChildArticleViaKBDisplayHierarchyNavigationPG(
			kbArticleTitle = "Knowledge Base Article Parent Title 2",
			kbChildArticleTitle = "Knowledge Base Article Parent Title 2 Child 1");

		KBArticle.viewImageViaContentCmd(
			kbMarkdownImage = "lds-first-launch-3.png",
			kbMarkdownImageCaption = "Figure 1: The First Start wizard makes it easy to configure the runtime that ships with Liferay Developer Studio.");
	}

	@priority = "4"
	@refactorneeded
	test ViewImportedKBArticlesWithMultiplePeerHierarchies {
		KBAdmin.openKBAdmin(siteURLKey = "guest");

		KBArticle.importCP(
			applyNumericalPrefix = "true",
			kbZipFile = "KBArticlesWithMarkdown.zip");

		Navigator.gotoPage(pageName = "Knowledge Base Display Page");

		KBArticle.gotoKBArticleViaKBDisplayHierarchyNavigationPG(kbArticleTitle = "Knowledge Base Article Parent Title 1");

		KBArticle.viewParentArticleHierarchyViaKBDisplayPG(kbArticleTitle = "Knowledge Base Article Parent Title");

		KBArticle.viewChildArticleHierarchyViaKBDisplayPG(kbArticleTitle = "Knowledge Base Article Parent Title 1");

		KBArticle.viewChildArticleHierarchyViaKBDisplayPG(kbArticleTitle = "Knowledge Base Article Parent Title 2");

		KBArticle.viewChildArticleHierarchyViaKBDisplayPG(kbArticleTitle = "Knowledge Base Article Parent Title 3");

		KBArticle.viewChildArticleHierarchyViaKBDisplayPG(kbArticleTitle = "Knowledge Base Article Parent Title 4");

		KBArticle.viewChildArticleHierarchyViaKBDisplayPG(kbArticleTitle = "Knowledge Base Article Parent Title 5");
	}

	@priority = "4"
	@refactorneeded
	test ViewImportedKBArticleWithImagesAsKBAttachments {
		KBAdmin.openKBAdmin(siteURLKey = "guest");

		KBArticle.importCP(
			applyNumericalPrefix = "true",
			kbZipFile = "KBArticlesWithMetadata.zip");

		Navigator.gotoPage(pageName = "Knowledge Base Display Page");

		KBArticle.gotoKBArticleViaKBDisplayHierarchyNavigationPG(kbArticleTitle = "Knowledge Base Article Parent Title 1");

		KBArticle.gotoKBChildArticleViaKBDisplayHierarchyNavigationPG(
			kbArticleTitle = "Knowledge Base Article Parent Title 2",
			kbChildArticleTitle = "Knowledge Base Article Parent Title 2 Child 2");

		KBArticle.viewImageViaContentCmd();

		DMNavigator.openDocumentsAndMediaAdmin(siteURLKey = "guest");

		DMDocument.viewDefault();

		KBAdmin.openKBAdmin(siteURLKey = "guest");

		KBArticle.gotoChildArticleDescriptiveDetails(kbArticleTitle = "Knowledge Base Article Parent Title 2");

		KBArticle.viewAttachmentCP(kbArticleTitle = "Knowledge Base Article Parent Title 2 Child 2");
	}

	@priority = "4"
	@refactorneeded
	test ViewImportedKBArticleWithSourceURLMetadata {
		property test.name.skip.portal.instance = "KBDisplayWidget#ViewImportedKBArticleWithSourceURLMetadata";

		SystemSettings.openSystemSettingsAdmin();

		SystemSettings.gotoConfiguration(
			configurationCategory = "Knowledge Base",
			configurationName = "Service",
			configurationScope = "Site Scope");

		FormFields.enableCheckbox(fieldName = "sourceURLEnabled");

		SystemSettings.saveConfiguration();

		KBAdmin.openKBAdmin(siteURLKey = "guest");

		KBArticle.importCP(
			applyNumericalPrefix = "true",
			kbZipFile = "KBArticlesWithMetadata.zip");

		KBArticle.gotoViaKBAdminCP(
			hasChildArticle = "true",
			kbArticleTitle = "Knowledge Base Article Parent Title 2");

		KBArticle.viewChildArticleCP(
			kbChildArticleContent = "Using tabs is a good way to keep your portlet’s UI clean and organized.",
			kbChildArticleTitle = "Knowledge Base Article Parent Title 2 Child 2");

		KBArticle.viewSourceURLCP(
			kbArticleSourceURL = "https://github.com/liferay/liferay-docs/blob/master/develop/tutorials/articles/liferayui-taglibs/knowledge-base-article-parent-title-2-child-2.markdown",
			kbChildArticleTitle = "Knowledge Base Article Parent Title 2 Child 2");

		Navigator.gotoPage(pageName = "Knowledge Base Display Page");

		KBArticle.viewSourceURLPG(
			kbArticleSourceURL = "https://github.com/liferay/liferay-docs/blob/master/develop/tutorials/articles/adt/intro.markdown",
			kbArticleTitle = "Knowledge Base Article Parent Title 1 Child 1");
	}

	@priority = "3"
	@refactorneeded
	test ViewKBArticlesWithTheSamePriorityViaNavigationLinks {
		KBAdmin.openKBAdmin(siteURLKey = "guest");

		for (var kbCount : list "1,2,3") {
			KBArticle.addCP(
				kbArticleContent = "Knowledge Base Article Parent Content ${kbCount}",
				kbArticleTitle = "Knowledge Base Article Parent Title ${kbCount}");
		}

		for (var kbCount : list "1,2,3") {
			KBArticle.viewKBArticlePriorityCP(
				kbArticleTitle = "Knowledge Base Article Parent Title ${kbCount}",
				kbPriority = "${kbCount}.0");
		}

		KBArticle.viewKBArticlePriorityCP(
			kbArticleTitle = "Knowledge Base Article Parent Title 2",
			kbPriority = "2.0");

		KBArticle.viewKBArticlePriorityCP(
			kbArticleTitle = "Knowledge Base Article Parent Title 3",
			kbPriority = "3.0");

		Navigator.gotoPage(pageName = "Knowledge Base Display Page");

		KBArticle.viewParentArticleHierarchyViaKBDisplayPG(kbArticleTitle = "Knowledge Base Article Parent Title");

		KBArticle.gotoKBArticleViaKBDisplayHierarchyNavigationPG(kbArticleTitle = "Knowledge Base Article Parent Title 1");

		KBArticle.viewViaKBDisplayPG(
			kbArticleContent = "Knowledge Base Article Parent Content 1",
			kbArticleTitle = "Knowledge Base Article Parent Title 1");

		KBArticle.gotoNextArticlePG(kbDisplayNextArticle = "Knowledge Base Article Parent Title 2");

		KBArticle.viewViaKBDisplayPG(
			kbArticleContent = "Knowledge Base Article Parent Content 2",
			kbArticleTitle = "Knowledge Base Article Parent Title 2");

		KBArticle.gotoNextArticlePG(kbDisplayNextArticle = "Knowledge Base Article Parent Title 3");

		KBArticle.viewViaKBDisplayPG(
			kbArticleContent = "Knowledge Base Article Parent Content 3",
			kbArticleTitle = "Knowledge Base Article Parent Title 3");

		KBArticle.gotoPreviousArticle(kbDisplayPreviousArticle = "Knowledge Base Article Parent Title 2");

		KBArticle.viewViaKBDisplayPG(
			kbArticleContent = "Knowledge Base Article Parent Content 2",
			kbArticleTitle = "Knowledge Base Article Parent Title 2");

		KBArticle.gotoPreviousArticle(kbDisplayPreviousArticle = "Knowledge Base Article Parent Title 1");

		KBArticle.viewViaKBDisplayPG(
			kbArticleContent = "Knowledge Base Article Parent Content 1",
			kbArticleTitle = "Knowledge Base Article Parent Title 1");

		Navigator.gotoPage(pageName = "Knowledge Base Display Page");

		KBArticle.viewParentArticleHierarchyViaKBDisplayPG(kbArticleTitle = "Knowledge Base Article Parent Title");

		KBArticle.gotoKBArticleViaKBDisplayHierarchyNavigationPG(kbArticleTitle = "Knowledge Base Article Parent Title 1");

		KBArticle.viewViaKBDisplayPG(
			kbArticleContent = "Knowledge Base Article Parent Content 1",
			kbArticleTitle = "Knowledge Base Article Parent Title 1");

		KBArticle.gotoNextArticlePG(kbDisplayNextArticle = "Knowledge Base Article Parent Title 2");

		KBArticle.viewViaKBDisplayPG(
			kbArticleContent = "Knowledge Base Article Parent Content 2",
			kbArticleTitle = "Knowledge Base Article Parent Title 2");

		KBArticle.gotoNextArticlePG(kbDisplayNextArticle = "Knowledge Base Article Parent Title 3");

		KBArticle.viewViaKBDisplayPG(
			kbArticleContent = "Knowledge Base Article Parent Content 3",
			kbArticleTitle = "Knowledge Base Article Parent Title 3");

		KBArticle.gotoPreviousArticle(kbDisplayPreviousArticle = "Knowledge Base Article Parent Title 2");

		KBArticle.viewViaKBDisplayPG(
			kbArticleContent = "Knowledge Base Article Parent Content 2",
			kbArticleTitle = "Knowledge Base Article Parent Title 2");

		KBArticle.gotoPreviousArticle(kbDisplayPreviousArticle = "Knowledge Base Article Parent Title 1");

		KBArticle.viewViaKBDisplayPG(
			kbArticleContent = "Knowledge Base Article Parent Content 1",
			kbArticleTitle = "Knowledge Base Article Parent Title 1");
	}

	@priority = "4"
	@refactorneeded
	test ViewKBArticleWithKBChildArticleHierarchyNavigation {
		KBAdmin.openKBAdmin(siteURLKey = "guest");

		for (var articleCount : list "1,2,3") {
			KBArticle.addCP(
				kbArticleContent = "Knowledge Base Article Parent Content ${articleCount}",
				kbArticleTitle = "Knowledge Base Article Parent Title ${articleCount}");
		}

		for (var articleCount : list "1,2,3") {
			KBArticle.addChildArticleViaKBAdmin(
				kbArticleTitle = "Knowledge Base Article Parent Title 1",
				kbChildArticleContent = "Knowledge Base Article Parent Content 1 Child ${articleCount}",
				kbChildArticleTitle = "Knowledge Base Article Parent Title 1 Child ${articleCount}");
		}

		KBArticle.addChildArticleViaKBAdmin(
			kbArticleTitle = "Knowledge Base Article Parent Title 2",
			kbChildArticleContent = "Knowledge Base Article Parent Content 2 Child 1",
			kbChildArticleTitle = "Knowledge Base Article Parent Title 2 Child 1");

		KBArticle.addChildArticleViaKBAdmin(
			kbArticleTitle = "Knowledge Base Article Parent Title 2",
			kbChildArticleContent = "Knowledge Base Article Parent Content 2 Child 2",
			kbChildArticleTitle = "Knowledge Base Article Parent Title 2 Child 2");

		KBArticle.addChildArticleViaKBAdmin(
			kbArticleTitle = "Knowledge Base Article Parent Title 3",
			kbChildArticleContent = "Knowledge Base Article Parent Content 3 Child 1",
			kbChildArticleTitle = "Knowledge Base Article Parent Title 3 Child 1");

		Navigator.gotoPage(pageName = "Knowledge Base Display Page");

		KBArticle.gotoKBArticleViaKBDisplayHierarchyNavigationPG(kbArticleTitle = "Knowledge Base Article Parent Title 1");

		KBArticle.viewViaKBDisplayPG(
			kbArticleContent = "Knowledge Base Article Parent Content 1",
			kbArticleTitle = "Knowledge Base Article Parent Title 1");

		KBArticle.viewParentArticleHierarchyViaKBDisplayPG(kbArticleTitle = "Knowledge Base Article Parent Title");

		KBArticle.viewChildArticleHierarchyViaKBDisplayPG(kbArticleTitle = "Knowledge Base Article Parent Title 1");

		KBArticle.viewChildArticleHierarchyViaKBDisplayPG(kbArticleTitle = "Knowledge Base Article Parent Title 2");

		KBArticle.viewChildArticleHierarchyViaKBDisplayPG(kbArticleTitle = "Knowledge Base Article Parent Title 3");

		KBArticle.gotoKBChildArticleViaKBDisplayHierarchyNavigationPG(
			kbArticleTitle = "Knowledge Base Article Parent Title 2",
			kbChildArticleTitle = "Knowledge Base Article Parent Title 2 Child 2");

		KBArticle.viewViaKBDisplayPG(
			kbArticleContent = "Knowledge Base Article Parent Content 2 Child 2",
			kbArticleTitle = "Knowledge Base Article Parent Title 2 Child 2");
	}

	@priority = "3"
	@refactorneeded
	test ViewKBArticleWithTheSameTitleAndPriorityInDifferentFolders {
		KBAdmin.openKBAdmin(siteURLKey = "guest");

		KBFolder.addCP(
			kbFolderDescription = "KB Folder Description",
			kbFolderName = "KB Root Folder");

		KBFolder.gotoCP(kbFolderName = "KB Root Folder");

		KBFolder.addCP(
			kbFolderDescription = "KB Folder 1",
			kbFolderName = "KB Folder 1");

		KBFolder.gotoCP(kbFolderName = "KB Folder 1");

		KBArticle.addCP(
			kbArticleContent = "Knowledge Base Article Content 1",
			kbArticleTitle = "Knowledge Base Article Title 1");

		KBArticle.addCP(
			kbArticleContent = "Knowledge Base Article Content 2",
			kbArticleTitle = "Knowledge Base Article Title 2");

		KBAdmin.openKBAdmin(siteURLKey = "guest");

		KBFolder.gotoCP(kbFolderName = "KB Root Folder");

		KBFolder.addCP(
			kbFolderDescription = "KB Folder 2",
			kbFolderName = "KB Folder 2");

		KBFolder.gotoCP(kbFolderName = "KB Folder 2");

		KBArticle.addCP(
			kbArticleContent = "Knowledge Base Article Content 1",
			kbArticleTitle = "Knowledge Base Article Title 1");

		KBArticle.addCP(
			kbArticleContent = "Knowledge Base Article Content 2",
			kbArticleTitle = "Knowledge Base Article Title 2");

		Navigator.gotoPage(pageName = "Knowledge Base Display Page");

		KBArticle.configureKBFolderDisplay(kbFolderName = "KB Root Folder");

		Navigator.gotoPage(pageName = "Knowledge Base Display Page");

		KBArticle.gotoKBArticleViaKBDisplayHierarchyNavigationPG(kbArticleTitle = "Knowledge Base Article Title 1");

		KBArticle.viewViaKBDisplayPG(
			kbArticleContent = "Knowledge Base Article Content 1",
			kbArticleTitle = "Knowledge Base Article Title 1");

		KBArticle.gotoKBArticleViaKBDisplayHierarchyNavigationPG(kbArticleTitle = "Knowledge Base Article Title 2");

		KBArticle.viewViaKBDisplayPG(
			kbArticleContent = "Knowledge Base Article Content 2",
			kbArticleTitle = "Knowledge Base Article Title 2");

		KBFolder.gotoViaKBDisplayPG(kbFolderName = "KB Folder 2");

		KBArticle.viewViaKBDisplayPG(
			kbArticleContent = "Knowledge Base Article Content 2",
			kbArticleTitle = "Knowledge Base Article Title 2");
	}

	@priority = "3"
	@refactorneeded
	test ViewNoDuplicateKBSuggestionAfterRefresh {
		KBAdmin.openKBAdmin(siteURLKey = "guest");

		KBArticle.addCP(
			kbArticleContent = "Knowledge Base Article Content",
			kbArticleTitle = "Knowledge Base Article Title");

		Navigator.gotoPage(pageName = "Knowledge Base Display Page");

		KBArticleSuggestion.addPG(
			kbArticleTitle = "Knowledge Base Article Title",
			kbSuggestionBody = "Knowledge Base Article Suggestion");

		Refresh();

		KBArticleSuggestion.viewPG(
			kbSuggestionBody = "Knowledge Base Article Suggestion",
			kbSuggestionUserName = "Test Test",
			status = "New");

		KBArticleSuggestion.viewNoDuplicatePG(
			kbSuggestionBody = "Knowledge Base Article Suggestion",
			kbSuggestionUserName = "Test Test",
			kbThereAreSuggestionsLink = "There is one suggestion. (1 Pending)",
			status = "New");
	}

}