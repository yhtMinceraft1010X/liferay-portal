@component-name = "portal-wcm"
definition {

	property portal.release = "true";
	property portal.upstream = "true";
	property testray.main.component.name = "Content and Application Templates";

	setUp {
		TestCase.setUpPortalInstance();

		User.firstLoginPG();

		JSONGroup.addGroup(groupName = "Test Site Name");

		JSONStaging.enableLocalStaging(
			branchingPrivate = "false",
			branchingPublic = "false",
			groupName = "Test Site Name");
	}

	tearDown {
		var testPortalInstance = PropsUtil.get("test.portal.instance");

		if ("${testPortalInstance}" == "true") {
			PortalInstances.tearDownCP();
		}
		else {
			JSONGroup.deleteGroupByName(groupName = "Test Site Name");
		}
	}

	@description = "Add a widget template in staging site."
	@priority = "4"
	@uitest
	test AddWidgetTemplateInStagingSite {
		task ("Add a widget templates in staging site") {
			ApplicationDisplayTemplates.openWidgetTemplatesAdmin(siteURLKey = "test-site-name-staging");

			ApplicationDisplayTemplates.addCP(
				adtFile = "adt_asset_publisher_rich_summary.ftl",
				adtType = "Asset Publisher");
		}

		task ("Publish to Live") {
			Staging.openStagingAdmin(siteURLKey = "test-site-name-staging");

			Staging.publishCustomPublication(checkContentNameList = "Template");
		}

		task ("View the widget template in live site") {
			ApplicationDisplayTemplates.openWidgetTemplatesAdmin(siteURLKey = "test-site-name");

			ApplicationDisplayTemplates.viewTemplate(adtType = "Asset Publisher");
		}
	}

	@description = "This is a use case for LPS-137903. View the new GUI in Widget Templates in staging model."
	@priority = "4"
	@uitest
	test ViewTemplateEditorSidebarPanel {
		task ("Enable Staging Model") {
			JSONStaging.enableLocalStaging(
				branchingPrivate = "false",
				branchingPublic = "false",
				groupName = "Test Site Name");
		}

		task ("Add a widget template for Asset Publisher") {
			ApplicationDisplayTemplates.openWidgetTemplatesAdmin(siteURLKey = "test-site-name-staging");

			ApplicationDisplayTemplates.addADTWithFieldEntryCP(
				fieldName = "Asset Entry",
				portletName = "Asset Publisher");

			Alert.viewSuccessMessage();

			ApplicationDisplayTemplates.viewTemplate(adtType = "Asset Publisher");
		}

		task ("View sidebar panels in template editor") {
			ApplicationDisplayTemplates.gotoWidgetTemplatesEllipsisItem(
				item = "Edit",
				widgetTemplateName = "Asset Publisher Test ADT");
		}

		task ("View Elements panel") {
			Navigator.gotoSidebarTab(tabName = "Elements");

			for (var fieldName : list "Asset Entries*,Asset Entry,Current URL,Locale,Portlet Preferences,Template ID,Theme Display,HTTP Request,Render Request,Render Response,Asset Publisher Helper") {
				AssertElementPresent(
					key_fieldName = "${fieldName}",
					locator1 = "DDMEditTemplate#SCRIPT_FIELD");
			}
		}

		task ("View Properties panel") {
			Navigator.gotoSidebarTab(tabName = "Properties");

			for (var fieldName : list "Template Key,URL,WebDAV URL") {
				AssertVisible(
					key_text = "${fieldName}",
					locator1 = "TextInput#ANY");
			}

			AssertVisible(locator1 = "TextArea#DESCRIPTION");

			AssertSelectedLabel(
				locator1 = "Select#SELECT_ID",
				selectFieldId = "smallImageSource",
				value1 = "No Image");
		}
	}

	@description = "View the widget template applied to Categories Navigation in staging and live site."
	@priority = "3"
	test ViewWidgetTemplateAppliedToCategoriesNavigationInStagingAndLiveSite {
		property testray.component.names = "Categories,Content and Application Templates,Staging";

		task ("Add a widget page") {
			JSONLayout.addPublicLayout(
				groupName = "Test Site Name (Staging)",
				layoutName = "ADT Test Page",
				site = "false");
		}

		task ("Change the layout of page to 1 Column") {
			JSONLayout.updateLayoutTemplateOfPublicLayout(
				groupName = "Test Site Name (Staging)",
				layoutName = "ADT Test Page",
				layoutTemplate = "1 Column",
				site = "false");
		}

		task ("Add three vocabularies") {
			for (var n : list "1,2,3") {
				JSONCategory.addVocabulary(
					groupName = "Test Site Name (Staging)",
					site = "false",
					title = "Vocabulary Name ${n}");
			}
		}

		task ("Add a category to last two vocabularies") {
			for (var n : list "2,3") {
				JSONCategory.addCategory(
					groupName = "Test Site Name (Staging)",
					site = "false",
					title = "Category Name ${n}",
					vocabularyName = "Vocabulary Name ${n}");
			}
		}

		task ("Add a Categories Navigation to page") {
			JSONLayout.addWidgetToPublicLayout(
				groupName = "Test Site Name (Staging)",
				layoutName = "ADT Test Page",
				site = "false",
				widgetName = "Category filter");
		}

		task ("View the displayed content in Categories Navigation") {
			Navigator.gotoStagedSitePage(
				pageName = "ADT Test Page",
				siteName = "Test Site Name");

			CategoriesNavigationPortlet.viewDefaultPG(
				categoryName = "Category Name 2",
				vocabularyName = "Vocabulary Name 2");

			CategoriesNavigationPortlet.viewDefaultPG(
				categoryName = "Category Name 3",
				vocabularyName = "Vocabulary Name 3");
		}

		task ("Add a widget template for Categories Navigation") {
			ApplicationDisplayTemplates.openWidgetTemplatesAdmin(siteURLKey = "test-site-name-staging");

			ApplicationDisplayTemplates.addCP(
				adtFile = "adt_categories_navigation_multi_column.ftl",
				adtType = "Category filter");
		}

		task ("Select the widget template in Categories Navigation") {
			Navigator.gotoStagedSitePage(
				pageName = "ADT Test Page",
				siteName = "Test Site Name");

			ApplicationDisplayTemplates.selectPortletADT(templateName = "Category filter Test ADT");
		}

		task ("View the widget template applied to Categories Navigation in staging site") {
			Navigator.gotoStagedSitePage(
				pageName = "ADT Test Page",
				siteName = "Test Site Name");

			CategoriesNavigationPortlet.viewCustomADTMultiColumnPG(
				categoryNameList = "Null,Null,Category Name 2,Category Name 3",
				columnList = "1,2,3,4",
				vocabularyNameList = "Topic,Vocabulary Name 1,Vocabulary Name 2,Vocabulary Name 3");
		}

		task ("Publish to Live") {
			Staging.gotoPublishToLive();

			Staging.publishToLive();
		}

		task ("View the widget template applied to Categories Navigation in live site") {
			Navigator.gotoSitePage(
				pageName = "ADT Test Page",
				siteName = "Test Site Name");

			CategoriesNavigationPortlet.viewCustomADTMultiColumnPG(
				categoryNameList = "Null,Null,Category Name 2,Category Name 3",
				columnList = "1,2,3,4",
				vocabularyNameList = "Topic,Vocabulary Name 1,Vocabulary Name 2,Vocabulary Name 3");
		}
	}

}