@component-name = "portal-wcm"
definition {

	property portal.release = "true";
	property portal.upstream = "true";
	property testray.main.component.name = "Fragments";

	setUp {
		task ("Set up instance and sign in") {
			TestCase.setUpPortalInstance();

			User.firstLoginPG();
		}

		task ("Add a site via JSON") {
			JSONGroup.addGroup(groupName = "Test Site Name");
		}

		task ("Navigate to fragments admin") {
			FragmentsAdminNavigator.openFragmentsAdmin(siteURLKey = "test-site-name");
		}
	}

	tearDown {
		var testPortalInstance = PropsUtil.get("test.portal.instance");

		if ("${testPortalInstance}" == "true") {
			PortalInstances.tearDownCP();
		}
		else {
			JSONGroup.deleteGroupByName(groupName = "Test Site Name");

			Navigator.openURL();

			Site.tearDownCP();
		}
	}

	@description = "This is a test case for LPS-80625. The fragment importer should not allow non-zip files to be imported."
	@priority = "5"
	@uitest
	test CannotImportNonZip {
		property portal.acceptance = "true";

		task ("Navigate to the fragment import screen") {
			FragmentsAdminNavigator.gotoImportCollection();
		}

		task ("Attempt to import a non-zip file") {
			UploadCommonFile(
				locator1 = "TextInput#FILE",
				value1 = "Document_1.doc");
		}

		task ("Assert the form validator displays an error that the file is not a zip file") {
			Pause(locator1 = "10000");

			AssertTextEquals(
				locator1 = "Message#ERROR_FORM_VALIDATION",
				value1 = "Please enter a file with a valid extension (zip).");

			IFrame.closeFrame();
		}
	}

	@description = "This is a test case for LPS-101256. Can edit inline content of fragment in composition."
	@priority = "4"
	test EditInlineContentOfImportedFragmentComposition {
		task ("Import a fragment collection") {
			FragmentsAdmin.importCollectionFromDependenciesFolder(collectionZipFile = "collections-InlineContent-edited.zip");
		}

		task ("Add an imported fragment composition to content page") {
			JSONLayout.addPublicLayout(
				groupName = "Test Site Name",
				layoutName = "Test Page Name",
				type = "content");

			ContentPagesNavigator.openEditContentPage(
				pageName = "Test Page Name",
				siteName = "Test Site Name");

			PageEditor.addFragment(
				collectionName = "Collection Name",
				composition = "true",
				fragmentName = "New Fragment Name");
		}

		task ("View the inline content of imported fragment composition") {
			AssertTextEquals(
				key_elementHeader = "//div[contains(@class,'page-editor__topper') and contains(.,'Heading')]//*[contains(@data-lfr-editable-id,'element-text')]",
				key_position = "1",
				locator1 = "PageEditor#CONTAINER_CONTAINER_NESTED_ELEMENT",
				value1 = "Modify Text");
		}

		task ("Edit the inline text of Heading") {
			PageEditor.editFragmentText(
				fragmentName = "Heading",
				id = "element-text",
				text = "New editable fragment text");
		}

		task ("View the edited inline content of Heading") {
			PageEditor.viewFragmentEditableText(
				editableText = "New editable fragment text",
				fragmentName = "Heading",
				id = "element-text");
		}
	}

	@description = "This is a test case for LPS-101256. The fragment composition could be exported and imported."
	@priority = "4"
	test ExportFragmentCompositionAndImport {
		task ("Add a fragment collection") {
			JSONFragment.addFragmentCollection(
				description = "Collection Description",
				groupName = "Test Site Name",
				name = "Collection Name");
		}

		task ("Add a new fragment") {
			FragmentsAdminNavigator.openFragmentsAdmin(siteURLKey = "test-site-name");

			FragmentsAdminNavigator.gotoCollection(collectionName = "Collection Name");

			FragmentsAdmin.addFragment(fragmentName = "Fragment Name");

			FragmentsEditor.gotoConfigurationTab();

			var jsonSnippet = '''
{
	"fieldSets": [
		{
			"label": "Button",
				"fields": [
				{
					"name": "buttonText",
					"label": "Button Text",
					"description": "button-text",
					"type": "text",
					"dataType": "string",
					"defaultValue": "Go Somewhere"
				}
			]
		}
	]
}
			''';

			FragmentsEditor.typeConfigurationEditor(content = "${jsonSnippet}");

			FragmentsEditor.waitForAutoSave();

			FragmentsEditor.gotoCodeTab();

			var htmlSnippet = '''
<div class="fragment-configuration">
	<button type="button" class="btn btn-primary">Go Somewhere</button>
</div>
			''';

			FragmentsEditor.typeHtmlEditor(content = "${htmlSnippet}");

			FragmentsEditor.waitForAutoSave();

			FragmentsEditor.publish();
		}

		task ("Add a Container to a content page") {
			JSONLayout.addPublicLayout(
				groupName = "Test Site Name",
				layoutName = "Test Page Name",
				type = "content");

			ContentPagesNavigator.openEditContentPage(
				pageName = "Test Page Name",
				siteName = "Test Site Name");

			PageEditor.addFragment(
				collectionName = "Layout Elements",
				fragmentName = "Container");
		}

		task ("Add a new fragment into Container") {
			PageEditor.addElementToContainer(
				collectionName = "Collection Name",
				fragmentName = "Fragment Name",
				navTab = "Fragments");
		}

		task ("Save the fragment composition as fragment") {
			PageEditor.gotoSaveAsFragment(fragmentName = "Container");

			PageEditorComposition.saveAsFragment(
				collectionName = "Collection Name",
				fragmentName = "New Fragment Name");

			PageEditor.clickPublish();
		}

		task ("Export the fragment collection") {
			FragmentsAdminNavigator.openFragmentsAdmin(siteURLKey = "test-site-name");

			FragmentsAdminNavigator.gotoCollection(collectionName = "Collection Name");

			FragmentsAdmin.exportCollection(collectionName = "Collection Name");
		}

		task ("Import the fragment collection into a new site") {
			JSONGroup.addGroup(groupName = "New Site Name");

			FragmentsAdminNavigator.openFragmentsAdmin(siteURLKey = "new-site-name");

			FragmentsAdmin.importCollectionFromTempFolder();
		}

		task ("Add an imported fragment composition to content page") {
			JSONLayout.addPublicLayout(
				groupName = "New Site Name",
				layoutName = "Test Page Name",
				type = "content");

			ContentPagesNavigator.openEditContentPage(
				pageName = "Test Page Name",
				siteName = "New Site Name");

			PageEditor.addFragment(
				collectionName = "Collection Name",
				composition = "true",
				fragmentName = "New Fragment Name");
		}

		task ("View the fragment composition is rendered well") {
			AssertElementPresent(
				key_elementHeader = "/div[contains(.,'Fragment Name')]//li[contains(@class,'title')]",
				key_position = "1",
				locator1 = "PageEditor#CONTAINER_CONTAINER_NESTED_ELEMENT");
		}
	}

	@description = "Fragment collections can be exported and imported."
	@priority = "5"
	@uitest
	test ExportImportCollectionViewFragment {
		property portal.acceptance = "true";

		task ("Add a fragment collection") {
			JSONFragment.addFragmentCollection(
				description = "Collection Description",
				groupName = "Test Site Name",
				name = "Collection Name");
		}

		task ("Add a new fragment") {
			FragmentsAdminNavigator.openFragmentsAdmin(siteURLKey = "test-site-name");

			FragmentsAdminNavigator.gotoCollection(collectionName = "Collection Name");

			FragmentsAdmin.addFragment(fragmentName = "Fragment Name");

			FragmentsEditor.typeFileContent(fileName = "fragment_button.html");

			FragmentsEditor.waitForAutoSave();

			FragmentsEditor.typeFileContent(fileName = "fragment_button.css");

			FragmentsEditor.waitForAutoSave();

			FragmentsEditor.typeFileContent(fileName = "fragment_button.js");

			FragmentsEditor.waitForAutoSave();

			PortletEntry.publish();
		}

		task ("View the new fragment is shown in collection") {
			FragmentsAdmin.viewEntryPresent(entry = "Fragment Name");
		}

		task ("Export the fragment collection") {
			FragmentsAdmin.exportCollection(collectionName = "Collection Name");
		}

		task ("Import the fragment collection into a new site") {
			JSONGroup.addGroup(groupName = "New Site Name");

			FragmentsAdminNavigator.openFragmentsAdmin(siteURLKey = "new-site-name");

			FragmentsAdmin.importCollectionFromTempFolder();
		}

		task ("Navigate to the fragment collection in the new site") {
			FragmentsAdminNavigator.gotoCollection(collectionName = "Collection Name");
		}

		task ("Assert the section fragment survived the import and the fragment preview displays the Button fragment") {
			FragmentsAdminNavigator.gotoEntry(entry = "Fragment Name");

			SelectFrame.selectFrameNoLoading(locator1 = "FragmentEditor#PREVIEW_IFRAME");

			for (var button : list "Submit,Cancel") {
				Button.viewPresent(button = "${button}");
			}

			IFrame.selectTopFrame();
		}
	}

	@description = "This is a test case for LPS-101255. The composition of fragments can be saved in an existing collection."
	@priority = "5"
	@uitest
	test ExportImportFragmentCompositionWithCustomConfigurations {
		property portal.acceptance = "true";

		task ("Add a document with an image") {
			JSONDocument.addFileWithUploadedFile(
				dmDocumentDescription = "DM Document Description",
				dmDocumentTitle = "Document_1.jpg",
				groupName = "Test Site Name",
				mimeType = "image/jpeg",
				sourceFileName = "Document_1.jpg");
		}

		task ("Add a Container to content page") {
			JSONLayout.addPublicLayout(
				groupName = "Test Site Name",
				layoutName = "Test Page Name",
				type = "content");

			ContentPagesNavigator.openEditContentPage(
				pageName = "Test Page Name",
				siteName = "Test Site Name");

			PageEditor.addFragment(
				collectionName = "Layout Elements",
				fragmentName = "Container");
		}

		task ("Add a direct image as background image of Container") {
			PageEditor.editLayoutContainer(
				backgroundImage = "Direct",
				entryTitle = "Document_1.jpg",
				navItem = "Documents and Media",
				panel = "Styles");
		}

		task ("Add a Heading fragment into Container") {
			PageEditor.addElementToContainer(
				collectionName = "Basic Components",
				fragmentName = "Heading",
				navTab = "Fragments");
		}

		task ("Configure the styles of Heading") {
			PageEditor.gotoStyle(fragmentName = "Heading");

			PageEditor.configureFragment(
				alignment = "Center",
				textColor = "Danger");

			PageEditor.gotoConfiguration(fragmentName = "Heading");

			PageEditor.configureFragment(headingLevel = "H2");
		}

		task ("Save the fragment composition as fragment") {
			PageEditor.gotoSaveAsFragment(fragmentName = "Container");

			PageEditorComposition.saveAsFragment(fragmentName = "New Fragment Name");
		}

		task ("Export the fragment collection") {
			FragmentsAdminNavigator.openFragmentsAdmin(siteURLKey = "test-site-name");

			FragmentsAdminNavigator.gotoCollection(collectionName = "Saved Fragments");

			FragmentsAdmin.exportCollection(collectionName = "Saved Fragments");
		}

		task ("Import the fragment collection into a new site") {
			JSONGroup.addGroup(groupName = "New Site Name");

			FragmentsAdminNavigator.openFragmentsAdmin(siteURLKey = "new-site-name");

			FragmentsAdmin.importCollectionFromTempFolder();
		}

		task ("Add an imported fragment composition to content page") {
			JSONLayout.addPublicLayout(
				groupName = "New Site Name",
				layoutName = "Test Page Name",
				type = "content");

			ContentPagesNavigator.openEditContentPage(
				pageName = "Test Page Name",
				siteName = "New Site Name");

			PageEditor.addFragment(
				collectionName = "Saved Fragments",
				composition = "true",
				fragmentName = "New Fragment Name");
		}

		task ("View the background image of Container") {
			PageEditor.viewFragment(fragmentName = "Container");

			ContentPages.viewFragmentBackgroundImage(image = "Document_1.jpg");
		}

		task ("View the styles of Heading") {
			PageEditor.viewFragmentStyle(
				alignment = "center",
				fragmentName = "heading",
				headingLevel = "H2",
				textColor = "danger");
		}
	}

	@description = "This is a test case for LPS-101256. Can duplicate fragment in layout elements in composition."
	@priority = "4"
	test ExportImportFragmentCompositionWithDuplicatedFragment {
		task ("Add a Container to a content page") {
			JSONLayout.addPublicLayout(
				groupName = "Test Site Name",
				layoutName = "Test Page Name",
				type = "content");

			ContentPagesNavigator.openEditContentPage(
				pageName = "Test Page Name",
				siteName = "Test Site Name");

			PageEditor.addFragment(
				collectionName = "Layout Elements",
				fragmentName = "Container");
		}

		task ("Add a Grid element into Container") {
			PageEditor.addElementToContainer(
				collectionName = "Layout Elements",
				fragmentName = "Grid",
				navTab = "Fragments");
		}

		task ("Add a Heading to first module of Grid") {
			PageEditor.addElementToColumn(
				collectionName = "Basic Components",
				columnNumber = "1",
				fragmentName = "Heading",
				navTab = "Fragments");
		}

		task ("Duplicate the Heading fragment") {
			PageEditor.gotoDuplicate(fragmentName = "Heading");
		}

		task ("Add a Heading to the bottom of Grid inside Container") {
			PageEditor.addElementToContainer(
				collectionName = "Basic Components",
				fragmentName = "Heading",
				navTab = "Fragments",
				targetElementName = "Grid");

			AssertElementPresent(
				key_columnNumber = "1",
				key_elementHeader = "//div[contains(@class,'page-editor__topper')][1]/div[contains(.,'Heading')]//li[contains(@class,'title')]",
				key_position = "1",
				locator1 = "PageEditor#GRID_COLUMN_NESTED_ELEMENT");

			AssertElementPresent(
				key_columnNumber = "1",
				key_elementHeader = "//div[contains(@class,'page-editor__topper')][2]/div[contains(.,'Heading')]//li[contains(@class,'title')]",
				key_position = "1",
				locator1 = "PageEditor#GRID_COLUMN_NESTED_ELEMENT");

			AssertElementPresent(
				key_elementHeader = "/div[contains(@class,'page-editor__topper')][2]/div[contains(.,'Heading')]//li[contains(@class,'title')]",
				key_position = "1",
				locator1 = "PageEditor#CONTAINER_CONTAINER_NESTED_ELEMENT");
		}

		task ("Save the fragment composition as fragment") {
			PageEditor.gotoSaveAsFragment(fragmentName = "Container");

			PageEditorComposition.saveAsFragment(fragmentName = "New Composition");
		}

		task ("Export the fragment collection") {
			FragmentsAdminNavigator.openFragmentsAdmin(siteURLKey = "test-site-name");

			FragmentsAdminNavigator.gotoCollection(collectionName = "Saved Fragments");

			FragmentsAdmin.exportCollection(collectionName = "Saved Fragments");
		}

		task ("Import the fragment collection into a new site") {
			JSONGroup.addGroup(groupName = "New Site Name");

			FragmentsAdminNavigator.openFragmentsAdmin(siteURLKey = "new-site-name");

			FragmentsAdmin.importCollectionFromTempFolder();
		}

		task ("Add an imported fragment composition to content page") {
			JSONLayout.addPublicLayout(
				groupName = "New Site Name",
				layoutName = "Test Page Name",
				type = "content");

			ContentPagesNavigator.openEditContentPage(
				pageName = "Test Page Name",
				siteName = "New Site Name");

			PageEditor.addFragment(
				collectionName = "Saved Fragments",
				composition = "true",
				fragmentName = "New Composition");
		}

		task ("View the fragment composition is rendered well") {
			AssertElementPresent(
				key_columnNumber = "1",
				key_elementHeader = "//div[contains(@class,'page-editor__topper')][1]/div[contains(.,'Heading')]//li[contains(@class,'title')]",
				key_position = "1",
				locator1 = "PageEditor#GRID_COLUMN_NESTED_ELEMENT");

			AssertElementPresent(
				key_columnNumber = "1",
				key_elementHeader = "//div[contains(@class,'page-editor__topper')][2]/div[contains(.,'Heading')]//li[contains(@class,'title')]",
				key_position = "1",
				locator1 = "PageEditor#GRID_COLUMN_NESTED_ELEMENT");

			AssertElementPresent(
				key_elementHeader = "/div[contains(@class,'page-editor__topper')][2]/div[contains(.,'Heading')]//li[contains(@class,'title')]",
				key_position = "1",
				locator1 = "PageEditor#CONTAINER_CONTAINER_NESTED_ELEMENT");
		}
	}

	@description = "This is a test for LPS-101255. The edited inline contents will be reset to default value when save a composition of fragments without Save Inline Content."
	@priority = "5"
	@uitest
	test ExportImportFragmentCompositionWithInlineContent {
		property portal.acceptance = "true";

		task ("Add a Grid to content page") {
			JSONLayout.addPublicLayout(
				groupName = "Test Site Name",
				layoutName = "Test Page Name",
				type = "content");

			ContentPagesNavigator.openEditContentPage(
				pageName = "Test Page Name",
				siteName = "Test Site Name");

			PageEditor.addFragment(
				collectionName = "Layout Elements",
				fragmentName = "Grid");
		}

		task ("Add a Heading fragment into the first module of Grid") {
			PageEditor.addElementToColumn(
				collectionName = "Basic Components",
				columnNumber = "1",
				fragmentName = "Heading",
				navTab = "Fragments");
		}

		task ("Edit inline content of Heading") {
			PageEditor.editFragmentText(
				fragmentName = "Heading",
				id = "element-text",
				text = "New editable fragment text");
		}

		task ("Add an Image fragment into the second module of Grid") {
			PageEditor.addElementToColumn(
				collectionName = "Basic Components",
				columnNumber = "2",
				fragmentName = "Image",
				navTab = "Fragments");
		}

		task ("Edit the inline image of Image") {
			PageEditor.editFragmentImage(
				contributed = "true",
				fileName = "Document_1.png",
				fragmentName = "Image",
				id = "image");
		}

		task ("Add a HTML fragment into the third module of Grid") {
			PageEditor.addElementToColumn(
				collectionName = "Basic Components",
				columnNumber = "3",
				fragmentName = "HTML",
				navTab = "Fragments");
		}

		task ("Edit the inline text of HTML") {
			PageEditor.editFragmentHTML(
				editableId = "element-html",
				fileName = "fragment_freemarker_basic.html",
				fragmentName = "HTML");
		}

		task ("Save the fragment composition as fragment with inline content") {
			PageEditor.gotoSaveAsFragment(fragmentName = "Grid");

			PageEditorComposition.saveAsFragment(
				fragmentName = "New Fragment Name",
				saveInlineContent = "true");
		}

		task ("Export the fragment collection") {
			FragmentsAdminNavigator.openFragmentsAdmin(siteURLKey = "test-site-name");

			FragmentsAdminNavigator.gotoCollection(collectionName = "Saved Fragments");

			FragmentsAdmin.exportCollection(collectionName = "Saved Fragments");
		}

		task ("Import the fragment collection into a new site") {
			JSONGroup.addGroup(groupName = "New Site Name");

			FragmentsAdminNavigator.openFragmentsAdmin(siteURLKey = "new-site-name");

			FragmentsAdmin.importCollectionFromTempFolder();
		}

		task ("Add an imported fragment composition to content page") {
			JSONLayout.addPublicLayout(
				groupName = "New Site Name",
				layoutName = "Test Page Name",
				type = "content");

			ContentPagesNavigator.openEditContentPage(
				pageName = "Test Page Name",
				siteName = "New Site Name");

			PageEditor.addFragment(
				collectionName = "Saved Fragments",
				composition = "true",
				fragmentName = "New Fragment Name");
		}

		task ("View the edited inline contents of imported fragment composition") {
			PageEditor.clickEditableField(
				contributed = "true",
				fragmentName = "Heading",
				id = "element-text");

			AssertElementPresent(
				key_columnNumber = "1",
				key_elementHeader = "//div[contains(@class,'page-editor__topper')][1]//h1[contains(.,'New editable fragment text')]",
				key_position = "1",
				locator1 = "PageEditor#GRID_COLUMN_NESTED_ELEMENT");

			PageEditor.clickEditableField(
				contributed = "true",
				fragmentName = "Image",
				id = "image");

			AssertElementPresent(
				key_columnNumber = "2",
				key_elementHeader = "//div[contains(@class,'page-editor')]//img[contains(@src,'Document_1.png')]",
				key_position = "1",
				locator1 = "PageEditor#GRID_COLUMN_NESTED_ELEMENT");

			PageEditor.clickEditableField(
				contributed = "true",
				fragmentName = "HTML",
				id = "element-html");

			AssertElementPresent(
				key_columnNumber = "3",
				key_elementHeader = "//div[contains(@class,'page-editor')]//*[contains(.,'Basic Text')]",
				key_position = "1",
				locator1 = "PageEditor#GRID_COLUMN_NESTED_ELEMENT");
		}
	}

	@description = "This is a test case for LPS-110462. The value of editable field is reset in saved composition when unchecking Save Mapping Configuration."
	@priority = "4"
	test ExportImportFragmentCompositionWithMappingConfiguration {
		task ("Add a document with an image") {
			JSONDocument.addFileWithUploadedFile(
				dmDocumentDescription = "DM Document Description",
				dmDocumentTitle = "Document_1.jpg",
				groupName = "Test Site Name",
				mimeType = "image/jpeg",
				sourceFileName = "Document_1.jpg");
		}

		task ("Add a Container to content page") {
			JSONLayout.addPublicLayout(
				groupName = "Test Site Name",
				layoutName = "Test Page Name",
				type = "content");

			ContentPagesNavigator.openEditContentPage(
				pageName = "Test Page Name",
				siteName = "Test Site Name");

			PageEditor.addFragment(
				collectionName = "Layout Elements",
				fragmentName = "Container");
		}

		task ("Add an Image fragment into Container") {
			PageEditor.addElementToContainer(
				collectionName = "Basic Components",
				fragmentName = "Image",
				navTab = "Fragments");
		}

		task ("Map the Preview Image field of document to Imag fragment") {
			PageEditorMapping.gotoEditableFieldMapping(
				fragmentName = "Image",
				id = "image");

			PageEditorMapping.mapEditableFieldToAsset(
				entryTitle = "Document_1.jpg",
				navItem = "Documents and Media");

			Select(
				locator1 = "PageEditorEditableLink#FIELD",
				value1 = "Preview Image");

			SelectFrame(value1 = "relative=top");

			PageEditor.waitForAutoSave();
		}

		task ("View the mapped image shown in Image fragment") {
			PageEditor.viewFragmentEditableImage(
				id = "image-square",
				imageDescription = "",
				imageFileName = "Document_1.jpg");
		}

		task ("Save the fragment composition as fragment with mapping configuration") {
			PageEditor.gotoSaveAsFragment(fragmentName = "Container");

			PageEditorComposition.saveAsFragment(
				fragmentName = "New Fragment Name",
				saveMappingConfigurationAndLink = "true");
		}

		task ("Export the fragment collection") {
			FragmentsAdminNavigator.openFragmentsAdmin(siteURLKey = "test-site-name");

			FragmentsAdminNavigator.gotoCollection(collectionName = "Saved Fragments");

			FragmentsAdmin.exportCollection(collectionName = "Saved Fragments");
		}

		task ("Import the fragment collection into a new site") {
			JSONGroup.addGroup(groupName = "New Site Name");

			FragmentsAdminNavigator.openFragmentsAdmin(siteURLKey = "new-site-name");

			FragmentsAdmin.importCollectionFromTempFolder();
		}

		task ("Add an imported fragment composition to content page") {
			JSONLayout.addPublicLayout(
				groupName = "New Site Name",
				layoutName = "Test Page Name",
				type = "content");

			ContentPagesNavigator.openEditContentPage(
				pageName = "Test Page Name",
				siteName = "New Site Name");

			PageEditor.addFragment(
				collectionName = "Saved Fragments",
				composition = "true",
				fragmentName = "New Fragment Name");
		}

		task ("View the mapped image shown in Image fragment") {
			PageEditor.viewFragmentEditableImage(
				id = "image-square",
				imageDescription = "",
				imageFileName = "Document_1.jpg");
		}
	}

	@description = "This is a use case for LPS-120198. Export and import fragment composition linked to page."
	@priority = "4"
	test ExportImportFragmentCompositionWithMappingLink {
		task ("Add a widget page") {
			JSONLayout.addPublicLayout(
				groupName = "Test Site Name",
				layoutName = "Test Widget Page Name");
		}

		task ("Add a Grid to a content page") {
			JSONLayout.addPublicLayout(
				groupName = "Test Site Name",
				layoutName = "Test Page Name",
				type = "content");

			ContentPagesNavigator.openEditContentPage(
				pageName = "Test Page Name",
				siteName = "Test Site Name");

			PageEditor.addFragment(
				collectionName = "Layout Elements",
				fragmentName = "Grid");
		}

		task ("Add a Card into the second module of Grid") {
			PageEditor.addElementToColumn(
				collectionName = "Basic Components",
				columnNumber = "2",
				fragmentName = "Card",
				navTab = "Fragments");
		}

		task ("Map the widget page to link type editable field of Card") {
			PageEditorEditableLink.mapEditableFieldToPage(
				fragmentName = "Card",
				id = "link",
				pageName = "Test Widget Page Name");
		}

		task ("View the selected page is shown in Page field") {
			AssertTextEquals(
				key_fieldLabel = "Page",
				locator1 = "PageEditor#ITEM_SELECTOR_CONTENT_INPUT",
				value1 = "Test Widget Page Name");
		}

		task ("View selected page is linked to Card fragment") {
			AssertVisible(
				key_append = "[normalize-space(text())='Go Somewhere']",
				key_URL = "/web/test-site-name/test-widget-page-name",
				locator1 = "Fragment#FRAGMENT_LINK");
		}

		task ("Save the fragment composition as fragment with mapping link") {
			PageEditor.gotoSaveAsFragment(fragmentName = "Grid");

			PageEditorComposition.saveAsFragment(
				fragmentName = "New Composition",
				saveMappingConfigurationAndLink = "true");
		}

		task ("Export the fragment composition") {
			FragmentsAdminNavigator.openFragmentsAdmin(siteURLKey = "test-site-name");

			FragmentsAdminNavigator.gotoCollection(collectionName = "Saved Fragments");

			FragmentsAdmin.exportCollection(collectionName = "Saved Fragments");
		}

		task ("Import the fragment collection into a new site") {
			JSONGroup.addGroup(groupName = "New Site");

			FragmentsAdminNavigator.openFragmentsAdmin(siteURLKey = "new-site");

			FragmentsAdmin.importCollectionFromTempFolder();
		}

		task ("Add an imported fragment composition to content page") {
			JSONLayout.addPublicLayout(
				groupName = "New Site",
				layoutName = "Test Page Name",
				type = "content");

			ContentPagesNavigator.openEditContentPage(
				pageName = "Test Page Name",
				siteName = "New Site");

			PageEditor.addFragment(
				collectionName = "Saved Fragments",
				composition = "true",
				fragmentName = "New Composition");

			AssertVisible(
				key_append = "[normalize-space(text())='Go Somewhere']",
				key_URL = "/web/test-site-name/test-widget-page-name",
				locator1 = "Fragment#FRAGMENT_LINK");

			PortletEntry.publish();
		}

		task ("Navigate to content page in new site") {
			ContentPagesNavigator.openViewContentPage(
				pageName = "Test Page Name",
				siteName = "New Site");

			Click(
				key_append = "[normalize-space(text())='Go Somewhere']",
				key_URL = "/web/test-site-name/test-widget-page-name",
				locator1 = "Fragment#FRAGMENT_LINK");
		}

		task ("Assert redirect to widget page in the first site") {
			AssertTextEquals(
				locator1 = "ControlMenu#HEADING",
				value1 = "Test Widget Page Name");

			AssertLocation.assertPartialLocation(value1 = "web/test-site-name/test-widget-page-name");
		}
	}

	@description = "This is a test for LPS-101255. The edited inline contents will be reset to default value when save a composition of fragments without Save Inline Content."
	@priority = "5"
	@uitest
	test ExportImportFragmentCompositionWithoutInlineContent {
		property portal.acceptance = "true";

		task ("Add a Grid to content page") {
			JSONLayout.addPublicLayout(
				groupName = "Test Site Name",
				layoutName = "Test Page Name",
				type = "content");

			ContentPagesNavigator.openEditContentPage(
				pageName = "Test Page Name",
				siteName = "Test Site Name");

			PageEditor.addFragment(
				collectionName = "Layout Elements",
				fragmentName = "Grid");
		}

		task ("Add a Heading fragment into the first module of Grid") {
			PageEditor.addElementToColumn(
				collectionName = "Basic Components",
				columnNumber = "1",
				fragmentName = "Heading",
				navTab = "Fragments");
		}

		task ("Edit inline content of Heading") {
			PageEditor.editFragmentText(
				fragmentName = "Heading",
				id = "element-text",
				text = "New editable fragment text");
		}

		task ("Add an Image fragment into the second module of Grid") {
			PageEditor.addElementToColumn(
				collectionName = "Basic Components",
				columnNumber = "2",
				fragmentName = "Image",
				navTab = "Fragments");
		}

		task ("Edit the inline image of Image") {
			PageEditor.editFragmentImage(
				contributed = "true",
				fileName = "Document_1.png",
				fragmentName = "Image",
				id = "image");
		}

		task ("Add a HTML fragment into the third module of Grid") {
			PageEditor.addElementToColumn(
				collectionName = "Basic Components",
				columnNumber = "3",
				fragmentName = "HTML",
				navTab = "Fragments");
		}

		task ("Edit the inline text of HTML") {
			PageEditor.editFragmentHTML(
				editableId = "element-html",
				fileName = "fragment_freemarker_basic.html",
				fragmentName = "HTML");
		}

		task ("Save the fragment composition as fragment without inline content") {
			PageEditor.gotoSaveAsFragment(fragmentName = "Grid");

			PageEditorComposition.saveAsFragment(fragmentName = "New Fragment Name");
		}

		task ("Export the fragment collection") {
			FragmentsAdminNavigator.openFragmentsAdmin(siteURLKey = "test-site-name");

			FragmentsAdminNavigator.gotoCollection(collectionName = "Saved Fragments");

			FragmentsAdmin.exportCollection(collectionName = "Saved Fragments");
		}

		task ("Import the fragment collection into a new site") {
			JSONGroup.addGroup(groupName = "New Site Name");

			FragmentsAdminNavigator.openFragmentsAdmin(siteURLKey = "new-site-name");

			FragmentsAdmin.importCollectionFromTempFolder();
		}

		task ("Add an imported fragment composition to content page") {
			JSONLayout.addPublicLayout(
				groupName = "New Site Name",
				layoutName = "Test Page Name",
				type = "content");

			ContentPagesNavigator.openEditContentPage(
				pageName = "Test Page Name",
				siteName = "New Site Name");

			PageEditor.addFragment(
				collectionName = "Saved Fragments",
				composition = "true",
				fragmentName = "New Fragment Name");
		}

		task ("View the default inline contents of imported fragment composition") {
			PageEditor.clickEditableField(
				contributed = "true",
				fragmentName = "Heading",
				id = "element-text");

			AssertElementPresent(
				key_columnNumber = "1",
				key_elementHeader = "//div[contains(@class,'page-editor__topper')][1]//h1[contains(.,'Heading Example')]",
				key_position = "1",
				locator1 = "PageEditor#GRID_COLUMN_NESTED_ELEMENT");

			PageEditor.clickEditableField(
				contributed = "true",
				fragmentName = "Image",
				id = "image");

			AssertElementNotPresent(
				key_columnNumber = "2",
				key_elementHeader = "//div[contains(@class,'page-editor')]//img[contains(@src,'Document_1.png')]",
				key_position = "1",
				locator1 = "PageEditor#GRID_COLUMN_NESTED_ELEMENT");

			PageEditor.clickEditableField(
				contributed = "true",
				fragmentName = "HTML",
				id = "element-html");

			AssertElementPresent(
				key_columnNumber = "3",
				key_elementHeader = "//div[contains(@class,'page-editor')]//h1[contains(.,'HTML Example')]",
				key_position = "1",
				locator1 = "PageEditor#GRID_COLUMN_NESTED_ELEMENT");
		}
	}

	@description = "This is a test case for LPS-110462. The value of editable field is reset in saved composition when unchecking Save Mapping Configuration."
	@priority = "4"
	test ExportImportFragmentCompositionWithoutMappingConfiguration {
		task ("Add a web content") {
			JSONWebcontent.addWebContent(
				content = "WC WebContent Content",
				groupName = "Test Site Name",
				title = "WC WebContent Title");
		}

		task ("Add a Container to a content page") {
			JSONLayout.addPublicLayout(
				groupName = "Test Site Name",
				layoutName = "Test Page Name",
				type = "content");

			ContentPagesNavigator.openEditContentPage(
				pageName = "Test Page Name",
				siteName = "Test Site Name");

			PageEditor.addFragment(
				collectionName = "Layout Elements",
				fragmentName = "Container");
		}

		task ("Add a Heading fragment into Container") {
			PageEditor.addElementToContainer(
				collectionName = "Basic Components",
				fragmentName = "Heading",
				navTab = "Fragments");
		}

		task ("Map the Title field of web content to Heading fragment") {
			PageEditorMapping.gotoEditableFieldMapping(
				fragmentName = "Heading",
				id = "text");

			PageEditorMapping.mapEditableFieldToAsset(
				entryTitle = "WC WebContent Title",
				navItem = "Web Content");

			Select(
				locator1 = "PageEditorEditableLink#FIELD",
				value1 = "Title");

			SelectFrame(value1 = "relative=top");

			PageEditor.waitForAutoSave();
		}

		task ("Save the fragment composition as fragment without mapping configuration") {
			PageEditor.gotoSaveAsFragment(fragmentName = "Container");

			PageEditorComposition.saveAsFragment(fragmentName = "New Fragment Name");
		}

		task ("Export the fragment collection") {
			FragmentsAdminNavigator.openFragmentsAdmin(siteURLKey = "test-site-name");

			FragmentsAdminNavigator.gotoCollection(collectionName = "Saved Fragments");

			FragmentsAdmin.exportCollection(collectionName = "Saved Fragments");
		}

		task ("Import the fragment collection into a new site") {
			JSONGroup.addGroup(groupName = "New Site Name");

			FragmentsAdminNavigator.openFragmentsAdmin(siteURLKey = "new-site-name");

			FragmentsAdmin.importCollectionFromTempFolder();
		}

		task ("Add an imported fragment composition to content page") {
			JSONLayout.addPublicLayout(
				groupName = "New Site Name",
				layoutName = "Test Page Name",
				type = "content");

			ContentPagesNavigator.openEditContentPage(
				pageName = "Test Page Name",
				siteName = "New Site Name");

			PageEditor.addFragment(
				collectionName = "Saved Fragments",
				composition = "true",
				fragmentName = "New Fragment Name");
		}

		task ("View the Heading Example shown in Heading") {
			PageEditor.viewFragmentEditableText(
				editableText = "Heading Example",
				fragmentName = "Heading",
				id = "element-text");
		}
	}

	@description = "This is a test for LPS-98501. Export and Import a global collection with fragment."
	@priority = "4"
	test ExportImportGlobalCollectionWithFragment {
		task ("Add a fragment collection in Global site") {
			JSONFragment.addFragmentCollection(
				groupName = "Global",
				name = "Global Collection Name");
		}

		task ("Add a fragment in global fragment collection") {
			var newLine = '''
''';

			var htmlSnippet = '''
<${configuration.headingLevel} class="component-heading mb-0 text-break" data-lfr-editable-id="element-text" data-lfr-editable-type="text">
	Heading Example
</${configuration.headingLevel}>
			''';
			var htmlSnippet = StringUtil.regexReplaceAll("${htmlSnippet}", "${newLine}", "");

			var jsonSnippet = '''
{
	"fieldSets": [
		{
			"fields": [
				{
					"dataType": "string",
					"defaultValue": "h1",
					"label": "heading-level",
					"name": "headingLevel",
					"type": "select",
					"typeOptions": {
						"validValues": [
							{
								"value": "h1"
							},
							{
								"value": "h2"
							},
							{
								"value": "h3"
							},
							{
								"value": "h4"
							},
							{
								"value": "h5"
							},
							{
								"value": "h6"
							}
						]
					}
				}
			]
		}
	]
}
			''';
			var jsonSnippet = StringUtil.regexReplaceAll("${jsonSnippet}", "${newLine}", "");

			JSONFragment.addSection(
				collectionName = "Global Collection Name",
				configuration = "${jsonSnippet}",
				fragmentStatus = "Approved",
				groupName = "Global",
				html = "${htmlSnippet}",
				name = "Global Fragment Name");
		}

		task ("Export the global fragment collection") {
			FragmentsAdminNavigator.openFragmentsAdmin(siteURLKey = "global");

			FragmentsAdmin.exportCollection(collectionName = "Global Collection Name");
		}

		task ("Delete the global fragment collection") {
			Collection.deleteCollection(collectionName = "Global Collection Name");

			Collection.viewNotPresent(collectionName = "Global Collection Name");
		}

		task ("Import the global fragment collection zip file") {
			FragmentsAdmin.importCollectionFromTempFolder();
		}

		task ("View the imported fragment collection and fragment") {
			FragmentsAdminNavigator.gotoCollection(collectionName = "Global Collection Name");

			FragmentsAdmin.viewEntryPresent(entry = "Global Fragment Name");
		}

		task ("Delete the global fragment collection") {
			JSONFragment.deleteFragmentCollection(
				groupName = "Global",
				name = "Global Collection Name");
		}
	}

	@description = "This is a test for LPS-98501. Export and Import a global fragment."
	@priority = "4"
	test ExportImportGlobalFragment {
		task ("Add a fragment collection in Global site") {
			JSONFragment.addFragmentCollection(
				groupName = "Global",
				name = "Global Collection Name");
		}

		task ("Add a fragment in global fragment collection") {
			var newLine = '''
''';

			var htmlSnippet = '''
<${configuration.headingLevel} class="component-heading mb-0 text-break" data-lfr-editable-id="element-text" data-lfr-editable-type="text">
	Heading Example
</${configuration.headingLevel}>
			''';
			var htmlSnippet = StringUtil.regexReplaceAll("${htmlSnippet}", "${newLine}", "");

			var jsonSnippet = '''
{
	"fieldSets": [
		{
			"fields": [
				{
					"dataType": "string",
					"defaultValue": "h1",
					"label": "heading-level",
					"name": "headingLevel",
					"type": "select",
					"typeOptions": {
						"validValues": [
							{
								"value": "h1"
							},
							{
								"value": "h2"
							},
							{
								"value": "h3"
							},
							{
								"value": "h4"
							},
							{
								"value": "h5"
							},
							{
								"value": "h6"
							}
						]
					}
				}
			]
		}
	]
}
			''';
			var jsonSnippet = StringUtil.regexReplaceAll("${jsonSnippet}", "${newLine}", "");

			JSONFragment.addSection(
				collectionName = "Global Collection Name",
				configuration = "${jsonSnippet}",
				fragmentStatus = "Approved",
				groupName = "Global",
				html = "${htmlSnippet}",
				name = "Global Fragment Name");
		}

		task ("Export the global fragment") {
			FragmentsAdminNavigator.openFragmentsAdmin(siteURLKey = "global");

			FragmentsAdmin.exportFragment(
				collectionName = "Global Collection Name",
				fragmentName = "Global Fragment Name");
		}

		task ("Delete the global fragment") {
			FragmentsAdmin.deleteEntry(entry = "Global Fragment Name");

			FragmentsAdmin.viewEntryNotPresent(entry = "Global Fragment Name");
		}

		task ("Import the global fragment zip file") {
			FragmentsAdminNavigator.gotoCollection(collectionName = "Global Collection Name");

			FragmentsAdmin.importFragmentFromTempFolder();
		}

		task ("View the imported global fragment") {
			FragmentsAdmin.viewEntryPresent(entry = "Global Fragment Name");
		}

		task ("Delete the global fragment collection") {
			JSONFragment.deleteFragmentCollection(
				groupName = "Global",
				name = "Global Collection Name");
		}
	}

	@description = "This is a test case for LPS-120957. Export and Import multiple collections."
	@priority = "4"
	test ExportImportMultipleCollections {
		task ("Add three collections") {
			for (var collectionName : list "Collection Name 1,Collection Name 2,Collection Name 3") {
				JSONFragment.addFragmentCollection(
					groupName = "Test Site Name",
					name = "${collectionName}");
			}
		}

		task ("Add a new fragment in each collection") {
			var htmlSnippet = FragmentsAPI.getFragmentFileContent(fileName = "checkbox_configuration.html");
			var jsonSnippet = FragmentsAPI.getFragmentFileContent(fileName = "checkbox_configuration.json");

			JSONFragment.addSection(
				collectionName = "Collection Name 1",
				configuration = "${jsonSnippet}",
				fragmentStatus = "Approved",
				groupName = "Test Site Name",
				html = "${htmlSnippet}",
				name = "Checkbox Fragment Example");

			var htmlSnippet = FragmentsAPI.getFragmentFileContent(fileName = "colorPalette_configuration.html");
			var jsonSnippet = FragmentsAPI.getFragmentFileContent(fileName = "colorPalette_configuration.json");

			JSONFragment.addSection(
				collectionName = "Collection Name 2",
				configuration = "${jsonSnippet}",
				fragmentStatus = "Approved",
				groupName = "Test Site Name",
				html = "${htmlSnippet}",
				name = "ColorPalette Fragment Example");

			var htmlSnippet = FragmentsAPI.getFragmentFileContent(fileName = "text_configuration.html");
			var jsonSnippet = FragmentsAPI.getFragmentFileContent(fileName = "placeholder_configuration.json");

			JSONFragment.addSection(
				collectionName = "Collection Name 3",
				configuration = "${jsonSnippet}",
				fragmentStatus = "Approved",
				groupName = "Test Site Name",
				html = "${htmlSnippet}",
				name = "Text Fragment Example");
		}

		task ("Export three new collections") {
			FragmentsAdminNavigator.openFragmentsAdmin(siteURLKey = "test-site-name");

			FragmentsAdminNavigator.gotoExportCollection();

			FragmentsAdmin.exportMultipleCollections(collectionNameList = "Collection Name 1,Collection Name 2,Collection Name 3");

			DownloadTempFile();
		}

		task ("Import the fragment collection into a new site") {
			JSONGroup.addGroup(groupName = "Site Name");

			FragmentsAdminNavigator.openFragmentsAdmin(siteURLKey = "site-name");

			FragmentsAdmin.importCollectionFromTempFolder();
		}

		task ("Assert collections and fragments are imported") {
			var n = "1";

			for (var fragmentName : list "Checkbox Fragment Example,ColorPalette Fragment Example,Text Fragment Example") {
				FragmentsAdminNavigator.gotoCollection(collectionName = "Collection Name ${n}");

				FragmentsAdmin.viewEntryWorkflowStatus(
					entry = "${fragmentName}",
					workflowStatus = "Approved");

				var n = ${n} + 1;

				Pause(locator1 = "3000");
			}
		}
	}

	@description = "This is a test case for LPS-101256. The fragment replacing HTML,CSS,JS,JSON in dependencies survives after importing."
	@priority = "4"
	test ImportFragmentWithHTMLCSSJSJSONReplaced {
		task ("Import a fragment collection") {
			FragmentsAdmin.importCollectionFromDependenciesFolder(collectionZipFile = "collections-HTMLCSSJSJSON-replace.zip");
		}

		task ("Add an imported fragment composition to content page") {
			JSONLayout.addPublicLayout(
				groupName = "Test Site Name",
				layoutName = "Test Page Name",
				type = "content");

			ContentPagesNavigator.openEditContentPage(
				pageName = "Test Page Name",
				siteName = "Test Site Name");

			PageEditor.addFragment(
				collectionName = "Collection Name",
				composition = "true",
				fragmentName = "New Fragment Name");
		}

		task ("Assert changes from fragment are propagated to composition of fragment") {
			PageEditor.gotoConfiguration(fragmentName = "Fragment Name");

			AssertElementPresent(
				key_elementHeader = "/div[contains(@class,'page-editor__topper')][1]/div[contains(.,'Fragment Name')]//li[contains(@class,'title')]",
				key_position = "1",
				locator1 = "PageEditor#CONTAINER_CONTAINER_NESTED_ELEMENT");

			AssertElementPresent(
				key_color = "success",
				key_text = "Here we go",
				locator1 = "Button#ANY_WITH_COLOR");

			AssertElementPresent(locator1 = "//div[contains(@class,'page-editor__fragment-content')][contains(.,'border: solid violet')]");

			PageEditor.clickConfigurableField(fragmentName = "Fragment Name");

			AssertTextEquals(
				key_fieldLabel = "Button Text",
				locator1 = "TextInput#GENERIC_TEXT_INPUT",
				value1 = "Go Somewhere");

			ClickNoError(
				key_text = "Here we go",
				locator1 = "Button#ANY");

			AssertConfirm(value1 = "Here we go");
		}
	}

	@description = "This is a testcase for LPS-96113. All fragments in dependencies should validate successfully."
	@priority = "5"
	@uitest
	test ValidateDependenciesFragments {
		property portal.acceptance = "true";

		task ("Import a fragment collection") {
			FragmentsAdminNavigator.gotoImportCollection();

			UploadFileWithCustomPath.uploadArchiveFile(
				fileName = "fragment-collection.zip",
				filePath = "enduser/wem/fragment");

			SelectFrameTop();

			Button.clickImport();
		}

		task ("Assert the zip imported properly") {
			AssertElementNotPresent(locator1 = "Message#ERROR");
		}

		task ("Assert that all fragments validated") {
			AssertElementNotPresent(
				key_infoMessage = "The following fragments have validation issues",
				locator1 = "Message#INFO_SPECIFIC");
		}
	}

	@description = "This is a test case for LPS-128652. View the changes of fragment on content page still persist after overwrite the fragment."
	@priority = "4"
	test ViewChangesOfFragmentOnContentPagePersistAfterOverwriteTheFragment {
		property test.name.skip.portal.instance = "FragmentsExportImport#ViewChangesOfFragmentOnContentPagePersistAfterOverwriteTheFragment";

		task ("Enable Propagate Fragment Changes Automatically in System Settings") {
			ApplicationsMenu.gotoPortlet(
				category = "Configuration",
				panel = "Control Panel",
				portlet = "System Settings");

			SystemSettings.gotoConfiguration(
				configurationCategory = "Page Fragments",
				configurationName = "Page Fragments",
				configurationScope = "Virtual Instance Scope");

			FormFields.enableCheckbox(fieldName = "Propagate Fragment Changes Automatically");

			SystemSettings.saveConfiguration();
		}

		task ("Add a fragment collection") {
			JSONFragment.addFragmentCollection(
				groupName = "Test Site Name",
				name = "Collection Name");
		}

		task ("Copy the Tabs fragment to new collection") {
			FragmentsAdminNavigator.openFragmentsAdmin(siteURLKey = "test-site-name");

			FragmentsAdminNavigator.gotoCollection(collectionName = "Basic Components");

			FragmentsAdminNavigator.gotoEntryEllipsisItem(
				entry = "Tabs",
				item = "Copy To");

			FragmentsAdmin.selectCollection(collectionName = "Collection Name");
		}

		task ("Export the new collection") {
			FragmentsAdmin.exportCollection(collectionName = "Collection Name");
		}

		task ("Add a content page") {
			JSONLayout.addPublicLayout(
				groupName = "Test Site Name",
				layoutName = "Test Page Name",
				type = "content");
		}

		task ("Navigate to the content page at edit mode") {
			ContentPagesNavigator.openEditContentPage(
				pageName = "Test Page Name",
				siteName = "Test Site Name");
		}

		task ("Add a Container to page") {
			PageEditor.addFragment(
				collectionName = "Layout Elements",
				fragmentName = "Container");
		}

		task ("Add a Grid element into Container") {
			PageEditor.addElementToContainer(
				collectionName = "Layout Elements",
				fragmentName = "Grid",
				navTab = "Fragments");
		}

		task ("Add two copied Tabs fragments into the second module of Grid") {
			for (var fragmentName : list "Tabs (Copy),Tabs (Copy)") {
				PageEditor.addElementToColumn(
					collectionName = "Collection Name",
					columnNumber = "2",
					fragmentName = "${fragmentName}",
					navTab = "Fragments");
			}
		}

		task ("Increase number of tabs to 5 in the first copied Tabs") {
			PageEditor.gotoConfiguration(fragmentName = "Tabs (Copy)");

			PageEditor.configureFragment(numberOfTabs = "5");
		}

		task ("Edit inline text of the fifth tabs") {
			PageEditor.editFragmentText(
				className = "tabs",
				fragmentName = "Tabs (Copy)",
				id = "title5",
				text = "Test");
		}

		task ("View the inline text of the fifth tabs") {
			PageEditor.viewFragmentEditableText(
				className = "tabs",
				editableText = "Test",
				fragmentName = "Tabs (Copy)",
				id = "title5");

			PageEditor.clickPublish();
		}

		task ("Navigate to Fragments admin") {
			FragmentsAdminNavigator.openFragmentsAdmin(siteURLKey = "test-site-name");
		}

		task ("Import the fragment collection") {
			FragmentsAdmin.importCollectionFromTempFolder();
		}

		task ("Navigate to the content page in edit mode") {
			ContentPagesNavigator.openEditContentPage(
				pageName = "Test Page Name",
				siteName = "Test Site Name");
		}

		task ("View the inline text of the fifth tabs") {
			PageEditor.viewFragmentEditableText(
				className = "tabs",
				editableText = "Test",
				fragmentName = "Tabs (Copy)",
				id = "title5");
		}
	}

}