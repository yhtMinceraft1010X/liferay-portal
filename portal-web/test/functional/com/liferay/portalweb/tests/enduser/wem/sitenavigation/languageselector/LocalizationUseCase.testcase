@component-name = "portal-wcm"
definition {

	property portal.release = "true";
	property portal.upstream = "true";
	property testray.component.names = "Localization";
	property testray.main.component.name = "Localization";

	setUp {
		task ("Set up instance and sign in") {
			TestCase.setUpPortalInstance();

			User.firstLoginPG();
		}

		task ("Add a site") {
			JSONGroup.addGroup(groupName = "Test Site Name");
		}

		task ("Add a widget page") {
			JSONLayout.addPublicLayout(
				groupName = "Test Site Name",
				layoutName = "Test Page Name");
		}

		task ("Add a Language Selector to page") {
			JSONLayout.addWidgetToPublicLayout(
				groupName = "Test Site Name",
				layoutName = "Test Page Name",
				widgetName = "Language Selector");
		}
	}

	tearDown {
		var testPortalInstance = PropsUtil.get("test.portal.instance");

		if ("${testPortalInstance}" == "true") {
			PortalInstances.tearDownCP();
		}
		else {
			JSONGroup.deleteGroupByName(groupName = "Test Site Name");
		}
	}

	@description = "The user can choose which languages will be available to site via Language Selector widget."
	@priority = "4"
	test ChooseCurrentLanguagesViaLanguageSelector {
		property test.name.skip.portal.instance = "LocalizationUsecase#ChooseCurrentLanguagesViaLanguageSelector";

		task ("Define English, German and Spanish as current languages via Language Selector") {
			Navigator.gotoSitePage(
				pageName = "Test Page Name",
				siteName = "Test Site Name");

			Portlet.gotoPortletOptions(
				portletName = "Language Selector",
				portletOption = "Configuration");

			IFrame.selectConfigurationFrame();

			LanguagePortlet.configureLanguages(currentLanguageList = "English (United States),German (Germany),Spanish (Spain)");

			IFrame.closeFrame();
		}

		task ("View English is shown as current language in Language Selector") {
			LanguagePortlet.viewCurrentLanguage(locale = "en-US");
		}

		task ("View German and Spanish are shown as available languages") {
			LanguagePortlet.viewLanguages(
				availableLanguageList = "deutsch-Deutschland,español-España",
				displayTemplate = "Icon Menu",
				unavailableLanguageList = "français-France,magyar-Magyarország,日本語-日本,português-Brasil,中文-中国,العربية-السعودية,suomi-Suomi,català-Espanya,nederlands-Nederland,svenska-Sverige");
		}
	}

	@description = "Display Language Selector templates."
	@priority = "4"
	test DisplayTemplatesViaLanguageSelector {
		task ("View available languages are shown with Icon Menu template") {
			Navigator.gotoSitePage(
				pageName = "Test Page Name",
				siteName = "Test Site Name");

			LanguagePortlet.viewCurrentLanguage(locale = "en-US");

			LanguagePortlet.viewLanguages(
				availableLanguageList = "العربية-السعودية,català-Espanya,中文-中国,nederlands-Nederland,suomi-Suomi,français-France,deutsch-Deutschland,magyar-Magyarország,日本語-日本,português-Brasil,español-España,svenska-Sverige",
				displayTemplate = "Icon Menu");
		}

		task ("Define Icon as display template in Language Selector") {
			Portlet.gotoPortletOptions(
				portletName = "Language Selector",
				portletOption = "Configuration");

			IFrame.selectConfigurationFrame();

			LanguagePortlet.configureDisplaySettings(
				displayCurrentLocale = "true",
				displayTemplate = "Icon");

			IFrame.closeFrame();
		}

		task ("View available languages are shown with Icon template") {
			LanguagePortlet.viewLanguages(
				availableIconList = "en-us,ar-sa,ca-es,zh-cn,nl-nl,fi-fi,fr-fr,de-de,hu-hu,ja-jp,pt-br,es-es,sv-se",
				displayTemplate = "Icon");
		}

		task ("Define Long Text as display template in Language Selector") {
			Portlet.gotoPortletOptions(
				portletName = "Language Selector",
				portletOption = "Configuration");

			IFrame.selectConfigurationFrame();

			LanguagePortlet.configureDisplaySettings(
				displayCurrentLocale = "true",
				displayTemplate = "Long Text");

			IFrame.closeFrame();
		}

		task ("View available languages are shown with Long Text template") {
			LanguagePortlet.viewLanguages(
				availableLanguageList = "العربية,català,中文,nederlands,suomi,français,deutsch,magyar,日本語,português,español,svenska",
				displayTemplate = "Long Text");
		}

		task ("Define Select Box as display template in Language Selector") {
			Portlet.gotoPortletOptions(
				portletName = "Language Selector",
				portletOption = "Configuration");

			IFrame.selectConfigurationFrame();

			LanguagePortlet.configureDisplaySettings(
				displayCurrentLocale = "true",
				displayTemplate = "Select Box");

			IFrame.closeFrame();
		}

		task ("View available languages are shown with Select Box template") {
			LanguagePortlet.viewLanguages(
				availableLanguageList = "english,العربية,català,中文,nederlands,suomi,français,deutsch,magyar,日本語,português,español,svenska",
				displayTemplate = "Select Box");
		}

		task ("Define Short Text as display template in Language Selector") {
			Portlet.gotoPortletOptions(
				portletName = "Language Selector",
				portletOption = "Configuration");

			IFrame.selectConfigurationFrame();

			LanguagePortlet.configureDisplaySettings(
				displayCurrentLocale = "true",
				displayTemplate = "Short Text");

			IFrame.closeFrame();
		}

		task ("View available languages are shown with Short Text template") {
			LanguagePortlet.viewLanguages(
				availableLanguageList = "ar,ca,中文,nl,fi,fr,de,hu,日本語,pt,es,sv",
				displayTemplate = "Short Text");
		}
	}

	@description = "View current languages via Language Selector after being removed in the portal settings."
	@priority = "4"
	test ViewRemovedCurrentPortalLanguagesViaLanguageSelector {
		property test.name.skip.portal.instance = "LocalizationUsecase#ViewRemovedCurrentPortalLanguagesViaLanguageSelector";

		task ("Define English, Chinese and French as current languages in portal settings") {
			PortalSettings.gotoConfiguration(
				configurationCategory = "Localization",
				configurationName = "Language",
				configurationScope = "Virtual Instance Scope");

			PortalSettings.configureCurrentLanguagesCP(currentPortalLanguages = "English (United States),Chinese (China),French (France)");
		}

		task ("View available languages are shown in Language Selector") {
			Navigator.gotoSitePage(
				pageName = "Test Page Name",
				siteName = "Test Site Name");

			LanguagePortlet.viewCurrentLanguage(locale = "en-US");

			LanguagePortlet.viewLanguages(
				availableLanguageList = "français-France,中文-中国",
				displayTemplate = "Icon Menu",
				unavailableLanguageList = "العربية-السعودية,català-Espanya,nederlands-Nederland,suomi-Suomi,deutsch-Deutschland,magyar-Magyarország,日本語-日本,português-Brasil,español-España,svenska-Sverige");
		}

		task ("View Chinese, French and English are shown as current language in Language Selector") {
			Portlet.gotoPortletOptions(
				portletName = "Language Selector",
				portletOption = "Configuration");

			IFrame.selectConfigurationFrame();

			LanguagePortlet.viewLanguagesSettings(currentLanguageList = "Chinese (China),French (France),English (United States)");
		}
	}

	@description = "View current languages via Language Selector after being removed in the site settings."
	@priority = "4"
	test ViewRemovedCurrentSiteLanguagesViaLanguageSelector {
		property test.name.skip.portal.instance = "LocalizationUsecase#ViewRemovedCurrentSiteLanguagesViaLanguageSelector";

		task ("Define English, German and Spanish as current languages in site settings") {
			Site.openSiteSettingsAdmin(siteURLKey = "test-site-name");

			Site.configureCurrentLanguagesCP(
				currentSiteLanguages = "English (United States),German (Germany),Spanish (Spain)",
				defaultSiteLanguage = "English (United States)",
				displaySettings = "Display Settings");
		}

		task ("View available languages are shown in Language Selector") {
			Navigator.gotoSitePage(
				pageName = "Test Page Name",
				siteName = "Test Site Name");

			LanguagePortlet.viewCurrentLanguage(locale = "en-US");

			LanguagePortlet.viewLanguages(
				availableLanguageList = "deutsch-Deutschland,español-España",
				displayTemplate = "Icon Menu",
				unavailableLanguageList = "العربية-السعودية,català-Espanya,nederlands-Nederland,suomi-Suomi,magyar-Magyarország,日本語-日本,português-Brasil,français-France,中文-中国,svenska-Sverige");
		}

		task ("View English, German and Spanish are shown as current language in Language Selector") {
			Portlet.gotoPortletOptions(
				portletName = "Language Selector",
				portletOption = "Configuration");

			IFrame.selectConfigurationFrame();

			LanguagePortlet.viewLanguagesSettings(currentLanguageList = "English (United States),German (Germany),Spanish (Spain)");
		}
	}

}