@component-name = "portal-wcm"
definition {

	property portal.release = "true";
	property portal.upstream = "true";
	property testray.main.component.name = "Style Books";

	setUp {
		task ("Set up instance and sign in") {
			TestCase.setUpPortalInstance();

			User.firstLoginPG();
		}

		task ("Add a site via JSON") {
			JSONGroup.addGroup(groupName = "Test Site Name");
		}
	}

	tearDown {
		var testPortalInstance = PropsUtil.get("test.portal.instance");

		if ("${testPortalInstance}" == "true") {
			PortalInstances.tearDownCP();
		}
		else {
			for (var groupName : list "Test Site Name,New Site Name") {
				JSONGroup.deleteGroupByName(groupName = "${groupName}");
			}
		}
	}

	@description = "This is a test for LPS-134860. The user could export and import a style book."
	@priority = "3"
	test ExportImportStyleBook {
		task ("Add a style book with times as Font Family Base") {
			StyleBooksAdmin.openStyleBooksAdmin(siteURLKey = "test-site-name");

			StyleBooksAdmin.addStyleBooks(styleBookName = "Test Style Book Name");

			StyleBooks.selectForm(formName = "Typography");

			Type(
				key_fieldLabel = "Font Family Base",
				key_groupLabel = "Font Family",
				locator1 = "TextInput#GROUPED_TEXT_INPUT",
				value1 = "times");

			StyleBooks.clickManagementBar();

			StyleBooks.waitForAutoSave();

			StyleBooks.clickPublish();
		}

		task ("Export the style book") {
			StyleBooksAdmin.gotoStyleBookEllipsisItem(
				item = "Export",
				styleBookName = "Test Style Book Name");

			DownloadTempFile();
		}

		task ("Import the style book into a new site") {
			JSONGroup.addGroup(groupName = "New Site Name");

			StyleBooksAdmin.openStyleBooksAdmin(siteURLKey = "new-site-name");

			StyleBooksAdmin.importStyleBookFromTempFolder();
		}

		task ("View the Font Family Base is times") {
			StyleBooksAdmin.gotoStyleBookEllipsisItem(
				item = "Edit",
				styleBookName = "Test Style Book Name");

			StyleBooks.selectForm(formName = "Typography");

			AssertTextEquals(
				key_fieldLabel = "Font Family Base",
				key_groupLabel = "Font Family",
				locator1 = "TextInput#GROUPED_TEXT_INPUT",
				value1 = "times");
		}
	}

}