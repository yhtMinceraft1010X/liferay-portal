task upgradeTestResources(type: Zip)

String jasperVersion = "5.2.0"

File jasperFile = file("jasperreports-${jasperVersion}-project.zip")
FileTree jasperZipTree = zipTree(jasperFile)

dependencies {
	testIntegrationCompile group: "commons-io", name: "commons-io", version: "2.8.0"
	testIntegrationCompile project(":apps:portal-reports-engine:portal-reports-engine-api")
	testIntegrationCompile project(":dxp:apps:portal-reports-engine-console:portal-reports-engine-console-jasper")
	testIntegrationCompile project(":test:arquillian-extension-junit-bridge")
}

processTestIntegrationResources {
	from jasperZipTree.files
}

upgradeTestResources {
	archiveName = "test-resources.zip"
	destinationDir = temporaryDir

	doLast {
		delete jasperFile

		boolean success = archivePath.renameTo(jasperFile)

		if (!success) {
			throw new GradleException("Unable to rename ${archivePath} to ${jasperFile}")
		}
	}

	include "**/csvdatasource/data/"
	include "**/csvdatasource/reports/"
	include "**/xlsdatasource/data/"
	include "**/xlsdatasource/reports/"
	include "**/xmldatasource/data/"
	include "**/xmldatasource/reports/"

	includeEmptyDirs = false
	from jasperZipTree

	outputs.upToDateWhen {
		false
	}
}