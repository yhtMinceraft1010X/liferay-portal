@component-name = "portal-workflow"
definition {

	property portal.release = "true";
	property portal.upstream = "true";
	property test.run.environment = "EE";
	property testray.main.component.name = "Workflow Metrics";

	setUp {
		TestCase.setUpPortalInstance();

		var testPortalInstance = PropsUtil.get("test.portal.instance");

		User.firstLoginPG();

		Navigator.openURL();

		ApplicationsMenu.gotoPortlet(
			category = "Workflow",
			panel = "Applications",
			portlet = "Metrics");
	}

	tearDown {
		var testPortalInstance = PropsUtil.get("test.portal.instance");

		if ("${testPortalInstance}" == "true") {
			PortalInstances.tearDownCP();
		}
	}

	@description = "LPS-105495 - Verify that the user can transition tasks adding a comment to it"
	@priority = "4"
	test CanAddCommentToATransitionTask {
		ApplicationsMenu.gotoPortlet(
			category = "Workflow",
			panel = "Applications",
			portlet = "Process Builder");

		Navigator.gotoNavItem(navItem = "Configuration");

		Workflow.configureWorkflow(
			workflowDefinition = "Single Approver",
			workflowResourceValue = "Blogs Entry");

		JSONBlog.addEntry(
			entryContent = "Blogs Entry",
			entryTitle = "Blogs Entry Title");

		UserBar.gotoDropdownItem(dropdownItem = "My Workflow Tasks");

		Workflow.assignToMeTaskByActions(
			workflowAssetTitle = "Blogs Entry Title",
			workflowAssetType = "Blogs Entry",
			workflowTask = "Review");

		WorkflowMetrics.goToWorkflowAllItems(workflowProcessName = "Single Approver");

		WorkflowMetrics.goToBulkTransitionSelectTasks();

		WorkflowMetrics.selectAllTransitionTasks();

		WorkflowMetrics.chooseTransitionPerStep(
			comment = "Test Comment",
			taskName = "Review",
			transitionOption = "Approve");

		UserBar.gotoDropdownItem(dropdownItem = "My Submissions");

		Workflow._viewActivities(
			workflowActivityComment = "Test Comment",
			workflowAssetTitle = "Blogs Entry Title");
	}

	@description = "LPS-108138 - Verify that users can bulk-transition pending tasks assigned to themselves from more than 1 instance via Workflow Metrics"
	@priority = "4"
	test CanBulkTransitionPendingTasksFromMultipleInstances {
		ApplicationsMenu.gotoPortlet(
			category = "Workflow",
			panel = "Applications",
			portlet = "Process Builder");

		Navigator.gotoNavItem(navItem = "Configuration");

		Workflow.configureWorkflow(
			workflowDefinition = "Single Approver",
			workflowResourceValue = "Blogs Entry");

		JSONBlog.addEntry(
			entryContent = "Blogs Entry Content",
			entryTitle = "Blogs Entry Title 1");

		JSONBlog.addEntry(
			entryContent = "Blogs Entry Content",
			entryTitle = "Blogs Entry Title 2");

		UserBar.gotoDropdownItem(dropdownItem = "My Workflow Tasks");

		Workflow.assignToMeTaskByActions(
			workflowAssetTitle = "Blogs Entry Title 1",
			workflowAssetType = "Blogs Entry",
			workflowTask = "Review");

		Workflow.assignToMeTaskByActions(
			workflowAssetTitle = "Blogs Entry Title 2",
			workflowAssetType = "Blogs Entry",
			workflowTask = "Review");

		WorkflowMetrics.goToWorkflowAllItems(workflowProcessName = "Single Approver");

		WorkflowMetrics.goToBulkTransitionSelectTasks();

		WorkflowMetrics.selectAllTransitionTasks();

		WorkflowMetrics.chooseTransitionPerStep(
			taskName = "Review",
			transitionOption = "Reject");

		WorkflowMetrics.viewProcessStepDisplaysUpdateStatus(workflowAssetTitle = "Blogs Entry Title 1");

		WorkflowMetrics.viewProcessStepDisplaysUpdateStatus(workflowAssetTitle = "Blogs Entry Title 2");
	}

	@description = "LPS-108138 - Verify that tasks that are not assigned to the current users can't be transitioned via bulk transition action"
	@priority = "4"
	test CanNotBulkTransitionTasksNotAssignedToCurrentUser {
		property portal.upstream = "quarantine";

		var siteName = TestCase.getSiteName(siteName = "${siteName}");

		JSONUser.addUser(
			userEmailAddress = "usertest@liferay.com",
			userFirstName = "User",
			userLastName = "Test",
			userScreenName = "usertest");

		JSONRole.assignRoleToUser(
			roleTitle = "Power User",
			userEmailAddress = "usertest@liferay.com");

		JSONRole.assignRoleToUser(
			roleTitle = "Administrator",
			userEmailAddress = "usertest@liferay.com");

		ProductMenu.gotoPortlet(
			category = "Configuration",
			portlet = "Workflow",
			site = "${siteName}");

		Workflow.configureWorkflow(
			workflowDefinition = "Single Approver",
			workflowResourceValue = "Blogs Entry");

		JSONBlog.addEntry(
			entryContent = "Blogs Entry Content",
			entryTitle = "Blog Entry 1");

		JSONBlog.addEntry(
			entryContent = "Blogs Entry Content",
			entryTitle = "Blog Entry 2");

		JSONBlog.addEntry(
			entryContent = "Blogs Entry Content",
			entryTitle = "Blog Entry 3");

		WorkflowMetrics.goToDashboardAllItemsWithFilter(
			workflowFilterOption = "PENDING",
			workflowMetricsTab = "DASHBOARD",
			workflowProcess = "Single Approver");

		Click(
			key_orderBy = "Creation Date",
			locator1 = "WorkflowAllItems#WORKFLOW_ALL_ITEMS_ORDER_BY");

		Click(
			key_orderBy = "Creation Date",
			locator1 = "WorkflowAllItems#WORKFLOW_ALL_ITEMS_ORDER_BY");

		WorkflowMetrics.selectTransitionTask(task = "1");

		WorkflowMetrics.selectNewAssignee(
			nameOfAssignee = "User Test",
			taskList = "Blogs Entry: Blog Entry 1");

		WorkflowMetrics.selectTransitionTask(task = "2");

		WorkflowMetrics.selectNewAssignee(
			nameOfAssignee = "Test Test",
			taskList = "Blogs Entry: Blog Entry 2");

		AssertElementPresent(
			key_itemTitle = "Blogs Entry: Blog Entry 1",
			locator1 = "WorkflowAllItems#REASSIGN_CHECKBOX_DISABLED");

		if (IsNotChecked.isNotCheckedNotVisible(locator1 = "Checkbox#LEXICON_SELECT_ALL_INPUT")) {
			Click(locator1 = "Checkbox#LEXICON_SELECT_ALL_CONTAINER");
		}

		Click(
			locator1 = "WorkflowAllItems#KEBAB_DROPDOWN",
			task = "1");

		MenuItem.clickNoError(menuItem = "Transition");

		Click(
			key_task = "1",
			locator1 = "WorkflowTransitionModal#TRANSITION_MODAL_CHECKBOX_ITEM");

		Click(locator1 = "Button#NEXT");

		AssertElementPresent(
			key_tilte = "Blogs Entry: Blog Entry 2",
			locator1 = "WorkflowTransitionModal#TRANSITION_MODAL_ASSERT_REVIEW");

		AssertElementNotPresent(
			key_tilte = "Blogs Entry: Blog Entry 3",
			locator1 = "WorkflowTransitionModal#TRANSITION_MODAL_ASSERT_REVIEW");

		WorkflowMetrics.chooseTransitionPerStep(
			taskName = "Review",
			transitionOption = "Approve");

		WorkflowMetrics.goToDashboardAllItemsWithFilter(
			workflowFilterOption = "COMPLETED",
			workflowMetricsTab = "PERFORMANCE",
			workflowProcess = "Single Approver");

		AssertElementPresent(
			key_workflowInstanceName = "Blogs Entry: Blog Entry 2",
			locator1 = "WorkflowAllItems#WORKFLOW_INSTANCE_NAME");
	}

	@description = "LPS-108138 - Verify that the message -The selected steps have transitioned successfully- is displayed when several steps are transitioned at once"
	@priority = "3"
	test MessageIsDisplayedWhenSeveralStepsAreTransitionedAtOnce {
		ApplicationsMenu.gotoPortlet(
			category = "Workflow",
			panel = "Applications",
			portlet = "Process Builder");

		Navigator.gotoNavItem(navItem = "Configuration");

		Workflow.configureWorkflow(
			workflowDefinition = "Single Approver",
			workflowResourceValue = "Blogs Entry");

		for (var blogNumber : list "1,2") {
			JSONBlog.addEntry(
				entryContent = "Blogs Entry ${blogNumber} Content",
				entryTitle = "Blogs Entry Title ${blogNumber}");
		}

		UserBar.gotoDropdownItem(dropdownItem = "My Workflow Tasks");

		Navigator.gotoNavTab(navTab = "Assigned to My Roles");

		for (var instanceNameNumber : list "1,2") {
			Workflow.assignTaskToUser(
				assetTitle = "Blogs Entry Title ${instanceNameNumber}",
				assetType = "Blogs Entry",
				assigneeName = "test (Test Test)",
				workflowTask = "Review");
		}

		WorkflowMetrics.goToWorkflowProcessMetrics(workflowProcessName = "Single Approver");

		Click(locator1 = "WorkflowMetrics#DASHBOARD_TOTAL_PENDING_ITEMS_VALUE");

		PortletEntry.selectAll();

		Click(
			locator1 = "WorkflowAllItems#REASSIGN_ICON",
			task = "1");

		WorkflowMetrics.selectAllTransitionTasks();

		WorkflowMetrics.selectTransition(
			taskName = "Review",
			transitionOption = "Approve");

		WorkflowMetrics.selectTransition(
			taskName = "Review",
			transitionOption = "Approve");

		Button.clickDone();

		Alert.viewSuccessMessageText(successMessage = "The selected steps have transitioned successfully");
	}

	@description = "LPS-108138 - Verify that the message -Select a transition- and -All steps require a transition to be selected to complete this action.- are displayed when the user clicks on Done without selecting all transitions"
	@priority = "3"
	test MessageIsDisplayedWhenSomeTransitionIsNotSelected {
		ApplicationsMenu.gotoPortlet(
			category = "Workflow",
			panel = "Applications",
			portlet = "Process Builder");

		Navigator.gotoNavItem(navItem = "Configuration");

		Workflow.configureWorkflow(
			workflowDefinition = "Single Approver",
			workflowResourceValue = "Blogs Entry");

		JSONBlog.addEntry(
			entryContent = "Blogs Entry",
			entryTitle = "Blogs Entry Title 1");

		JSONBlog.addEntry(
			entryContent = "Blogs Entry",
			entryTitle = "Blogs Entry Title 2");

		UserBar.gotoDropdownItem(dropdownItem = "My Workflow Tasks");

		Workflow.assignToMeTaskByActions(
			workflowAssetTitle = "Blogs Entry Title 1",
			workflowAssetType = "Blogs Entry",
			workflowTask = "Review");

		Workflow.assignToMeTaskByActions(
			workflowAssetTitle = "Blogs Entry Title 2",
			workflowAssetType = "Blogs Entry",
			workflowTask = "Review");

		WorkflowMetrics.goToWorkflowAllItems(workflowProcessName = "Single Approver");

		WorkflowMetrics.goToBulkTransitionSelectTasks();

		WorkflowMetrics.selectAllTransitionTasks();

		Button.clickDone();

		AssertElementPresent(
			key_errorMessage = "All steps require a transition to be selected to complete this action.",
			locator1 = "WorkflowTransitionModal#TRANSITION_ERROR_MESSAGE");
	}

	@description = "LPS-108138 - Verify that, in -Choose Transition Per Step- modal, the user can add comments when transitioning tasks"
	@priority = "4"
	test OnChooseTransitionPerStepCanAddCommentToATransitionTask {
		ApplicationsMenu.gotoPortlet(
			category = "Workflow",
			panel = "Applications",
			portlet = "Process Builder");

		Navigator.gotoNavItem(navItem = "Configuration");

		Workflow.configureWorkflow(
			workflowDefinition = "Single Approver",
			workflowResourceValue = "Blogs Entry");

		JSONBlog.addEntry(
			entryContent = "Blogs Entry",
			entryTitle = "Blogs Entry Title 1");

		JSONBlog.addEntry(
			entryContent = "Blogs Entry",
			entryTitle = "Blogs Entry Title 2");

		UserBar.gotoDropdownItem(dropdownItem = "My Workflow Tasks");

		Workflow.assignToMeTaskByActions(
			workflowAssetTitle = "Blogs Entry Title 1",
			workflowAssetType = "Blogs Entry",
			workflowTask = "Review");

		Workflow.assignToMeTaskByActions(
			workflowAssetTitle = "Blogs Entry Title 2",
			workflowAssetType = "Blogs Entry",
			workflowTask = "Review");

		WorkflowMetrics.goToWorkflowAllItems(workflowProcessName = "Single Approver");

		WorkflowMetrics.goToBulkTransitionSelectTasks();

		WorkflowMetrics.selectAllTransitionTasks();

		WorkflowMetrics.chooseTransitionPerStep(
			comment = "Test",
			taskName = "Review",
			transitionOption = "Approve");

		UserBar.gotoDropdownItem(dropdownItem = "My Submissions");

		Workflow._viewActivities(
			workflowActivityComment = "Test",
			workflowAssetTitle = "Blogs Entry Title 1");

		UserBar.gotoDropdownItem(dropdownItem = "My Submissions");

		Workflow._viewActivities(
			workflowActivityComment = "Test",
			workflowAssetTitle = "Blogs Entry Title 2");
	}

	@description = "LPS-108138 - Verify that users can filter the tasks to be transitioned by steps"
	@priority = "3"
	test OnChooseTransitionPerStepCanFilterByStep {
		ApplicationsMenu.gotoPortlet(
			category = "Workflow",
			panel = "Applications",
			portlet = "Process Builder");

		Navigator.gotoNavItem(navItem = "Configuration");

		Workflow.configureWorkflow(
			workflowDefinition = "Single Approver",
			workflowResourceValue = "Blogs Entry");

		JSONBlog.addEntry(
			entryContent = "Blogs Entry A",
			entryTitle = "First Blogs Entry Title");

		JSONBlog.addEntry(
			entryContent = "Blogs Entry B",
			entryTitle = "Second Blogs Entry Title");

		UserBar.gotoDropdownItem(dropdownItem = "My Workflow Tasks");

		Workflow.assignToMeTaskByActions(
			workflowAssetTitle = "First Blogs Entry Title",
			workflowAssetType = "Blogs Entry",
			workflowTask = "Review");

		Workflow.assignToMeTaskByActions(
			workflowAssetTitle = "Second Blogs Entry Title",
			workflowAssetType = "Blogs Entry",
			workflowTask = "Review");

		Workflow.rejectTaskByDetails(
			workflowAssetContent = "Blogs Entry A",
			workflowAssetTitle = "First Blogs Entry Title",
			workflowAssetType = "Blogs Entry",
			workflowTask = "Review");

		ApplicationsMenu.gotoPortlet(
			category = "Workflow",
			panel = "Applications",
			portlet = "Metrics");

		WorkflowMetrics.goToWorkflowProcessMetrics(workflowProcessName = "Single Approver");

		Click(locator1 = "WorkflowMetrics#DASHBOARD_TOTAL_PENDING_ITEMS_TITLE");

		WorkflowMetrics.goToDashboardAllItemsWithFilter(
			workflowFilterOption = "PENDING",
			workflowMetricsTab = "DASHBOARD",
			workflowProcess = "Single Approver");

		WorkflowMetrics.goToBulkTransitionSelectTasks();

		Click(locator1 = "WorkflowReassignModal#PROCESS_STEP_DROPDOWN");

		Click(
			key_processName = "Review",
			locator1 = "WorkflowReassignModal#PROCESS_STEP_DROPDOWN_OPTION");

		Click(locator1 = "WorkflowReassignModal#PROCESS_STEP_DROPDOWN");

		AssertElementPresent(
			key_itemName = "Blogs Entry: Second Blogs Entry Title",
			locator1 = "WorkflowReassignModal#ITEM_SUBJECT_CHECK");

		AssertElementNotPresent(
			key_itemName = "Blogs Entry: First Blogs Entry Title",
			locator1 = "WorkflowReassignModal#ITEM_SUBJECT_CHECK");

		Click(locator1 = "WorkflowReassignModal#CLEAR_SEARCH_MODAL_BUTTON");

		Click(locator1 = "WorkflowReassignModal#PROCESS_STEP_DROPDOWN");

		Click(
			key_processName = "Update",
			locator1 = "WorkflowReassignModal#PROCESS_STEP_DROPDOWN_OPTION");

		Click(locator1 = "WorkflowReassignModal#PROCESS_STEP_DROPDOWN");

		AssertElementPresent(
			key_itemName = "Blogs Entry: First Blogs Entry Title",
			locator1 = "WorkflowReassignModal#ITEM_SUBJECT_CHECK");

		AssertElementNotPresent(
			key_itemName = "Blogs Entry: Second Blogs Entry Title",
			locator1 = "WorkflowReassignModal#ITEM_SUBJECT_CHECK");
	}

	@description = "LPS-108138 - Verify that only the tasks selected on -Select Steps to Transition- are being considered to be transitioned in the -Choose Transition Per Step- step"
	@priority = "4"
	test OnlySelectedTasksAreConsideredToBeTransitioned {
		ApplicationsMenu.gotoPortlet(
			category = "Workflow",
			panel = "Applications",
			portlet = "Process Builder");

		Navigator.gotoNavItem(navItem = "Configuration");

		Workflow.configureWorkflow(
			workflowDefinition = "Single Approver",
			workflowResourceValue = "Blogs Entry");

		JSONBlog.addEntry(
			entryContent = "Blogs Entry Content",
			entryTitle = "Blogs Entry Title 1");

		JSONBlog.addEntry(
			entryContent = "Blogs Entry Content",
			entryTitle = "Blogs Entry Title 2");

		UserBar.gotoDropdownItem(dropdownItem = "My Workflow Tasks");

		Workflow.assignToMeTaskByActions(
			workflowAssetTitle = "Blogs Entry Title 1",
			workflowAssetType = "Blogs Entry",
			workflowTask = "Review");

		Workflow.assignToMeTaskByActions(
			workflowAssetTitle = "Blogs Entry Title 2",
			workflowAssetType = "Blogs Entry",
			workflowTask = "Review");

		WorkflowMetrics.goToDashboardAllItemsWithFilter(
			workflowFilterOption = "PENDING",
			workflowMetricsTab = "DASHBOARD",
			workflowProcess = "Single Approver");

		WorkflowMetrics.goToTransitionTasks();

		Click(
			key_task = "1",
			locator1 = "WorkflowTransitionModal#TRANSITION_MODAL_CHECKBOX_ITEM");

		Click(locator1 = "Button#NEXT");

		AssertElementPresent(
			key_tilte = "Blogs Entry: Blogs Entry Title 1",
			locator1 = "WorkflowTransitionModal#TRANSITION_MODAL_ASSERT_REVIEW");

		AssertElementNotPresent(
			key_tilte = "Blogs Entry: Blogs Entry Title 2",
			locator1 = "WorkflowTransitionModal#TRANSITION_MODAL_ASSERT_REVIEW");
	}

	@description = "LPS-108138 - Verify that when there are 11 or more instances to be transitioned with the same step, only the first 10 instances are displayed with the -Show All/Show Less- buttons available to be selected"
	@priority = "3"
	test ShowAllLessButtonAvailable {
		ApplicationsMenu.gotoPortlet(
			category = "Workflow",
			panel = "Applications",
			portlet = "Process Builder");

		Navigator.gotoNavItem(navItem = "Configuration");

		Workflow.configureWorkflow(
			workflowDefinition = "Single Approver",
			workflowResourceValue = "Blogs Entry");

		var count = "1";

		while ("${count}" != "12") {
			JSONBlog.addEntry(
				entryContent = "Blogs Entry",
				entryTitle = "Blogs Entry Title ${count}");

			var count = ${count} + 1;
		}

		UserBar.gotoDropdownItem(dropdownItem = "My Workflow Tasks");

		var count = "1";

		while ("${count}" != "12") {
			Workflow.assignToMeTaskByActions(
				workflowAssetTitle = "Blogs Entry Title ${count}",
				workflowAssetType = "Blogs Entry",
				workflowTask = "Review");

			var count = ${count} + 1;
		}

		WorkflowMetrics.goToWorkflowProcessMetrics(workflowProcessName = "Single Approver");

		Click(locator1 = "WorkflowMetrics#DASHBOARD_TOTAL_PENDING_ITEMS_VALUE");

		WorkflowMetrics.goToBulkTransitionSelectTasks();

		Click(locator1 = "WorkflowMetricsAllItemsListTransitionTasks#SELECT_ALL_TRANSITION_TASKS");

		Click(locator1 = "Button#SELECT_ALL");

		Button.clickNext();

		AssertElementPresent(locator1 = "WorkflowMetricsAllItemsListTransitionTasks#WORKFLOW_ALL_ITEMS_SHOW_ALL_OR_LESS_BUTTON");

		var count = "1";

		while ("${count}" != "11") {
			AssertVisible(
				key_taskName = "Blogs Entry Title ${count}",
				locator1 = "WorkflowTransitionModal#TRANSITION_TASK_NAME");

			var count = ${count} + 1;
		}

		Click(locator1 = "WorkflowMetricsAllItemsListTransitionTasks#WORKFLOW_ALL_ITEMS_SHOW_ALL_OR_LESS_BUTTON");

		var count = "1";

		while ("${count}" != "12") {
			AssertVisible(
				key_taskName = "Blogs Entry Title ${count}",
				locator1 = "WorkflowTransitionModal#TRANSITION_TASK_NAME");

			var count = ${count} + 1;
		}

		Click(locator1 = "WorkflowMetricsAllItemsListTransitionTasks#WORKFLOW_ALL_ITEMS_SHOW_ALL_OR_LESS_BUTTON");

		var count = "1";

		while ("${count}" != "11") {
			AssertVisible(
				key_taskName = "Blogs Entry Title ${count}",
				locator1 = "WorkflowTransitionModal#TRANSITION_TASK_NAME");

			var count = ${count} + 1;
		}
	}

	@description = "LPS-108138 - Verify that the tasks on -Choose Transition Per Step- are grouped by versioned tasks -when there are changes in the workflow process-"
	@priority = "4"
	test TasksAreGroupedByVersionedTasks {
		property test.name.skip.portal.instance = "WorkflowMetricsAllItemsListTransitionTasks#TasksAreGroupedByVersionedTasks";

		ApplicationsMenu.gotoPortlet(
			category = "Workflow",
			panel = "Applications",
			portlet = "Process Builder");

		Navigator.gotoNavItem(navItem = "Configuration");

		Workflow.configureWorkflow(
			workflowDefinition = "Single Approver",
			workflowResourceValue = "Blogs Entry");

		JSONBlog.addEntry(
			entryContent = "Blogs Entry Content",
			entryTitle = "Blogs Entry Title");

		ApplicationsMenu.gotoPortlet(
			category = "Workflow",
			panel = "Applications",
			portlet = "Process Builder");

		Workflow.gotoEditWorkflowDefinition(workflowDefinitionTitle = "Single Approver");

		KaleoDesigner.selectNode(nodeNodeLabel = "update");

		KaleoDesigner.deleteNode(nodeNodeLabel = "update");

		Button.click(button = "Update");

		JSONBlog.addEntry(
			entryContent = "Blogs Entry Content",
			entryTitle = "Blogs Entry Title 2");

		UserBar.gotoDropdownItem(dropdownItem = "My Workflow Tasks");

		Workflow.assignToMeTaskByActions(
			workflowAssetTitle = "Blogs Entry Title",
			workflowAssetType = "Blogs Entry",
			workflowTask = "Review");

		Workflow.assignToMeTaskByActions(
			workflowAssetTitle = "Blogs Entry Title 2",
			workflowAssetType = "Blogs Entry",
			workflowTask = "Review");

		WorkflowMetrics.goToWorkflowAllItems(workflowProcessName = "Single Approver");

		if (IsNotChecked.isNotCheckedNotVisible(locator1 = "Checkbox#LEXICON_SELECT_ALL_INPUT")) {
			Click(locator1 = "Checkbox#LEXICON_SELECT_ALL_CONTAINER");
		}

		Click(
			locator1 = "WorkflowAllItems#KEBAB_DROPDOWN",
			task = "1");

		Click(locator1 = "WorkflowAllItems#TRANSITION_DROPDOWN");

		WorkflowMetrics.selectAllTransitionTasks();

		AssertElementPresent(
			key_taskName = "Blogs Entry Title",
			locator1 = "WorkflowTransitionModal#TRANSITION_MODAL_ASSERT_REVIEW_TASK_1");

		AssertElementPresent(
			key_taskName = "Blogs Entry Title 2",
			locator1 = "WorkflowTransitionModal#TRANSITION_MODAL_ASSERT_REVIEW_TASK_2");
	}

	@description = "LPS-105495 - Verify that tasks with more than 10 possible transitions have a scroll inside the dropdown field"
	@priority = "3"
	test TasksWithMoreThan10PossibleTransitionsHasScroll {
		property test.name.skip.portal.instance = "WorkflowMetricsAllItemsListTransitionTasks#TasksWithMoreThan10PossibleTransitionsHasScroll";

		ApplicationsMenu.gotoPortlet(
			category = "Workflow",
			panel = "Applications",
			portlet = "Process Builder");

		Workflow.uploadDefinition(
			workflowDefinitionFile = "10-transitions-workflow-definition.xml",
			workflowDefinitionTitle = "Transitions Definition");

		Navigator.gotoNavItem(navItem = "Configuration");

		Workflow.configureWorkflow(
			workflowDefinition = "Transitions Definition",
			workflowResourceValue = "Blogs Entry");

		JSONBlog.addEntry(
			entryContent = "Blogs Entry Content",
			entryTitle = "Blogs Entry Title");

		UserBar.gotoDropdownItem(dropdownItem = "My Workflow Tasks");

		Workflow.assignToMeTaskByActions(
			workflowAssetTitle = "Blogs Entry Title",
			workflowAssetType = "Blogs Entry",
			workflowTask = "Review");

		WorkflowMetrics.goToDashboardAllItemsWithFilter(
			workflowFilterOption = "PENDING",
			workflowMetricsTab = "DASHBOARD",
			workflowProcess = "Transitions Definition");

		Click(
			locator1 = "WorkflowAllItems#KEBAB_DROPDOWN",
			task = "1");

		AssertElementPresent(locator1 = "WorkflowAllItems#TRANSITION_DROPDOWN_KEBAB");
	}

}