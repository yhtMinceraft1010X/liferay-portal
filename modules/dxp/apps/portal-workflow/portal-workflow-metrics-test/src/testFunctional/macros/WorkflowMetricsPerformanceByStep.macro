definition {

	macro assertStepNameByProcessVersion {
		var key_dateRange = "${version}";
		var key_rowName = "${rowName}";
		var key_rowNumber = "${rowNumber}";

		Click(locator1 = "WorkflowMetricsPerformanceByStep#PROCESS_VERSION_DROPDOWN");

		Click(
			key_dateRange = "${version}",
			locator1 = "WorkflowMetricsPerformanceByStep#DATE_RANGE_DROPDOWN_OPTION");

		AssertElementPresent(
			key_rowName = "${rowName}",
			key_rowNumber = "${rowNumber}",
			locator1 = "WorkflowMetrics#METRICS_PERFORMANCE_BY_STEP_ROW_ORDER_CHECK");
	}

	macro goToPerformanceTab {
		Click(locator1 = "WorkflowMetrics#METRICS_PERFORMANCE_TAB");
	}

	macro searchForStep {
		var key_stepName = "${stepName}";

		Click(
			locator1 = "WorkflowMetricsPerformanceByStep#METRICS_PERFORMANCE_BY_STEP_PAGE_INPUT_FIELD",
			value1 = "${stepName}");

		Type(
			locator1 = "WorkflowMetricsPerformanceByStep#METRICS_PERFORMANCE_BY_STEP_PAGE_INPUT_FIELD",
			value1 = "${stepName}");

		KeyPress(
			locator1 = "TextInput#SEARCH",
			value1 = "\RETURN");
	}

	macro selectDateRangeFilter {
		key_dateRange = "${dateRange}";

		Click(locator1 = "WorkflowMetricsPerformanceByStep#DATE_RANGE_DROPDOWN");

		Click(locator1 = "WorkflowMetricsPerformanceByStep#DATE_RANGE_DROPDOWN_OPTION");
	}

	macro viewPerformanceByStepTooltip {
		Click(locator1 = "WorkflowMetrics#METRICS_PERFORMANCE_TAB");

		AssertElementPresent(locator1 = "WorkflowMetrics#METRICS_PERFORMANCE_BY_STEP_TOOLTIP");
	}

	macro viewPerformanceByStepValues {
		var key_stepName = "${stepName}";

		AssertElementPresent(locator1 = "WorkflowMetricsPerformanceByStep#METRICS_PERFORMANCE_BY_STEP_PAGE_TABLE_STEP_NAME");

		AssertTextEquals(
			locator1 = "WorkflowMetricsPerformanceByStep#METRICS_PERFORMANCE_BY_STEP_PAGE_TABLE_SLA_BREACHED_VALUE",
			value1 = "${slaBreachedValue}");

		AssertTextEquals(
			locator1 = "WorkflowMetricsPerformanceByStep#METRICS_PERFORMANCE_BY_STEP_PAGE_TABLE_AVG_COMPLETION_TIME_VALUE",
			value1 = "${avgCompletionTimeValue}");

		if ("${isNotPresent}" != "") {
			for (var isStepNotPresent : list "${isNotPresent}") {
				var key_stepName = "${isStepNotPresent}";

				AssertElementNotPresent(locator1 = "WorkflowMetricsPerformanceByStep#METRICS_PERFORMANCE_BY_STEP_PAGE_TABLE_STEP_NAME");
			}
		}
	}

}