@component-name = "portal-workflow-ee"
definition {

	property portal.release = "true";
	property portal.upstream = "true";
	property test.run.environment = "EE";
	property testray.main.component.name = "Kaleo Forms Admin";

	var kfFieldSetName = "New Kaleo Field Set";
	var workflowAssetType = "Process";

	setUp {
		TestCase.setUpPortalInstance();

		User.firstLoginPG();

		ProductMenu.gotoPortlet(
			category = "Content & Data",
			portlet = "Kaleo Forms Admin");

		KaleoFormsAdmin.addProcess();

		KaleoFormsAdmin.addProcessDetails(
			kfProcessDescription = "Kaleo Forms Process Description",
			kfProcessName = "Kaleo Forms Process");

		KaleoFormsAdmin.next();

		KaleoFormsAdmin.chooseFieldSet(
			kfFieldSetName = "Contacts",
			kfProcessName = "Kaleo Forms Process");

		KaleoFormsAdmin.next();

		KaleoFormsAdmin.chooseWorkflow(
			kfProcessName = "Kaleo Forms Process",
			workflowDefinitionTitle = "Single Approver");

		KaleoFormsAdmin.next();

		KaleoFormsAdmin.assignForm(
			kfProcessName = "Kaleo Forms Process",
			workflowTask = "create");

		KaleoFormsAdmin.addForm(kfFormName = "Created Task Form");

		KaleoFormsAdmin.saveForm();

		KaleoFormsAdmin.chooseForm(kfFormName = "Created Task Form");

		KaleoFormsAdmin.saveProcess(kfProcessName = "Kaleo Forms Process");

		KaleoFormsNavigator.gotoManageFieldSets();
	}

	tearDown {
		var testPortalInstance = PropsUtil.get("test.portal.instance");

		if ("${testPortalInstance}" == "true") {
			PortalInstances.tearDownCP();
		}
		else {
			KaleoFormsAdmin.tearDownProcess();

			DDLTemplate.tearDownTemplateCP();

			DDLTemplate.tearDownTemplateCP(ddlDataDefinitionName = "To Do");

			Workflow.tearDownSpecificWorkflowDefinition(workflowDefinitionTitle = "Edited Workflow Definition");

			Workflow.tearDownWorkflowDefinitions();

			PagesAdmin.tearDownCP();
		}
	}

	@description = "Verify that the user can create a new field set on Manage FIeld Sets Area."
	@priority = "5"
	test CanCreateANewFieldSet {
		Click(locator1 = "Button#PLUS");

		KaleoFormsManageFieldSets.createNewFieldSet(
			fieldSetName = "New Field Set",
			fieldType = "Text");
	}

	@description = "Verify that the user can delete a field set."
	@priority = "5"
	test CanDeleteAFieldSet {
		KaleoFormsManageFieldSets.deleteOneFieldSet(fieldSetName = "Inventory");

		KaleoFormsManageFieldSets.viewFieldSetCannotBeFound(fieldSetName = "Inventory");
	}

	@description = "Verify that user can filter by modified date and ID."
	@priority = "3"
	test CanFilterByModifiedDate {
		ManagementBar.setFilterAndOrder(filterBy = "Modified Date");

		KaleoFormsManageFieldSets.viewFieldSetOrder(fieldSetList = "Contacts,Events,Inventory,Issues Tracking,Meeting Minutes,To Do");

		KaleoFormsManageFieldSets.editFieldSets(
			nameFieldset = "Contacts",
			newNameFieldset = "Contacts Test");

		ManagementBar.setFilterAndOrder(filterBy = "Modified Date");

		KaleoFormsManageFieldSets.viewFieldSetOrder(fieldSetList = "Events,Inventory,Issues Tracking,Meeting Minutes,To Do,Contacts Test");
	}

	@description = "Verify that the user can not delete a field set that is required by one or more templates."
	@priority = "4"
	test CanNotDeleteFieldSetInUse {
		KaleoFormsManageFieldSets.viewFieldSetCannotBeDeleted(fieldSetName = "Contacts");
	}

	@description = "Verify that user can Reverse sort Direction and if the elements reverse position."
	@priority = "3"
	test CanReverseSortDirectionTheElement {
		KaleoFormsManageFieldSets.clickFilterAndOrderBy(filterOption = "ID");

		KaleoFormsManageFieldSets.viewUpDirectionOfSortButton();

		KaleoFormsManageFieldSets.viewFieldSetOrder(fieldSetList = "Contacts,Events,Inventory,Issues Tracking,Meeting Minutes,To Do");

		Workflow.clickRevertSortDirection();

		KaleoFormsManageFieldSets.viewFieldSetOrder(fieldSetList = "To Do,Meeting Minutes,Issues Tracking,Inventory,Events,Contacts");
	}

	@description = "Verify that the user can search for a field set on manage field set page."
	@priority = "4"
	test CanSearchFieldSet {
		KaleoFormsManageFieldSets.searchFieldSet(fieldSetName = "Inventory");
	}

	@description = "Verify that user can view of values on columns -ID-, -Name-,  -Description-, -Scope-, -Modified Date-."
	@priority = "3"
	test CanViewValuesOnColumns {
		KaleoFormsManageFieldSets.editFieldSets(
			nameFieldset = "Contacts",
			newDescription = "Contacts description edited",
			newNameFieldset = "Contacts Edited");

		KaleoFormsManageFieldSets.viewValuesOnColumns(
			descriptionField = "Contacts description edited",
			modifiedDateField = "0 Seconds Ago",
			nameField = "Contacts Edited",
			rowNumber = "6");
	}

	@description = "Verify that the user can navigate to the next page."
	@priority = "3"
	test NavigateToTheNextPage {
		property portal.upstream = "quarantine";

		Pagination.changePagination(itemsPerPage = "4");

		KaleoFormsAdmin.viewNextFieldSetPage();

		Pagination.viewResults(results = "Showing 5 to 6 of 6 entries.");

		KaleoFormsManageFieldSets.viewPageNumber(pageNumberList = "2");
	}

	@description = "Verify that when there are no results for the search, it displays the message -No Field Sets were found-."
	@priority = "3"
	test NotViewResultTheSearch {
		KaleoFormsManageFieldSets.viewFieldSetCannotBeFound(fieldSetName = "Acrw23f");
	}

}