definition {

	macro addFieldSet {
		if (IsElementPresent(locator1 = "Icon#HEADER_VERTICAL_ELLIPSIS")) {
			KaleoFormsNavigator.gotoManageFieldSets();

			LexiconEntry.gotoAdd();

			PortletEntry.inputName(name = "${kfFieldSetName}");
		}
		else {
			KaleoFormsAdmin.viewNewProcessProgressBar(
				kfProgressName = "Fields",
				kfProgressStep = "2");

			AssertClick(
				locator1 = "KaleoFormsAdminEditProcess#ADD_FIELD_SET_BUTTON",
				value1 = "Add Field Set");

			SelectFrame(locator1 = "IFrame#MODAL_BODY");

			PortletEntry.inputName(name = "${kfFieldSetName}");
		}
	}

	macro addForm {
		SelectFrame(locator1 = "DDMEditStructure#IFRAME");

		LexiconEntry.gotoAdd();

		Click(locator1 = "DDMEditStructure#NAME_FIELD");

		PortletEntry.inputName(name = "${kfFormName}");

		for (var field : list "Fieldset,Paragraph,Separator") {
			DynamicDataMapping.viewCanAddField(field = "${field}");
		}
	}

	macro addProcess {
		ProductMenu.gotoPortlet(
			category = "Content &amp; Data",
			portlet = "Kaleo Forms Admin");

		AssertTextEquals(
			locator1 = "Portlet#HEADER",
			value1 = "Kaleo Forms Admin");

		LexiconEntry.gotoAdd();
	}

	macro addProcessDetails {
		KaleoFormsAdmin.viewNewProcessProgressBar(
			kfProgressName = "Details",
			kfProgressStep = "1");

		AssertTextEquals(
			locator1 = "KaleoFormsAdminEditProcess#NEXT_BUTTON_INACTIVE",
			value1 = "Next");

		var key_fieldName = "name";

		Type(
			locator1 = "FormFields#TEXT_FIELD",
			value1 = "${kfProcessName}");

		KaleoFormsAdmin.editDescription(
			fieldName = "Description",
			fieldValue = "${kfProcessDescription}");
	}

	macro addWorkflow {
		KaleoFormsAdmin.viewNewProcessProgressBar(
			kfProgressName = "Workflow",
			kfProgressStep = "3");

		AssertClick(
			locator1 = "KaleoFormsAdminEditProcess#ADD_WORKFLOW_BUTTON",
			value1 = "Add Workflow");

		SelectFrame(locator1 = "IFrame#MODAL_BODY");

		PortletEntry.inputTitle(title = "${workflowDefinitionTitle}");
	}

	macro assertPaginationBarNotPresent {
		AssertElementNotPresent(locator1 = "Pagination#PAGINATION_BAR");
	}

	macro assertPaginationBarPresent {
		AssertElementPresent(locator1 = "Pagination#PAGINATION_BAR");
	}

	macro assignForm {
		KaleoFormsAdmin.viewNewProcessProgressBar(
			kfProgressName = "Forms",
			kfProgressStep = "4");

		if ("${kfPendingForm}" == "true") {
			AssertTextEquals(
				locator1 = "Message#INFO",
				value1 = "Updating the field set or workflow will cause loss of data.");
		}

		WaitForLiferayEvent.initializeLiferayEventLog();

		LexiconEntry.gotoEntryMenuItem(
			menuItem = "Assign Form",
			rowEntry = "${workflowTask}");
	}

	macro assignNewForm {
		KaleoFormsAdmin.editProcess(kfProcessName = "${kfProcessName}");

		KaleoFormsAdmin.gotoProgressStep(kfProgressName = "Forms");

		KaleoFormsAdmin.unassignForm(workflowTask = "${workflowTask}");

		KaleoFormsAdmin.assignForm(workflowTask = "${workflowTask}");

		KaleoFormsAdmin.addForm(kfFormName = "${kfFormNameNew}");

		KaleoFormsAdmin.saveForm();

		KaleoFormsAdmin.chooseForm(kfFormName = "${kfFormNameNew}");

		KaleoFormsAdmin.saveProcess();
	}

	macro cancelProcessRecordDetailsView {
		AssertClick(
			locator1 = "Button#CANCEL",
			value1 = "Cancel");
	}

	macro checkboxesInProcessTable {
		Check(
			key_checkboxOption = "${checkProcessName}",
			locator1 = "Checkbox#SPECIFIC_CHECKBOX_IN_TABLE");
	}

	macro chooseFieldSet {
		KaleoFormsAdmin.viewNewProcessProgressBar(
			kfProgressName = "Fields",
			kfProgressStep = "2");

		var key_kfFieldSetName = "${kfFieldSetName}";

		if ("${kfPendingForm}" == "true") {
			AssertTextEquals(
				locator1 = "Message#INFO",
				value1 = "Updating the field set or workflow will cause loss of data.");
		}

		LexiconEntry.gotoEntryMenuItem(
			menuItem = "Choose",
			rowEntry = "${kfFieldSetName}");

		AssertTextEquals(
			locator1 = "KaleoFormsAdminEditProcess#SELECTED_FIELD_SET",
			value1 = "${kfFieldSetName}");
	}

	macro chooseForm {
		DynamicDataMapping.selectDynamicDataMappingFrame();

		var key_kfFormName = "${kfFormName}";

		AssertClick(
			locator1 = "KaleoFormsAdminEditProcessForm#FORM_TABLE_NAME_LINK",
			value1 = "${kfFormName}");

		SelectFrame(value1 = "relative=top");

		WaitForLiferayEvent.initializeLiferayEventLog();
	}

	macro chooseWorkflow {
		KaleoFormsAdmin.viewNewProcessProgressBar(
			kfProgressName = "Workflow",
			kfProgressStep = "3");

		Refresh();

		if ("${kfPendingForm}" == "true") {
			AssertTextEquals(
				locator1 = "Message#INFO",
				value1 = "Updating the field set or workflow will cause loss of data.");
		}

		if (isSet(workflowDefinitionTitleEdit)) {
			var key_workflowDefinitionTitle = "${workflowDefinitionTitleEdit}";

			AssertTextEquals(
				locator1 = "KaleoFormsAdminEditProcess#WORKFLOW_TABLE_TITLE",
				value1 = "${workflowDefinitionTitleEdit}");
		}
		else {
			var key_workflowDefinitionTitle = "${workflowDefinitionTitle}";

			AssertTextEquals(
				locator1 = "KaleoFormsAdminEditProcess#WORKFLOW_TABLE_TITLE",
				value1 = "${workflowDefinitionTitle}");
		}

		LexiconEntry.gotoEntryMenuItem(
			menuItem = "Choose",
			rowEntry = "${workflowDefinitionTitle}");

		KaleoFormsAdmin.viewSelectedWorkflow(
			edit = "${edit}",
			workflowDefinitionTitle = "${workflowDefinitionTitle}");
	}

	macro closeWorkflowModal {
		SelectFrame(value1 = "relative=top");

		if (IsElementPresent(locator1 = "IFrame#MODAL_BODY")) {
			Click(locator1 = "Icon#CLOSE");
		}

		WaitForLiferayEvent.initializeLiferayEventLog();
	}

	macro createKaleoFormsProcess {
		KaleoFormsAdmin.addProcessDetails(
			kfProcessDescription = "${kfProcessDescription}",
			kfProcessName = "${kfProcessName}");

		KaleoFormsAdmin.next();

		KaleoFormsAdmin.chooseFieldSet(
			kfFieldSetName = "${kfFieldSetName}",
			kfProcessName = "${kfProcessName}");

		KaleoFormsAdmin.next();

		KaleoFormsAdmin.chooseWorkflow(
			kfProcessName = "${kfProcessName}",
			workflowDefinitionTitle = "${workflowDefinitionTitle}");

		KaleoFormsAdmin.next();

		KaleoFormsAdmin.assignForm(
			kfProcessName = "${kfProcessName}",
			workflowTask = "${workflowTask}");

		KaleoFormsAdmin.addForm(kfFormName = "${kfFormName}");

		KaleoFormsAdmin.saveForm();

		KaleoFormsAdmin.chooseForm(kfFormName = "${kfFormName}");

		KaleoFormsAdmin.saveProcess(kfProcessName = "${kfProcessName}");
	}

	macro deactivateWorkflow {
		LexiconEntry.gotoEntryMenuItemNoError(
			menuItem = "Unpublish",
			rowEntry = "${workflowDefinitionTitle}");

		Alert.viewSuccessMessage();
	}

	macro deleteFieldSet {
		KaleoFormsNavigator.gotoManageFieldSets();

		if (IsElementPresent(locator1 = "DDMSelectStructure#DDM_STRUCTURE_ELLIPSIS_1")) {
			PortletEntry.selectAll();

			ClickNoError(locator1 = "Icon#TIMES");

			AssertConfirm(value1 = "Are you sure you want to delete this? It will be deleted immediately.");

			if ("${kfProcessExists}" == "true") {
				Alert.viewRequestFailedToComplete();

				AssertTextEquals.assertPartialText(
					locator1 = "Message#ERROR_2",
					value1 = "The structure cannot be deleted because it is required by one or more structure links.");
			}
			else {
				Alert.viewSuccessMessage();
			}
		}
	}

	macro deleteForm {
		KaleoFormsNavigator.gotoManageForms(kfFieldSetName = "${kfFieldSetName}");

		Search.searchCP(searchTerm = "${kfFormName}");

		LexiconEntry.gotoEntryMenuItemNoError(
			menuItem = "Delete",
			rowEntry = "${kfFormName}");

		AssertConfirm(value1 = "Are you sure you want to delete this? It will be deleted immediately.");

		Alert.viewSuccessMessage();
	}

	macro deleteProcess {
		if (isSet(kfProcessName)) {
			var key_rowEntry = "${kfProcessName}";

			Click(locator1 = "Icon#ROW_VERTICAL_ELLIPSIS");
		}
		else {
			Click(locator1 = "Icon#BODY_VERTICAL_ELLIPSIS");
		}

		MenuItem.clickNoError(menuItem = "Delete");

		AssertConfirm(value1 = "Are you sure you want to delete this? It will be deleted immediately.");

		if (IsElementNotPresent(locator1 = "Icon#BODY_VERTICAL_ELLIPSIS")) {
			KaleoFormsAdmin.noProcessesWereFound();
		}
		else {
			AssertTextNotPresent(
				locator1 = "KaleoFormsAdmin#PROCESS_TABLE_NAME",
				value1 = "${kfProcessName}");
		}
	}

	macro deleteProcessRecord {
		var key_rowEntry = "${kfProcessFieldData}";

		Click(locator1 = "Icon#BODY_VERTICAL_ELLIPSIS");

		MenuItem.clickNoError(menuItem = "Delete");

		AssertConfirm(value1 = "Are you sure you want to delete this? It will be deleted immediately.");

		Alert.viewSuccessMessage();

		KaleoFormsAdmin.viewProcessRecordTableRecordNotPresent(kfProcessFieldData = "${kfProcessFieldData}");
	}

	macro deleteViaDeleteIcon {
		ClickNoError(locator1 = "Icon#DELETE");

		AssertConfirm(value1 = "Are you sure you want to delete this? It will be deleted immediately.");

		Alert.viewSuccessMessage();
	}

	macro deleteWorkflowDraft {
		LexiconEntry.gotoEntryMenuItemNoError(
			menuItem = "Delete",
			rowEntry = "${workflowDefinitionTitle}");

		AssertConfirm(value1 = "Are you sure you want to delete this? It will be deleted immediately.");

		Alert.viewSuccessMessage();
	}

	macro editDescription {
		Pause(locator1 = "1000");

		Type(
			key_fieldName = "${fieldName}",
			locator1 = "KaleoFormsAdmin#DESCRIPTION_FIELD",
			value1 = "${fieldValue}");
	}

	macro editFieldSet {
		KaleoFormsAdmin.viewNewProcessProgressBar(
			kfFieldSetName = "${kfFieldSetName}",
			kfProgressName = "Fields",
			kfProgressStep = "2");

		LexiconEntry.gotoEntryMenuItem(
			menuItem = "Edit",
			rowEntry = "${kfFieldSetName}");

		SelectFrame(locator1 = "IFrame#MODAL_BODY");

		PortletEntry.inputName(name = "${kfFieldSetNameEdit}");
	}

	macro editFieldSetPagination {
		Click(locator1 = "KaleoFormsAdminEditProcess#FIELD_SET_PAGINATION_SELECTOR_BUTTON");

		var key_paginationNumber = "${paginationNumber}";

		MenuItem.click(menuItem = "${paginationNumber}");

		AssertTextEquals.assertPartialText(
			locator1 = "KaleoFormsAdminEditProcess#FIELD_SET_PAGINATION_SELECTOR_BUTTON",
			value1 = "${paginationNumber} Entries");
	}

	macro editForm {
		var key_kfFormName = "${kfFormName}";

		AssertClick(
			locator1 = "KaleoFormsAdminEditProcessForm#FORM_TABLE_NAME",
			value1 = "${kfFormName}");

		LexiconEntry.gotoEntryMenuItem(
			menuItem = "Edit Form",
			rowEntry = "${kfFormName}");

		SelectFrame(locator1 = "IFrame#MODAL_BODY");

		PortletEntry.inputName(name = "${kfFormNameEdit}");

		for (var field : list "Fieldset,Paragraph,Separator") {
			DynamicDataMapping.viewCanAddField(field = "${field}");
		}
	}

	macro editProcess {
		LexiconEntry.gotoEntryMenuItem(
			menuItem = "Edit",
			rowEntry = "${kfProcessName}");
	}

	macro editProcessDetails {
		KaleoFormsAdmin.viewNewProcessProgressBar(
			kfProgressName = "Details",
			kfProgressStep = "1");

		if (isSet(kfProcessNameEdit)) {
			var key_fieldName = "name";

			Type(
				locator1 = "FormFields#TEXT_FIELD",
				value1 = "${kfProcessNameEdit}");
		}

		if (isSet(kfProcessDescriptionEdit)) {
			KaleoFormsAdmin.editDescription(
				fieldName = "Description",
				fieldValue = "${kfProcessDescriptionEdit}");
		}

		if ("${kfPendingForm}" == "true") {
			AssertTextEquals(
				locator1 = "Message#INFO",
				value1 = "Updating the field set or workflow will cause loss of data.");
		}
	}

	macro editWorkflow {
		KaleoFormsAdmin.viewNewProcessProgressBar(
			kfProgressName = "Workflow",
			kfProgressStep = "3");

		LexiconEntry.gotoEntryMenuItem(
			menuItem = "Edit",
			rowEntry = "${workflowDefinitionTitle}");

		SelectFrame(locator1 = "IFrame#MODAL_BODY");

		PortletEntry.inputTitle(title = "${workflowDefinitionTitleEdit}");
	}

	macro editWorkflowDraft {
		var key_workflowDefinitionTitle = "${workflowDefinitionTitle}";

		AssertTextEquals(
			locator1 = "KaleoFormsAdminEditProcess#WORKFLOW_TABLE_TITLE",
			value1 = "${workflowDefinitionTitle}");

		WaitForLiferayEvent.initializeLiferayEventLog();

		LexiconEntry.gotoEntryMenuItem(
			menuItem = "Edit",
			rowEntry = "${workflowDefinitionTitle}");
	}

	macro exportRecords {
		LexiconEntry.gotoEntryMenuItem(
			menuItem = "Export",
			rowEntry = "${kfProcessName}");

		Select(
			locator1 = "KaleoFormsAdminViewProcessRecords#EXPORT_DIALOG_BOX_FILE_EXTENSION_SELECT",
			value1 = "${fileExtension}");

		if ("${approvedOnly}" == "true") {
			Check(locator1 = "KaleoFormsAdminViewProcessRecords#EXPORT_DIALOG_BOX_APPROVED_SUBMISSIONS_CHECKBOX");
		}

		AssertClick(
			locator1 = "KaleoFormsAdminViewProcessRecords#EXPORT_DIALOG_BOX_OK_BUTTON",
			value1 = "OK");

		DownloadTempFile();
	}

	macro gotoProgressStep {
		var key_kfProgressName = "${kfProgressName}";

		Click(locator1 = "KaleoFormsAdminEditProcess#FORM_NAVIGATOR_PROGRESS_BAR_NAME");
	}

	macro next {
		AssertElementNotPresent(locator1 = "KaleoFormsAdminEditProcess#NEXT_BUTTON_INACTIVE");

		AssertClick(
			locator1 = "Button#NEXT",
			value1 = "Next");
	}

	macro noProcessesWereFound. {
		AssertTextEquals(
			locator1 = "Message#EMPTY_INFO",
			value1 = "No processes were found.");
	}

	macro populateFieldDate {
		var key_fieldName = "${fieldName}";

		Type(
			locator1 = "TextInput#DDM",
			value1 = "${kfProcessFieldData}");

		Click(locator1 = "TextInput#DDM");
	}

	macro populateFieldSelect {
		var key_fieldName = "${fieldName}";

		Select(
			locator1 = "DDMField#SELECT",
			value1 = "${kfProcessFieldData}");
	}

	macro populateFieldText {
		var key_fieldName = "${fieldName}";

		Type(
			locator1 = "TextInput#DDM",
			value1 = "${kfProcessFieldData}");
	}

	macro populateFieldTextBox {
		var fieldName = StringUtil.replace("${fieldName}", " ", "_");
		var key_fieldName = "${fieldName}";

		Type(
			locator1 = "DDLEditRecord#TEXT_BOX_FIELD",
			value1 = "${kfProcessFieldData}");
	}

	macro publishWorkflow {
		AssertClick(
			key_text = "Publish",
			locator1 = "Button#ANY",
			value1 = "Publish");

		Alert.viewSuccessMessage();

		SelectFrame(value1 = "relative=top");

		if (IsElementPresent(locator1 = "IFrame#MODAL_BODY")) {
			Click(locator1 = "Icon#CLOSE");
		}

		WaitForLiferayEvent.initializeLiferayEventLog();
	}

	macro reactivateInactiveWorkflowDefinition {
		WaitForLiferayEvent.initializeLiferayEventLog();

		LexiconEntry.gotoEntryMenuItemNoError(
			menuItem = "Edit",
			rowEntry = "${workflowDefinitionTitle}");

		SelectFrame(locator1 = "IFrame#MODAL_BODY");

		KaleoFormsAdmin.publishWorkflow();
	}

	macro saveAndContinueForm {
		DynamicDataMapping.selectDynamicDataMappingFrame();

		AssertClick(
			locator1 = "Button#SAVE_AND_CONTINUE",
			value1 = "Save and Continue");

		Alert.viewSuccessMessage();
	}

	macro saveFieldSet {
		DDLDataDefinition.save();

		SelectFrame(value1 = "relative=top");

		if (IsElementPresent(locator1 = "IFrame#MODAL_BODY")) {
			Click(locator1 = "KaleoFormsAdmin#CLOSE_BUTTON");
		}

		WaitForLiferayEvent.initializeLiferayEventLog();
	}

	macro saveForm {
		DynamicDataMapping.selectDynamicDataMappingFrame();

		Workflow.saveDefinition();
	}

	macro saveFormEdit {
		DynamicDataMapping.selectDynamicDataMappingFrame();

		PortletEntry.save();

		SelectFrame(value1 = "relative=top");

		Click(locator1 = "Icon#CLOSE");

		var key_kfFormName = "${kfFormName}";

		AssertTextEquals(
			locator1 = "KaleoFormsAdminEditProcessForm#FORM_TABLE_NAME",
			value1 = "${kfFormName}");
	}

	macro saveProcess {
		KaleoFormsAdmin.viewNewProcessProgressBar(
			kfProgressName = "Forms",
			kfProgressStep = "4");

		if (IsElementNotPresent(key_text = "Save",locator1 = "Button#ANY")) {
			Refresh();
		}

		Button.clickSave();
	}

	macro saveProcessRecord {
		PortletEntry.save();
	}

	macro submitNewProcess {
		KaleoFormsAdmin.viewProcessRecord(kfProcessName = "${kfProcessName}");

		LexiconEntry.gotoAdd();
	}

	macro tearDownProcess {
		Navigator.openURL();

		ProductMenu.gotoPortlet(
			category = "Content &amp; Data",
			portlet = "Kaleo Forms Admin");

		while (IsElementPresent(locator1 = "Icon#BODY_VERTICAL_ELLIPSIS")) {
			KaleoFormsAdmin.deleteProcess();
		}

		KaleoFormsAdmin.noProcessesWereFound();
	}

	macro unassignForm {
		KaleoFormsAdmin.gotoProgressStep(kfProgressName = "Forms");

		LexiconEntry.gotoEntryMenuItem(
			menuItem = "Unassign Form",
			rowEntry = "${workflowTask}");
	}

	macro updateWorkflow {
		Click.mouseDownMouseUp(
			key_text = "Update",
			locator1 = "Button#ANY");

		SelectFrame(value1 = "relative=top");

		Click(locator1 = "Icon#CLOSE");
	}

	macro viewExportRecords {
		LexiconEntry.gotoEntryMenuItem(
			menuItem = "Export",
			rowEntry = "${kfProcessName}");

		AssertElementPresent(locator1 = "KaleoFormsAdminViewProcessRecords#EXPORT_TABLE_MODAL");

		AssertElementPresent(locator1 = "KaleoFormsAdminViewProcessRecords#EXPORT_DIALOG_BOX_FILE_EXTENSION_SELECT");
	}

	macro viewFieldRequired {
		if ("${progressTitle}" == "Details") {
			KaleoFormsAdmin.viewNewProcessProgressBar(
				kfProgressName = "${progressTitle}",
				kfProgressStep = "1");

			AssertElementPresent(locator1 = "KaleoFormsAdminEditProcess#FIELD_NAME_REQUIRED");

			AssertElementPresent(locator1 = "KaleoFormsAdminEditProcess#NEXT_BUTTON_INACTIVE");

			Type(
				key_fieldName = "name",
				locator1 = "FormFields#TEXT_FIELD",
				value1 = "${kfProcessName}");

			AssertElementPresent(locator1 = "Button#NEXT");
		}

		else if ("${progressTitle}" == "Fields") {
			KaleoFormsAdmin.viewNewProcessProgressBar(
				kfProgressName = "${progressTitle}",
				kfProgressStep = "2");

			AssertElementPresent(locator1 = "KaleoFormsAdminEditProcess#NEXT_BUTTON_INACTIVE");

			KaleoFormsAdmin.chooseFieldSet(
				kfFieldSetName = "${kfFieldSetName}",
				kfProcessName = "${kfProcessName}");

			AssertElementPresent(locator1 = "Button#NEXT");
		}

		else if ("${progressTitle}" == "Workflow") {
			KaleoFormsAdmin.viewNewProcessProgressBar(
				kfProgressName = "${progressTitle}",
				kfProgressStep = "3");

			AssertElementPresent(locator1 = "KaleoFormsAdminEditProcess#NEXT_BUTTON_INACTIVE");

			KaleoFormsAdmin.chooseWorkflow(
				kfProcessName = "${kfProcessName}",
				workflowDefinitionTitle = "${workflowDefinitionTitle}");

			AssertElementPresent(locator1 = "Button#NEXT");
		}

		else {
			KaleoFormsAdmin.viewNewProcessProgressBar(
				kfProgressName = "${progressTitle}",
				kfProgressStep = "4");

			AssertElementPresent(locator1 = "KaleoFormsAdminEditProcess#SAVE_BUTTON_INACTIVE");

			KaleoFormsAdmin.assignForm(
				kfProcessName = "${kfProcessName}",
				workflowTask = "${workflowTask}");

			KaleoFormsAdmin.addForm(kfFormName = "${kfFormName}");

			KaleoFormsAdmin.saveForm();

			KaleoFormsAdmin.chooseForm(kfFormName = "${kfFormName}");

			AssertElementPresent(locator1 = "Button#SAVE");

			KaleoFormsAdmin.saveProcess(kfProcessName = "${kfProcessName}");

			Alert.viewSuccessMessage();
		}
	}

	macro viewFormEdit {
		LexiconEntry.gotoEntryMenuItem(
			menuItem = "Edit Form",
			rowEntry = "${kfFormNameEdit}");

		SelectFrame(locator1 = "IFrame#MODAL_BODY");

		AssertTextEquals(
			locator1 = "DDMEditStructure#NAME_FIELD",
			value1 = "${kfFormNameEdit}");
	}

	macro viewFormsNotPresent {
		DynamicDataMapping.selectDynamicDataMappingFrame();

		var key_workflowTask = "${workflowTask}";

		AssertElementNotPresent(locator1 = "KaleoFormsAdminEditProcess#FORMS_TABLE_TASK");

		PortletEntry.viewEmptyMessage(entity = "Templates");

		SelectFrame(value1 = "relative=top");
	}

	macro viewInvalidDefinitionMessage {
		Alert.viewRequestFailedToComplete();

		AssertTextEquals.assertPartialText(
			locator1 = "Message#ERROR_1",
			value1 = "Please add at least one field.");
	}

	macro viewNewProcessProgressBar {
		var key_kfProgressName = "${kfProgressName}";

		AssertTextEquals(
			locator1 = "KaleoFormsAdminEditProcess#FORM_NAVIGATOR_PROGRESS_BAR_NAME",
			value1 = "${kfProgressName}");

		var key_kfProgressName = StringUtil.lowerCase("${kfProgressName}");

		AssertTextEquals(
			locator1 = "KaleoFormsAdminEditProcess#FORM_NAVIGATOR_PROGRESS_BAR_STEP",
			value1 = "${kfProgressStep}");
	}

	macro viewNextFieldSetPage {
		Click(locator1 = "Pagination#NEXT_LINK");
	}

	macro viewNoDuplicateForms {
		var key_kfFormName = "${kfFormName}";

		AssertTextEquals(
			locator1 = "KaleoFormsAdminEditProcessForm#FORM_TABLE_NAME",
			value1 = "${kfFormName}");

		AssertElementNotPresent(locator1 = "KaleoFormsAdminEditProcessForm#FORM_TABLE_NAME_2");
	}

	macro viewNoPublishedWorkflowDefinitions {
		AssertVisible(locator1 = "KaleoFormsAdmin#KALEO_WORKFLOW_PUBLISHED_EMPTY_MESSAGE");
	}

	macro viewNotSelectedFieldSet {
		AssertElementNotPresent(
			key_kfFieldSetName = "${kfFieldSetName}",
			locator1 = "KaleoFormsAdminEditProcess#SELECTED_FIELD_SET",
			value1 = "${key_kfFieldSetName}");
	}

	macro viewNotSelectedWorkflow {
		AssertTextNotEquals(
			locator1 = "KaleoFormsAdminEditProcess#SELECTED_WORKFLOW",
			value1 = "${workflowDefinitionTitle}");
	}

	macro viewNoUnpublishedWorkflowDefinitions {
		KaleoFormsAdmin.viewUnpublishedWorkflowDefinitions();

		AssertVisible(locator1 = "KaleoFormsAdmin#KALEO_WORKFLOW_UNPUBLISHED_EMPTY_MESSAGE");
	}

	macro viewPermissionsAndAssertModal {
		LexiconEntry.gotoEllipsisMenuItem(menuItem = "Permissions");

		AssertElementPresent(
			key_title = "Permissions",
			locator1 = "Header#H3_TITLE");

		AssertElementPresent(locator1 = "WorkflowDefinition#DEFINITION_TABLE_TITLE_PERMISSIONS");

		IFrame.selectPermissionsFrame();

		PortletEntry.save();

		Alert.viewSuccessMessage();
	}

	macro viewPreviousFieldSetPage {
		Click(locator1 = "Pagination#FIRST_LINK");
	}

	macro viewProcess {
		var key_kfProcessName = "${kfProcessName}";

		AssertTextEquals(
			locator1 = "KaleoFormsAdmin#PROCESS_TABLE_NAME",
			value1 = "${kfProcessName}");

		var key_rowEntry = "${kfProcessName}";

		AssertElementPresent(locator1 = "Icon#ROW_VERTICAL_ELLIPSIS");
	}

	macro viewProcessByPosition {
		AssertElementPresent(
			key_order = "${order}",
			key_title = "${title}",
			locator1 = "AssetWorkflow#DETAILS_WORKFLOW_TITLE_ORDER");
	}

	macro viewProcessDetails {
		KaleoFormsAdmin.editProcess(kfProcessName = "${kfProcessName}");

		KaleoFormsAdmin.viewNewProcessProgressBar(
			kfProgressName = "Details",
			kfProgressStep = "1");

		var key_fieldName = "name";

		AssertTextEquals(
			locator1 = "FormFields#TEXT_FIELD",
			value1 = "${kfProcessName}");

		var key_fieldName = "Description";

		AssertTextEquals(
			locator1 = "KaleoFormsAdmin#DESCRIPTION_FIELD",
			value1 = "${kfProcessDescription}");

		KaleoFormsAdmin.next();
	}

	macro viewProcessFieldSet {
		KaleoFormsAdmin.viewNewProcessProgressBar(
			kfProgressName = "Fields",
			kfProgressStep = "2");

		var key_kfFieldSetName = "${kfFieldSetName}";

		AssertTextEquals(
			locator1 = "KaleoFormsAdminEditProcess#SELECTED_FIELD_SET",
			value1 = "${kfFieldSetName}");

		KaleoFormsAdmin.next();
	}

	macro viewProcessForms {
		KaleoFormsAdmin.viewNewProcessProgressBar(
			kfProgressName = "Forms",
			kfProgressStep = "4");

		var key_kfFormName = "${kfFormName}";

		AssertTextEquals(
			locator1 = "KaleoFormsAdminEditProcessForm#FORM_TABLE_NAME",
			value1 = "${kfFormName}");

		AssertElementNotPresent(locator1 = "Button#SAVE_INACTIVE");

		AssertElementPresent(locator1 = "Button#SAVE");
	}

	macro viewProcessFormsDeleted {
		KaleoFormsAdmin.viewNewProcessProgressBar(
			kfProgressName = "Forms",
			kfProgressStep = "4");

		KaleoFormsAdmin.assignForm(
			kfPendingForm = "${kfPendingForm}",
			kfProcessName = "${kfProcessName}",
			workflowTask = "${workflowTask}");

		AssertElementNotPresent(
			key_kfFormName = "${kfFormName}",
			locator1 = "KaleoFormsAdminEditProcessForm#FORM_TABLE_NAME_LINK");
	}

	macro viewProcessNoRecords {
		AssertTextEquals(
			locator1 = "Message#EMPTY_INFO",
			value1 = "No ${kfProcessName} records were found");
	}

	macro viewProcessRecord {
		LexiconEntry.gotoEntryMenuItem(
			menuItem = "View",
			rowEntry = "${kfProcessName}");
	}

	macro viewProcessRecordDetails {
		LexiconEntry.gotoEntryMenuItem(
			menuItem = "View",
			rowEntry = "${kfProcessFieldData}");

		AssertTextEquals(
			locator1 = "AssetWorkflow#WORKFLOW_STATUS",
			value1 = "${kfStatus}");
	}

	macro viewProcessRecordDetailsField {
		var key_fieldName = "${fieldName}";
		var key_kfProcessFieldData = "${kfProcessFieldData}";

		AssertTextEquals.assertPartialText(
			locator1 = "KaleoFormsAdminViewProcessRecordsDetails#CURRENT_RECORD_FIELD",
			value1 = "${kfProcessFieldData}");
	}

	macro viewProcessRecordTable {
		var key_kfAuthor = "${kfAuthor}";

		AssertTextEquals(
			locator1 = "KaleoFormsAdminViewProcessRecords#RECORD_TABLE_AUTHOR",
			value1 = "${kfAuthor}");

		var key_kfProcessFieldData = "${kfProcessFieldData}";

		AssertTextEquals(
			locator1 = "KaleoFormsAdminViewProcessRecords#RECORD_TABLE_RECORD",
			value1 = "${kfProcessFieldData}");

		var key_kfStatus = "${kfStatus}";

		AssertTextEquals(
			locator1 = "KaleoFormsAdminViewProcessRecords#RECORD_TABLE_STATUS",
			value1 = "${kfStatus}");
	}

	macro viewProcessRecordTableRecordNotPresent {
		var key_kfProcessFieldData = "${kfProcessFieldData}";

		AssertElementNotPresent(locator1 = "KaleoFormsAdminViewProcessRecords#RECORD_TABLE_RECORD");
	}

	macro viewProcessWorkflow {
		KaleoFormsAdmin.viewNewProcessProgressBar(
			kfProgressName = "Workflow",
			kfProgressStep = "3");

		AssertTextEquals(
			locator1 = "KaleoFormsAdminEditProcess#SELECTED_WORKFLOW",
			value1 = "${workflowDefinitionTitle}");

		KaleoFormsAdmin.next();
	}

	macro viewProgressBarIsShownCorrectly {
		AssertElementPresent(
			key_iconType = "${iconType}",
			key_stepProcess = "${stepProcess}",
			locator1 = "KaleoFormsAdminEditProcess#FORM_NAVIGATOR_MULTI_STEP_PROGRESS_BAR");
	}

	macro viewPublishedWorkflowDefinitions {
		AssertClick(
			locator1 = "KaleoFormsAdminEditProcess#WORKFLOW_NAVIGATION_PUBLISHED_TAB",
			value1 = "Published");
	}

	macro viewSelectedFieldSet {
		var key_kfFieldSetName = "${kfFieldSetName}";

		AssertTextEquals(
			locator1 = "KaleoFormsAdminEditProcess#SELECTED_FIELD_SET",
			value1 = "${kfFieldSetName}");
	}

	macro viewSelectedLanguage {
		Click(
			key_locale = "${defaultLanguage}",
			locator1 = "Translation#CURRENT_LOCALE_NAME");

		AssertElementPresent(
			key_infoLaguage = "${infoLaguage}",
			key_tagLanguage = "${dropdownTagLanguage}",
			locator1 = "KaleoFormsAdminEditProcess#DROPDOWN_LANGUAGE");

		Click(
			key_locale = "${defaultLanguage}",
			locator1 = "Translation#CURRENT_LOCALE_DESCRIPTION");

		AssertElementPresent(
			key_infoLaguage = "${infoLaguage}",
			key_tagLanguage = "${dropdownTagLanguage}",
			locator1 = "KaleoFormsAdminEditProcess#DROPDOWN_LANGUAGE");
	}

	macro viewSelectedWorkflow {
		if ("${edit}" == "true") {
			AssertTextEquals(
				locator1 = "KaleoFormsAdminEditProcess#SELECTED_WORKFLOW",
				value1 = "${workflowDefinitionTitle}");
		}
		else {
			AssertTextEquals(
				locator1 = "KaleoFormsAdminEditProcess#SELECTED_WORKFLOW",
				value1 = "${workflowDefinitionTitle}");
		}
	}

	macro viewTableDisplaysColumns {
		for (var tableHeader : list "ID,Name,Description,Modified Date") {
			AssertElementPresent(
				key_tableHeader = "${tableHeader}",
				locator1 = "LexiconTable#TABLE_HEADER");
		}
	}

	macro viewTaskNotPresent {
		var key_workflowTask = "${workflowTask}";

		AssertElementNotPresent(locator1 = "KaleoFormsAdminEditProcess#FORMS_TABLE_TASK");
	}

	macro viewTaskPresent {
		var key_workflowTask = "${workflowTask}";

		AssertElementPresent(locator1 = "KaleoFormsAdminEditProcess#FORMS_TABLE_TASK");
	}

	macro viewUnpublishedWorkflowDefinitions {
		AssertClick(
			locator1 = "KaleoFormsAdminEditProcess#WORKFLOW_NAVIGATION_UNPUBLISHED_TAB",
			value1 = "Unpublished");
	}

	macro viewWorkflowCannotBeUnpublish {
		LexiconEntry.gotoEntryMenuItemNoError(
			menuItem = "Unpublish",
			rowEntry = "${workflowDefinitionTitle}");

		Alert.viewErrorMessage(errorMessage = "Unable to delete the workflow definition that is in use.");
	}

}