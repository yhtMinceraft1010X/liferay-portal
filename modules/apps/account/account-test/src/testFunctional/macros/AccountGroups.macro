definition {

	macro addAssignee {
		LexiconEntry.gotoAdd();

		Pause(locator1 = "3000");

		SelectFrame(
			key_title = "Assign Accounts to ${accountGroupName}",
			locator1 = "IFrame#MODAL_ANY");

		if (isSet(assigneeAll)) {
			PortletEntry.selectAll();
		}
		else {
			for (var assigneeName : list "${assigneeNames}") {
				Search.searchCP(searchTerm = "${assigneeName}");

				Check(
					key_tableEntry = "${assigneeName}",
					locator1 = "LexiconTable#TABLE_ENTRY_CHECKBOX");
			}
		}

		SelectFrameTop();

		Button.click(button = "Assign");

		Alert.viewSuccessMessage();
	}

	macro addCP {
		LexiconEntry.gotoAdd();

		AssertTextEquals(
			locator1 = "Portlet#HEADER",
			value1 = "Add Account Group");

		PortletEntry.inputName(name = "${accountGroupName}");

		if (isSet(accountGroupDescription)) {
			PortletEntry.inputDescription(description = "${accountGroupDescription}");
		}

		Button.clickSave();

		Alert.viewSuccessMessage();
	}

	macro editCP {
		AccountGroups.openAccountGroupsAdmin();

		Search.searchCP(searchTerm = "${accountGroupName}");

		LexiconEntry.gotoEntryMenuItem(
			menuItem = "Edit",
			rowEntry = "${accountGroupName}");

		AssertTextEquals(
			locator1 = "Portlet#HEADER",
			value1 = "Edit ${accountGroupName}");

		PortletEntry.inputName(name = "${accountGroupNameEdit}");

		if (isSet(accountGroupDescriptionEdit)) {
			PortletEntry.inputDescription(description = "${accountGroupDescriptionEdit}");
		}

		Button.clickSave();

		Alert.viewSuccessMessage();

		AssertTextEquals(
			locator1 = "Portlet#HEADER",
			value1 = "Edit ${accountGroupNameEdit}");
	}

	macro openAccountGroupsAdmin {
		Navigator.openWithAppendToBaseURL(urlAppend = "group/control_panel/manage?p_p_id=com_liferay_account_admin_web_internal_portlet_AccountGroupsAdminPortlet");
	}

	macro removeAccount {
		Search.searchCP(searchTerm = "${accountGroupName}");

		if ("${removeType}" == "Toolbar Remove Icon") {
			LexiconEntry.selectEntry(key_rowEntry = "${accountGroupName}");

			ClickNoError.clickNoMouseOver(locator1 = "ManagementBar#REMOVE");

			AssertConfirm(value1 = "Are you sure you want to remove the selected accounts?");

			Alert.viewSuccessMessage();
		}
		else if ("${removeType}" == "Management Toolbar") {
			LexiconEntry.selectEntry(key_rowEntry = "${accountGroupName}");

			Click(locator1 = "Icon#TOOLBAR_ELLIPSIS");

			MenuItem.clickNoError(menuItem = "Remove");

			AssertConfirm(value1 = "Are you sure you want to remove the selected accounts?");

			Alert.viewSuccessMessage();
		}
		else {
			ClickNoError(
				key_entryName = "${accountGroupName}",
				locator1 = "LexiconTable#TABLE_ENTRY_DELETE");

			AssertConfirm(value1 = "Are you sure you want to remove this account?");

			Alert.viewSuccessMessage();
		}

		LexiconEntry.viewNoEntry(rowEntry = "${accountGroupName}");
	}

	macro viewAssignee {
		AssertElementPresent(
			key_tab = "Accounts",
			locator1 = "NavTab#ACTIVE_TAB_LINK");

		AssertTextEquals.assertPartialText(
			key_tableEntry = "${assigneeName}",
			locator1 = "LexiconTable#TABLE_ENTRY",
			value1 = "${assigneeName}");
	}

	macro viewDefault {
		if (IsElementPresent(key_tab = "${tabName}", locator1 = "NavTab#ACTIVE_TAB_LINK")) {
			AssertElementPresent(
				key_tab = "${tabName}",
				locator1 = "NavTab#ACTIVE_TAB_LINK");
		}

		var tabName = StringUtil.toLowerCase("${tabName}");

		AssertTextEquals(
			locator1 = "Message#EMPTY_INFO",
			value1 = "No ${tabName} were found.");
	}

	macro viewEntry {
		LexiconEntry.viewEntryName(rowEntry = "${accountGroupName}");

		if (isSet(accountGroupDescription)) {
			AssertTextEquals(
				key_rowEntry = "${accountGroupName}",
				locator1 = "ContentRow#ENTRY_CONTENT_ENTRY_DESCRIPTION_TD3",
				value1 = "${accountGroupDescription}");
		}

		if (isSet(accountGroupAccount)) {
			AssertTextEquals(
				key_columnName = "lfr-accounts-column",
				key_rowEntry = "${accountGroupName}",
				locator1 = "ContentRow#ENTRY_CONTENT_ROW_NUMBER_TD",
				value1 = "${accountGroupAccount}");
		}
	}

}