definition {

	macro _addAccountEntry {
		Variables.assertDefined(parameterList = "${accountEntryName},${accountEntryType},${accountEntryStatus}");

		var companyId = JSONCompany.getCompanyId();

		var portalURL = JSONCompany.getPortalURL();

		var userId = JSONUserSetter.setUserId(userEmailAddress = "${creatorEmailAddress}");

		var curl = '''
			${portalURL}/api/jsonws/account.accountentry/add-account-entry \
				-u ${creatorEmailAddress}:${creatorPassword} \
				-d userId=${userId} \
				-d parentAccountEntryId=0 \
				-d name=${accountEntryName} \
				-d description=${accountEntryDescription} \
				-d domains= \
				-d email= \
				-d logoBytes= \
				-d taxIdNumber= \
				-d type=${accountEntryType} \
				-d status=${accountEntryStatus}
		''';

		com.liferay.poshi.runner.util.JSONCurlUtil.post("${curl}");
	}

	@summary = "Get the 'accountEntryId' from '${accountEntryName}'"
	macro _getAccountEntryId {
		Variables.assertDefined(parameterList = "${accountEntryName}");

		if (!(isSet(creatorEmailAddress))) {
			var creatorEmailAddress = "test@liferay.com";
		}

		if (!(isSet(creatorPassword))) {
			var creatorPassword = "test";
		}

		var companyId = JSONCompany.getCompanyId();

		var portalURL = JSONCompany.getPortalURL();

		var userId = JSONUserSetter.setUserId(userEmailAddress = "${creatorEmailAddress}");

		var curl = '''
			${portalURL}/api/jsonws/account.accountentry/get-account-entries \
				-u ${creatorEmailAddress}:${creatorPassword} \
				-d companyId=${companyId} \
				-d status=0 \
				-d <CURL_DATA[start=-1]CURL_DATA> \
				-d <CURL_DATA[end=-1]CURL_DATA> \
				-d -orderByComparator=
		''';
		var accountEntryId = JSONCurlUtil.post("${curl}", "$.[?(@['name'] == '${accountEntryName}')]['accountEntryId']");

		echo("curl: ${curl}");

		echo("accountEntryId: ${accountEntryId}");

		if ("${accountEntryId}" == "") {
			fail("FAIL. Cannot find account entry.");
		}
		else {
			return "${accountEntryId}";
		}
	}

	macro _setAccountExternalReferenceCode {
		Variables.assertDefined(parameterList = "${accountEntryName},${externalReferenceCode}");

		var portalURL = JSONCompany.getPortalURL();

		var accountEntryId = JSONAccountEntryAPI._getAccountEntryId(accountEntryName = "${accountEntryName}");

		var curl = '''
			${portalURL}/api/jsonws/account.accountentry/update-external-reference-code \
				-u test@liferay.com:test \
  				-d accountEntryId=${accountEntryId} \
  				-d externalReferenceCode=${externalReferenceCode}
		''';

		com.liferay.poshi.runner.util.JSONCurlUtil.post("${curl}");
	}

}