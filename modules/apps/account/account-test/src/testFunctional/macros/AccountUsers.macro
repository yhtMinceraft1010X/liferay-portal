definition {

	macro addUser {
		LexiconEntry.gotoAdd();

		Pause(locator1 = "3000");

		SelectFrame(locator1 = "IFrame#SELECT_ACCOUNT");

		AssertClick(
			key_accountName = "${accountName}",
			locator1 = "AccountUsers#SELECT_AN_ACCOUNT_CHOOSE_BUTTON",
			value1 = "Choose");

		SelectFrame(value1 = "relative=top");

		WaitForElementPresent(
			key_accountName = "${accountName}",
			locator1 = "AccountUsers#ADD_NEW_USER_HEADER");

		Type(
			locator1 = "UsersAndOrganizationsEditUser#DETAILS_SCREEN_NAME_FIELD",
			value1 = "${userScreenName}");

		Type(
			locator1 = "UsersAndOrganizationsEditUser#DETAILS_EMAIL_ADDRESS_FIELD",
			value1 = "${userEmailAddress}");

		Type(
			locator1 = "UsersAndOrganizationsEditUser#DETAILS_FIRST_NAME_FIELD",
			value1 = "${userFirstName}");

		Type(
			locator1 = "UsersAndOrganizationsEditUser#DETAILS_LAST_NAME_FIELD",
			value1 = "${userLastName}");

		AssertClick(
			locator1 = "Button#SAVE",
			value1 = "Save");

		Alert.viewSuccessMessage();
	}

	macro deactivateUser {
		Search.searchCP(searchTerm = "${userFirstName}");

		LexiconEntry.gotoEntryMenuItemNoError(
			menuItem = "Deactivate",
			rowEntry = "${userFirstName}");

		AssertConfirm(value1 = "Are you sure you want to deactivate this user?");

		Alert.viewSuccessMessage();
	}

	macro editUser {
		if ("${accountManagementWidget}" == "true") {
			LexiconEntry.gotoEntry(rowEntry = "${accountUser}");
		}

		else {
			LexiconEntry.gotoEntryMenuItem(
				menuItem = "Edit",
				rowEntry = "${accountUser}");

			AssertTextEquals.assertPartialText(
				locator1 = "Portlet#HEADER",
				value1 = "Edit User");
		}

		if (isSet(editScreenName)) {
			Type(
				locator1 = "UsersAndOrganizationsEditUser#DETAILS_SCREEN_NAME_FIELD",
				value1 = "${editScreenName}");
		}

		if (isSet(editEmailAddress)) {
			Type(
				locator1 = "UsersAndOrganizationsEditUser#DETAILS_EMAIL_ADDRESS_FIELD",
				value1 = "${editEmailAddress}");
		}

		if (isSet(editFirstName)) {
			Type(
				locator1 = "UsersAndOrganizationsEditUser#DETAILS_FIRST_NAME_FIELD",
				value1 = "${editFirstName}");
		}

		Button.clickSave();

		if (IsElementPresent(locator1 = "IFrame#CONFIRM_PASSWORD")) {
			User.confirmPassword(password = "${password}");
		}

		Alert.viewSuccessMessage();
	}

	macro openAccountUsersAdmin {
		Navigator.openWithAppendToBaseURL(urlAppend = "group/control_panel/manage?p_p_id=com_liferay_account_admin_web_internal_portlet_AccountUsersAdminPortlet");
	}

	macro removeAccount {
		LexiconEntry.gotoEntry(rowEntry = "${accountUser}");

		Navigator.gotoNavUnderline(navUnderline = "General");

		Navigator.gotoNavNested(navNested = "Accounts");

		LexiconEntry.viewEntryName(rowEntry = "${accountName}");

		Click(
			key_entryName = "${accountName}",
			locator1 = "LexiconTable#TABLE_ENTRY_DELETE");

		PortletEntry.save();

		LexiconEntry.viewNoEntry(rowEntry = "${accountName}");
	}

	macro selectAccounts {
		SelectFrame(locator1 = "IFrame#SELECT_ACCOUNTS");

		Check(
			key_tableEntry = "${accountName}",
			locator1 = "LexiconTable#TABLE_ENTRY_CHECKBOX");

		SelectFrame(value1 = "relative=top");

		Button.clickSelect();

		AssertElementPresent(locator1 = "Search#SEARCH_INFO");
	}

	macro setUserPassword {
		LexiconEntry.gotoEntryMenuItem(
			menuItem = "Edit",
			rowEntry = "${accountUser}");

		AssertTextEquals.assertPartialText(
			locator1 = "Portlet#HEADER",
			value1 = "Edit User");

		UserNavigator.gotoPassword();

		Type(
			locator1 = "TextInput#NEW_PASSWORD",
			value1 = "${accountUserPassword}");

		Type(
			locator1 = "TextInput#ENTER_AGAIN",
			value1 = "${accountUserPassword}");

		Button.clickSave();

		Alert.viewSuccessMessage();
	}

	macro viewEntryAccounts {
		AssertElementPresent(
			key_columnName = "lfr-accounts-column",
			key_rowEntry = "${accountUserName}",
			locator1 = "ContentRow#ENTRY_CONTENT_ROW_NUMBER_TD",
			value1 = "${accountName}");
	}

	macro viewValidDomains {
		WaitForLiferayEvent.initializeLiferayEventLog();

		AssertClick(
			key_text = "View Valid Domains",
			locator1 = "Button#ANY",
			value1 = "View Valid Domains");

		Pause(locator1 = "3000");

		SelectFrame(locator1 = "IFrame#VALID_DOMAINS");

		AssertTextEquals(
			locator1 = "Message#INFO",
			value1 = "Info:The following email domains are shared across all of this user's accounts.");

		for (var validDomainName : list "${validDomainNames}") {
			LexiconEntry.viewEntryName(rowEntry = "${validDomainName}");
		}

		SelectFrame.selectFrameTop();

		Click(locator1 = "Icon#CLOSE");

		SelectFrame.selectFrameTop();
	}

}