@component-name = "portal-document-management"
definition {

	property portal.release = "true";
	property portal.upstream = "true";
	property testray.main.component.name = "Depot";

	setUp {
		TestCase.setUpPortalInstance();

		User.firstLoginPG();

		JSONGroup.addGroup(groupName = "Site Name");

		JSONDepot.addDepot(
			depotDescription = "This is the description of a depot",
			depotName = "Test Depot Name");
	}

	tearDown {
		var testPortalInstance = PropsUtil.get("test.portal.instance");

		if ("${testPortalInstance}" == "true") {
			PortalInstances.tearDownCP();
		}
		else {
			JSONDepot.deleteDepot(depotName = "Test Depot Name");

			Site.tearDownCP();

			User.logoutPG();
		}
	}

	@description = "This ensures that a user can select depot web content from different repositories through WCD on a connected site."
	@priority = "4"
	@refactordone
	test CanChangeWCDSelection {
		JSONLayout.addPublicLayout(
			groupName = "Site Name",
			layoutName = "Test New Page Name");

		JSONLayout.addWidgetToPublicLayout(
			groupName = "Site Name",
			layoutName = "Test New Page Name",
			widgetName = "Web Content Display");

		JSONWebcontent.addWebContent(
			content = "WC WebContent Depot 1",
			groupName = "Test Depot Name",
			site = "false",
			title = "WC WebContent Title 1");

		JSONDepot.addDepot(
			depotDescription = "This is the description of a depot",
			depotName = "Test Depot Name 2");

		JSONWebcontent.addWebContent(
			content = "WC WebContent Depot 2",
			groupName = "Test Depot Name 2",
			site = "false",
			title = "WC WebContent Title 2");

		for (var depotName : list "Test Depot Name,Test Depot Name 2") {
			JSONDepot.connectSite(
				depotName = "${depotName}",
				groupName = "Site Name");
		}

		Navigator.openSitePage(
			pageName = "Test New Page Name",
			siteName = "Site Name");

		WebContentDisplayPortlet.selectWebContent(
			depotName = "Test Depot Name",
			webContentTitle = "WC WebContent Title 1");

		Navigator.openSitePage(
			pageName = "Test New Page Name",
			siteName = "Site Name");

		WebContentDisplayPortlet.changeWebContent(
			depotName = "Test Depot Name 2",
			webContentTitle = "WC WebContent Title 2");

		Navigator.openSitePage(
			pageName = "Test New Page Name",
			siteName = "Site Name");

		WebContent.viewPGViaWCD(webContentContent = "WC WebContent Depot 2");
	}

	@description = "This ensures that web content using a structure stored in AL can be removed on a connected site."
	@priority = "4"
	@refactordone
	test CanRemoveCustomizedWC {
		DepotNavigator.openDepotWebContentStructuresAdmin(depotName = "Test Depot Name");

		WebContentStructures.addCP(
			structureDescription = "WC Structure Description",
			structureName = "WC Structure Name");

		DataEngine.addField(
			fieldFieldLabel = "Text",
			fieldName = "Text");

		WebContentStructures.saveCP(structureName = "WC Structure Name");

		DepotNavigator.openDepotWebContentTemplatesAdmin(depotName = "Test Depot Name");

		WebContentTemplates.addCP(
			structureName = "WC Structure Name",
			templateDescription = "WC Template Description",
			templateFieldNames = "Text",
			templateName = "WC Template Name");

		JSONDepot.connectSite(
			depotName = "Test Depot Name",
			groupName = "Site Name");

		DepotNavigator.openDepotAdmin();

		Depot.configureDDMStructures(
			depotName = "Test Depot Name",
			disconnectDisabled = "true",
			siteName = "Site Name",
			tableEntryText = "Yes");

		WebContentNavigator.openWebContentAdmin(siteURLKey = "site-name");

		WebContentNavigator.gotoAddWithStructureCP(structureName = "WC Structure Name");

		WebContent.addWithStructureCP(
			structureName = "WC Structure Name",
			templateName = "WC Template Name",
			webContentText = "Text",
			webContentTitle = "Web Content Title");

		PortletEntry.publish();

		DepotNavigator.openDepotWebContentAdmin(depotName = "Test Depot Name");

		WebContentStructures.deleteCP(
			structureName = "WC Structure Name",
			webContentHasReference = "true");

		WebContentNavigator.openWebContentAdmin(siteURLKey = "site-name");

		WebContent.deleteCP(
			siteURLKey = "site-name",
			webContentTitle = "Web Content Title");
	}

	@description = "This test covers LPS-117063 and LPS-117068. It ensures that web content using a structure stored in AL can be selected without NPE throwing and removed from the disconnected site."
	@priority = "4"
	@refactordone
	test CanRemoveCustomizedWCFromDisconnectedSite {
		JSONDepot.addDepot(
			depotDescription = "This is the description of a depot",
			depotName = "Test Remove Customized WC Depot Name");

		JSONLayout.addPublicLayout(
			groupName = "Site Name",
			layoutName = "Page Name");

		DepotNavigator.openDepotWebContentStructuresAdmin(depotName = "Test Remove Customized WC Depot Name");

		WebContentStructures.addCP(
			structureDescription = "WC Structure Description",
			structureName = "WC Structure Name");

		DataEngine.addField(
			fieldFieldLabel = "Text",
			fieldName = "Text");

		WebContentStructures.saveCP(structureName = "WC Structure Name");

		DepotNavigator.openDepotWebContentTemplatesAdmin(depotName = "Test Remove Customized WC Depot Name");

		WebContentTemplates.addCP(
			structureName = "WC Structure Name",
			templateDescription = "WC Template Description",
			templateFieldNames = "Text",
			templateName = "WC Template Name");

		JSONDepot.connectSite(
			depotName = "Test Remove Customized WC Depot Name",
			groupName = "Site Name");

		DepotNavigator.openDepotAdmin();

		Depot.configureDDMStructures(
			depotName = "Test Remove Customized WC Depot Name",
			siteName = "Site Name",
			tableEntryText = "Yes");

		WebContentNavigator.openWebContentAdmin(siteURLKey = "site-name");

		WebContentNavigator.gotoAddWithStructureCP(structureName = "WC Structure Name");

		WebContent.addWithStructureCP(
			structureName = "WC Structure Name",
			templateName = "WC Template Name",
			webContentText = "Text",
			webContentTitle = "Web Content Title");

		PortletEntry.publish();

		DepotNavigator.openDepotAdmin();

		Depot.configureDDMStructures(
			depotName = "Test Remove Customized WC Depot Name",
			makeWCUnavailable = "true",
			siteName = "Site Name",
			tableEntryText = "No");

		JSONDepot.disconnectSite(groupName = "Site Name");

		JSONLayout.addWidgetToPublicLayout(
			groupName = "Site Name",
			layoutName = "Page Name",
			widgetName = "Web Content Display");

		Navigator.openSitePage(
			pageName = "Page Name",
			siteName = "Site Name");

		WebContentDisplayPortlet.selectWebContent(webContentTitle = "Web Content Title");

		AssertConsoleTextNotPresent(value1 = "java.lang.NullPointerException");

		WebContentNavigator.openWebContentAdmin(siteURLKey = "site-name");

		WebContent.deleteCP(
			siteURLKey = "site-name",
			webContentTitle = "Web Content Title");
	}

	@description = "This test covers LPS-118395. It ensures that a web content in a depot can be selected through the page template."
	@priority = "4"
	@refactordone
	test CanSelectDepotContentViaPageTemplate {
		JSONWebcontent.addWebContent(
			content = "WC WebContent Content",
			groupName = "Test Depot Name",
			site = "false",
			title = "WC Depot WebContent");

		JSONLayoutpagetemplate.addLayoutPageTemplateCollection(
			groupName = "Site Name",
			layoutPageTemplateCollectionName = "Page Template Collection");

		JSONLayoutpagetemplate.addLayoutPageTemplateEntry(
			groupName = "Site Name",
			layoutPageTemplateCollectionName = "Page Template Collection",
			layoutPageTemplateEntryName = "Page Template Name",
			layoutPageTemplateStatus = "Approved",
			layoutPageTemplateType = "Widget Page Template");

		JSONDepot.connectSite(
			depotName = "Test Depot Name",
			groupName = "Site Name");

		PageTemplatesAdmin.openPageTemplatesAdmin(siteURLKey = "site-name");

		Navigator.gotoNavNested(navNested = "Page Template Collection");

		PageTemplatesAdmin.gotoPageTemplate(pageTemplateName = "Page Template Name");

		Portlet.addPG(portletName = "Web Content Display");

		WebContentDisplayPortlet.selectWebContent(
			depotName = "Test Depot Name",
			webContentTitle = "WC Depot WebContent");

		SitePages.openPagesAdmin(siteURLKey = "site-name");

		SitePages.addPublicPage(
			collectionName = "Page Template Collection",
			pageName = "Page Name",
			pageTemplateName = "Page Template Name");

		Navigator.openSitePage(
			pageName = "Page Name",
			siteName = "Site Name");

		WebContent.viewPGViaWCD(webContentContent = "WC WebContent Content");
	}

	@description = "This test covers LPS-105119. It ensures that a web content in a depot can be selected through the field."
	@priority = "4"
	@refactordone
	test CanSelectDepotContentViaWebContentField {
		JSONLayout.addPublicLayout(
			groupName = "Site Name",
			layoutName = "Page Name");

		JSONWebcontent.addWebContent(
			content = "WC WebContent Content",
			groupName = "Test Depot Name",
			site = "false",
			title = "WC Depot WebContent");

		JSONDepot.connectSite(
			depotName = "Test Depot Name",
			groupName = "Site Name");

		WebContentStructures.openWebContentStructuresAdmin(siteURLKey = "site-name");

		WebContentStructures.addCP(
			structureDescription = "WC Structure Description",
			structureName = "WC Structure Name");

		DataEngine.addField(
			fieldFieldLabel = "Web Content",
			fieldName = "Web Content");

		WebContentStructures.saveCP(structureName = "WC Structure Name");

		WebContentNavigator.openWebContentTemplatesAdmin(siteURLKey = "site-name");

		LexiconEntry.gotoAdd();

		WebContentTemplates.inputTemplateAddScreenFields(
			structureName = "WC Structure Name",
			templateDescription = "WC Template Description",
			templateFieldNames = "Web Content",
			templateName = "WC Template Name");

		DDLDataDefinition.save();

		WebContentNavigator.openWebContentAdmin(siteURLKey = "site-name");

		WebContentNavigator.gotoAddWithStructureCP(structureName = "WC Structure Name");

		WebContent.addWithStructureCP(
			depotName = "Test Depot Name",
			fieldName = "WebContent",
			structureName = "WC Structure Name",
			templateName = "WC Template Name",
			webContentTitle = "WC WebContent Title",
			webContentWebContent = "WC Depot WebContent");

		PortletEntry.publish();

		JSONLayout.addWidgetToPublicLayout(
			groupName = "Site Name",
			layoutName = "Page Name",
			widgetName = "Web Content Display");

		Navigator.openSitePage(
			pageName = "Page Name",
			siteName = "Site Name");

		WebContentDisplayPortlet.selectWebContent(webContentTitle = "WC WebContent Title");

		Navigator.openSitePage(
			pageName = "Page Name",
			siteName = "Site Name");

		WebContent.viewPGViaWCD(webContentContent = "WC Depot WebContent");
	}

	@description = "This ensures that a depot web content can be selected through the fragment in a content page on a connected site."
	@priority = "5"
	@refactordone
	test CanSelectFromFragmentInContentPage {
		JSONDepot.addDepot(
			depotDescription = "This is the description of a depot",
			depotName = "Test Fragment Depot Name");

		JSONWebcontent.addWebContent(
			content = "WC WebContent Depot",
			groupName = "Test Fragment Depot Name",
			site = "false",
			title = "WC WebContent Title");

		JSONDepot.connectSite(
			depotName = "Test Fragment Depot Name",
			groupName = "Site Name");

		SitePages.openPagesAdmin(siteURLKey = "site-name");

		ContentPages.addPage(pageName = "Test Content Page Name");

		PageEditor.addFragment(
			collectionName = "Content Display",
			fragmentName = "Content Display");

		PageEditor.editContentDisplay(
			depotName = "Test Fragment Depot Name",
			fragmentName = "Content Display",
			webcontent = "true",
			webContentContent = "WC WebContent Depot",
			webContentTitle = "WC WebContent Title");

		PageEditor.clickPublish();

		Navigator.openWithAppendToBaseURL(urlAppend = "web/site-name/test-content-page-name");

		AssertTextEquals.assertPartialTextAceEditor(
			locator1 = "PageEditor#CONTENT_PAGE_WEB_CONTENT_CONTENT",
			value1 = "WC WebContent Depot");
	}

	@description = "This ensures that a depot web content can be selected through the fragment in a display page template on a connected site."
	@priority = "5"
	@refactordone
	test CanSelectFromFragmentInDisplayPageTemplate {
		JSONWebcontent.addWebContent(
			content = "WC WebContent Depot",
			groupName = "Test Depot Name",
			site = "false",
			title = "WC WebContent Title");

		JSONDepot.connectSite(
			depotName = "Test Depot Name",
			groupName = "Site Name");

		DisplayPageTemplatesAdmin.openDisplayPagesAdmin(siteURLKey = "site-name");

		DisplayPageTemplatesAdmin.addDisplayPage(
			contentType = "Web Content Article",
			displayPageName = "Display Page Name",
			subType = "Basic Web Content");

		PageEditor.addFragment(
			collectionName = "Content Display",
			fragmentName = "Content Display");

		PageEditor.editContentDisplay(
			depotName = "Test Depot Name",
			fragmentName = "Content Display",
			webcontent = "true",
			webContentContent = "WC WebContent Depot",
			webContentTitle = "WC WebContent Title");

		PageEditor.clickPublish();

		WebContentNavigator.openWebContentAdmin(siteURLKey = "site-name");

		WebContentNavigator.gotoAddCP();

		WebContent.addCP(
			webContentContent = "Webcontent Content",
			webContentTitle = "Webcontent Title");

		WebContent.editDisplayPage(
			pageName = "Display Page Name",
			pageType = "Display Page Templates");

		PortletEntry.publish();

		Navigator.openWithAppendToBaseURL(urlAppend = "web/site-name/w/webcontent-title");

		AssertTextEquals.assertPartialTextAceEditor(
			locator1 = "PageEditor#CONTENT_PAGE_WEB_CONTENT_CONTENT",
			value1 = "WC WebContent Depot");
	}

	@description = "This ensures that a depot web content can be selected through WCD on a connected site."
	@priority = "5"
	@refactordone
	test CanSelectFromWCD {
		JSONWebcontent.addWebContent(
			content = "WC WebContent Depot",
			groupName = "Test Depot Name",
			site = "false",
			title = "WC WebContent Title");

		JSONDepot.connectSite(
			depotName = "Test Depot Name",
			groupName = "Site Name");

		JSONLayout.addPublicLayout(
			groupName = "Site Name",
			layoutName = "Test Page");

		JSONLayout.addWidgetToPublicLayout(
			groupName = "Site Name",
			layoutName = "Test Page",
			widgetName = "Web Content Display");

		Navigator.openSitePage(
			pageName = "Test Page",
			siteName = "Site Name");

		WebContentDisplayPortlet.selectWebContent(
			depotName = "Test Depot Name",
			webContentTitle = "WC WebContent Title");

		Navigator.openSitePage(
			pageName = "Test Page",
			siteName = "Site Name");

		WebContent.viewPGViaWCD(webContentContent = "WC WebContent Depot");
	}

	@description = "This ensures that a depot web content can be selected through WCD in a content page on a connected site."
	@priority = "4"
	@refactordone
	test CanSelectFromWidgetInContentPage {
		JSONWebcontent.addWebContent(
			content = "WC WebContent Depot",
			groupName = "Test Depot Name",
			site = "false",
			title = "WC WebContent Title");

		JSONDepot.connectSite(
			depotName = "Test Depot Name",
			groupName = "Site Name");

		SitePages.openPagesAdmin(siteURLKey = "site-name");

		ContentPages.addPage(pageName = "Test Content Page Name");

		PageEditor.addWidget(portletName = "Web Content Display");

		WebContentDisplayPortlet.selectWebContent(
			contentPage = "true",
			depotName = "Test Depot Name",
			webContentTitle = "WC WebContent Title");

		PageEditor.clickPublish();

		Navigator.openWithAppendToBaseURL(urlAppend = "web/site-name/test-content-page-name");

		WebContent.viewPGViaWCD(webContentContent = "WC WebContent Depot");
	}

	@description = "This ensures that a depot web content can be selected through WCD in a display page template on a connected site."
	@priority = "4"
	@refactordone
	test CanSelectFromWidgetInDisplayPageTemplate {
		JSONWebcontent.addWebContent(
			content = "WC WebContent Depot",
			groupName = "Test Depot Name",
			site = "false",
			title = "WC WebContent Title");

		JSONDepot.connectSite(
			depotName = "Test Depot Name",
			groupName = "Site Name");

		DisplayPageTemplatesAdmin.openDisplayPagesAdmin(siteURLKey = "site-name");

		DisplayPageTemplatesAdmin.addDisplayPage(
			contentType = "Web Content Article",
			displayPageName = "Display Page Name",
			subType = "Basic Web Content");

		PageEditor.addWidget(portletName = "Web Content Display");

		WebContentDisplayPortlet.selectWebContent(
			contentPage = "true",
			depotName = "Test Depot Name",
			webContentTitle = "WC WebContent Title");

		Refresh();

		WebContent.viewPGViaWCD(webContentContent = "WC WebContent Depot");
	}

	@description = "This ensures that a user can use a structure stored in AL to create a template on a connected site."
	@priority = "5"
	@refactordone
	test CanSelectStructureInDepotViaTemplateOnSite {
		JSONDepot.connectSite(
			depotName = "Test Depot Name",
			groupName = "Site Name");

		DepotNavigator.openDepotAdmin();

		Depot.configureDDMStructures(
			depotName = "Test Depot Name",
			siteName = "Site Name",
			tableEntryText = "Yes");

		DepotNavigator.openDepotWebContentStructuresAdmin(depotName = "Test Depot Name");

		WebContentStructures.addCP(
			structureDescription = "WC Structure Description",
			structureName = "WC Structure Name");

		DataEngine.addField(
			fieldFieldLabel = "Text",
			fieldName = "Text");

		WebContentStructures.saveCP(structureName = "WC Structure Name");

		WebContentNavigator.openWebContentTemplatesAdmin(siteURLKey = "site-name");

		WebContentTemplates.addCP(
			structureName = "WC Structure Name",
			templateDescription = "WC Template Description",
			templateName = "WC Template Name");
	}

	@description = "This ensures that a user can use structures stored in AL to create articles and display them in AP on a connected site."
	@priority = "5"
	@refactordone
	test CanViewCustomizedWCInAP {
		JSONLayout.addPublicLayout(
			groupName = "Site Name",
			layoutName = "Page Name");

		for (var count : list "1,2") {
			JSONDepot.addDepot(
				depotDescription = "This is the description of a depot",
				depotName = "Test Depot Name${count}");
		}

		for (var count : list "1,2") {
			DepotNavigator.openDepotWebContentStructuresAdmin(depotName = "Test Depot Name${count}");

			WebContentStructures.addCP(
				structureDescription = "WC Structure Description",
				structureName = "WC Structure Name${count}");

			DataEngine.addField(
				fieldFieldLabel = "Text",
				fieldName = "Text");

			WebContentStructures.saveCP(structureName = "WC Structure Name${count}");

			DepotNavigator.openDepotWebContentTemplatesAdmin(depotName = "Test Depot Name${count}");

			WebContentTemplates.addCP(
				structureName = "WC Structure Name${count}",
				templateDescription = "WC Template Description",
				templateFieldNames = "Text",
				templateName = "WC Template Name${count}");
		}

		for (var count : list "1,2") {
			JSONDepot.connectSite(
				depotName = "Test Depot Name${count}",
				groupName = "Site Name");
		}

		for (var count : list "1,2") {
			DepotNavigator.openDepotAdmin();

			Depot.configureDDMStructures(
				depotName = "Test Depot Name${count}",
				siteName = "Site Name",
				tableEntryText = "Yes");
		}

		for (var count : list "1,2") {
			WebContentNavigator.openWebContentAdmin(siteURLKey = "site-name");

			WebContentNavigator.gotoAddWithStructureCP(structureName = "WC Structure Name${count}");

			WebContent.addWithStructureCP(
				structureName = "WC Structure Name${count}",
				templateName = "WC Template Name${count}",
				webContentText = "Text${count}",
				webContentTitle = "Web Content Title${count}");

			PortletEntry.publish();
		}

		JSONLayout.addWidgetToPublicLayout(
			groupName = "Site Name",
			layoutName = "Page Name",
			widgetName = "Asset Publisher");

		Navigator.openSitePage(
			pageName = "Page Name",
			siteName = "Site Name");

		for (var count : list "1,2") {
			AssertTextEquals.assertPartialText(
				key_assetTitle = "Web Content Title${count}",
				locator1 = "AP#ASSET_ABSTRACT_TITLE",
				value1 = "Web Content Title${count}");
		}
	}

	@description = "This ensures that a user can use a structure stored in AL to create an article and display it in WCD on a connected site."
	@priority = "5"
	@refactordone
	test CanViewCustomizedWCInWCD {
		JSONLayout.addPublicLayout(
			groupName = "Site Name",
			layoutName = "Page Name");

		DepotNavigator.openDepotWebContentStructuresAdmin(depotName = "Test Depot Name");

		WebContentStructures.addCP(
			structureDescription = "WC Structure Description",
			structureName = "WC Structure Name");

		DataEngine.addField(
			fieldFieldLabel = "Text",
			fieldName = "Text");

		WebContentStructures.saveCP(structureName = "WC Structure Name");

		DepotNavigator.openDepotWebContentTemplatesAdmin(depotName = "Test Depot Name");

		WebContentTemplates.addCP(
			structureName = "WC Structure Name",
			templateDescription = "WC Template Description",
			templateFieldNames = "Text",
			templateName = "WC Template Name");

		JSONDepot.connectSite(
			depotName = "Test Depot Name",
			groupName = "Site Name");

		DepotNavigator.openDepotAdmin();

		Depot.configureDDMStructures(
			depotName = "Test Depot Name",
			siteName = "Site Name",
			tableEntryText = "Yes");

		WebContentStructures.openWebContentStructuresAdmin(siteURLKey = "site-name");

		AssertElementNotPresent(
			key_tableEntry = "WC Structure Name",
			locator1 = "LexiconTable#TABLE_ENTRY_TITLE");

		WebContentNavigator.openWebContentAdmin(siteURLKey = "site-name");

		WebContentNavigator.gotoAddWithStructureCP(structureName = "WC Structure Name");

		WebContent.addWithStructureCP(
			structureName = "WC Structure Name",
			templateName = "WC Template Name",
			webContentText = "Web Content Text",
			webContentTitle = "Web Content Title");

		PortletEntry.publish();

		JSONLayout.addWidgetToPublicLayout(
			groupName = "Site Name",
			layoutName = "Page Name",
			widgetName = "Web Content Display");

		Navigator.openSitePage(
			pageName = "Page Name",
			siteName = "Site Name");

		WebContentDisplayPortlet.selectWebContent(webContentTitle = "Web Content Title");

		Navigator.openSitePage(
			pageName = "Page Name",
			siteName = "Site Name");

		WebContent.viewPGViaWCD(webContentContent = "Web Content Text");
	}

	@description = "This ensures that a user can see information of WC structure from more iframe about which AL owns it."
	@priority = "4"
	@refactordone
	test CanViewStructuresScopeList {
		for (var i : list "1,2,3") {
			JSONDepot.addDepot(
				depotDescription = "This is the description of a depot",
				depotName = "Test Depot Name${i}");
		}

		for (var i : list "1,2,3") {
			DepotNavigator.openDepotWebContentStructuresAdmin(depotName = "Test Depot Name${i}");

			WebContentStructures.addCP(
				structureDescription = "WC Structure Description",
				structureName = "WC Structure Name${i}");

			DataEngine.addField(
				fieldFieldLabel = "Text",
				fieldName = "Text");

			WebContentStructures.saveCP(structureName = "WC Structure Name${i}");

			DepotNavigator.openDepotWebContentTemplatesAdmin(depotName = "Test Depot Name${i}");

			WebContentTemplates.addCP(
				structureName = "WC Structure Name${i}",
				templateDescription = "WC Template Description",
				templateFieldNames = "Text",
				templateName = "WC Template Name${i}");
		}

		for (var i : list "1,2,3") {
			JSONDepot.connectSite(
				depotName = "Test Depot Name${i}",
				groupName = "Site Name");
		}

		for (var i : list "1,2,3") {
			DepotNavigator.openDepotAdmin();

			Depot.configureDDMStructures(
				depotName = "Test Depot Name${i}",
				siteName = "Site Name",
				tableEntryText = "Yes");
		}

		for (var i : list "4,5,6,7,8") {
			WebContentStructures.openWebContentStructuresAdmin(siteURLKey = "site-name");

			WebContentStructures.addCP(
				structureDescription = "WC Structure Description",
				structureName = "WC Structure Name${i}");

			DataEngine.addField(
				fieldFieldLabel = "Text",
				fieldName = "Text");

			WebContentStructures.saveCP(structureName = "WC Structure Name${i}");
		}

		WebContentNavigator.openWebContentAdmin(siteURLKey = "site-name");

		WebContentNavigator.gotoMoreStructuresViaPlusButton(pageMod = "8 of 10");

		for (var i : list "1,2,3") {
			WebContentStructures.viewScopesViaMoreIFrame(
				scopeName = "Test Depot Name${i}",
				structureName = "WC Structure Name${i}");
		}
	}

}