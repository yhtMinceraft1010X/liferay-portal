@component-name = "portal-lima"
definition {

	property portal.release = "true";
	property portal.upstream = "true";
	property testray.main.component.name = "Translations Management";

	setUp {
		TestCase.setUpPortalInstance();

		User.firstLoginPG();

		JSONGroup.addGroup(groupName = "Site Name");

		DMNavigator.openToConfigInSystemSettings(portlet = "Google Cloud Translator");

		var valueReference = Translations.setGoogleCloudTranslationServiceKey();

		SystemSettings.configureSystemSetting(
			enableSetting = "true",
			settingFieldName = "Enabled",
			settingName = "Service Account Private Key",
			settingValue = "${valueReference}");

		JSONWebcontent.addWebContent(
			content = "This is the content of a web article",
			description = "This is the description of a web article",
			groupName = "Site Name",
			title = "This is the title of a web article");
	}

	tearDown {
		JSONGroup.deleteGroupByName(groupName = "Site Name");
	}

	@description = "This ensures that an invalid Google translation service key cannot be saved."
	@priority = "5"
	test CannotSaveWhenConfigurationIsInvalid {
		property portal.acceptance = "true";
		property test.name.skip.portal.instance = "AutoTranslationGoogle#CannotSaveWhenConfigurationIsBlank";

		DMNavigator.openToConfigInSystemSettings(portlet = "Google Cloud Translator");

		SystemSettings.resetConfiguration();

		Translations.saveInvalidConfiguration(settingValue = "");

		DMNavigator.openToConfigInSystemSettings(portlet = "Google Cloud Translator");

		Translations.saveInvalidConfiguration(settingValue = "This is an invalid input");
	}

	@description = "This ensures that the web content content can be translated independently"
	@priority = "5"
	test CanTranslateContentFieldIndependently {
		property test.name.skip.portal.instance = "AutoTranslationGoogle#CanTranslateContentFieldIndependently";

		Translations.openToTranslateEntry(
			groupName = "Site Name",
			siteURLKey = "site-name",
			sourceLanguageId = "en_US",
			targetLanguageId = "ja_JP",
			webContentTitle = "This is the title of a web article");

		// End the test if the Google refresh token limit is exceeded.

		Translations.translateSpecificField(fieldName = "Content");

		if (IsTextEqual.isPartialText(key_fieldName = "Content", locator1 = "Message#SUCCESS_SPECIFIC_FORM_FIELD_REQUIRED", value1 = "Field Translated")) {
			Translations.viewTargetFields(
				webContentContent = "これはウェブ記事の内容です",
				webContentDescription = "This is the description of a web article",
				webContentTitle = "This is the title of a web article");

			PortletEntry.publish();

			Translations.openToTranslateEntry(
				groupName = "Site Name",
				siteURLKey = "site-name",
				sourceLanguageId = "en_US",
				targetLanguageId = "ja_JP",
				webContentTitle = "This is the title of a web article");

			Translations.viewTargetFields(
				webContentContent = "これはウェブ記事の内容です",
				webContentDescription = "This is the description of a web article",
				webContentTitle = "This is the title of a web article");
		}
		else if (IsTextEqual.isPartialText(key_fieldName = "Content", locator1 = "Message#SUCCESS_SPECIFIC_FORM_FIELD_REQUIRED", value1 = "There is a problem with the translation service. Please contact your administrator.")) {
			echo("There is a Google network issue. Ending test.");
		}
	}

	@description = "This ensures that google translation can be used to auto-translate a content page."
	@priority = "5"
	test CanTranslateContentPage {
		property test.name.skip.portal.instance = "AutoTranslationGoogle#CanTranslateContentPage";

		JSONLayout.addPublicLayout(
			groupName = "Site Name",
			layoutName = "Content Page Name",
			type = "content");

		ContentPagesNavigator.openEditContentPage(
			pageName = "Content Page Name",
			siteName = "Site Name");

		PageEditor.addFragment(
			collectionName = "Basic Components",
			fragmentName = "Button");

		PagesAdmin.openPagesAdmin(siteURLKey = "site-name");

		WaitForPageLoad();

		ContentPagesNavigator.goToTranslation(pageName = "Content Page Name");

		Translations.changeTargetLocale(
			currentTargetLocale = "ar-SA",
			newTargetLocale = "zh-CN");

		Translations.autoTranslate();

		// End the test if the Google refresh token limit is exceeded.

		if (IsTextEqual.isPartialText(locator1 = "Toolbar#FEEDBACK", value1 = "Successfully received translations.")) {
			Translations.viewTargetFields(
				contentPageName = "内容页面名称",
				link = "去一个地方");

			PortletEntry.publish();

			ContentPagesNavigator.openEditContentPage(
				pageName = "Content Page Name",
				siteName = "Site Name");

			PageEditor.changeLocale(
				currentLocale = "en-US",
				locale = "zh-CN");

			ContentPages.viewFragmentText(
				fragmentName = "button",
				id = "link",
				text = "去一个地方");

			PageEditor.clickPublish();

			ContentPagesNavigator.openViewContentPage(
				pageName = "Content Page Name",
				siteName = "Site Name");

			Navigator.ensureLocaleURL(locale = "zh");

			Page.viewCurrentPage(pageName = "内容页面名称");
		}
		else if (IsTextEqual.isPartialText(locator1 = "Toolbar#FEEDBACK", value1 = "There is a problem with the translation service. Please contact your administrator.")) {
			echo("There is a Google network issue. Ending test.");
		}
	}

	@description = "This ensures that the fragment filed on content page can be translated independently."
	@priority = "5"
	test CanTranslateContentPageFieldIndependently {
		property test.name.skip.portal.instance = "AutoTranslationGoogle#CanTranslateContentPageFieldIndependently";

		JSONLayout.addPublicLayout(
			groupName = "Site Name",
			layoutName = "Content Page Name",
			type = "content");

		ContentPagesNavigator.openEditContentPage(
			pageName = "Content Page Name",
			siteName = "Site Name");

		PageEditor.addFragment(
			collectionName = "Basic Components",
			fragmentName = "Heading");

		PageEditor.editFragmentText(
			fragmentName = "Heading",
			id = "element-text",
			text = "Heading");

		PagesAdmin.openPagesAdmin(siteURLKey = "site-name");

		WaitForPageLoad();

		ContentPagesNavigator.goToTranslation(pageName = "Content Page Name");

		Translations.changeTargetLocale(
			currentTargetLocale = "ar-SA",
			newTargetLocale = "de-DE");

		Translations.translateSpecificField(fieldName = "element-text");

		if (IsTextEqual.isPartialText(key_fieldName = "element-text", locator1 = "Message#SUCCESS_SPECIFIC_FORM_FIELD_REQUIRED", value1 = "Field Translated")) {
			Translations.viewTargetFields(
				contentPageName = "Content Page Name",
				elementText = "Üerschrift");

			PortletEntry.publish();

			ContentPagesNavigator.openEditContentPage(
				pageName = "Content Page Name",
				siteName = "Site Name");

			PageEditor.changeLocale(
				currentLocale = "en-US",
				locale = "de-DE");

			PageEditor.viewFragmentEditableText(
				editableText = "Üerschrift",
				fragmentName = "Heading",
				id = "element-text");

			PageEditor.clickPublish();

			ContentPagesNavigator.openViewContentPage(
				pageName = "Content Page Name",
				siteName = "Site Name");

			Navigator.ensureLocaleURL(locale = "de");

			Page.viewCurrentPage(pageName = "Content Page Name");

			ContentPages.viewFragmentText(
				fragmentName = "heading",
				id = "element-text",
				text = "Üerschrift");
		}
		else if (IsTextEqual.isPartialText(key_fieldName = "element-text", locator1 = "Message#SUCCESS_SPECIFIC_FORM_FIELD_REQUIRED", value1 = "There is a problem with the translation service. Please contact your administrator.")) {
			echo("There is a Google network issue. Ending test.");
		}
	}

	@description = "This ensures that the web content description can be translated independently"
	@priority = "5"
	test CanTranslateDescriptionFieldIndependently {
		property test.name.skip.portal.instance = "AutoTranslationGoogle#CanTranslateDescriptionFieldIndependently";

		Translations.openToTranslateEntry(
			groupName = "Site Name",
			siteURLKey = "site-name",
			sourceLanguageId = "en_US",
			targetLanguageId = "es_ES",
			webContentTitle = "This is the title of a web article");

		// End the test if the Google refresh token limit is exceeded.

		Translations.translateSpecificField(fieldName = "Description");

		if (IsTextEqual.isPartialText(key_fieldName = "Description", locator1 = "Message#SUCCESS_SPECIFIC_FORM_FIELD_REQUIRED", value1 = "Field Translated")) {
			Translations.viewTargetFields(
				webContentContent = "This is the content of a web article",
				webContentDescription = "Esta es la descripción de un artículo web.",
				webContentTitle = "This is the title of a web article");

			PortletEntry.publish();

			Translations.openToTranslateEntry(
				groupName = "Site Name",
				siteURLKey = "site-name",
				sourceLanguageId = "en_US",
				targetLanguageId = "es_ES",
				webContentTitle = "This is the title of a web article");

			Translations.viewTargetFields(
				webContentContent = "This is the content of a web article",
				webContentDescription = "Esta es la descripción de un artículo web.",
				webContentTitle = "This is the title of a web article");
		}
		else if (IsTextEqual.isPartialText(key_fieldName = "Description", locator1 = "Message#SUCCESS_SPECIFIC_FORM_FIELD_REQUIRED", value1 = "There is a problem with the translation service. Please contact your administrator.")) {
			echo("There is a Google network issue. Ending test.");
		}
	}

	@description = "This ensures that the numeric and decimal fields of a customized web content can be translated independently"
	@priority = "5"
	test CanTranslateNumericAndDecimalFieldsIndependently {
		property test.name.skip.portal.instance = "AutoTranslationGoogle#CanTranslateNumericAndDecimalFieldsIndependently";

		WebContentNavigator.openWebContentAdmin(siteURLKey = "site-name");

		NavItem.gotoStructures();

		WebContentStructures.addCP(
			structureDescription = "WC Structure Description",
			structureName = "WC Structure Name");

		DataEngine.addField(
			fieldFieldLabel = "Numeric",
			fieldName = "Numeric");

		DataEngine.addField(
			fieldFieldLabel = "Decimal",
			fieldName = "Numeric",
			numericType = "Decimal");

		WebContentStructures.saveCP(structureName = "WC Structure Name");

		WebContentNavigator.openWebContentAdmin(siteURLKey = "site-name");

		WebContentNavigator.gotoAddWithStructureCP(structureName = "WC Structure Name");

		WebContent.addWithStructureCP(
			numericType = "Decimal",
			webContentNumeric = "1.0",
			webContentNumericFieldLabel = "Decimal");

		WebContent.addWithStructureCP(
			webContentNumeric = "10",
			webContentTitle = "This is the title of the numeric field");

		PortletEntry.publish();

		Translations.openToTranslateEntry(
			groupName = "Site Name",
			siteURLKey = "site-name",
			sourceLanguageId = "en_US",
			targetLanguageId = "es_ES",
			webContentTitle = "This is the title of the numeric field");

		// End the test if the Google refresh token limit is exceeded.

		Translations.translateSpecificField(fieldName = "Decimal");

		if (IsTextEqual.isPartialText(key_fieldName = "Decimal", locator1 = "Message#SUCCESS_SPECIFIC_FORM_FIELD_REQUIRED", value1 = "Field Translated")) {
			Translations.viewTargetFields(
				webContentDecimal = "1.0",
				webContentNumber = "10");

			Click(
				key_fieldName = "Numeric",
				locator1 = "TextInput#TRANSLATIONS_TARGET_FIELD",
				value1 = "10");

			Translations.translateSpecificField(fieldName = "Numeric");

			Translations.viewTargetFields(
				webContentDecimal = "1.0",
				webContentNumber = "10");

			PortletEntry.publish();

			Translations.openToTranslateEntry(
				groupName = "Site Name",
				siteURLKey = "site-name",
				sourceLanguageId = "en_US",
				targetLanguageId = "es_ES",
				webContentTitle = "This is the title of the numeric field");

			Translations.viewTargetFields(
				webContentDecimal = "1,0",
				webContentNumber = "10");
		}
		else if (IsTextEqual.isPartialText(key_fieldName = "Decimal", locator1 = "Message#SUCCESS_SPECIFIC_FORM_FIELD_REQUIRED", value1 = "There is a problem with the translation service. Please contact your administrator.")) {
			echo("There is a Google network issue. Ending test.");
		}
	}

	@description = "This ensures that the rich text field of a customized web content can be translated independently"
	@priority = "5"
	test CanTranslateRichTextFieldIndependently {
		property test.name.skip.portal.instance = "AutoTranslationGoogle#CanTranslateRichTextFieldIndependently";

		WebContentNavigator.openWebContentAdmin(siteURLKey = "site-name");

		NavItem.gotoStructures();

		WebContentStructures.addCP(
			structureDescription = "WC Structure Description",
			structureName = "WC Structure Name");

		DataEngine.addField(
			fieldFieldLabel = "Rich Text",
			fieldName = "Rich Text");

		WebContentStructures.saveCP(structureName = "WC Structure Name");

		WebContentNavigator.openWebContentAdmin(siteURLKey = "site-name");

		WebContentNavigator.gotoAddWithStructureCP(structureName = "WC Structure Name");

		WebContent.addWithStructureCP(
			webContentRichText = "This is the content of the rich text field",
			webContentTitle = "This is the title of the rich text field");

		PortletEntry.publish();

		Translations.openToTranslateEntry(
			groupName = "Site Name",
			siteURLKey = "site-name",
			sourceLanguageId = "en_US",
			targetLanguageId = "ja_JP",
			webContentTitle = "This is the title of the rich text field");

		// End the test if the Google refresh token limit is exceeded.

		Translations.translateSpecificField(fieldName = "Title");

		if (IsTextEqual.isPartialText(key_fieldName = "Title", locator1 = "Message#SUCCESS_SPECIFIC_FORM_FIELD_REQUIRED", value1 = "Field Translated")) {
			Translations.viewTargetFields(
				webContentSourceHtml = "This is the content of the rich text field",
				webContentTitle = "これはリッチテキストフィールドのタイトルです");

			Translations.translateSpecificField(fieldName = "Rich Text");

			Translations.viewTargetFields(
				webContentSourceHtml = "これはリッチテキストフィールドの内容です",
				webContentTitle = "これはリッチテキストフィールドのタイトルです");

			PortletEntry.publish();

			Translations.openToTranslateEntry(
				groupName = "Site Name",
				siteURLKey = "site-name",
				sourceLanguageId = "en_US",
				targetLanguageId = "ja_JP",
				webContentTitle = "This is the title of the rich text field");

			Translations.viewTargetFields(
				webContentSourceHtml = "これはリッチテキストフィールドの内容です",
				webContentTitle = "これはリッチテキストフィールドのタイトルです");
		}
		else if (IsTextEqual.isPartialText(key_fieldName = "Title", locator1 = "Message#SUCCESS_SPECIFIC_FORM_FIELD_REQUIRED", value1 = "There is a problem with the translation service. Please contact your administrator.")) {
			echo("There is a Google network issue. Ending test.");
		}
	}

	@description = "This ensures that the text box field of a customized web content can be translated independently"
	@priority = "5"
	test CanTranslateTextBoxFieldIndependently {
		property test.name.skip.portal.instance = "AutoTranslationGoogle#CanTranslateTextBoxFieldIndependently";

		JSONWebcontent.deleteWebContent(
			groupName = "Site Name",
			title = "This is the title of a web article");

		WebContentNavigator.openWebContentAdmin(siteURLKey = "site-name");

		NavItem.gotoStructures();

		WebContentStructures.addCP(
			structureDescription = "WC Structure Description",
			structureName = "WC Structure Name");

		DataEngine.addField(
			fieldFieldLabel = "Text Box",
			fieldName = "Text",
			fieldType = "Multiple Lines");

		WebContentStructures.saveCP(structureName = "WC Structure Name");

		WebContentNavigator.openWebContentAdmin(siteURLKey = "site-name");

		WebContentNavigator.gotoAddWithStructureCP(structureName = "WC Structure Name");

		WebContent.addWithStructureCP(
			fieldType = "Multiple Lines",
			webContentText = "This is the content of the text box field",
			webContentTextFieldLabel = "Text Box",
			webContentTitle = "This is the title of a web article");

		PortletEntry.publish();

		Translations.openToTranslateEntry(
			groupName = "Site Name",
			siteURLKey = "site-name",
			sourceLanguageId = "en_US",
			targetLanguageId = "ja_JP",
			webContentTitle = "This is the title of a web article");

		// End the test if the Google refresh token limit is exceeded.

		Translations.translateSpecificField(fieldName = "Title");

		if (IsTextEqual.isPartialText(key_fieldName = "Title", locator1 = "Message#SUCCESS_SPECIFIC_FORM_FIELD_REQUIRED", value1 = "Field Translated")) {
			Translations.viewTargetFields(
				webContentTextBox = "This is the content of the text box field",
				webContentTitle = "これはウェブ記事のタイトルです");

			Translations.translateSpecificField(fieldName = "Text Box");

			Translations.viewTargetFields(
				webContentTextBox = "これはテキストボックスフィールドの内容です",
				webContentTitle = "これはウェブ記事のタイトルです");

			PortletEntry.publish();

			Translations.openToTranslateEntry(
				groupName = "Site Name",
				siteURLKey = "site-name",
				sourceLanguageId = "en_US",
				targetLanguageId = "ja_JP",
				webContentTitle = "This is the title of a web article");

			Translations.viewTargetFields(
				webContentTextBox = "これはテキストボックスフィールドの内容です",
				webContentTitle = "これはウェブ記事のタイトルです");
		}
		else if (IsTextEqual.isPartialText(key_fieldName = "Title", locator1 = "Message#SUCCESS_SPECIFIC_FORM_FIELD_REQUIRED", value1 = "There is a problem with the translation service. Please contact your administrator.")) {
			echo("There is a Google network issue. Ending test.");
		}
	}

	@description = "This ensures that the text field of a customized web content can be translated independently"
	@priority = "5"
	test CanTranslateTextFieldIndependently {
		property test.name.skip.portal.instance = "AutoTranslationGoogle#CanTranslateTextFieldIndependently";

		JSONWebcontent.deleteWebContent(
			groupName = "Site Name",
			title = "This is the title of a web article");

		WebContentNavigator.openWebContentAdmin(siteURLKey = "site-name");

		NavItem.gotoStructures();

		WebContentStructures.addCP(
			structureDescription = "WC Structure Description",
			structureName = "WC Structure Name");

		DataEngine.addField(
			fieldFieldLabel = "Text",
			fieldName = "Text");

		WebContentStructures.saveCP(structureName = "WC Structure Name");

		WebContentNavigator.openWebContentAdmin(siteURLKey = "site-name");

		WebContentNavigator.gotoAddWithStructureCP(structureName = "WC Structure Name");

		WebContent.addWithStructureCP(
			webContentText = "This is the content of the text field",
			webContentTitle = "This is the title of a web article");

		PortletEntry.publish();

		Translations.openToTranslateEntry(
			groupName = "Site Name",
			siteURLKey = "site-name",
			sourceLanguageId = "en_US",
			targetLanguageId = "ja_JP",
			webContentTitle = "This is the title of a web article");

		// End the test if the Google refresh token limit is exceeded.

		Translations.translateSpecificField(fieldName = "Title");

		if (IsTextEqual.isPartialText(key_fieldName = "Title", locator1 = "Message#SUCCESS_SPECIFIC_FORM_FIELD_REQUIRED", value1 = "Field Translated")) {
			Translations.viewTargetFields(
				webContentText = "This is the content of the text field",
				webContentTitle = "これはウェブ記事のタイトルです");

			Translations.translateSpecificField(fieldName = "Text");

			Translations.viewTargetFields(
				webContentText = "これはテキストフィールドの内容です",
				webContentTitle = "これはウェブ記事のタイトルです");

			PortletEntry.publish();

			Translations.openToTranslateEntry(
				groupName = "Site Name",
				siteURLKey = "site-name",
				sourceLanguageId = "en_US",
				targetLanguageId = "ja_JP",
				webContentTitle = "This is the title of a web article");

			Translations.viewTargetFields(
				webContentText = "これはテキストフィールドの内容です",
				webContentTitle = "これはウェブ記事のタイトルです");
		}
		else if (IsTextEqual.isPartialText(key_fieldName = "Title", locator1 = "Message#SUCCESS_SPECIFIC_FORM_FIELD_REQUIRED", value1 = "There is a problem with the translation service. Please contact your administrator.")) {
			echo("There is a Google network issue. Ending test.");
		}
	}

	@description = "This ensures that the web content title can be translated independently"
	@priority = "5"
	test CanTranslateTitleFieldIndependently {
		property test.name.skip.portal.instance = "AutoTranslationGoogle#CanTranslateTitleFieldIndependently";

		Translations.openToTranslateEntry(
			groupName = "Site Name",
			siteURLKey = "site-name",
			sourceLanguageId = "en_US",
			targetLanguageId = "ja_JP",
			webContentTitle = "This is the title of a web article");

		// End the test if the Google refresh token limit is exceeded.

		Translations.translateSpecificField(fieldName = "Title");

		if (IsTextEqual.isPartialText(key_fieldName = "Title", locator1 = "Message#SUCCESS_SPECIFIC_FORM_FIELD_REQUIRED", value1 = "Field Translated")) {
			Translations.viewTargetFields(
				webContentContent = "This is the content of a web article",
				webContentDescription = "This is the description of a web article",
				webContentTitle = "これはウェブ記事のタイトルです");

			PortletEntry.publish();

			Translations.openToTranslateEntry(
				groupName = "Site Name",
				siteURLKey = "site-name",
				sourceLanguageId = "en_US",
				targetLanguageId = "ja_JP",
				webContentTitle = "This is the title of a web article");

			Translations.viewTargetFields(
				webContentContent = "This is the content of a web article",
				webContentDescription = "This is the description of a web article",
				webContentTitle = "これはウェブ記事のタイトルです");
		}
		else if (IsTextEqual.isPartialText(key_fieldName = "Title", locator1 = "Message#SUCCESS_SPECIFIC_FORM_FIELD_REQUIRED", value1 = "There is a problem with the translation service. Please contact your administrator.")) {
			echo("There is a Google network issue. Ending test.");
		}
	}

	@description = "This ensures that Google Translation can be used to auto-translate a Web Content into Japanese"
	@priority = "5"
	test CanTranslateWCToJapaneseWithGoogle {
		property portal.acceptance = "true";
		property test.name.skip.portal.instance = "AutoTranslationGoogle#CanTranslateWCToJapaneseWithGoogle";

		WebContentNavigator.openWebContentAdmin(siteURLKey = "site-name");

		Translations.gotoTranslate(webContentTitle = "This is the title of a web article");

		Translations.changeTargetLocale(
			currentTargetLocale = "ar-SA",
			newTargetLocale = "ja-JP");

		Translations.autoTranslate();

		// End the test if the Google refresh token limit is exceeded.

		if (IsTextEqual.isPartialText(locator1 = "Toolbar#FEEDBACK", value1 = "Successfully received translations.")) {
			Translations.viewTargetFields(
				webContentContent = "これはウェブ記事の内容です",
				webContentDescription = "これはウェブ記事の説明です",
				webContentTitle = "これはウェブ記事のタイトルです");

			PortletEntry.publish();

			Translations.openTranslationAdmin(siteURLKey = "site-name");

			Translations.viewTranslationEntry(
				language = "ja-JP",
				status = "Approved",
				title = "Translation of This is the title of a web article to ja-JP");
		}
		else if (IsTextEqual.isPartialText(locator1 = "Toolbar#FEEDBACK", value1 = "There is a problem with the translation service. Please contact your administrator.")) {
			echo("There is a Google network issue. Ending test.");
		}
	}

	@description = "The Auto Translate button should not appear in the side-by-side view after Google Translations are enabled then disabled"
	@priority = "5"
	test CanViewTranslationAfterDisablingGoogleConfiguration {
		property test.name.skip.portal.instance = "AutoTranslationGoogle#CanViewTranslationAfterDisablingGoogleConfiguration";

		WebContentNavigator.openWebContentAdmin(siteURLKey = "site-name");

		Translations.gotoTranslate(webContentTitle = "This is the title of a web article");

		Translations.changeTargetLocale(
			currentTargetLocale = "ar-SA",
			newTargetLocale = "es-ES");

		Translations.autoTranslate();

		// End the test if the Google refresh token limit is exceeded.

		if (IsTextEqual.isPartialText(locator1 = "Toolbar#FEEDBACK", value1 = "Successfully received translations.")) {
			PortletEntry.publish();

			DMNavigator.openToConfigInSystemSettings(portlet = "Google Cloud Translator");

			SystemSettings.configureSystemSetting(
				enableSetting = "false",
				settingFieldName = "Enabled");

			SystemSettings.saveConfiguration();

			WebContentNavigator.openWebContentAdmin(siteURLKey = "site-name");

			Translations.gotoTranslate(webContentTitle = "This is the title of a web article");

			AssertElementNotPresent(
				key_text = "Auto Translate",
				locator1 = "Button#ANY");

			Translations.changeTargetLocale(
				currentTargetLocale = "ar-SA",
				newTargetLocale = "es-ES");

			Translations.viewTargetFields(
				webContentContent = "Este es el contenido de un artículo web.",
				webContentDescription = "Esta es la descripción de un artículo web.",
				webContentTitle = "Este es el título de un artículo web.");
		}
		else if (IsTextEqual.isPartialText(locator1 = "Toolbar#FEEDBACK", value1 = "There is a problem with the translation service. Please contact your administrator.")) {
			echo("There is a Google network issue. Ending test.");
		}
	}

	@description = "The Auto Translate button should be disabled when the fields are null."
	@priority = "4"
	test TranslateButtonIsDisabledWhenFieldIsNull {
		property test.name.skip.portal.instance = "AutoTranslationGoogle#TranslateButtonIsDisabledWhenFieldIsNull";

		JSONWebcontent.addWebContent(
			content = "This is the content of a web article",
			groupName = "Site Name",
			title = "Title Translate Button is Disabled When Field is Null");

		Translations.openToTranslateEntry(
			groupName = "Site Name",
			siteURLKey = "site-name",
			sourceLanguageId = "en_US",
			targetLanguageId = "es_ES",
			webContentTitle = "Title Translate Button is Disabled When Field is Null");

		Translations.viewEnabledFieldTranslationButtons(fieldNameList = "Title,Content");

		Translations.viewDisabledFieldTranslationButtons(fieldNameList = "Description");
	}

}