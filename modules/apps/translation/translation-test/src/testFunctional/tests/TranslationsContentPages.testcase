@component-name = "portal-lima"
definition {

	property portal.release = "true";
	property portal.upstream = "true";
	property testray.main.component.name = "Translations Management";

	// This testcase validates the Content Page translations feature for both default and custom experiences.

	setUp {
		TestCase.setUpPortalInstance();

		User.firstLoginPG();

		JSONGroup.addGroup(groupName = "Test Site Name");

		JSONLayout.addPublicLayout(
			groupName = "Test Site Name",
			layoutName = "Test Content Page Name",
			type = "content");
	}

	tearDown {
		var testPortalInstance = PropsUtil.get("test.portal.instance");

		if ("${testPortalInstance}" == "true") {
			PortalInstances.tearDownCP();
		}
		else {
			User.logoutPG();
		}
	}

	@description = "This ensures that a translator without permissions cannot translate a content page to Chinese."
	@priority = "5"
	test CannotTranslateToChineseWithoutPermissions {
		property custom.properties = "jsonws.web.service.paths.excludes=";
		property portal.acceptance = "true";

		ContentPagesNavigator.openEditContentPage(
			pageName = "Test Content Page Name",
			siteName = "Test Site Name");

		PageEditor.addFragment(
			collectionName = "Basic Components",
			fragmentName = "Button");

		PageEditor.clickPublish();

		Permissions.setUpRegRoleLoginUserCP(
			roleTitle = "Regrole Name",
			userEmailAddress = "userea@liferay.com",
			userFirstName = "userfn",
			userLastName = "userln",
			userScreenName = "usersn");

		Permissions.definePermissionViaJSONAPI(
			resourceAction = "ACCESS_IN_CONTROL_PANEL",
			resourceName = "com_liferay_layout_admin_web_portlet_GroupPagesPortlet",
			roleTitle = "Regrole Name");

		Permissions.definePermissionViaJSONAPI(
			resourceAction = "VIEW_SITE_ADMINISTRATION",
			resourceName = "com.liferay.portal.kernel.model.Group",
			roleTitle = "Regrole Name");

		Permissions.definePermissionViaJSONAPI(
			resourceAction = "UPDATE_LAYOUT_CONTENT",
			resourceName = "com.liferay.portal.kernel.model.Layout",
			roleTitle = "Regrole Name");

		Permissions.definePermissionViaJSONAPI(
			resourceAction = "TRANSLATE",
			resourceName = "com.liferay.translation.es_ES",
			roleTitle = "Regrole Name");

		User.logoutAndLoginPG(
			userLoginEmailAddress = "userea@liferay.com",
			userLoginFullName = "userfn userln");

		PagesAdmin.openPagesAdmin(siteURLKey = "test-site-name");

		WaitForPageLoad();

		ContentPagesNavigator.goToTranslation(pageName = "Test Content Page Name");

		Translations.viewTargetLocales(
			currentTargetLocale = "es-ES",
			noTargetLocale = "zh-CN",
			targetLocale = "es-ES");
	}

	@description = "This ensures that a translator can modify languages in translations when he has permissions for."
	@priority = "5"
	test CanOnlyTranslateToSpanishWithExperience {
		property custom.properties = "jsonws.web.service.paths.excludes=";

		Permissions.setUpRegRoleLoginUserCP(
			roleTitle = "Regrole Name",
			userEmailAddress = "userea@liferay.com",
			userFirstName = "userfn",
			userLastName = "userln",
			userScreenName = "usersn");

		Permissions.definePermissionViaJSONAPI(
			resourceAction = "ACCESS_IN_CONTROL_PANEL",
			resourceName = "com_liferay_layout_admin_web_portlet_GroupPagesPortlet",
			roleTitle = "Regrole Name");

		Permissions.definePermissionViaJSONAPI(
			resourceAction = "VIEW_SITE_ADMINISTRATION",
			resourceName = "com.liferay.portal.kernel.model.Group",
			roleTitle = "Regrole Name");

		Permissions.definePermissionViaJSONAPI(
			resourceAction = "UPDATE_LAYOUT_CONTENT",
			resourceName = "com.liferay.portal.kernel.model.Layout",
			roleTitle = "Regrole Name");

		Permissions.definePermissionViaJSONAPI(
			resourceAction = "TRANSLATE",
			resourceName = "com.liferay.translation.es_ES",
			roleTitle = "Regrole Name");

		Permissions.definePermissionViaJSONAPI(
			resourceAction = "VIEW",
			resourceName = "com.liferay.segments.model.SegmentsExperience",
			roleTitle = "Regrole Name");

		JSONSegmentsentry.addSegment(
			fieldName = "Email Address",
			groupName = "Test Site Name",
			operator = "contains",
			segmentName = "Segment Email Address",
			text = "test@liferay.com");

		ContentPagesNavigator.openEditContentPage(
			pageName = "Test Content Page Name",
			siteName = "Test Site Name");

		PageEditor.addExperience(
			experienceName = "Segment Email Experience",
			segmentName = "Segment Email Address");

		PageEditor.addFragment(
			collectionName = "Basic Components",
			fragmentName = "Heading");

		PageEditor.clickPublish();

		User.logoutAndLoginPG(
			userLoginEmailAddress = "userea@liferay.com",
			userLoginFullName = "userfn userln");

		PagesAdmin.openPagesAdmin(siteURLKey = "test-site-name");

		WaitForPageLoad();

		ContentPagesNavigator.goToTranslation(pageName = "Test Content Page Name");

		Translations.viewTargetLocales(
			currentTargetLocale = "es-ES",
			noTargetLocale = "zh-CN,ar-SA,ca-ES,nl-NL,fi-FI,fr-FR,de-DE,hu-HU,ja-JP,pt-BR,sv-SE",
			targetLocale = "es-ES");

		Translations.translateFragmentField(
			experienceName = "Segment Email Experience",
			ff = "true",
			fieldName = "element-text",
			fragmentText = "esta es la nueva experiencia");

		PortletEntry.publish();

		User.logoutAndLoginPG(
			userLoginEmailAddress = "test@liferay.com",
			userLoginFullName = "Test Test");

		ContentPagesNavigator.openEditContentPage(
			pageName = "Test Content Page Name",
			siteName = "Test Site Name");

		PageEditor.changeExperience(newExperience = "Segment Email Experience");

		PageEditor.prioritizeExperience(experienceName = "Segment Email Experience");

		PageEditor.changeLocale(
			currentLocale = "en-US",
			locale = "es-ES");

		PageEditor.viewFragmentEditableText(
			editableText = "esta es la nueva experiencia",
			fragmentName = "Heading",
			id = "element-text");

		PageEditor.clickPublish();

		Navigator.openWithAppendToBaseURL(urlAppend = "es/web/test-site-name/test-content-page-name");

		AssertTextEquals(
			locator1 = "Experience#FRAGMENT_HEADING",
			value1 = "esta es la nueva experiencia");
	}

	@description = "This ensures that a heading on a content page with a new experience can be translated and published properly."
	@priority = "5"
	test CanPublishTranslatedHeadingWithExperience {
		JSONSegmentsentry.addSegment(
			fieldName = "Email Address",
			groupName = "Test Site Name",
			operator = "contains",
			segmentName = "Segment Email Address",
			text = "test@liferay.com");

		ContentPagesNavigator.openEditContentPage(
			pageName = "Test Content Page Name",
			siteName = "Test Site Name");

		PageEditor.addExperience(
			experienceName = "Segment Email Experience",
			segmentName = "Segment Email Address");

		PageEditor.addFragment(
			collectionName = "Basic Components",
			fragmentName = "Heading");

		PagesAdmin.openPagesAdmin(siteURLKey = "test-site-name");

		WaitForPageLoad();

		ContentPagesNavigator.goToTranslation(pageName = "Test Content Page Name");

		Translations.changeTargetLocale(
			currentTargetLocale = "ar-SA",
			newTargetLocale = "ja-JP");

		Translations.translateFragmentField(
			experienceName = "Segment Email Experience",
			ff = "true",
			fieldName = "element-text",
			fragmentText = "経験をテストする");

		PortletEntry.publish();

		Translations.openTranslationAdmin(siteURLKey = "test-site-name");

		Translations.viewTranslationEntry(
			language = "ja-JP",
			status = "Approved",
			title = "Translation of Test Content Page Name (Segment Email Experience) to ja-JP");

		ContentPagesNavigator.openEditContentPage(
			pageName = "Test Content Page Name",
			siteName = "Test Site Name");

		PageEditor.changeExperience(newExperience = "Segment Email Experience");

		PageEditor.prioritizeExperience(experienceName = "Segment Email Experience");

		PageEditor.changeLocale(
			currentLocale = "en-US",
			locale = "ja-JP");

		PageEditor.viewFragmentEditableText(
			editableText = "経験をテストする",
			fragmentName = "Heading",
			id = "element-text");

		PageEditor.clickPublish();

		Navigator.openWithAppendToBaseURL(urlAppend = "ja/web/test-site-name/test-content-page-name");

		AssertTextEquals(
			locator1 = "Experience#FRAGMENT_HEADING",
			value1 = "経験をテストする");
	}

	@description = "This ensures that a button on a content page can be translated and published properly."
	@priority = "5"
	test CanTranslateAndPublishButtonFragment {
		property custom.properties = "jsonws.web.service.paths.excludes=";

		ContentPagesNavigator.openEditContentPage(
			pageName = "Test Content Page Name",
			siteName = "Test Site Name");

		PageEditor.addFragment(
			collectionName = "Basic Components",
			fragmentName = "Button");

		PageEditor.clickPublish();

		PagesAdmin.openPagesAdmin(siteURLKey = "test-site-name");

		WaitForPageLoad();

		ContentPagesNavigator.goToTranslation(pageName = "Test Content Page Name");

		Translations.changeTargetLocale(
			currentTargetLocale = "ar-SA",
			newTargetLocale = "ja-JP");

		Translations.translateFragmentField(
			ff = "true",
			fieldName = "link",
			fragmentText = "これはボタンリンクです");

		PortletEntry.publish();

		ContentPagesNavigator.openEditContentPage(
			pageName = "Test Content Page Name",
			siteName = "Test Site Name");

		PageEditor.clickPublish();

		ContentPagesNavigator.openViewContentPage(
			locale = "ja",
			pageName = "Test Content Page Name",
			siteName = "Test Site Name");

		ContentPages.viewFragmentText(
			fragmentName = "button",
			id = "link",
			text = "これはボタンリンクです");
	}

	@description = "This ensures that an HTML fragment on a content page can be translated and published properly."
	@priority = "5"
	test CanTranslateAndPublishHTMLFragment {
		property custom.properties = "jsonws.web.service.paths.excludes=";
		property portal.acceptance = "true";

		ContentPagesNavigator.openEditContentPage(
			pageName = "Test Content Page Name",
			siteName = "Test Site Name");

		PageEditor.addFragment(
			collectionName = "Basic Components",
			fragmentName = "HTML");

		PageEditor.clickPublish();

		PagesAdmin.openPagesAdmin(siteURLKey = "test-site-name");

		WaitForPageLoad();

		ContentPagesNavigator.goToTranslation(pageName = "Test Content Page Name");

		Translations.changeTargetLocale(
			currentTargetLocale = "ar-SA",
			newTargetLocale = "es-ES");

		Translations.translateFragmentField(
			ff = "true",
			fieldName = "HTML",
			fragmentText = "este es un campo html");

		PortletEntry.publish();

		ContentPagesNavigator.openEditContentPage(
			pageName = "Test Content Page Name",
			siteName = "Test Site Name");

		PageEditor.clickPublish();

		var portalURL = PropsUtil.get("portal.url");

		ContentPagesNavigator.openViewContentPage(
			locale = "es",
			pageName = "Test Content Page Name",
			siteName = "Test Site Name");

		ContentPages.viewFragmentText(
			fragmentName = "html",
			id = "element-html",
			text = "este es un campo html");
	}

	@description = "This ensures that the translated page field can be inherited by new experience."
	@priority = "5"
	test TranslatedPageFieldCanBeInherited {
		JSONSegmentsentry.addSegment(
			fieldName = "Email Address",
			groupName = "Test Site Name",
			operator = "contains",
			segmentName = "Segment Email Address",
			text = "test@liferay.com");

		ContentPagesNavigator.openEditContentPage(
			pageName = "Test Content Page Name",
			siteName = "Test Site Name");

		PageEditor.addExperience(
			experienceName = "Segment Email Experience",
			segmentName = "Segment Email Address");

		PagesAdmin.openPagesAdmin(siteURLKey = "test-site-name");

		WaitForPageLoad();

		ContentPagesNavigator.goToTranslation(pageName = "Test Content Page Name");

		Translations.changeTargetLocale(
			currentTargetLocale = "ar-SA",
			newTargetLocale = "ja-JP");

		Translations.translateFragmentField(
			ff = "true",
			fieldName = "Name",
			fragmentText = "テストページ");

		PortletEntry.publish();

		ContentPagesNavigator.openEditContentPage(
			pageName = "Test Content Page Name",
			siteName = "Test Site Name");

		PageEditor.clickPublish();

		ContentPagesNavigator.openViewContentPage(
			pageName = "Test Content Page Name",
			siteName = "Test Site Name");

		Navigator.ensureLocaleURL(locale = "ja");

		Page.viewCurrentPage(pageName = "テストページ");

		ContentPagesNavigator.openViewContentPage(
			pageName = "Test Content Page Name",
			siteName = "Test Site Name");

		ContentPages.changeExperience(experienceName = "Segment Email Experience");

		Navigator.ensureLocaleURL(locale = "ja");

		Page.viewCurrentPage(pageName = "テストページ");
	}

}