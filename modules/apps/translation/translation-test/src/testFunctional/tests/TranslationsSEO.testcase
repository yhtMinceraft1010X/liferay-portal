@component-name = "portal-lima"
definition {

	property portal.acceptance = "true";
	property portal.release = "true";
	property portal.upstream = "true";
	property testray.main.component.name = "Translations Management";

	setUp {
		TestCase.setUpPortalInstance();

		User.firstLoginPG();

		JSONGroup.addGroup(groupName = "Test Site Name");
	}

	tearDown {
		var testPortalInstance = PropsUtil.get("test.portal.instance");

		if ("${testPortalInstance}" == "true") {
			PortalInstances.tearDownCP();
		}
		else {
			JSONGroup.deleteGroupByName(groupName = "Test Site Name");

			User.logoutPG();
		}
	}

	@description = "This ensures that all languages hreflang tag will be displayed after disabling OTH from SEO settings."
	@priority = "4"
	test CanViewAllHreflangTagsAfterDisabling {
		property test.name.skip.portal.instance = "TranslationsSEO#CanViewAllHreflangTagsAfterDisabling";

		var portalURL = PropsUtil.get("portal.url");

		TranslationsSEO.enableOnlyTranslatedHreflangs(configurationScope = "System Settings");

		JSONLayout.addPublicLayout(
			groupName = "Test Site Name",
			layoutName = "Translation Widget Page");

		Navigator.openToGroupPagesPortlet(
			groupName = "Test Site Name",
			layoutName = "Translation Widget Page",
			portlet = "General",
			siteURLKey = "test-site-name");

		PagesAdmin.addLocalizedName(
			localeList = "es-ES",
			localizedNameList = "P치gina de traducci칩n");

		TranslationsSEO.disableOnlyTranslatedHreflangs(configurationScope = "System Settings");

		Navigator.openSpecificURL(url = "${portalURL}/c/portal/logout");

		Navigator.openSpecificURL(url = "${portalURL}/web/test-site-name/translation-widget-page");

		TranslationsSEO.viewHreflangTags(localeList = "en_US,ar_SA,ca_ES,zh_CN,nl_NL,fi_FI,fr_FR,de_DE,hu_HU,ja_JP,pt_BR,es_ES,sv_SE");
	}

	@description = "This ensures that all languages hreflang tag of an untranslated page can be viewed when the OTH is disabled."
	@priority = "4"
	test CanViewPageDefaultHreflangTags {
		var portalURL = PropsUtil.get("portal.url");

		JSONLayout.addPublicLayout(
			groupName = "Test Site Name",
			layoutName = "Translation Widget Page");

		JSONLayout.addPublicLayout(
			groupName = "Test Site Name",
			layoutName = "Translation Content Page",
			type = "content");

		Navigator.openSpecificURL(url = "${portalURL}/c/portal/logout");

		Navigator.openSpecificURL(url = "${portalURL}/web/test-site-name/translation-widget-page");

		TranslationsSEO.viewHreflangTags(localeList = "en_US,ar_SA,ca_ES,zh_CN,nl_NL,fi_FI,fr_FR,de_DE,hu_HU,ja_JP,pt_BR,es_ES,sv_SE");

		Navigator.openSpecificURL(url = "${portalURL}/web/test-site-name/translation-content-page");

		TranslationsSEO.viewHreflangTags(localeList = "en_US,ar_SA,ca_ES,zh_CN,nl_NL,fi_FI,fr_FR,de_DE,hu_HU,ja_JP,pt_BR,es_ES,sv_SE");
	}

	@description = "This ensures that only translated page hreflang is tag displayed when a user accesses an untranslated content page."
	@priority = "5"
	test CanViewTranslatedLanguageHreflangTagOnContentPage {
		property test.name.skip.portal.instance = "TranslationsSEO#CanViewTranslatedLanguageHreflangTagOnContentPage";

		var portalURL = PropsUtil.get("portal.url");

		TranslationsSEO.enableOnlyTranslatedHreflangs(configurationScope = "Instance Settings");

		JSONLayout.addPublicLayout(
			groupName = "Test Site Name",
			layoutName = "Translation Content Page",
			type = "content");

		ContentPagesNavigator.openEditContentPage(
			pageName = "Translation Content Page",
			siteName = "Test Site Name");

		PageEditor.addFragment(
			collectionName = "Basic Components",
			fragmentName = "Paragraph");

		PageEditor.changeLocale(
			currentLocale = "en-US",
			locale = "es-ES");

		PageEditor.editFragmentText(
			fragmentName = "Paragraph",
			id = "element-text",
			text = "New editable fragment es-ES text");

		PageEditor.clickPublish();

		Navigator.openSpecificURL(url = "${portalURL}/c/portal/logout");

		Navigator.openSpecificURL(url = "${portalURL}/web/test-site-name/translation-content-page");

		TranslationsSEO.viewHreflangTags(localeList = "en_US,es_ES");

		TranslationsSEO.viewNoHreflangTags(localeList = "ar_SA,ca_ES,zh_CN,nl_NL,fi_FI,fr_FR,de_DE,hu_HU,ja_JP,pt_BR,sv_SE");

		Navigator.openSpecificURL(url = "${portalURL}/zh/web/test-site-name/translation-content-page");

		Page.assertCanonicalURL(customCanonicalURL = "${portalURL}/web/test-site-name");

		Page.assertDefaultAlternateURL(portalURL = "${portalURL}/web/test-site-name");

		TranslationsSEO.viewHreflangTags(localeList = "en_US,es_ES");

		TranslationsSEO.viewNoHreflangTags(localeList = "ar_SA,ca_ES,zh_CN,nl_NL,fi_FI,fr_FR,de_DE,hu_HU,ja_JP,pt_BR,sv_SE");
	}

	@description = "This ensures that only translated page hreflang tag is displayed when a user accesses an untranslated display page."
	@priority = "5"
	test CanViewTranslatedLanguageHreflangTagOnDisplayPage {
		property test.name.skip.portal.instance = "TranslationsSEO#CanViewTranslatedLanguageHreflangTagOnDisplayPage";

		var portalURL = PropsUtil.get("portal.url");

		TranslationsSEO.enableOnlyTranslatedHreflangs(
			configurationScope = "Site Settings",
			siteURLKey = "test-site-name");

		JSONLayout.addPublicLayout(
			groupName = "Test Site Name",
			layoutName = "Translation Widget Page");

		JSONLayout.addWidgetToPublicLayout(
			groupName = "Test Site Name",
			layoutName = "Translation Widget Page",
			widgetName = "Asset Publisher");

		JSONLayoutpagetemplate.addDisplayPageTemplateEntry(
			contentType = "Web Content Article",
			displayPageTemplateEntryName = "Display Page Name",
			groupName = "Test Site Name",
			subType = "Basic Web Content");

		JSONWebcontent.addWebContent(
			content = "Webcontent Content",
			groupName = "Test Site Name",
			title = "Webcontent Title");

		DisplayPageTemplatesAdmin.openDisplayPagesAdmin(siteURLKey = "test-site-name");

		DisplayPageTemplatesAdmin.gotoDisplayPage(displayPageName = "Display Page Name");

		PageEditor.addFragment(
			collectionName = "Basic Components",
			fragmentName = "Heading");

		PageEditor.changeLocale(
			currentLocale = "en-US",
			locale = "ja-JP");

		PageEditor.editFragmentText(
			fragmentName = "Heading",
			id = "element-text",
			text = "New editable fragment ja-JP text");

		PageEditor.clickPublish();

		DisplayPageTemplatesAdmin.markDisplayPageAsDefault(displayPageName = "Display Page Name");

		Navigator.openSpecificURL(url = "${portalURL}/c/portal/logout");

		Navigator.openSpecificURL(url = "${portalURL}/web/test-site-name/translation-widget-page");

		AssetPublisherPortlet.clickAssetTitle(assetTitle = "Webcontent Title");

		TranslationsSEO.viewHreflangTags(localeList = "en_US,ja_JP");

		TranslationsSEO.viewNoHreflangTags(localeList = "ar_SA,ca_ES,zh_CN,nl_NL,fi_FI,fr_FR,de_DE,hu_HU,es_ES,pt_BR,sv_SE");

		Navigator.openSpecificURL(url = "${portalURL}/es/web/test-site-name/translation-widget-page");

		AssetPublisherPortlet.clickAssetTitle(assetTitle = "Webcontent Title");

		Page.assertCanonicalURL(customCanonicalURL = "${portalURL}/web/test-site-name/w/webcontent-title");

		Page.assertDefaultAlternateURL(portalURL = "${portalURL}/web/test-site-name/w/webcontent-title");

		TranslationsSEO.viewHreflangTags(localeList = "en_US,ja_JP");

		TranslationsSEO.viewNoHreflangTags(localeList = "ar_SA,ca_ES,zh_CN,nl_NL,fi_FI,fr_FR,de_DE,hu_HU,es_ES,pt_BR,sv_SE");
	}

	@description = "This ensures that only translated page hreflang tag is displayed when a user accesses an untranslated widget page."
	@priority = "5"
	test CanViewTranslatedLanguageHreflangTagOnWidgetPage {
		property test.name.skip.portal.instance = "TranslationsSEO#CanViewTranslatedLanguageHreflangTagOnWidgetPage";

		var portalURL = PropsUtil.get("portal.url");

		TranslationsSEO.enableOnlyTranslatedHreflangs(configurationScope = "System Settings");

		JSONLayout.addPublicLayout(
			groupName = "Test Site Name",
			layoutName = "Translation Widget Page");

		Navigator.openToGroupPagesPortlet(
			groupName = "Test Site Name",
			layoutName = "Translation Widget Page",
			portlet = "General",
			siteURLKey = "test-site-name");

		PagesAdmin.addLocalizedName(
			localeList = "es-ES",
			localizedNameList = "P치gina de traducci칩n");

		Navigator.openSpecificURL(url = "${portalURL}/c/portal/logout");

		Navigator.openSpecificURL(url = "${portalURL}/web/test-site-name/translation-widget-page");

		TranslationsSEO.viewHreflangTags(localeList = "en_US,es_ES");

		TranslationsSEO.viewNoHreflangTags(localeList = "ar_SA,ca_ES,zh_CN,nl_NL,fi_FI,fr_FR,de_DE,hu_HU,ja_JP,pt_BR,sv_SE");

		Navigator.openSpecificURL(url = "${portalURL}/ja/web/test-site-name/translation-widget-page");

		Page.assertCanonicalURL(customCanonicalURL = "${portalURL}/web/test-site-name");

		Page.assertDefaultAlternateURL(portalURL = "${portalURL}/web/test-site-name");

		TranslationsSEO.viewHreflangTags(localeList = "en_US,es_ES");

		TranslationsSEO.viewNoHreflangTags(localeList = "ar_SA,ca_ES,zh_CN,nl_NL,fi_FI,fr_FR,de_DE,hu_HU,ja_JP,pt_BR,sv_SE");
	}

}