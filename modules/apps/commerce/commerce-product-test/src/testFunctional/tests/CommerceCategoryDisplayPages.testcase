@component-name = "portal-commerce"
definition {

	property portal.release = "quarantine";
	property portal.upstream = "quarantine";
	property testray.main.component.name = "Shopping Experience";

	setUp {
		TestCase.setUpPortalInstance();

		User.firstLoginPG();
	}

	tearDown {
		Navigator.openURL();

		if (IsElementPresent(locator1 = "UserBar#USER_AVATAR_IMAGE")) {
			User.logoutPG();
		}

		User.firstLoginPG();

		CommerceConfiguration.commerceAdminTearDown();

		CommerceConfiguration.commerceInitializersTearDown();
	}

	@description = "This is a test for COMMERCE-7843. Verify when a default page for categories is not set, the old logic is still applied"
	@priority = "3"
	test AssertOldLogicIsAppliedIfDefaultCategoryDisplayPageIsNotSet {
		task ("Given a Minium site") {
			CommerceAccelerators.initializeNewSiteViaAccelerator(siteName = "Minium");
		}

		task ("And given a Private Category Navigation page") {
			JSONLayout.addPrivateLayout(
				groupName = "Minium",
				layoutName = "Categories Navigation");

			JSONLayout.addWidgetToPrivateLayout(
				groupName = "Minium",
				layoutName = "Categories Navigation",
				widgetName = "Commerce Categories Navigation");

			ApplicationsMenu.gotoSite(site = "Minium");

			CommerceNavigator.gotoMiniumSidebarItem(menuItem = "Categories Navigation");

			Portlet.gotoPortletOptions(
				portletName = "Commerce Categories Navigation",
				portletOption = "Configuration");

			CommerceNavigator.changeVocabulary(newVocabularyName = "Minium");

			JSONLayout.addWidgetToPrivateLayout(
				groupName = "Minium",
				layoutName = "Categories Navigation",
				widgetName = "Category Content");
		}

		task ("When a user navigates to the page and clicks on a category") {
			Refresh();

			CommerceNavigator.selectItemFromTreeNode(itemName = "Engine");
		}

		task ("Then the user should be redirected to the Categories Navigation Widget") {
			ControlMenu.viewHeaderTitle(headerTitle = "Categories Navigation");

			AssertLocation.assertPartialLocation(value1 = "group/minium/g/engine");

			Portlet.viewBody(
				portletBody = "Engine",
				portletName = "Category Content");
		}
	}

	@description = "This is a test for COMMERCE-7846. Create a private widget page and a private content page with a Category Content widget and define it as the default category display page, and then try if it works on the front."
	@priority = "3"
	test ViewDefaultCategoryDisplayPageWithPrivateWidgetAndContentPages {
		task ("Given a Minium site") {
			CommerceAccelerators.initializeNewSiteViaAccelerator(siteName = "Minium");
		}

		task ("And given the Minium site has a private Categories Navigation page with a Commerce Categories Navigation widget") {
			JSONLayout.addPrivateLayout(
				groupName = "Minium",
				layoutName = "Categories Navigation",
				type = "content");

			ContentPagesNavigator.openEditContentPage(
				pageName = "Categories Navigation",
				privateLayout = "true",
				siteName = "Minium");

			PageEditor.addWidget(portletName = "Commerce Categories Navigation");

			Portlet.gotoPortletOptions(
				portletName = "Commerce Categories Navigation",
				portletOption = "Configuration");

			CommerceNavigator.changeVocabulary(newVocabularyName = "Minium");

			PageEditor.clickPublish();
		}

		task ("And given the Minium site has a private widget page with a Category Content widget") {
			JSONLayout.addPrivateLayout(
				groupName = "Minium",
				layoutName = "Test Private Widget Page");

			JSONLayout.addWidgetToPrivateLayout(
				groupName = "Minium",
				layoutName = "Test Private Widget Page",
				widgetName = "Category Content");
		}

		task ("And Given the widget page is set as the Default Category Display Page") {
			CommerceChannels.openChannelsAdmin();

			CommerceNavigator.gotoEntry(entryName = "Minium Portal");

			CommerceEntry.gotoMenuTab(menuTab = "Category Display Pages");

			CommerceChannels.chooseDefaultDisplayPage(
				pageName = "Test Private Widget Page",
				pageType = "Private");

			CommerceEntry.assertDefaultDisplayPage(
				pageName = "Test Private Widget Page",
				pageType = "Private");
		}

		task ("When I go to the Categories Navigation page and click on a category") {
			ApplicationsMenu.gotoSite(site = "Minium");

			CommerceNavigator.gotoMiniumSidebarItem(menuItem = "Categories Navigation");

			CommerceNavigator.selectItemFromTreeNode(itemName = "Transmission");
		}

		task ("Then I should be redirected to the private widget page with the category shown on the Category Content Widget") {
			ControlMenu.viewHeaderTitle(headerTitle = "Test Private Widget Page");

			AssertLocation.assertPartialLocation(value1 = "group/minium/g/transmission");

			Portlet.viewBody(
				portletBody = "Transmission",
				portletName = "Category Content");
		}

		task ("Given the Minium site has a private content page with a Category Content widget") {
			JSONLayout.addPrivateLayout(
				groupName = "Minium",
				layoutName = "Test Private Content Page",
				type = "content");

			ContentPagesNavigator.openEditContentPage(
				pageName = "Test Private Content Page",
				privateLayout = "true",
				siteName = "Minium");

			PageEditor.addWidget(portletName = "Category Content");

			PageEditor.clickPublish();
		}

		task ("And given the content page is set as the Default Category Display Page") {
			CommerceChannels.openChannelsAdmin();

			CommerceNavigator.gotoEntry(entryName = "Minium Portal");

			CommerceEntry.gotoMenuTab(menuTab = "Category Display Pages");

			CommerceChannels.chooseDefaultDisplayPage(
				pageName = "Test Private Content Page",
				pageType = "Private");

			CommerceEntry.assertDefaultDisplayPage(
				pageName = "Test Private Content Page",
				pageType = "Private");
		}

		task ("When I go to the Categories Navigation page and click on a category") {
			ApplicationsMenu.gotoSite(site = "Minium");

			CommerceNavigator.gotoMiniumSidebarItem(menuItem = "Categories Navigation");

			CommerceNavigator.selectItemFromTreeNode(itemName = "Brake System");
		}

		task ("Then I should be redirected to the private content page with the category shown on the Category Content widget") {
			ControlMenu.viewHeaderTitle(headerTitle = "Test Private Content Page");

			AssertLocation.assertPartialLocation(value1 = "group/minium/g/brake-system");

			AssertTextEquals(
				locator1 = "CommerceEntry#CATEGORY_CONTENT_TITLE",
				value1 = "Brake System");
		}
	}

	@description = "This is a test for COMMERCE-7844. Create a public widget page and a public content page with a Category Content widget and define it as the default category display page, and then try if it works on the front"
	@priority = "3"
	test ViewDefaultCategoryDisplayPageWithPublicWidgetAndContentPages {
		task ("Given a Minium site") {
			CommerceAccelerators.initializeNewSiteViaAccelerator(siteName = "Minium");
		}

		task ("And given the Minium site has a private Categories Navigation page with a Commerce Categories Navigation widget") {
			JSONLayout.addPrivateLayout(
				groupName = "Minium",
				layoutName = "Categories Navigation",
				type = "content");

			ContentPagesNavigator.openEditContentPage(
				pageName = "Categories Navigation",
				privateLayout = "true",
				siteName = "Minium");

			PageEditor.addWidget(portletName = "Commerce Categories Navigation");

			Portlet.gotoPortletOptions(
				portletName = "Commerce Categories Navigation",
				portletOption = "Configuration");

			CommerceNavigator.changeVocabulary(newVocabularyName = "Minium");

			PageEditor.clickPublish();
		}

		task ("And given the Minium site has a public widget page with a Category Content widget") {
			JSONLayout.addPublicLayout(
				groupName = "Minium",
				layoutName = "Test Public Widget Page");

			JSONLayout.addWidgetToPublicLayout(
				groupName = "Minium",
				layoutName = "Test Public Widget Page",
				widgetName = "Category Content");
		}

		task ("And Given the widget page is set as the Default Category Display Page") {
			CommerceChannels.openChannelsAdmin();

			CommerceNavigator.gotoEntry(entryName = "Minium Portal");

			CommerceEntry.gotoMenuTab(menuTab = "Category Display Pages");

			CommerceChannels.chooseDefaultDisplayPage(
				pageName = "Test Public Widget Page",
				pageType = "Public");

			CommerceEntry.assertDefaultDisplayPage(
				pageName = "Test Public Widget Page",
				pageType = "Public");
		}

		task ("When I go to the Categories Navigation page and click on a category") {
			ApplicationsMenu.gotoSite(site = "Minium");

			CommerceNavigator.gotoMiniumSidebarItem(menuItem = "Categories Navigation");

			CommerceNavigator.selectItemFromTreeNode(itemName = "Engine");
		}

		task ("Then I should be redirected to the public widget page with the category shown on the Category Content Widget") {
			ControlMenu.viewHeaderTitle(headerTitle = "Test Public Widget Page");

			AssertLocation.assertPartialLocation(value1 = "group/minium/g/engine");

			Portlet.viewBody(
				portletBody = "Engine",
				portletName = "Category Content");
		}

		task ("Given the Minium site has a public content page with a Category Content widget") {
			JSONLayout.addPublicLayout(
				groupName = "Minium",
				layoutName = "Test Public Content Page",
				type = "content");

			ContentPagesNavigator.openEditContentPage(
				pageName = "Test Public Content Page",
				siteName = "Minium");

			PageEditor.addWidget(portletName = "Category Content");

			PageEditor.clickPublish();
		}

		task ("And given the content page is set as the Default Category Display Page") {
			CommerceChannels.openChannelsAdmin();

			CommerceNavigator.gotoEntry(entryName = "Minium Portal");

			CommerceEntry.gotoMenuTab(menuTab = "Category Display Pages");

			CommerceChannels.chooseDefaultDisplayPage(
				pageName = "Test Public Content Page",
				pageType = "Public");

			CommerceEntry.assertDefaultDisplayPage(
				pageName = "Test Public Content Page",
				pageType = "Public");
		}

		task ("When I go to the Categories Navigation page and click on a category") {
			ApplicationsMenu.gotoSite(site = "Minium");

			CommerceNavigator.gotoMiniumSidebarItem(menuItem = "Categories Navigation");

			CommerceNavigator.selectItemFromTreeNode(itemName = "Suspension");
		}

		task ("Then I should be redirected to the public content page with the category shown on the Category Content widget") {
			ControlMenu.viewHeaderTitle(headerTitle = "Test Public Content Page");

			AssertLocation.assertPartialLocation(value1 = "group/minium/g/suspension");

			AssertTextEquals(
				locator1 = "CommerceEntry#CATEGORY_CONTENT_TITLE",
				value1 = "Suspension");
		}
	}

}