@component-name = "portal-commerce"
definition {

	property portal.release = "true";
	property portal.upstream = "true";
	property osgi.module.configuration.file.names = "com.liferay.commerce.shop.by.diagram.configuration.CSDiagramCPTypeConfiguration.config";
	property osgi.module.configurations = "enabled=&quot;true&quot;";
	property test.run.environment = "EE";
	property testray.main.component.name = "Product Info Management";

	setUp {
		TestCase.setUpPortalInstance();

		User.firstLoginPG();
	}

	tearDown {
		Navigator.openURL();

		if (IsElementPresent(locator1 = "UserBar#USER_AVATAR_IMAGE")) {
			User.logoutPG();
		}

		User.firstLoginPG();

		CommerceConfiguration.commerceAdminTearDown();

		CommerceConfiguration.commerceInitializersTearDown();
	}

	@description = "This is a test for COMMERCE-7025. AddANewPin"
	@priority = "3"
	test AddANewPin {
		task ("Given a diagram product with an image") {
			CommerceJSONProductsAPI._addCommerceProduct(
				catalogName = "Master",
				productName = "Diagram T-Shirt",
				productType = "diagram");

			CommerceProducts.openProductsAdmin();

			CommerceNavigator.searchEntry(entryName = "Diagram T-Shirt");

			CommerceNavigator.gotoEntry(entryName = "Diagram T-Shirt");

			CommerceEntry.gotoMenuTab(menuTab = "Diagram");

			CommerceProductImages.addDiagramImages(
				imageTitles = "Black",
				productType = "Diagram");

			Button.clickPublish();

			Alert.viewSuccessMessage();
		}

		task ("When a pin is added on the product") {
			CommerceDiagram.addPin(
				label = "Test",
				pinNumber = "1",
				quantity = "1",
				selectType = "Not Linked to a Catalog");
		}

		task ("Then the pin should be saved on the product diagram") {
			AssertElementPresent(
				key_pinNumber = "1",
				locator1 = "CommerceDiagrams#DIAGRAM_PIN");
		}
	}

	@description = "This is a test for COMMERCE-7019. AddDiagramProduct"
	@priority = "3"
	test AddDiagramProduct {
		CommerceNavigator.gotoPortlet(
			category = "Product Management",
			portlet = "Products");

		CPCommerceCatalog.newProductsEntry(
			productCatalog = "Master",
			productName = "Diagram T-Shirt",
			productType = "Diagram");

		CommerceEntry.viewProductsEntry(
			productFullDescription = "Diagram T-Shirt Full Description",
			productName = "Diagram T-Shirt",
			productShortDescription = "Diagram T-Shirt Short Description");

		CommerceEntry.gotoMenuTab(menuTab = "Diagram");

		Click(locator1 = "Icon#BACK");

		CommerceNavigator.searchEntry(entryName = "Diagram T-Shirt");

		AssertElementPresent(
			key_entryName = "Diagram T-Shirt",
			locator1 = "CommerceNavigation#ENTRY_NAME",
			value1 = "Diagram T-Shirt");

		CommerceNavigator.gotoEntry(entryName = "Diagram T-Shirt");
	}

	@description = "This is a test for COMMERCE-7100. Verify user can change a diagram product type to SVG."
	@priority = "3"
	test ChangeDiagramType {
		task ("Given a diagram product with an svg diagram image") {
			CommerceJSONProductsAPI._addCommerceProduct(
				catalogName = "Master",
				productName = "Diagram Product",
				productType = "diagram");

			CommerceProducts.openProductsAdmin();

			CommerceNavigator.gotoEntry(entryName = "Diagram Product");

			CommerceEntry.gotoMenuTab(menuTab = "Diagram");

			CommerceProductImages.addDiagramImages(
				imageTitles = "sbd",
				imageType = "svg",
				productType = "Diagram");

			Button.clickPublish();

			Alert.viewSuccessMessage();
		}

		task ("And given the Diagram Settings Type is Default") {
			CommerceDiagram.assertDiagramSettingsType(settingsType = "Default");

			CommerceDiagram.assertPinToolTipOpens();
		}

		task ("When I change the Diagram Settings Type to SVG and click on pin 1") {
			CommerceDiagram.changeSettingsType(settingsType = "SVG");

			CommerceDiagram.viewPin(pinNumber = "1");
		}

		task ("Then a tooltip opens with information on pin 1") {
			CommerceDiagram.assertPinToolTipInfo(
				pinNumber = "1",
				pinType = "SKU",
				quantity = "1");

			Click(
				key_text = "Cancel",
				locator1 = "Button#ANY_SECONDARY");

			Click(
				key_imageItem = "rect",
				locator1 = "CommerceDiagrams#DIAGRAM_IMAGE_SVG_SPECIFIC");

			CommerceDiagram.assertPinToolTipNotOpen();
		}
	}

	@description = "This is a test for COMMERCE-7023. EditTheDefaultSettingForPinsCreation"
	@priority = "3"
	test EditTheDefaultSettingForPinsCreation {
		property portal.acceptance = "quarantine";
		property portal.release = "quarantine";
		property portal.upstream = "quarantine";

		// TODO COMMERCE-8268 Test needs refactoring due to a failure locating the Default Diameter button.

		CommerceNavigator.gotoPortlet(
			category = "Product Management",
			portlet = "Products");

		CPCommerceCatalog.newProductsEntry(
			productCatalog = "Master",
			productName = "Diagram T-Shirt",
			productType = "Diagram");

		CommerceEntry.gotoMenuTab(menuTab = "Diagram");

		CommerceProductImages.addDiagramImages(
			imageTitles = "Black",
			productType = "Diagram");

		Button.clickPublish();

		Alert.viewSuccessMessage();

		CommerceDiagram.addPin(
			label = "Test",
			pinNumber = "1",
			quantity = "1",
			selectType = "Not Linked to a Catalog");

		Click(
			key_text = "Default Diameter",
			locator1 = "Button#ANY");

		AssertElementPresent(
			key_radius = "10",
			locator1 = "CommerceEntry#VERIFY_PIN_RADIUS");

		CommerceEntry.verifyCostumRadiusWithCostumValue(key_list = "1,2,3,4,5");

		AssertElementPresent(
			key_radius = "15",
			locator1 = "CommerceEntry#VERIFY_PIN_RADIUS");

		CommerceEntry.verifyCostumRadiusWithDefaultValue();

		CommerceEntry.changePinColor(color = "#FF0000");

		AssertElementPresent(
			key_strokeColor = "#FF0000",
			locator1 = "CommerceEntry#VERIFY_PIN_COLOR_STROKE");

		Button.clickPublish();
	}

	@description = "This is a test for COMMERCE-7126. RemoveDiagramProduct"
	@priority = "3"
	test RemoveDiagramProduct {
		CommerceNavigator.gotoPortlet(
			category = "Product Management",
			portlet = "Products");

		CPCommerceCatalog.newProductsEntry(
			productCatalog = "Master",
			productName = "Diagram T-Shirt",
			productType = "Diagram");

		CommerceEntry.viewProductsEntry(
			productFullDescription = "Diagram T-Shirt Full Description",
			productName = "Diagram T-Shirt",
			productShortDescription = "Diagram T-Shirt Short Description");

		CommerceEntry.gotoMenuTab(menuTab = "Diagram");

		Click(locator1 = "Icon#BACK");

		CommerceNavigator.searchEntry(entryName = "Diagram T-Shirt");

		ClickNoError(
			key_value = "Diagram T-Shirt",
			locator1 = "CommerceEntry#BODY_VERTICAL_ELLIPSIS");

		CommerceEntry.clickNoError(menuItem = "Delete");

		AssertElementNotPresent(
			key_entryName = "Diagram T-Shirt",
			locator1 = "CommerceNavigation#ENTRY_NAME",
			value1 = "Diagram T-Shirt");

		if (IsElementNotPresent(locator1 = "CommerceNavigation#EMPTY_SEARCH_CONTAINER")) {
			fail("Something went wrong after removing the product the page is not loading correctly");
		}

		if (IsElementPresent(locator1 = "Message#ERROR")) {
			fail("Something went wrong after removing the product an error message is present");
		}
	}

	@description = "This test asserts that a pin can be removed from a diagram product image"
	@priority = "3"
	test RemovePin {
		task ("Given a diagram product with an image") {
			CommerceNavigator.gotoPortlet(
				category = "Product Management",
				portlet = "Products");

			CPCommerceCatalog.newProductsEntry(
				productCatalog = "Master",
				productName = "Diagram T-Shirt",
				productType = "Diagram");

			CommerceEntry.gotoMenuTab(menuTab = "Diagram");

			CommerceProductImages.addDiagramImages(
				imageTitles = "Black",
				productType = "Diagram");

			Button.clickPublish();

			Alert.viewSuccessMessage();
		}

		task ("And given a pin on the product") {
			CommerceDiagram.addPin(
				label = "Test",
				pinNumber = "1",
				quantity = "1",
				selectType = "Not Linked to a Catalog");
		}

		task ("When the pin is deleted") {
			Click(
				key_pinNumber = "1",
				locator1 = "CommerceDiagrams#DIAGRAM_PIN");

			Button.clickDelete();

			Refresh();
		}

		task ("Then the pin should no longer be present") {
			AssertElementNotPresent(
				key_pinNumber = "1",
				locator1 = "CommerceDiagrams#DIAGRAM_PIN");
		}
	}

	@description = "This is a test for COMMERCE-7021. Upload an already mapped new image"
	@priority = "3"
	test UploadAlreadyMappedNewImage {
		task ("Given a diagram product with a svg diagram image.") {
			CommerceJSONProductsAPI._addCommerceProduct(
				catalogName = "Master",
				productName = "Diagram Product",
				productType = "diagram");

			CommerceProducts.openProductsAdmin();

			CommerceNavigator.gotoEntry(entryName = "Diagram Product");

			CommerceEntry.gotoMenuTab(menuTab = "Diagram");

			CommerceProductImages.addDiagramImages(
				imageTitles = "sbd",
				imageType = "svg",
				productType = "Diagram");

			Button.clickPublish();

			Alert.viewSuccessMessage();
		}

		task ("When the Diagram Settings Type is put to SVG and on pin 1") {
			CommerceDiagram.changeSettingsType(settingsType = "SVG");

			CommerceDiagram.viewPin(pinNumber = "1");
		}

		task ("Then verify position is 1 and type is "Linked to a SKU"") {
			CommerceDiagram.assertPinToolTipInfo(
				pinNumber = "1",
				pinType = "SKU",
				quantity = "1");

			Click(
				key_text = "Cancel",
				locator1 = "Button#ANY_SECONDARY");
		}
	}

	@description = "This is a test for COMMERCE-7020. UploadANotMappedNewImage"
	@priority = "3"
	test UploadANotMappedNewImage {
		CommerceNavigator.gotoPortlet(
			category = "Product Management",
			portlet = "Products");

		CPCommerceCatalog.newProductsEntry(
			productCatalog = "Master",
			productName = "Diagram T-Shirt",
			productType = "Diagram");

		CommerceEntry.gotoMenuTab(menuTab = "Diagram");

		CommerceProductImages.addDiagramImages(
			imageTitles = "Black",
			productType = "Diagram");

		Button.clickPublish();

		Alert.viewSuccessMessage();

		WaitForElementPresent(locator1 = "CommerceDiagrams#DIAGRAM_IMAGE");

		AssertElementNotPresent(
			key_color = "#ffffff",
			locator1 = "CommerceEntry#VERIFY_PIN");

		Refresh();

		AssertElementPresent(locator1 = "CommerceDiagrams#DIAGRAM_IMAGE");
	}

	@description = "This is a test for COMMERCE-7036. UseDragForPin"
	@priority = "3"
	test UseDragForPin {
		CommerceJSONProductsAPI._addCommerceProduct(
			catalogName = "Master",
			productName = "Diagram T-Shirt",
			productType = "diagram");

		CommerceProducts.openProductsAdmin();

		CommerceNavigator.gotoEntry(entryName = "Diagram T-Shirt");

		CommerceEntry.gotoMenuTab(menuTab = "Diagram");

		CommerceProductImages.addDiagramImages(
			imageTitles = "Black",
			productType = "Diagram");

		Button.clickPublish();

		Alert.viewSuccessMessage();

		Refresh();

		CommerceDiagram.addPin(
			label = "Test",
			pinNumber = "1",
			quantity = "1",
			selectType = "Not Linked to a Catalog");

		DragAndDrop.dragAndDropReorganizeObjects(
			key_pinNumber = "1",
			locator1 = "CommerceDiagrams#DIAGRAM_PIN",
			locator2 = "CommerceDiagrams#DIAGRAM_IMAGE",
			position = "300,0");
	}

	@description = "Verify user can use the pin tooltip to edit the information of a diagram product image pin"
	@priority = "3"
	test UseToolTip {
		task ("Given a Minium site") {
			CommerceAccelerators.initializeNewSiteViaAccelerator(siteName = "Minium");
		}

		task ("And given a diagram product with a diagram image") {
			CommerceJSONProductsAPI._addCommerceProduct(
				catalogName = "Master",
				productName = "Diagram T-Shirt",
				productType = "diagram");

			CommerceProducts.openProductsAdmin();

			CommerceNavigator.searchEntry(entryName = "Diagram T-Shirt");

			CommerceNavigator.gotoEntry(entryName = "Diagram T-Shirt");

			CommerceEntry.gotoMenuTab(menuTab = "Diagram");

			CommerceProductImages.addDiagramImages(
				imageTitles = "Black",
				productType = "Diagram");

			Button.clickPublish();

			Alert.viewSuccessMessage();

			Refresh();
		}

		task ("And given a pin on the product") {
			CommerceDiagram.addPin(
				label = "Test",
				pinNumber = "1",
				quantity = "1",
				selectType = "Not Linked to a Catalog");
		}

		task ("When I change the pin information") {
			CommerceDiagram.changePinInfo(
				newPinNumber = "2",
				pinNumber = "1",
				quantity = "2",
				selectType = "Linked to a SKU",
				sku = "MIN93015");
		}

		task ("Then I should be able to see the updated pin information when I click on the pin") {
			CommerceDiagram.viewPin(pinNumber = "2");

			CommerceDiagram.assertPinToolTipInfo(
				pinNumber = "2",
				pinType = "SKU",
				quantity = "2",
				sku = "MIN93015");
		}
	}

	@description = "This is a test for COMMERCE-7035. UseZoomAndDragBackground"
	@priority = "3"
	test UseZoomAndDragBackground {
		property portal.acceptance = "quarantine";
		property portal.release = "quarantine";
		property portal.upstream = "quarantine";

		// TODO COMMERCE-8269 Test needs refactoring due to a failure locating the Reset Zoom button.

		CommerceNavigator.gotoPortlet(
			category = "Product Management",
			portlet = "Products");

		CPCommerceCatalog.newProductsEntry(
			productCatalog = "Master",
			productName = "Diagram T-Shirt",
			productType = "Diagram");

		CommerceEntry.gotoMenuTab(menuTab = "Diagram");

		CommerceProductImages.addDiagramImages(
			imageTitles = "Black",
			productType = "Diagram");

		Button.clickPublish();

		Alert.viewSuccessMessage();

		CommerceDiagram.usePreDefinedZoom(key_list = "50%,75%,100%,125%,150%,175%,200%");

		CommerceDiagram.useDragOnBackground(
			image_name = "Commerce_Black",
			position = "300,0");
	}

	@description = "This test asserts that the Mapped Products Table saves all info from pins"
	@priority = "3"
	test VerifyMappedProductsTableSavesAllPins {
		task ("Given a minium site") {
			CommerceAccelerators.initializeNewSiteViaAccelerator(siteName = "Minium");
		}

		task ("And Given two diagram products") {
			CommerceJSONProductsAPI._addCommerceProduct(
				catalogName = "Master",
				productName = "Diagram T-Shirt 2",
				productType = "diagram");

			CommerceJSONProductsAPI._addCommerceProduct(
				catalogName = "Master",
				productName = "Diagram T-Shirt",
				productType = "diagram");

			CommerceProducts.openProductsAdmin();

			CommerceNavigator.searchEntry(entryName = "Diagram T-Shirt");

			CommerceNavigator.gotoEntry(entryName = "Diagram T-Shirt");

			CommerceEntry.gotoMenuTab(menuTab = "Diagram");

			CommerceProductImages.addDiagramImages(
				imageTitles = "Black",
				productType = "Diagram");

			Button.clickPublish();

			Alert.viewSuccessMessage();
		}

		task ("When multiple pins are added to a diagram with an image") {
			CommerceDiagram.addPin(
				label = "Test",
				pinNumber = "1",
				position = "0,150",
				quantity = "1",
				selectType = "Not Linked to a Catalog");

			CommerceDiagram.addPin(
				pinNumber = "2",
				position = "150,100",
				quantity = "1",
				selectType = "Linked to a SKU",
				sku = "MIN93015");

			CommerceDiagram.addPin(
				diagram = "Diagram T-Shirt 2",
				pinNumber = "3",
				quantity = "1",
				selectType = "Linked to a Diagram");
		}

		task ("Then the Mapped Products table should save information for all pins") {
			for (var mappedProducts : list "Test,MIN93015,Diagram T-Shirt 2") {
				AssertElementPresent(
					key_productName = "${mappedProducts}",
					locator1 = "CommerceDiagrams#DIAGRAM_MAPPED_PRODUCTS_CONTENT");
			}
		}
	}

}