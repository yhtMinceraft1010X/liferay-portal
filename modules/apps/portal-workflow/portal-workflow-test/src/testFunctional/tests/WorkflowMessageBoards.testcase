@component-name = "portal-workflow"
definition {

	property portal.release = "true";
	property portal.upstream = "true";
	property testray.main.component.name = "Workflow";

	var pageName = "Message Boards Page";
	var portletName = "Message Boards";
	var workflowAssetType = "Message Boards Message";

	setUp {
		TestCase.setUpPortalInstance();

		User.firstLoginPG();

		Navigator.openURL();

		ProductMenu.gotoPortlet(
			category = "Configuration",
			panel = "Site Administration",
			portlet = "Workflow");

		Workflow.configureWorkflow(
			workflowDefinition = "Single Approver",
			workflowResourceValue = "Message Boards Message");

		ProductMenu.gotoPortlet(
			category = "Site Builder",
			panel = "Site Administration",
			portlet = "Pages");

		PagesAdmin.addPage(
			pageName = "Message Boards Page",
			sitePageType = "Widget Page");

		PagesAdmin.configurePage(
			columnName = "1 Column",
			pageName = "Message Boards Page",
			sitePageType = "Widget Page");

		Navigator.gotoPage(pageName = "Message Boards Page");

		Portlet.addPG(portletName = "Message Boards");
	}

	tearDown {
		var testPortalInstance = PropsUtil.get("test.portal.instance");

		if ("${testPortalInstance}" == "true") {
			PortalInstances.tearDownCP();
		}
		else {
			MessageboardsCategory.tearDownCP();

			MessageboardsThread.tearDownCP();

			MessageboardsPortlet.tearDownConfigurationGeneralCP();

			PagesAdmin.tearDownCP();

			Workflow.tearDownPortalWorkflow();

			Workflow.tearDownSiteWorkflow();
		}
	}

	@ignore = "true"
	@priority = "4"
	test ApproveMessageBoardsThreadPG {
		Navigator.gotoPage(pageName = "Message Boards Page");

		MessageboardsThread.addWithWorkflowPG(
			threadBody = "MB Thread Body",
			threadSubject = "MB Thread Subject");

		Navigator.gotoPage(pageName = "Message Boards Page");

		MessageboardsThread.viewWithWorkflowPG(
			threadBody = "MB Thread Body",
			threadSubject = "MB Thread Subject",
			workflowTask = "Review");

		UserBar.gotoDropdownItem(dropdownItem = "My Workflow Tasks");

		Workflow.assignToMeTaskByActions(
			workflowAssetTitle = "MB Thread Subject",
			workflowAssetType = "Message Boards Message",
			workflowTask = "Review");

		Navigator.gotoPage(pageName = "Message Boards Page");

		MessageboardsThread.viewWithWorkflowPG(
			pageName = "Message Boards Page",
			threadBody = "MB Thread Body",
			threadSubject = "MB Thread Subject",
			workflowTask = "Review");

		UserBar.gotoDropdownItem(dropdownItem = "My Workflow Tasks");

		Workflow.approveTaskByActions(
			workflowAssetTitle = "MB Thread Subject",
			workflowAssetType = "Message Boards Message",
			workflowTask = "Review");

		Navigator.gotoPage(pageName = "Message Boards Page");

		MessageboardsThread.gotoPG(
			lastPostUserName = "Test Test",
			replyCount = "0",
			threadBody = "MB Thread Body",
			threadSubject = "MB Thread Subject");

		MessageboardsThread.viewPG(
			threadBody = "MB Thread Body",
			threadSubject = "MB Thread Subject");

		Navigator.openURL();

		UserBar.gotoDropdownItem(dropdownItem = "My Submissions");

		Workflow.gotoViewCompletedAssetViaMySubmissions(workflowAssetTitle = "MB Thread Subject");

		MessageboardsThread.viewAsWorkflowAsset(
			threadBody = "MB Thread Body",
			threadSubject = "MB Thread Subject");

		MessageboardsThread.gotoViewInContext();

		MessageboardsThread.viewPG(
			threadBody = "MB Thread Body",
			threadSubject = "MB Thread Subject");
	}

	@description = "This is a test for LRQA-69700. Verify that the user can approve a thread."
	@priority = "4"
	test CanApproveAThread {
		ProductMenu.gotoPortlet(
			category = "Content & Data",
			panel = "Site Administration",
			portlet = "Message Boards");

		MessageboardsThread.addWithWorkflowCP(
			threadBody = "Thread Message Body",
			threadSubject = "Thread Message Subject");

		UserBar.gotoDropdownItem(dropdownItem = "My Workflow Tasks");

		Workflow.assignToMeTaskByActions(
			workflowAssetTitle = "Thread Message Subject",
			workflowAssetType = "Message Boards Message",
			workflowTask = "Review");

		Workflow.approveTaskByActions(
			workflowAssetTitle = "Thread Message Subject",
			workflowAssetType = "Message Boards Message",
			workflowTask = "Review");

		ProductMenu.gotoPortlet(
			category = "Content & Data",
			panel = "Site Administration",
			portlet = "Message Boards");

		Workflow.viewWorkflowStatus(status = "Approved");
	}

	@description = "This is a test for LRQA-70151. Verify that the user cannot reply to a Pending Thread."
	@priority = "3"
	test CannotReplyToPendingThread {
		ProductMenu.gotoPortlet(
			category = "Content & Data",
			panel = "Site Administration",
			portlet = "Message Boards");

		MessageboardsThread.addWithWorkflowCP(
			threadBody = "Thread Message Body",
			threadSubject = "Thread Message Subject");

		Navigator.gotoBack();

		Workflow.viewWorkflowStatus(status = "Pending");

		MessageboardsThread.gotoCP(threadSubject = "Thread Message Subject");

		MessageboardsThread.replyNotPresent();
	}

	@description = "This is a test for LRQA-69701. Verify that the user can reject a thread."
	@priority = "4"
	test CanRejectAThread {
		ProductMenu.gotoPortlet(
			category = "Content & Data",
			panel = "Site Administration",
			portlet = "Message Boards");

		MessageboardsThread.addWithWorkflowCP(
			threadBody = "Thread Message Body",
			threadSubject = "Thread Message Subject");

		UserBar.gotoDropdownItem(dropdownItem = "My Workflow Tasks");

		Workflow.assignToMeTaskByActions(
			workflowAssetTitle = "Thread Message Subject",
			workflowAssetType = "Message Boards Message",
			workflowTask = "Review");

		Workflow.rejectTaskByActions(
			workflowAssetTitle = "Thread Message Subject",
			workflowAssetType = "Message Boards Message",
			workflowTask = "Review");

		ProductMenu.gotoPortlet(
			category = "Content & Data",
			panel = "Site Administration",
			portlet = "Message Boards");

		Workflow.viewWorkflowStatus(status = "Pending");
	}

	@description = "This is a test for LRQA-69703. Verify that user can reply to an approved thread."
	@priority = "5"
	test CanReplyToApprovedThread {
		ProductMenu.gotoPortlet(
			category = "Content & Data",
			panel = "Site Administration",
			portlet = "Message Boards");

		MessageboardsThread.addWithWorkflowCP(
			threadBody = "Thread Message Body",
			threadSubject = "Thread Message Subject");

		UserBar.gotoDropdownItem(dropdownItem = "My Workflow Tasks");

		Workflow.assignToMeTaskByActions(
			workflowAssetTitle = "Thread Message Subject",
			workflowAssetType = "Message Boards Message",
			workflowTask = "Review");

		Workflow.approveTaskByActions(
			workflowAssetTitle = "Thread Message Subject",
			workflowAssetType = "Message Boards Message",
			workflowTask = "Review");

		ProductMenu.gotoPortlet(
			category = "Content & Data",
			panel = "Site Administration",
			portlet = "Message Boards");

		Workflow.viewWorkflowStatus(status = "Approved");

		MessageboardsThread.gotoCP(threadSubject = "Thread Message Subject");

		MessageboardsThread.replyCP(
			threadReplyBody = "Message Boards Body Reply",
			threadSubject = "Thread Message Subject");

		Button.clickSubmitForPublication();

		Workflow.viewWorkflowStatus(status = "Pending");
	}

	@description = "This is a test for LRQA-70153. Verify that the user can review the thread through My Workflow Tasks."
	@priority = "3"
	test CanReviewThreadThroughMyWorkflowTasks {
		ProductMenu.gotoPortlet(
			category = "Content & Data",
			panel = "Site Administration",
			portlet = "Message Boards");

		MessageboardsThread.addWithWorkflowCP(
			threadBody = "Thread Message Body",
			threadSubject = "Thread Message Subject");

		UserBar.gotoDropdownItem(dropdownItem = "My Workflow Tasks");

		Workflow.assignToMeTaskByActions(
			workflowAssetTitle = "Thread Message Subject",
			workflowAssetType = "Message Boards Message",
			workflowTask = "Review");

		Workflow.gotoTaskDetails(workflowAssetTitle = "Thread Message Subject");

		MyWorkflowTasks.viewTaskAssignedAndAssertTextsPresent(
			nameAuthor = "Test Test",
			titleMessageBoard = "Thread Message Subject");
	}

	@ignore = "true"
	@priority = "4"
	test RejectMessageBoardsThreadPG {
		property test.name.skip.portal.instance = "WorkflowadminPGMessageboards#RejectMessageBoardsThreadPG";

		Navigator.gotoPage(pageName = "Message Boards Page");

		MessageboardsThread.addWithWorkflowPG(
			pageName = "Message Boards Page",
			threadBody = "MB Thread Body",
			threadSubject = "MB Thread Subject");

		UserBar.gotoDropdownItem(dropdownItem = "My Workflow Tasks");

		Workflow.assignToMeTaskByActions(
			workflowAssetTitle = "MB Thread Subject",
			workflowAssetType = "Message Boards Message",
			workflowTask = "Review");

		UserBar.gotoDropdownItem(dropdownItem = "My Workflow Tasks");

		Workflow.rejectTaskByActions(
			workflowAssetTitle = "MB Thread Subject",
			workflowAssetType = "Message Boards Message",
			workflowTask = "Review");

		UserBar.gotoDropdownItem(dropdownItem = "My Workflow Tasks");

		Workflow.viewRejectedTask(
			workflowAssetTitle = "MB Thread Subject",
			workflowAssetType = "Message Boards Message");

		Navigator.gotoPage(pageName = "Message Boards Page");

		MessageboardsThread.viewWithWorkflowPG(
			pageName = "Message Boards Page",
			threadBody = "MB Thread Body",
			threadSubject = "MB Thread Subject",
			workflowTask = "Update");
	}

	@description = "This is a test for LRQA-70152. Verify that the reply to a thread has workflow associated."
	@priority = "4"
	test ReplyHasWorkflow {
		ProductMenu.gotoPortlet(
			category = "Content & Data",
			panel = "Site Administration",
			portlet = "Message Boards");

		MessageboardsThread.addWithWorkflowCP(
			threadBody = "Thread Message Body",
			threadSubject = "Thread Message Subject");

		UserBar.gotoDropdownItem(dropdownItem = "My Workflow Tasks");

		Workflow.assignToMeTaskByActions(
			workflowAssetTitle = "Thread Message Subject",
			workflowAssetType = "Message Boards Message",
			workflowTask = "Review");

		Workflow.approveTaskByActions(
			workflowAssetTitle = "Thread Message Subject",
			workflowAssetType = "Message Boards Message",
			workflowTask = "Review");

		ProductMenu.gotoPortlet(
			category = "Content & Data",
			panel = "Site Administration",
			portlet = "Message Boards");

		Workflow.viewWorkflowStatus(status = "Approved");

		MessageboardsThread.gotoCP(threadSubject = "Thread Message Subject");

		MessageboardsThread.replyCP(
			threadReplyBody = "Message Boards Body Reply",
			threadSubject = "Thread Message Subject");

		Button.clickSubmitForPublication();

		Workflow.viewWorkflowStatus(status = "Pending");

		UserBar.gotoDropdownItem(dropdownItem = "My Workflow Tasks");

		Workflow.assignToMeTaskByActions(
			workflowAssetTitle = "RE: Thread Message Subject",
			workflowAssetType = "Message Boards Message",
			workflowTask = "Review");

		Workflow.approveTaskByActions(
			workflowAssetTitle = "RE: Thread Message Subject",
			workflowAssetType = "Message Boards Message",
			workflowTask = "Review");

		ProductMenu.gotoPortlet(
			category = "Content & Data",
			panel = "Site Administration",
			portlet = "Message Boards");

		Workflow.viewWorkflowStatus(status = "Approved");

		MessageboardsThread.gotoCP(threadSubject = "Thread Message Subject");

		Workflow.viewNotPresentWorkflowStatus(statusNotPresent = "Pending");
	}

	@description = "This is a test for LRQA-70150. Verify that the user can edit an approved thread and the status returns to Pending."
	@priority = "3"
	test StatusReturnstoPendingWhenApprovedThreadIsEdited {
		ProductMenu.gotoPortlet(
			category = "Content & Data",
			panel = "Site Administration",
			portlet = "Message Boards");

		MessageboardsThread.addWithWorkflowCP(
			threadBody = "Thread Message Body",
			threadSubject = "Thread Message Subject");

		UserBar.gotoDropdownItem(dropdownItem = "My Workflow Tasks");

		Workflow.assignToMeTaskByActions(
			workflowAssetTitle = "Thread Message Subject",
			workflowAssetType = "Message Boards Message",
			workflowTask = "Review");

		Workflow.approveTaskByActions(
			workflowAssetTitle = "Thread Message Subject",
			workflowAssetType = "Message Boards Message",
			workflowTask = "Review");

		ProductMenu.gotoPortlet(
			category = "Content & Data",
			panel = "Site Administration",
			portlet = "Message Boards");

		Workflow.viewWorkflowStatus(status = "Approved");

		MessageboardsThread.editCP(
			threadBodyEdit = "Thread Message Body Edit",
			threadSubjectEdit = "Thread Message Subject Edit");

		PortletEntry.submitForPublication();

		Workflow.viewWorkflowStatus(status = "Pending");
	}

	@description = "This is a test for LRQA-69694. Verify that the process status is Pending when the user creates a new thread with workflow associated."
	@priority = "4"
	test WorkflowPendingForMessageBoard {
		ProductMenu.gotoPortlet(
			category = "Content & Data",
			panel = "Site Administration",
			portlet = "Message Boards");

		MessageboardsThread.addWithWorkflowCP(
			threadBody = "Thread Message Body",
			threadSubject = "Thread Message Subject");

		UserBar.gotoDropdownItem(dropdownItem = "My Workflow Tasks");

		Navigator.gotoNavTab(navTab = "Assigned to My Roles");

		Workflow.viewPendingTasks(
			workflowAssetTitle = "Thread Message Subject",
			workflowAssetType = "Message Boards Message",
			workflowTask = "Review");

		Workflow.gotoCompletedTaskAssignedToMePreview(workflowAssetTitle = "Thread Message Subject");

		Workflow.viewWorkflowStatus(status = "Pending");
	}

}