@component-name = "portal-object"
definition {

	property portal.acceptance = "true";
	property portal.release = "true";
	property portal.upstream = "true";
	property testray.main.component.name = "Object";

	setUp {
		TestCase.setUpPortalInstance();

		User.firstLoginPG();
	}

	tearDown {
		var testPortalInstance = PropsUtil.get("test.portal.instance");

		if ("${testPortalInstance}" == "true") {
			PortalInstances.tearDownCP();
		}
		else {
			ObjectAdmin.deleteAllCustomObjectsViaAPI();
		}
	}

	@description = "LPS-135397 - Verify that it is not possible to save another Layout as default"
	@priority = "4"
	test CannotSaveAnotherLayoutAsDefault {
		ObjectAdmin.addObjectViaAPI(
			labelName = "Custom Object 200",
			objectName = "CustomObject200",
			pluralLabelName = "Custom Objects 200");

		ObjectAdmin.addObjectFieldViaAPI(
			fieldBusinessType = "Text",
			fieldLabelName = "Custom Field",
			fieldName = "customField",
			fieldType = "String",
			isRequired = "false",
			objectName = "CustomObject200");

		for (var letter : list "A,B") {
			ObjectAdmin.addObjectLayoutViaAPI(
				layoutName = "Layout ${letter}",
				objectName = "CustomObject200");
		}

		ObjectAdmin.openObjectAdmin();

		ObjectPortlet.selectCustomObject(label = "Custom Object 200");

		ObjectAdmin.goToLayoutsTab();

		ObjectAdmin.selectSpecificKebabMenuOption(
			kebabOption = "View",
			objectLabel = "Layout A");

		ObjectAdmin.markLayoutAsDefault();

		ObjectAdmin.goToLayoutTabOnLayouts();

		ObjectAdmin.addTabFieldsOnLayout(
			blockName = "Block 1",
			columnsNumber = "1",
			fieldLabelName = "Custom Field",
			tabName = "Field Tab");

		Button.clickSave();

		Refresh();

		WaitForSPARefresh();

		ObjectAdmin.selectSpecificKebabMenuOption(
			kebabOption = "View",
			objectLabel = "Layout B");

		ObjectAdmin.markLayoutAsDefault();

		ObjectAdmin.goToLayoutTabOnLayouts();

		ObjectAdmin.addTabFieldsOnLayout(
			blockName = "Block 2",
			columnsNumber = "1",
			fieldLabelName = "Custom Field",
			tabName = "Field Tab");

		Button.clickSave();

		AssertTextEquals(
			value1 = "Error:There can only be one default object layout",
			locator1 = "Message#ERROR");
	}

	@description = "LPS-146808 - Verify it is possibel to view a Date entry and label"
	@priority = "4"
	test CanViewDateEntry {
		ObjectAdmin.addObjectViaAPI(
			labelName = "Custom Object 200",
			objectName = "CustomObject200",
			pluralLabelName = "Custom Objects 200");

		ObjectAdmin.addObjectFieldViaAPI(
			fieldBusinessType = "Date",
			fieldLabelName = "Custom Field",
			fieldName = "customObjectField",
			fieldType = "Date",
			isRequired = "false",
			objectName = "CustomObject200");

		ObjectAdmin.addObjectLayoutViaAPI(
			layoutName = "Layout Name",
			objectName = "CustomObject200");

		ObjectAdmin.publishObjectViaAPI(objectName = "CustomObject200");

		ObjectAdmin.addObjectSingleFieldEntryViaAPI(
			fieldName = "customObjectField",
			objectName = "CustomObject200",
			value = "2022-01-01");

		ObjectAdmin.goToCustomObject(objectName = "CustomObject200");

		ObjectPortlet.viewEntryDetails(entry = "Jan 1, 2022");

		ObjectPortlet.assertEntryDetailsLabelPresent(entryLabel = "Custom Field");

		ObjectPortlet.assertEntryDetailsValuePresent(entryValue = "01/01/2022");
	}

}