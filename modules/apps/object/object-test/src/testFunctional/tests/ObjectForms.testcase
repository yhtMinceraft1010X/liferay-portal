@component-name = "portal-object"
definition {

	property portal.acceptance = "true";
	property portal.release = "true";
	property portal.upstream = "true";
	property testray.main.component.name = "Object";

	setUp {
		TestCase.setUpPortalInstance();

		User.firstLoginPG();
	}

	tearDown {
		var testPortalInstance = PropsUtil.get("test.portal.instance");

		if ("${testPortalInstance}" == "true") {
			PortalInstances.tearDownCP();
		}
		else {
			ObjectAdmin.deleteAllCustomObjectsViaAPI();
		}
	}

	@description = "LPS-139472 - Verify that 'Allow Multiple Selections' option is not available for Select From List field when a form is mapped to an object"
	@priority = "4"
	test AllowMultipleSelectionsNotAvailable {
		Picklist.addPicklistViaAPI(picklistName = "Picklist Test");

		ObjectAdmin.addObjectViaAPI(
			labelName = "Custom Object 207",
			objectName = "CustomObject207",
			pluralLabelName = "Custom Objects 207");

		ObjectAdmin.openObjectAdmin();

		ObjectPortlet.selectCustomObject(label = "Custom Object 207");

		ObjectAdmin.goToFieldsTab();

		ObjectAdmin.addObjectFieldViaUI(
			fieldLabel = "Field Picklist",
			fieldPicklist = "Picklist Test",
			fieldType = "Picklist");

		Navigator.openURL();

		ObjectAdmin.publishObjectViaAPI(objectName = "CustomObject207");

		FormsAdmin.addForm();

		PopulateObjectFormsConfiguration.openFormSettings();

		PopulateObjectFormsConfiguration.selectObject(label = "Object");

		PopulateObjectFormsConfiguration.selectCustomObject(label = "Custom Object 207");

		Form.gotoAddField(fieldType = "Select from List");

		PopulateObjectFormsConfiguration.mapFormObjectField(
			fieldLabelName = "Field Picklist",
			formObjectField = "Choose an Option");

		Form.gotoAdvancedTab();

		AssertNotVisible(
			key_toggleSwitch = "Allow Multiple Selections",
			locator1 = "ToggleSwitch#ENABLE_TOGGLE_SWITCH_VIA_SPAN");
	}

	@description = "LPS-139464 - Verify that it is possible to delete a form mapped to an object after adding entries on it"
	@priority = "4"
	test CanDeleteFormMappedToObject {
		ObjectAdmin.addObjectViaAPI(
			labelName = "Custom Object 208",
			objectName = "CustomObject208",
			pluralLabelName = "Custom Objects 208");

		ObjectAdmin.addObjectFieldViaAPI(
			fieldBusinessType = "Text",
			fieldLabelName = "Custom Field",
			fieldName = "customObjectField",
			fieldType = "String",
			isRequired = "false",
			objectName = "CustomObject208");

		ObjectAdmin.publishObjectViaAPI(objectName = "CustomObject208");

		FormsAdmin.addForm();

		Form.editName(formName = "Form 1");

		PopulateObjectFormsConfiguration.openFormSettings();

		PopulateObjectFormsConfiguration.selectObject(label = "Object");

		PopulateObjectFormsConfiguration.selectCustomObject(label = "Custom Object 208");

		Form.gotoAddField(fieldType = "Text");

		PopulateObjectFormsConfiguration.mapFormField(fieldLabelName = "Custom Field");

		Form.publishForm();

		FormsAdminNavigator.gotoPublishedForm();

		FormViewBuilder.editText(
			fieldName = "Text",
			fieldValue = "Entry Test");

		FormPortlet.submitSuccessfully();

		Navigator.openURL();

		PopulateObjectFormsConfiguration.openForm();

		FormsAdmin.deleteForm(formName = "Form 1");

		AssertElementNotPresent(
			key_formName = "Form 1",
			locator1 = "FormsAdmin#FORMS_TABLE_NAME");
	}

	@description = "LPS-143064 - Verify that, when using objects as storage type, the user must be able to map to this field type to Rich Text Field and view its entries"
	@priority = "4"
	test CanMapAndViewEntriesForRichTextField {
		ObjectAdmin.addObjectViaAPI(
			labelName = "Custom Object 151597",
			objectName = "CustomObject151597",
			pluralLabelName = "Custom Objects 151597");

		ObjectAdmin.addObjectFieldViaAPI(
			fieldBusinessType = "RichText",
			fieldLabelName = "Custom Field",
			fieldName = "customObjectField",
			fieldType = "Clob",
			isRequired = "false",
			objectName = "CustomObject151597");

		ObjectAdmin.publishObjectViaAPI(objectName = "CustomObject151597");

		FormsAdmin.addForm();

		Form.editName(formName = "Form 1");

		PopulateObjectFormsConfiguration.openFormSettings();

		PopulateObjectFormsConfiguration.selectObject(label = "Object");

		PopulateObjectFormsConfiguration.selectCustomObject(label = "Custom Object 151597");

		Form.gotoAddField(fieldType = "Rich Text");

		PopulateObjectFormsConfiguration.mapFormField(fieldLabelName = "Custom Field");

		Form.publishForm();

		FormsAdminNavigator.gotoPublishedForm();

		FormFields.addRichTextContent(
			content = "By building a vibrant business, making technology useful, and investing in communities, we make it possible for people to reach their full potential to serve others.",
			fieldLabel = "Rich Text");

		FormPortlet.submitSuccessfully();

		Navigator.openURL();

		ObjectAdmin.goToCustomObject(objectName = "CustomObject151597");

		ObjectPortlet.viewEntry(entry = "By building a vibrant business, making technology useful, and investing in communities, we make it possible for people to reach their full potential to serve others.");
	}

	@description = "LPS-142659 - Verify if it's possible to map an Object field of Clob type and view its entries (Multiple Lines)"
	@priority = "4"
	test CanMapClobTypeAndViewEntriesMultipleLines {
		ObjectAdmin.addObjectViaAPI(
			labelName = "Custom Object 209",
			objectName = "CustomObject209",
			pluralLabelName = "Custom Objects 209");

		ObjectAdmin.addObjectFieldViaAPI(
			fieldBusinessType = "LongText",
			fieldLabelName = "Custom Field",
			fieldName = "customObjectField",
			fieldType = "Clob",
			isRequired = "false",
			objectName = "CustomObject209");

		ObjectAdmin.publishObjectViaAPI(objectName = "CustomObject209");

		FormsAdmin.addForm();

		Form.editName(formName = "Form 1");

		PopulateObjectFormsConfiguration.openFormSettings();

		PopulateObjectFormsConfiguration.selectObject(label = "Object");

		PopulateObjectFormsConfiguration.selectCustomObject(label = "Custom Object 209");

		Form.gotoAddField(fieldType = "Text");

		FormFields.selectTextFieldLineOption(textFieldLineOption = "Multiple Lines");

		PopulateObjectFormsConfiguration.mapFormField(fieldLabelName = "Custom Field");

		Form.publishForm();

		FormsAdminNavigator.gotoPublishedForm();

		FormViewBuilder.editClobText(fieldLong = "By building a vibrant business, making technology useful, and investing in communities, we make it possible for people to reach their full potential to serve others.");

		FormPortlet.submitSuccessfully();

		Navigator.openURL();

		ObjectAdmin.goToCustomObject(objectName = "CustomObject209");

		ObjectPortlet.viewEntry(entry = "By building a vibrant business, making technology useful, and investing in communities, we make it possible for people to reach their full potential to serve others.");
	}

	@description = "LPS-142659 - Verify if it's possible to map an Object field of Clob type and view its entries (Single Line)"
	@priority = "4"
	test CanMapClobTypeAndViewEntriesSingleLine {
		ObjectAdmin.addObjectViaAPI(
			labelName = "Custom Object 210",
			objectName = "CustomObject210",
			pluralLabelName = "Custom Objects 210");

		ObjectAdmin.addObjectFieldViaAPI(
			fieldBusinessType = "LongText",
			fieldLabelName = "Custom Field",
			fieldName = "customObjectField",
			fieldType = "Clob",
			isRequired = "false",
			objectName = "CustomObject210");

		ObjectAdmin.publishObjectViaAPI(objectName = "CustomObject210");

		FormsAdmin.addForm();

		Form.editName(formName = "Form 1");

		PopulateObjectFormsConfiguration.openFormSettings();

		PopulateObjectFormsConfiguration.selectObject(label = "Object");

		PopulateObjectFormsConfiguration.selectCustomObject(label = "Custom Object 210");

		Form.gotoAddField(fieldType = "Text");

		PopulateObjectFormsConfiguration.mapFormField(fieldLabelName = "Custom Field");

		Form.publishForm();

		FormsAdminNavigator.gotoPublishedForm();

		FormViewBuilder.editText(
			fieldName = "Text",
			fieldValue = "By building a vibrant business, making technology useful, and investing in communities, we make it possible for people to reach their full potential to serve others.");

		FormPortlet.submitSuccessfully();

		Navigator.openURL();

		ObjectAdmin.goToCustomObject(objectName = "CustomObject210");

		ObjectPortlet.viewEntry(entry = "By building a vibrant business, making technology useful, and investing in communities, we make it possible for people to reach their full potential to serve others.");
	}

	@description = "LPS-136456 - Verify that workflows cannot be added to forms that are mapped to an object"
	@priority = "4"
	test CannotAddWorkflow {
		FormsAdmin.addForm();

		PopulateObjectFormsConfiguration.openFormSettings();

		PopulateObjectFormsConfiguration.selectObject(label = "Object");

		Click(locator1 = "Modal#HEADER");

		ObjectPortlet.assertFieldLabelName(fieldLabelName = "Select Object");

		ObjectPortlet.assertFieldLabelNameNotPresent(fieldLabelName = "Select a Workflow");
	}

	@description = "LPS-138495 - Verify that Picklist entries cannot be edited in the Forms Sidebar"
	@priority = "4"
	test CannotEditPicklistEntries {
		Picklist.addPicklistViaAPI(picklistName = "Picklist Test");

		ObjectAdmin.addObjectViaAPI(
			labelName = "Custom Object 211",
			objectName = "CustomObject211",
			pluralLabelName = "Custom Objects 211");

		ObjectAdmin.openObjectAdmin();

		ObjectPortlet.selectCustomObject(label = "Custom Object 211");

		ObjectAdmin.goToFieldsTab();

		ObjectAdmin.addObjectFieldViaUI(
			fieldLabel = "Field Picklist",
			fieldPicklist = "Picklist Test",
			fieldType = "Picklist");

		Navigator.openURL();

		ObjectAdmin.publishObjectViaAPI(objectName = "CustomObject211");

		FormsAdmin.addForm();

		PopulateObjectFormsConfiguration.openFormSettings();

		PopulateObjectFormsConfiguration.selectObject(label = "Object");

		PopulateObjectFormsConfiguration.selectCustomObject(label = "Custom Object 211");

		Form.gotoAddField(fieldType = "Select from List");

		PopulateObjectFormsConfiguration.mapFormObjectField(
			fieldLabelName = "Field Picklist",
			formObjectField = "Choose an Option");

		Form.gotoBasicTab();

		ObjectPortlet.assertEntryDetailsLabelNotPresent(entryLabel = "Create List");
	}

	@description = "LPS-137316 - Verify it is not possible to select an unpublished Object"
	@priority = "4"
	test CannotSelectUnpublishedObject {
		ObjectAdmin.addObjectViaAPI(
			labelName = "Custom Object 212",
			objectName = "CustomObject212",
			pluralLabelName = "Custom Objects 212");

		FormsAdmin.addForm();

		PopulateObjectFormsConfiguration.openFormSettings();

		PopulateObjectFormsConfiguration.selectObject(label = "Object");

		PopulateObjectFormsConfiguration.viewDisplaySelectOptionNotPresent(label = "Custom Object 212");
	}

	@description = "LPS-135429 - It is possible to retrieve objects data from Data Providers and use in a Select from List field in Forms"
	@priority = "4"
	test CanRetrieveDataProvidersOnSelectFromListField {
		property test.name.skip.portal.instance = "ObjectForms#CanRetrieveDataProvidersOnSelectFromListField";

		ObjectAdmin.addObjectViaAPI(
			labelName = "Custom Object 213",
			objectName = "CustomObject213",
			pluralLabelName = "Custom Objects 213");

		ObjectAdmin.addObjectFieldViaAPI(
			fieldBusinessType = "Text",
			fieldLabelName = "Custom Field",
			fieldName = "customField",
			fieldType = "String",
			isRequired = "false",
			objectName = "CustomObject213");

		ObjectAdmin.publishObjectViaAPI(objectName = "CustomObject213");

		ObjectAdmin.addObjectSingleFieldEntryViaAPI(
			fieldName = "customField",
			objectName = "CustomObject213",
			value = "Entry Test");

		FormsAdminNavigator.enableLocalNetworkDataProvider();

		PopulateObjectFormsConfiguration.openForm();

		Navigator.gotoNavItem(navItem = "Data Providers");

		FormsAdminNavigator.gotoAddDataProvider();

		FormsAdmin.editDataProvider(
			name = "Object Entries",
			outputParameterLabel = "Entry",
			outputParameterName = "Object Name",
			outputParameterPath = "$.items..customField",
			outputParameterType = "List",
			password = "test",
			url = "http://localhost:8080/o/c/customobject213s",
			username = "test@liferay.com");

		Button.clickSave();

		WaitForSPARefresh();

		Navigator.openURL();

		PopulateObjectFormsConfiguration.openForm();

		FormsAdminNavigator.gotoAddForm();

		Form.editName(formName = "Form 1");

		Form.gotoAddField(fieldType = "Select from List");

		var fieldNameDataProvider = FormFields.getFieldName();

		Form.editFieldLabelMetal(fieldValue = "Data Provider Select Field");

		Form.editFieldDataProvider(
			dataProvider = "Object Entries",
			dataProviderOutput = "Entry");

		Form.publishForm();

		FormsAdminNavigator.gotoPublishedForm();

		FormViewBuilder.validatePredefinedSelectFromList(
			fieldName = "Data Provider Select Field",
			fieldValue = "Choose an Option");

		FormFields.viewSelectFromListOption(optionValue = "Entry Test");
	}

	@description = "LPS-135429 - It is possible to retrieve objects data from Data Providers and use in a Text field in Form"
	@priority = "4"
	test CanRetrieveDataProvidersOnTextField {
		property test.name.skip.portal.instance = "ObjectForms#CanRetrieveDataProvidersOnTextField";

		ObjectAdmin.addObjectViaAPI(
			labelName = "Custom Object 214",
			objectName = "CustomObject214",
			pluralLabelName = "Custom Objects 214");

		ObjectAdmin.addObjectFieldViaAPI(
			fieldBusinessType = "Text",
			fieldLabelName = "Custom Field",
			fieldName = "customObjectField",
			fieldType = "String",
			isRequired = "false",
			objectName = "CustomObject214");

		ObjectAdmin.publishObjectViaAPI(objectName = "CustomObject214");

		ObjectAdmin.addObjectSingleFieldEntryViaAPI(
			fieldName = "customObjectField",
			objectName = "CustomObject214",
			value = "Entry Test");

		FormsAdminNavigator.enableLocalNetworkDataProvider();

		PopulateObjectFormsConfiguration.openForm();

		Navigator.gotoNavItem(navItem = "Data Providers");

		FormsAdminNavigator.gotoAddDataProvider();

		FormsAdmin.editDataProvider(
			name = "Object Entries",
			outputParameterLabel = "Entry",
			outputParameterName = "Object Name",
			outputParameterPath = "$.items..customField",
			outputParameterType = "List",
			password = "test",
			url = "http://localhost:8080/o/c/customobject214s",
			username = "test@liferay.com");

		Button.clickSave();

		WaitForSPARefresh();

		Navigator.openURL();

		PopulateObjectFormsConfiguration.openForm();

		FormsAdminNavigator.gotoAddForm();

		Form.editName(formName = "Form 1");

		Form.gotoAddField(fieldType = "Text");

		Form.gotoAutocompleteTab();

		Form.enableAutocompleteConfiguration();

		Form.editTextFieldDataProvider(
			dataProvider = "Object Entries",
			dataProviderOutput = "Entry");

		Form.save();

		Form.publishForm();

		FormsAdminNavigator.gotoPublishedForm();

		FormViewBuilder.editText(
			fieldName = "Text",
			fieldValue = "Entry");

		FormPortlet.submitSuccessfully();

		Navigator.openURL();

		PopulateObjectFormsConfiguration.openForm();

		FormsAdminNavigator.gotoEditFormByName(formName = "Form 1");

		FormsAdminNavigator.gotoEntries();

		FormViewBuilder.viewEntriesTitle(key_entriesTitle = "Entry");
	}

	@description = "LPS-139692 - Verify that 'Select Object' field is required"
	@priority = "4"
	test SelectObjectFieldRequired {
		FormsAdmin.addForm();

		Form.editName(formName = "Form 1");

		Form.gotoAddField(fieldType = "Text");

		PopulateObjectFormsConfiguration.openFormSettings();

		PopulateObjectFormsConfiguration.selectObjectOnly(labelObject = "Object");

		Button.clickDone();

		PopulateObjectFormsConfiguration.publishFormObject();

		AssertTextEquals.assertPartialText(
			locator1 = "Message#ERROR",
			value1 = "You must define an object for the selected storage type.");
	}

}