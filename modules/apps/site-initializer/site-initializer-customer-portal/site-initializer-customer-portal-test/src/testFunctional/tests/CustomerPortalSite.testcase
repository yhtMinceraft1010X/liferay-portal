@component-name = "portal-solutions"
definition {

	property portal.release = "false";
	property portal.upstream = "true";
	property testray.main.component.name = "Customer Portal";
	property custom.properties = "javascript.single.page.application.enabled=false${line.separator}jsonws.web.service.paths.excludes=";

	setUp {
		TestCase.setUpPortalInstance();

		User.firstLoginUI();
	}

	tearDown {
		var testPortalInstance = PropsUtil.get("test.portal.instance");

		if ("${testPortalInstance}" == "true") {
			PortalInstances.tearDownCP();
		}
		else {
			CustomerPortal.customerPortalTearDown();

			User.logoutPG();
		}
	}

	@description = "LPS-146217 - Verify if when Customer Portal is created, Accounts Users will be created for it as well"
	@priority = "4"
	test AccountUsersAreCreatedForCP20 {
		CustomerPortal.createCPSite(siteName = "Customer Portal");

		AccountUsers.openAccountUsersAdmin();

		AssertElementPresent(
			locator1 = "CustomerPortalSite#ACCOUNTS_TABLE",
			value = "Test Test");
	}

	@description = "LPS-148456 - Verify if its possible login in Okta, Configure SAML Admin and create CP2.0"
	@priority = "5"
	test CanConnectWithOkta {
		task ("Login in Okta") {

			//var userEmailAddress = PropsUtil.get("okta.cp.email");
			//var userPassword = PropsUtil.get("okta.cp.password");

			var userEmailAddress = "garrettz@mbopartners.com.broken";
			var userPassword = "q4F63ZEH3njnnA34S761";

			Navigator.openSpecificURL(url = "https://login-dev.liferay.com/");

			Type(
				locator1 = "CustomerPortalSite#OKTA_USERNAME",
				value1 = "${userEmailAddress}");

			Type(
				locator1 = "CustomerPortalSite#OKTA_PASSWORD",
				value1 = "${userPassword}");

			Click(locator1 = "CustomerPortalSite#OKTA_SUBMIT");
		}

		Navigator.openURL();

		task ("Configure SAML Admin general info") {
			ApplicationsMenu.gotoPortlet(
				category = "Security",
				panel = "Control Panel",
				portlet = "SAML Admin");

			SAMLRole.configure(
				samlEntityId = "customer2.liferay.com",
				samlRoleType = "Service Provider");

			if (IsElementPresent(locator1 = "CustomerPortalSite#CERTIFICATE", value = "SIGNING")) {
				CustomerPortal.replaceCertificateAndPrivateKey(
					certificateUsage = "SIGNING",
					samlKeyPassword = "QATEST");

				SAMLRole.configureCertificateAndPrivateKey(
					certificateUsage = "ENCRYPTION",
					samlKeyPassword = "QATEST");

				Click(locator1 = "CPSAMLAdmin#SAML_ENABLED_CHECKBOX");
			}
			else {
				SAMLRole.configureCertificateAndPrivateKey(
					certificateUsage = "SIGNING",
					samlKeyPassword = "QATEST");

				SAMLRole.configureCertificateAndPrivateKey(
					certificateUsage = "ENCRYPTION",
					samlKeyPassword = "QATEST");

				Click(locator1 = "CPSAMLAdmin#SAML_ENABLED_CHECKBOX");
			}
		}

		task ("Configure IdP Connections") {

			//var entityId = PropsUtil.get("okta.cp.entityId");
			//var metadataURL = PropsUtil.get("okta.cp.metadataURL");

			var entityId = "http://www.okta.com/exk158id544C4j3KK0h8";
			var metadataURL = "https://provisioning.liferay.com/documents/20124/139691/customer-2.0-local-metadata.xml/eeac6c00-20b9-dafd-3059-4151d02d487b";

			SAMLNavigation.gotoIdentityProviderConnections();

			CustomerPortal.addIdPCForCustomerPortal(
				idpAttributeMapping = "emailAddress,UUID,screenName,lastName,firstName",
				idpEntityId = "${entityId}",
				idpMetadataURL = "${metadataURL}",
				idpNameId = "CP2",
				nameIdentifierFormat = "Unspecified");

			PortletEntry.save();
		}

		task ("Create CP2.0 and verify that it has been created") {
			CustomerPortal.createCPSite(siteName = "Customer Portal");

			ApplicationsMenu.gotoSite(key_site = "Customer Portal");

			AssertElementPresent(
				locator1 = "CP2LiferayContact#PROJECT_CARD",
				title = "Test Account 01");
		}

		task ("Teardown") {
			ApplicationsMenu.gotoPortlet(
				category = "Security",
				panel = "Control Panel",
				portlet = "SAML Admin");

			SAMLNavigation.gotoIdentityProviderConnections();

			SAMLPortlet.deleteSAMLConnection(samlEntityId = "${entityId}");

			ApplicationsMenu.gotoPortlet(
				category = "Security",
				panel = "Control Panel",
				portlet = "SAML Admin");

			Click(locator1 = "CPSAMLAdmin#SAML_ENABLED_CHECKBOX");

			SAMLRole.deleteCertificate();

			CustomerPortal.customerPortalTearDown();
		}
	}

	@description = "LPS-146215 - Verify if Customer Portal can be created"
	@priority = "5"
	test CanCP20BeCreated {
		ApplicationsMenu.gotoPortlet(
			category = "Sites",
			panel = "Control Panel",
			portlet = "Sites");

		LexiconEntry.gotoAdd();

		LexiconCard.clickCard(card = "Customer Portal");

		SelectFrame(
			key_title = "Add Site",
			locator1 = "IFrame#MODAL_ANY");

		PortletEntry.inputName(name = "Customer Portal");

		Click(locator1 = "Button#ADD");

		AssertElementPresent(locator1 = "Raylife#SAVE_LOADING_ANIMATION");

		while (IsElementPresent(locator1 = "Raylife#SAVE_LOADING_ANIMATION")) {
			Pause(locator1 = "1500");
		}

		Refresh();

		WaitForElementPresent(
			key_itemName = "Site Configuration",
			locator1 = "ListGroupItem#ITEM_TEXT");

		Click(
			key_itemName = "Site Configuration",
			locator1 = "ListGroupItem#ITEM_TEXT");

		AssertTextEquals(
			locator1 = "SitesEditSite#DETAILS_NAME",
			value1 = "Customer Portal");
	}

	@description = "LPS-146220 - Verify if when Customer Portal is created, Objects will be created for it as well"
	@priority = "4"
	test ObjectsAreCreatedForCP20 {
		CustomerPortal.createCPSite(siteName = "Customer Portal");

		ObjectAdmin.openObjectAdmin();

		AssertElementPresent(
			locator1 = "CustomerPortalSite#OBJECTS_TABLE",
			value = "Koroneiki Account");
	}

	@description = "LPS-146219 - Verify if when Customer Portal is created, Remote Apps will be created for it as well"
	@priority = "4"
	test RemoteAppsAreCreatedForCP20 {
		CustomerPortal.createCPSite(siteName = "Customer Portal");

		RemoteApps.goToRemoteAppsPortlet();

		AssertElementPresent(
			locator1 = "CustomerPortalSite#OBJECTS_TABLE",
			value = "Customer Portal");
	}

	@description = "LPS-146216 - Verify if when Customer Portal is created, Tests Accounts will be created for it as well"
	@priority = "4"
	test TestsAccountsAreCreatedForCP20 {
		CustomerPortal.createCPSite(siteName = "Customer Portal");

		Accounts.openAccountsAdmin();

		AssertElementPresent(
			locator1 = "CustomerPortalSite#ACCOUNTS_TABLE",
			value = "Test Account 01");
	}

}