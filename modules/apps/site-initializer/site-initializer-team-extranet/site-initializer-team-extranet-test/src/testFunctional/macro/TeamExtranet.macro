definition {

	var siteType = "Open";

	macro addCP {
		LexiconEntry.gotoAdd();

		if ("${singleSiteTypePermission}" != "true") {
			if (!(isSet(siteTemplateName))) {
				var siteTemplateName = "${siteType} Site";
			}

			LexiconCard.clickCard(card = "${siteTemplateName}");

			SelectFrame(locator1 = "IFrame#MODAL_BODY");

			PortletEntry.inputName(name = "${siteName}");

			Button.clickAdd();

			if (IsElementPresent(locator1 = "Message#ERROR_1", value1 = "Please enter a unique name.")) {
				SelectFrameTop();

				Site.tearDownCP();

				LexiconEntry.gotoAdd();

				LexiconCard.clickCard(card = "${siteTemplateName}");

				SelectFrame(locator1 = "IFrame#MODAL_BODY");

				PortletEntry.inputName(name = "${siteName}");

				Button.clickAdd();
			}

			SelectFrameTop();

			Alert.viewSuccessMessage();
		}

		if ("${siteType}" == "Site Template") {
			if ("${uncheckPropagation}" == "true") {
				Click(
					key_itemName = "Pages",
					locator1 = "ListGroupItem#ITEM_TEXT");

				Navigator.gotoNavTab(navTab = "Pages");

				Uncheck.uncheckToggleSwitch(locator1 = "Checkbox#ENABLE_PROPAGATION");

				PortletEntry.save();

				Navigator.gotoBack();
			}
		}

		while (IsElementNotPresent(key_itemName = "Site Configuration", locator1 = "ListGroupItem#ITEM_TEXT")) {
			WaitForPageLoad();
		}

		WaitForElementPresent(
			key_itemName = "Site Configuration",
			locator1 = "ListGroupItem#ITEM_TEXT");

		Click(
			key_itemName = "Site Configuration",
			locator1 = "ListGroupItem#ITEM_TEXT");

		if ("${membershipType}" == "Private") {
			Select(
				locator1 = "SitesEditSite#MEMBERSHIP_TYPE",
				value1 = "Private");
		}
		else if ("${membershipType}" == "Restricted") {
			Select(
				locator1 = "SitesEditSite#MEMBERSHIP_TYPE",
				value1 = "Restricted");
		}
		else {
			Select(
				locator1 = "SitesEditSite#MEMBERSHIP_TYPE",
				value1 = "Open");
		}

		if (isSet(parentSiteName)) {
			WaitForLiferayEvent.initializeLiferayEventLog();

			Click(
				key_fieldLabel = "Parent Site",
				key_text = "Change",
				locator1 = "Button#ANY_WITH_LABEL");

			var key_parentSiteName = "${parentSiteName}";

			SelectFrame(locator1 = "IFrame#MODAL_BODY");

			Search.searchCP(searchTerm = "${parentSiteName}");

			var key_site = "${parentSiteName}";

			AssertTextEquals(
				locator1 = "ContentRow#ENTRY_CONTENT_ROW_TD_1_SITE_LINK",
				value1 = "${parentSiteName}");

			Click(locator1 = "ContentRow#ENTRY_CONTENT_ROW_TD_1_SITE_LINK");

			var key_parentSiteName = "${parentSiteName}";

			SelectFrameTop();
		}

		ScrollWebElementIntoView(locator1 = "Button#SAVE");

		PortletEntry.save();

		AssertTextEquals(
			locator1 = "SitesEditSite#DETAILS_NAME",
			value1 = "${siteName}");
	}

	macro addSiteTemplateCP {
		TeamExtranet.addCP(
			membershipType = "${membershipType}",
			parentSiteName = "${parentSiteName}",
			siteName = "${siteName}",
			siteTemplateName = "${siteTemplateName}",
			siteType = "Site Template",
			uncheckPropagation = "${uncheckPropagation}");
	}

	macro editFragmentImage {
		Click(
			key_imageName = "${id}",
			locator1 = "TeamExtranet#BACKGROUND_IMAGE");

		Click(
			key_documentTitle = "${container}",
			locator1 = "OneDrive#FILES_CHECKBOX");

		Navigator.gotoNavTab(navTab = "Image Source");

		if (!(IsSelectedLabel(key_fieldLabel = "Source Selection", locator1 = "Select#GENERIC_SELECT_FIELD", value1 = "Direct"))) {
			Select(
				key_fieldLabel = "Source Selection",
				locator1 = "Select#GENERIC_SELECT_FIELD",
				value1 = "Direct");

			AssertSelectedLabel(
				key_fieldLabel = "Source Selection",
				locator1 = "Select#GENERIC_SELECT_FIELD",
				value1 = "Direct");
		}

		if (IsTextEqual(key_fieldLabel = "Image", locator1 = "PageEditor#ITEM_SELECTOR_CONTENT_INPUT", value1 = "")) {
			Click(
				key_fieldLabel = "Image",
				key_text = "plus",
				locator1 = "Icon#ANY_ITEM_SELECTOR");
		}
		else {
			Click(
				key_fieldLabel = "Image",
				key_text = "change",
				locator1 = "Icon#ANY_ITEM_SELECTOR");
		}

		if (isSet(imageFileName)) {
			ItemSelector.selectDMImage(
				imageFileName = "${imageFileName}",
				navTab = "Documents and Media");
		}
		else if (isSet(imageSourceURL)) {
			ItemSelector.addImageFromURL(
				imageSourceURL = "${imageSourceURL}",
				navTab = "URL");
		}
		else {
			SelectFrame(locator1 = "IFrame#MODAL_BODY");

			AssertElementPresent.assertVisible(locator1 = "Button#SELECT_FILE");

			UploadCommonFile.uploadCommonFileHiddenNoMouseOver(
				locator1 = "TextInput#FILE",
				value1 = "${fileName}");

			Button.clickAdd();
		}

		if (isSet(imageDescription)) {
			Type(
				key_text = "Image Description",
				locator1 = "TextInput#ANY",
				value1 = "${imageDescription}");

			Click(
				key_sidebarButton = "Browser",
				locator1 = "PageEditor#FRAGMENT_SIDEBAR_TITLE");

			PageEditor.waitForAutoSave();

			AssertTextEquals(
				key_text = "Image Description",
				locator1 = "TextInput#ANY",
				value1 = "${imageDescription}");
		}
	}

}