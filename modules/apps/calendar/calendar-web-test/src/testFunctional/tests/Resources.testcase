@component-name = "portal-calendar"
definition {

	property custom.properties = "default.layout.template.id=1_column${line.separator}company.default.time.zone=America/Los_Angeles";
	property portal.release = "true";
	property portal.upstream = "true";
	property testray.main.component.name = "Calendar";

	var pageName = "Calendar Page";
	var portletName = "Calendar";

	setUp {
		TestCase.setUpPortalInstance();

		User.firstLoginPG();

		ProductMenu.gotoPortlet(
			category = "Site Builder",
			portlet = "Pages");

		PagesAdmin.addPublicPage(
			pageName = "Calendar Page",
			sitePageType = "Widget Page");

		PagesAdmin.configurePage(
			columnName = "1 Column",
			pageName = "Calendar Page",
			privacy = "Public Pages",
			sitePageType = "Widget Page");

		Navigator.gotoPage(pageName = "Calendar Page");

		Portlet.addPG(
			pageName = "Calendar Page",
			portletName = "Calendar");
	}

	tearDown {
		var testPortalInstance = PropsUtil.get("test.portal.instance");

		if ("${testPortalInstance}" == "true") {
			PortalInstances.tearDownCP();
		}
		else {
			Navigator.gotoPage(pageName = "Calendar Page");

			if (IsElementPresent(locator1 = "Calendar#SCHEDULER_EVENT_GENERIC")) {
				CalendarEvent.tearDown();
			}

			Calendar.tearDown();

			CalendarConfiguration.tearDown();

			PagesAdmin.tearDownCP();
		}
	}

	@priority = "5"
	test AddAndViewOtherCalendars {
		property portal.acceptance = "true";

		CalendarResource.add(calendarResourceName = "Calendar Resource Name");

		Navigator.gotoPage(pageName = "Calendar Page");

		Calendar.add(
			calendarName = "Calendar Resource Name",
			calendarType = "Other Calendars");

		Navigator.gotoPage(pageName = "Calendar Page");

		CalendarResource.view(calendarResourceName = "Calendar Resource Name");

		Navigator.gotoPage(pageName = "Calendar Page");

		Calendar.view(
			calendarName = "Calendar Resource Name",
			calendarType = "Other Calendars");
	}

	@description = "LRQA-71886 - Verify that is possible to create an Inactive Resource"
	@priority = "3"
	test CanAddInactiveResource {
		CalendarNavigator.gotoAddResource();

		CalendarResource.editResourceName(calendarResourceName = "Resource Active");

		CalendarResource.save();

		CalendarNavigator.gotoAddResource();

		CalendarResource.editResourceName(calendarResourceName = "Resource Inactive");

		CalendarResource.deactivate();

		CalendarResource.save();

		CalendarResource.viewResourceNameNotPresent(calendarResourceName = "Resource Inactive");

		CalendarResource.resourceSetFilterAndOrder(filterBy = "No");

		CalendarResource.viewResourceName(calendarResourceName = "Resource Inactive");
	}

}