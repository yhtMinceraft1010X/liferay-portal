@component-name = "portal-calendar"
definition {

	property custom.properties = "default.layout.template.id=1_column${line.separator}company.default.time.zone=America/Los_Angeles";
	property portal.release = "true";
	property portal.upstream = "true";
	property testray.main.component.name = "Calendar";

	var pageName = "Calendar Page";
	var portletName = "Calendar";

	setUp {
		TestCase.setUpPortalInstance();

		User.firstLoginPG();

		ProductMenu.gotoPortlet(
			category = "Site Builder",
			portlet = "Pages");

		PagesAdmin.addPublicPage(
			pageName = "Calendar Page",
			sitePageType = "Widget Page");

		PagesAdmin.configurePage(
			columnName = "1 Column",
			pageName = "Calendar Page",
			privacy = "Public Pages",
			sitePageType = "Widget Page");

		Navigator.gotoPage(pageName = "Calendar Page");

		Portlet.addPG(
			pageName = "Calendar Page",
			portletName = "Calendar");
	}

	tearDown {
		var testPortalInstance = PropsUtil.get("test.portal.instance");

		if ("${testPortalInstance}" == "true") {
			PortalInstances.tearDownCP();
		}
		else {
			Navigator.gotoPage(pageName = "Calendar Page");

			if (IsElementPresent(locator1 = "Calendar#SCHEDULER_EVENT_GENERIC")) {
				CalendarEvent.tearDown();
			}

			Calendar.tearDown();

			CalendarConfiguration.tearDown();

			PagesAdmin.tearDownCP();
		}
	}

	@description = "LRQA-71925 - Verify that the user can change Permissions for the Resource"
	@priority = "3"
	test CanChangeResourcePermissions {
		CalendarNavigator.gotoAddResource();

		CalendarResource.editResourceName(calendarResourceName = "New Resource");

		CalendarResource.save();

		CalendarResource.gotoPermissions(key_calendarName = "New Resource");

		CalendarResource.viewMenuPermissionsModal();
	}

	@description = "LRQA-71926 - Verify that the user can Clear the search results"
	@priority = "3"
	test CanClearSearchResults {
		CalendarNavigator.gotoAddResource();

		CalendarResource.editResourceName(calendarResourceName = "New Resource1");

		CalendarResource.save();

		CalendarNavigator.gotoAddResource();

		CalendarResource.editResourceName(calendarResourceName = "New Resource2");

		CalendarResource.save();

		CalendarResource.search(searchTerm = "Resource1");

		CalendarResource.viewSearchSummaryResults(searchResults = '''1 Results for "Resource1"''');

		CalendarResource.viewResourceName(calendarResourceName = "New Resource1");

		CalendarResource.clearSearchResults();

		CalendarResource.notDisplayResultBar();

		CalendarResource.viewResourceName(calendarResourceName = "New Resource1");

		CalendarResource.viewResourceName(calendarResourceName = "New Resource2");
	}

	@description = "LRQA-71924 - Verify that the user can create a New Calendar for the Resource"
	@priority = "3"
	test CanCreateCalendarForResource {
		CalendarNavigator.gotoAddResource();

		CalendarResource.editResourceName(calendarResourceName = "New Resource");

		CalendarResource.save();

		CalendarNavigator.gotoResourceCalendars(calendarResourceName = "New Resource");

		CalendarResource.addCalendarByResource(
			calendarName = "Calendar for New Resource",
			menuItem = "View Calendars");

		CalendarResource.viewCalendarNameByResourceOntable(key_calendarName = "Calendar for New Resource");
	}

	@description = "LRQA-71922 - Verify that the user can filter by Active Resources"
	@priority = "3"
	test CanFilterByActiveResources {
		CalendarNavigator.gotoAddResource();

		CalendarResource.editResourceName(calendarResourceName = "Active Resource");

		CalendarResource.save();

		CalendarNavigator.gotoAddResource();

		CalendarResource.editResourceName(calendarResourceName = "Inactive Resource");

		CalendarResource.deactivate();

		CalendarResource.save();

		CalendarResource.viewResourceNameNotPresent(calendarResourceName = "Inactive Resource");

		CalendarResource.resourceSetFilterAndOrder(filterBy = "Yes");

		CalendarResource.viewResourceName(calendarResourceName = "Active Resource");
	}

	@description = "LRQA-71921 - Verify that the user can filter by Current Scope"
	@priority = "3"
	test CanFilterByCurrentScope {
		CalendarNavigator.gotoAddResource();

		CalendarResource.editResourceName(calendarResourceName = "Current Scope Resource");

		CalendarResource.save();

		CalendarResource.resourceSetFilterAndOrder(filterBy = "Current");

		CalendarResource.viewResourceName(calendarResourceName = "Current Scope Resource");
	}

	@description = "LRQA-71920 - Verify that the user can filter by Global Scope"
	@priority = "3"
	test CanFilterByGlobalScope {
		CalendarNavigator.gotoAddResource();

		CalendarResource.editResourceName(calendarResourceName = "Global Scope Resource");

		CalendarResource.save();

		CalendarResource.resourceSetFilterAndOrder(filterBy = "Global");

		CalendarResource.viewResourceNameNotPresent(calendarResourceName = "Global Scope Resource");
	}

	@description = "LRQA-71923 - Verify that the user can filter by Nonactive Resources"
	@priority = "3"
	test CanFilterByNonactiveResources {
		CalendarNavigator.gotoAddResource();

		CalendarResource.editResourceName(calendarResourceName = "Active Resource");

		CalendarResource.save();

		CalendarNavigator.gotoAddResource();

		CalendarResource.editResourceName(calendarResourceName = "Inactive Resource");

		CalendarResource.deactivate();

		CalendarResource.save();

		CalendarResource.viewResourceNameNotPresent(calendarResourceName = "Inactive Resource");

		CalendarResource.resourceSetFilterAndOrder(filterBy = "No");

		CalendarResource.viewResourceName(calendarResourceName = "Inactive Resource");
	}

	@description = "LRQA-71885 - Verify that the user can search by Resource Description"
	@priority = "4"
	test CanSearchByResourceDescription {
		CalendarNavigator.gotoAddResource();

		CalendarResource.editResourceName(calendarResourceName = "Resource Name");

		CalendarResource.editResourceDescription(calendarResourceDescription = "Resource Description");

		CalendarResource.save();

		CalendarResource.search(searchTerm = "Description");

		CalendarResource.viewSearchSummaryResults(searchResults = "1 Results for &quot;Description&quot;");

		CalendarResource.viewResourceName(calendarResourceName = "Resource Name");

		CalendarResource.clearSearchResults();

		CalendarResource.search(searchTerm = "Test Test");

		CalendarResource.viewSearchSummaryResults(searchResults = "0 Results for &quot;Test Test&quot;");

		CalendarResource.viewTableEmpty();
	}

	@priority = "4"
	test Delete {
		var calendarResourceName = "Calendar Resource Name";

		CalendarResource.add(calendarResourceName = "${calendarResourceName}");

		Navigator.gotoPage(pageName = "Calendar Page");

		CalendarResource.delete(calendarResourceName = "${calendarResourceName}");

		Navigator.gotoPage(pageName = "Calendar Page");

		CalendarResource.viewDefault(calendarResourceName = "${calendarResourceName}");
	}

	@description = "This is a use case for LPS-59743."
	@priority = "3"
	test Search {
		CalendarResource.add(calendarResourceName = "Room 1");

		CalendarResource.add(calendarResourceName = "Car1");

		CalendarResource.add(calendarResourceName = "Truck");

		Search.searchPG(
			portletName = "Calendar",
			searchTerm = "Room Car ");

		CalendarResource.viewResourceName(calendarResourceName = "Room 1");

		CalendarResource.viewResourceName(calendarResourceName = "Car1");

		CalendarResource.viewResourceNameNotPresent(calendarResourceName = "Truck");

		Search.searchPG(
			portletName = "Calendar",
			searchTerm = "Room");

		CalendarResource.viewResourceName(calendarResourceName = "Room 1");

		CalendarResource.viewResourceNameNotPresent(calendarResourceName = "Car1");

		CalendarResource.viewResourceNameNotPresent(calendarResourceName = "Truck");
	}

	@description = "This is a use case for LPS-62410."
	@priority = "4"
	test ViewEventDateAndTimeInResourcesAvailability {
		CalendarNavigator.gotoAddEvent();

		CalendarEvent.editTitle(eventTitle = "Event Title");

		CalendarEvent.gotoConfigureInvitations();

		CalendarEvent.viewResourcesAvailability();

		CalendarEvent.compareEventDateAndTimeToResourcesAvailabilityDateAndTime();

		CalendarEvent.setTimes(
			endDateTime = "11:00 AM",
			startDateTime = "08:00 AM");

		CalendarEvent.compareEventDateAndTimeToResourcesAvailabilityDateAndTime();

		CalendarEvent.setEndDateTomorrow();

		CalendarEvent.setTimes(
			endDateTime = "07:00 AM",
			startDateTime = "08:00 AM");

		CalendarEvent.compareEventDateAndTimeToResourcesAvailabilityDateAndTime();

		CalendarEvent.publish();
	}

	@description = "This is a use case for LPS-55171."
	@priority = "3"
	test ViewInactive {
		ApplicationsMenu.gotoPortlet(
			category = "Sites",
			panel = "Control Panel",
			portlet = "Sites");

		Site.addBlankCP(siteName = "Site Name");

		ProductMenu.gotoPortlet(
			category = "Site Builder",
			portlet = "Pages");

		PagesAdmin.addPublicPage(
			pageName = "Calendar Page",
			sitePageType = "Widget Page");

		PagesAdmin.configurePage(
			columnName = "1 Column",
			pageName = "Calendar Page",
			privacy = "Public Pages",
			sitePageType = "Widget Page");

		Navigator.gotoSitePage(
			pageName = "Calendar Page",
			siteName = "Site Name");

		Portlet.addPG(portletName = "Calendar");

		Navigator.gotoSitePage(
			pageName = "Calendar Page",
			siteName = "Site Name");

		CalendarNavigator.gotoAddResource();

		CalendarResource.editResourceName(calendarResourceName = "Calendar Resource");

		CalendarResource.deactivate();

		CalendarResource.save();

		CalendarResource.viewDefault();

		Navigator.gotoSitePage(
			pageName = "Calendar Page",
			siteName = "Site Name");

		Calendar.searchOtherCalendars(searchValue = "resource");

		Calendar.viewOtherCalendarsSearchResultNotPresent(calendarName = "Calendar Resource");

		CalendarNavigator.gotoAddEvent();

		CalendarEvent.viewInvitationResourceNotPresent(resourceNameList = "Calendar Resource");
	}

	@priority = "3"
	test ViewInvitedResourceAvailability {
		ApplicationsMenu.gotoPortlet(
			category = "Users",
			panel = "Control Panel",
			portlet = "Users and Organizations");

		User.addCP(
			userEmailAddress = "userea@liferay.com",
			userFirstName = "userfn",
			userLastName = "userln",
			userScreenName = "usersn");

		ApplicationsMenu.gotoPortlet(
			category = "Users",
			panel = "Control Panel",
			portlet = "Users and Organizations");

		User.editPasswordCP(
			userEmailAddress = "userea@liferay.com",
			userScreenName = "usersn");

		ProductMenu.gotoPortlet(
			category = "People",
			portlet = "Memberships");

		Site.assignUserAsMemberCP(
			userFirstName = "userfn",
			userLastName = "userln",
			userScreenName = "usersn");

		Navigator.gotoPage(pageName = "Calendar Page");

		Calendar.add(
			calendarName = "userfn userln",
			calendarType = "Other Calendars");

		Navigator.gotoPage(pageName = "Calendar Page");

		CalendarResource.add(calendarResourceName = "Calendar Resource Name");

		Navigator.gotoPage(pageName = "Calendar Page");

		CalendarResource.view(calendarResourceName = "Calendar Resource Name");

		Navigator.gotoPage(pageName = "Calendar Page");

		Calendar.add(
			calendarName = "Calendar Resource Name",
			calendarType = "Other Calendars");

		Navigator.gotoPage(pageName = "Calendar Page");

		CalendarNavigator.gotoAddEvent();

		CalendarEvent.add(
			calendarName = "userfn userln",
			eventTitle = "Calendar Event All Day Title",
			eventType = "Regular",
			optionSet = "true",
			optionSetValue = "inviteResource",
			pageName = "Calendar Page",
			resourcesAvailability = "true",
			specificOptionSetValue = "Test Test,Calendar Resource Name");

		Navigator.gotoPage(pageName = "Calendar Page");

		CalendarEvent.view(
			eventNumber = "2",
			eventTitle = "Calendar Event All Day Title");
	}

}