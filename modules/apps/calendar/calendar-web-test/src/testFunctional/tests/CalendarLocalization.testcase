@component-name = "portal-calendar"
definition {

	property custom.properties = "default.layout.template.id=1_column${line.separator}company.default.time.zone=America/Los_Angeles";
	property portal.release = "true";
	property portal.upstream = "true";
	property testray.main.component.name = "Calendar";

	var pageName = "Calendar Page";
	var portletName = "Calendar";

	setUp {
		TestCase.setUpPortalInstance();

		User.firstLoginPG();

		ProductMenu.gotoPortlet(
			category = "Site Builder",
			portlet = "Pages");

		PagesAdmin.addPage(
			pageName = "Calendar Page",
			sitePageType = "Widget Page");

		PagesAdmin.configurePage(
			columnName = "1 Column",
			pageName = "Calendar Page",
			sitePageType = "Widget Page");

		Navigator.gotoPage(pageName = "Calendar Page");

		Portlet.addPG(
			pageName = "Calendar Page",
			portletName = "Calendar");
	}

	tearDown {
		var testPortalInstance = PropsUtil.get("test.portal.instance");

		if ("${testPortalInstance}" == "true") {
			PortalInstances.tearDownCP();
		}
		else {
			Navigator.gotoPage(pageName = "Calendar Page");

			if (IsElementPresent(locator1 = "Calendar#SCHEDULER_EVENT_GENERIC")) {
				CalendarEvent.tearDown();
			}

			Calendar.tearDown();

			CalendarConfiguration.tearDown();

			PagesAdmin.tearDownCP();
		}
	}

	@description = "This is a use case for LPS-58649."
	@priority = "3"
	test EditLanguageSelectorAndViewEvent {
		property test.name.skip.portal.instance = "CalendarLocalization#EditLanguageSelectorAndViewEvent";

		Navigator.gotoPage(pageName = "Calendar Page");

		Portlet.addPG(
			pageName = "Calendar Page",
			portletName = "Language Selector");

		LanguagePortlet.selectLanguagePG(
			locale = "es_ES",
			textIcon = "español-España");

		CalendarNavigator.gotoView(viewType = "Week");

		CalendarScheduler.addLocalizedEvent(
			eventTitle = "Calendar Event Title",
			saveButton = "Guardar",
			viewType = "Week");

		CalendarScheduler.viewLocalizedEvent(
			eventTitle = "Calendar Event Title",
			viewTypeLocalized = "Semana");

		LanguagePortlet.selectLanguagePG(
			locale = "en_US",
			textIcon = "english-United States");

		CalendarScheduler.viewDay(eventTitle = "Calendar Event Title");
	}

	@description = "This is a use case for LPS-54786."
	@priority = "3"
	test ViewShowMoreLocalization {
		for (var eventNumber : list "1,2,3,4,5,6") {
			CalendarNavigator.gotoAddEvent();

			CalendarEvent.add(
				eventTitle = "Calendar Event ${eventNumber}",
				eventType = "All Day",
				pageName = "Calendar Page");
		}

		MyAccount.configureDisplaySettingsLanguage(languageName = "español (España)");

		Navigator.gotoPage(pageName = "Calendar Page");

		CalendarScheduler.viewShowMoreLocalizedViaDayView(
			messageLocalized = "Mostrar 1 más",
			viewTypeLocalized = "Día");

		CalendarScheduler.viewShowMoreLocalizedViaWeekView(
			messageLocalized = "Mostrar 1 más",
			viewTypeLocalized = "Semana");

		CalendarScheduler.viewShowMoreLocalizedViaMonthView(
			messageLocalized = "Mostrar 4 más",
			viewTypeLocalized = "Mes");

		Click(locator1 = "UserBar#USER_AVATAR_IMAGE");

		Click(
			key_dropdownItem = "Mi cuenta",
			locator1 = "UserBar#USER_AVATAR_DROPDOWN_ITEM");

		MyAccount.changeUserLanguage(
			languageName = "English (United States)",
			saveButton = "Guardar",
			successMessage = "Your request completed successfully.");
	}

}