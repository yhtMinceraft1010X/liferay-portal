@component-name = "data-engine"
definition {

	property portal.release = "true";
	property portal.upstream = "true";
	property testray.main.component.name = "Data Engine";

	setUp {
		TestCase.setUpPortalInstance();

		User.firstLoginPG();

		JSONGroup.addGroup(groupName = "Test Site Name");

		WebContentNavigator.openWebContentAdmin(siteURLKey = "test-site-name");
	}

	tearDown {
		var testPortalInstance = PropsUtil.get("test.portal.instance");

		if ("${testPortalInstance}" == "true") {
			PortalInstances.tearDownCP();
		}
		else {
			JSONGroup.deleteGroupByName(groupName = "Test Site Name");
		}
	}

	@description = "This is a test for LRQA-68573. This test verifies that is possible to duplicate the field."
	@priority = "4"
	test CanBeDuplicated {
		NavItem.gotoStructures();

		WebContentStructures.addCP(structureName = "WC Structure Title");

		DataEngine.addField(
			fieldFieldLabel = "Grid",
			fieldName = "Grid");

		DataEngine.duplicate(fieldFieldLabel = "Grid");

		WebContentStructures.saveCP();

		WebContentNavigator.gotoEditStructure(structureName = "WC Structure Title");

		DataEngine.viewField(
			fieldFieldLabel = "Grid",
			fieldName = "Grid");

		DataEngine.viewField(
			fieldFieldLabel = "Copy of Grid",
			fieldName = "Grid");
	}

	@description = "This is a test for LRQA-68573. This test verifies that Label and Help text can be edited."
	@priority = "5"
	test CanEditLabelAndHelpText {
		NavItem.gotoStructures();

		WebContentStructures.addCP(structureName = "WC Structure Title");

		DataEngine.addField(
			fieldFieldLabel = "Grid",
			fieldName = "Grid");

		DataEngine.editFieldTip(
			fieldFieldLabel = "Grid",
			fieldHelp = "Help Text");

		WebContentStructures.saveCP();

		WebContentNavigator.gotoEditStructure(structureName = "WC Structure Title");

		DataEngine.editFieldLabel(
			fieldFieldLabel = "Grid",
			fieldFieldLabelEdit = "Grid Edited");

		DataEngine.editFieldTip(
			fieldFieldLabel = "Grid Edited",
			fieldHelp = "Help Text Edited");

		WebContentStructures.saveCP();

		WebContentNavigator.gotoEditStructure(structureName = "WC Structure Title");

		DataEngine.viewField(
			fieldFieldLabel = "Grid Edited",
			fieldHelp = "Help Text Edited",
			fieldName = "Grid");
	}

	@description = "This is a test for LRQA-68573. This test verifies that Label and Help text can be set."
	@priority = "5"
	test CanSetLabelAndHelpText {
		NavItem.gotoStructures();

		WebContentStructures.addCP(structureName = "WC Structure Title");

		DataEngine.addField(
			fieldFieldLabel = "Grid",
			fieldName = "Grid");

		DataEngine.editFieldTip(
			fieldFieldLabel = "Grid",
			fieldHelp = "Help Text");

		WebContentStructures.saveCP();

		WebContentNavigator.gotoEditStructure(structureName = "WC Structure Title");

		DataEngine.viewField(
			fieldFieldLabel = "Grid",
			fieldHelp = "Help Text",
			fieldName = "Grid");
	}

	@description = "This is a test for LRQA-68573. This test verifies that Label can be shown or hidden."
	@priority = "3"
	test CanSetLabelToShownOrHidden {
		NavItem.gotoStructures();

		WebContentStructures.addCP(structureName = "WC Structure Title");

		DataEngine.addField(
			fieldFieldLabel = "Grid",
			fieldName = "Grid");

		DataEngine.toggleShowLabel(fieldLabel = "Grid");

		WebContentStructures.saveCP();

		WebContentNavigator.gotoEditStructure(structureName = "WC Structure Title");

		DataEngine.assertFieldNotPresent(fieldFieldLabel = "Grid");

		DataEngine.toggleShowLabel();

		WebContentStructures.saveCP();

		WebContentNavigator.gotoEditStructure(structureName = "WC Structure Title");

		DataEngine.viewField(
			fieldFieldLabel = "Grid",
			fieldName = "Grid");
	}

	@description = "This is a test for LRQA-68573. This test verifies that multiple options on Column and Row can be set."
	@priority = "5"
	test CanSetMultipleOptions {
		NavItem.gotoStructures();

		WebContentStructures.addCP(structureName = "WC Structure Title");

		DataEngine.addField(
			fieldFieldLabel = "Grid",
			fieldName = "Grid");

		for (var index : list "1,2,3") {
			DataEngine.addFieldOption(
				fieldFieldLabel = "Grid",
				index = "${index}",
				optionFieldLabel = "Rows",
				optionValue = "Option ${index}");
		}

		for (var index : list "1,2,3") {
			DataEngine.addFieldOption(
				fieldFieldLabel = "Grid",
				index = "${index}",
				optionFieldLabel = "Columns",
				optionValue = "Option ${index}");
		}

		WebContentStructures.saveCP();

		WebContentNavigator.gotoEditStructure(structureName = "WC Structure Title");

		DataEngine.viewField(
			fieldFieldLabel = "Grid",
			fieldName = "Grid");

		DataEngine.assertFieldOptionLabels(
			fieldFieldLabel = "Grid",
			optionFieldLabel = "Rows",
			optionLabelList = "Option 1,Option 2,Option 3");

		DataEngine.assertFieldOptionLabels(
			fieldFieldLabel = "Grid",
			optionFieldLabel = "Columns",
			optionLabelList = "Option 1,Option 2,Option 3");
	}

}