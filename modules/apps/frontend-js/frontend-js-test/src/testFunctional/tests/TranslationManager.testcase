@component-name = "portal-frontend-infrastructure"
definition {

	property osgi.modules.includes = "frontend-js-components-sample-web";
	property portal.acceptance = "true";
	property portal.release = "true";
	property portal.upstream = "true";
	property testray.component.names = "Translation Manager";
	property testray.main.component.name = "User Interface";

	setUp {
		TestCase.setUpPortalInstance();

		User.firstLoginPG();

		task ("Given translations manager sample portlet") {
			JSONLayout.addPublicLayout(
				groupName = "Guest",
				layoutName = "JS Components Sample Page");

			JSONLayout.addWidgetToPublicLayout(
				groupName = "Guest",
				layoutName = "JS Components Sample Page",
				widgetName = "JS Components Sample");

			Navigator.gotoPage(pageName = "JS Components Sample Page");
		}
	}

	tearDown {
		var testPortalInstance = PropsUtil.get("test.portal.instance");

		if ("${testPortalInstance}" == "true") {
			PortalInstances.tearDownCP();
		}
		else {
			JSONLayout.deletePublicLayout(
				groupName = "Guest",
				layoutName = "JS Components Sample Page");
		}
	}

	@description = "LPS-148143. Validates language can be added to translation list."
	@priority = "3"
	test CanAddLanguageToTranslationList {
		task ("Given manage translations modal") {
			TranslationManagerPortlet.goToManageTranslationsModal(
				key_currentLocale = "en-us",
				key_title = "Admin");
		}

		task ("When add language in Manage Translations table list") {
			ManageTranslations.addLanguage(key_locale = "zh-CN");
		}

		task ("Then save add language and assert the modal is closed") {
			Click(locator1 = "Button#DONE");

			AssertElementNotPresent(locator1 = "Modal#MODAL");
		}

		task ("Then assert the language is active in language selector") {
			TranslationManagerPortlet.clickSelectTranslation(
				key_currentLocale = "en-us",
				key_title = "Admin");

			AssertElementPresent(
				key_locale = "zh-CN",
				locator1 = "Translation#DROPDOWN_MENU_ITEM");
		}
	}

	@description = "LPS-147612. Validates translation can be synced in selector and text field."
	@priority = "3"
	test CanBeSyncedOnSelectedAndTranslatedLanguages {
		task ("When input different language text in aui tag field") {
			TranslationManagerPortlet.inputTextInAuiTagField(
				key_currentLocale = "en-us",
				key_leftTitle = "Default",
				key_locale = "fr-FR",
				key_rightTitle = "Admin",
				languagefirst = "Welcome to Liferay",
				languagesecond = "Bienvenue sur Liferay");
		}

		task ("When select language in aui tag selector") {
			TranslationManagerPortlet.selectLanguageInAuiTagSelector(
				key_currentLocale = "fr-fr",
				key_leftTitle = "Default",
				key_locale = "en-US");
		}

		task ("Then translated language is available to select") {
			TranslationManagerPortlet.selectLanguageInAuiTagSelector(
				key_currentLocale = "en-us",
				key_leftTitle = "Default",
				key_locale = "fr-FR");
		}

		task ("Then assert language selector is changed to the selected language") {
			AssertElementPresent(
				key_currentLocale = "fr-fr",
				key_title = "Default",
				locator1 = "TranslationManager#SELECT_TRANSLATION_LANGUAGE_AUI_TAG_MENU");

			AssertElementPresent(
				key_currentLocale = "fr-fr",
				key_title = "Admin",
				locator1 = "TranslationManager#SELECT_TRANSLATION_LANGUAGE_AUI_TAG_MENU");
		}

		task ("Then assert translation results displayed by the selected language") {
			AssertElementPresent(
				key_title = "Default",
				locator1 = "TranslationManager#AUI_TAG_INPUT_TEXT_FIELD",
				value1 = "Bienvenue sur Liferay");

			AssertElementPresent(
				key_title = "Admin",
				locator1 = "TranslationManager#AUI_TAG_INPUT_TEXT_FIELD",
				value1 = "Bienvenue sur Liferay");
		}
	}

	@description = "LPS-148142. Validates Manage Translations cancel add language by click cancel button."
	@priority = "3"
	test CancelAddLanguageByClickCancelButton {
		task ("Given manage translations modal") {
			TranslationManagerPortlet.goToManageTranslationsModal(
				key_currentLocale = "en-us",
				key_title = "Admin");
		}

		task ("When add language in Manage Translations table list") {
			ManageTranslations.addLanguage(key_locale = "zh-CN");
		}

		task ("Then cancel add a language with cancel button") {
			Click(locator1 = "Button#CANCEL");
		}

		task ("Then view language not active in selector menu") {
			TranslationManagerPortlet.clickSelectTranslation(
				key_currentLocale = "en-us",
				key_title = "Admin");

			AssertElementNotPresent(
				key_locale = "zh-CN",
				locator1 = "Translation#DROPDOWN_MENU_ITEM");
		}
	}

	@description = "LPS-148142. Validates Manage Translations cancel add language by click somewhere outside the modal."
	@priority = "3"
	test CancelAddLanguageByClickSomewhereOutsideModal {
		task ("Given manage translations modal") {
			TranslationManagerPortlet.goToManageTranslationsModal(
				key_currentLocale = "en-us",
				key_title = "Admin");
		}

		task ("When add language in Manage Translations table list") {
			ManageTranslations.addLanguage(key_locale = "zh-CN");
		}

		task ("Then cancel add a language with click somewhere outside the modal") {
			Click.mouseDownMouseUp(locator1 = "TranslationManager#AUI_TAG");

			AssertElementNotPresent(locator1 = "Modal#MODAL");
		}

		task ("Then view language not active in selector menu") {
			TranslationManagerPortlet.clickSelectTranslation(
				key_currentLocale = "en-us",
				key_title = "Admin");

			AssertElementNotPresent(
				key_locale = "zh-CN",
				locator1 = "Translation#DROPDOWN_MENU_ITEM");
		}
	}

	@description = "LPS-147946. Validates language can be deleted from the Manage Translation list."
	@priority = "3"
	test CanDeleteLanguageFromManageTranslationsList {
		task ("Given manage translations modal") {
			TranslationManagerPortlet.goToManageTranslationsModal(
				key_currentLocale = "en-us",
				key_title = "Admin");
		}

		task ("When delete Catalan language") {
			ManageTranslations.deleteAndSaveLanguage(key_language = "Catalan");
		}

		task ("Then assert Catalan language menu item is not present in select translation language dropdown") {
			TranslationManagerPortlet.clickSelectTranslation(
				key_currentLocale = "en-us",
				key_title = "Admin");

			AssertElementNotPresent(
				key_locale = "ca-ES",
				locator1 = "Translation#DROPDOWN_MENU_ITEM");
		}

		task ("Then assert Catalan language is not present in Manage Translations table list") {
			Click(locator1 = "TranslationManager#MANAGE_TRANSLATIONS");

			AssertElementNotPresent(
				locator1 = "ManageTranslations#LIST_ITEM_LANGUAGE",
				value1 = "Catalan");
		}
	}

	@description = "LPS-147499. Verifies default options are present in translation manager language menu."
	@priority = "3"
	test CanDisplayDefaultLanguageDropdownOptionsOnly {
		task ("When select translation language dropdown") {
			TranslationManagerPortlet.clickSelectTranslation(
				key_currentLocale = "en-us",
				key_title = "Admin");
		}

		task ("Then only default language options are available") {
			AssertElementPresent(
				key_locale = "en-US",
				locator1 = "Translation#DROPDOWN_MENU_ITEM");

			AssertElementPresent(
				key_locale = "ca-ES",
				locator1 = "Translation#DROPDOWN_MENU_ITEM");

			AssertElementPresent(
				key_locale = "fr-FR",
				locator1 = "Translation#DROPDOWN_MENU_ITEM");

			AssertElementPresent(locator1 = "TranslationManager#MANAGE_TRANSLATIONS");

			AssertElementNotPresent(
				key_index = "6",
				locator1 = "TranslationManager#SELECT_TRANSLATION_LANGUAGE_MENU_ITEM_INDEX");
		}
	}

	@description = "LPS-147946. Validates languages are searchable from the Manage Translations list."
	@priority = "3"
	test CanSearchLanguageFromManageTranslationsList {
		task ("Given manage translations modal") {
			TranslationManagerPortlet.goToManageTranslationsModal(
				key_currentLocale = "en-us",
				key_title = "Admin");
		}

		task ("When search for Catalan language") {
			ManageTranslations.searchLanguage(key_language = "Catalan");
		}

		task ("Then assert Catalan language is only present in Manage Translations table list") {
			AssertElementPresent(
				key_language = "Catalan",
				locator1 = "ManageTranslations#LIST_ITEM_LANGUAGE");

			AssertElementNotPresent(
				key_index = "2",
				locator1 = "ManageTranslations#TABLE_BODY_ROW_INDEX");
		}
	}

	@description = "LPS-148144. Validates language status is not translated after deleting and re-adding language on modal."
	@priority = "3"
	test DeletedTranslatedLanguageDoesNotHaveTranslatedStatus {
		task ("When input different language text in aui tag field") {
			TranslationManagerPortlet.inputTextInAuiTagField(
				key_currentLocale = "en-us",
				key_leftTitle = "Default",
				key_locale = "fr-FR",
				key_rightTitle = "Admin",
				languagefirst = "Welcome to Liferay",
				languagesecond = "Bienvenue sur Liferay");
		}

		task ("Given manage translations modal") {
			TranslationManagerPortlet.goToAuiTagManageTranslationsModal(
				key_currentLocale = "fr-fr",
				key_title = "Admin");
		}

		task ("When click done button after deleting French language on modal") {
			ManageTranslations.deleteAndSaveLanguage(key_language = "French");
		}

		task ("Given manage translations modal") {
			TranslationManagerPortlet.goToAuiTagManageTranslationsModal(
				key_currentLocale = "en-us",
				key_title = "Admin");
		}

		task ("Then re-add the French language on Manage Translations modal") {
			ManageTranslations.addLanguage(key_locale = "fr-FR");
		}

		task ("Then assert the French language is changed to Not Translated status") {
			AssertElementPresent(
				key_language = "French",
				key_status = "Not Translated",
				locator1 = "ManageTranslations#LANGUAGE_STATUS");

			Click(locator1 = "Modal#CLOSE_BUTTON");
		}
	}

	@description = "LPS-148144. Validates language status is still translated after not saving deleted translation."
	@priority = "3"
	test NotSavingDeletedTranslatedLanguageStillHasTranslatedStatus {
		task ("When input different language text in aui tag field") {
			TranslationManagerPortlet.inputTextInAuiTagField(
				key_currentLocale = "en-us",
				key_leftTitle = "Default",
				key_locale = "fr-FR",
				key_rightTitle = "Admin",
				languagefirst = "Welcome to Liferay",
				languagesecond = "Bienvenue sur Liferay");
		}

		task ("Given manage translations modal") {
			TranslationManagerPortlet.goToAuiTagManageTranslationsModal(
				key_currentLocale = "fr-fr",
				key_title = "Admin");
		}

		task ("When delete French language without click done button on modal") {
			ManageTranslations.deleteLanguage(key_language = "French");
		}

		task ("Then re-add the French language on Manage Translations modal") {
			ManageTranslations.addLanguage(key_locale = "fr-FR");
		}

		task ("Assert the French language is still translated status") {
			AssertElementPresent(
				key_language = "French",
				key_status = "Translated",
				locator1 = "ManageTranslations#LANGUAGE_STATUS");

			Click(locator1 = "Modal#CLOSE_BUTTON");
		}
	}

}