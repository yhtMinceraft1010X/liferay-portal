@component-name = "portal-frontend-infrastructure"
definition {

	property osgi.modules.includes = "frontend-js-components-sample-web";
	property portal.acceptance = "true";
	property portal.release = "true";
	property portal.upstream = "true";
	property testray.component.names = "Translation Manager";
	property testray.main.component.name = "User Interface";

	setUp {
		TestCase.setUpPortalInstance();

		User.firstLoginPG();

		JSONLayout.addPublicLayout(
			groupName = "Guest",
			layoutName = "JS Components Sample Page");

		JSONLayout.addWidgetToPublicLayout(
			groupName = "Guest",
			layoutName = "JS Components Sample Page",
			widgetName = "JS Components Sample");

		Navigator.gotoPage(pageName = "JS Components Sample Page");
	}

	tearDown {
		var testPortalInstance = PropsUtil.get("test.portal.instance");

		if ("${testPortalInstance}" == "true") {
			PortalInstances.tearDownCP();
		}
		else {
			JSONLayout.deletePublicLayout(
				groupName = "Guest",
				layoutName = "JS Components Sample Page");
		}
	}

	@description = "LPS-147946. Validates language can be deleted from the Manage Translation list."
	@priority = "3"
	test CanDeleteLanguageFromManageTranslationsList {
		task ("Given manage translations modal") {
			TranslationManagerPortlet.goToManageTranslationsModal(
				key_currentLocale = "en-us",
				key_title = "Admin");
		}

		task ("When delete Catalan language") {
			ManageTranslations.deleteLanguage(key_language = "Catalan");
		}

		task ("Then assert Catalan language menu item is not present in select translation language dropdown") {
			TranslationManagerPortlet.clickSelectTranslation(
				key_currentLocale = "en-us",
				key_title = "Admin");

			AssertElementNotPresent(
				key_locale = "ca-ES",
				locator1 = "Translation#DROPDOWN_MENU_ITEM");
		}

		task ("Then assert Catalan language is not present in Manage Translations table list") {
			Click(locator1 = "TranslationManager#MANAGE_TRANSLATIONS");

			AssertElementNotPresent(
				locator1 = "ManageTranslations#LIST_ITEM_LANGUAGE",
				value1 = "Catalan");
		}
	}

	@description = "LPS-147499. Verifies default options are present in translation manager language menu."
	@priority = "3"
	test DefaultLanguageIsAvailableAndViewsNoOtherLanguagesOptions {
		task ("Select translation language menu") {
			TranslationManagerPortlet.goToManageTranslationsModal(
				key_currentLocale = "en-us",
				key_title = "Admin");
		}

		task ("Assert only default language options are available") {
			AssertElementPresent(
				key_locale = "en-US",
				locator1 = "Translation#DROPDOWN_MENU_ITEM");

			AssertElementPresent(
				key_locale = "ca-ES",
				locator1 = "Translation#DROPDOWN_MENU_ITEM");

			AssertElementPresent(
				key_locale = "fr-FR",
				locator1 = "Translation#DROPDOWN_MENU_ITEM");

			AssertElementPresent(locator1 = "TranslationManager#MANAGE_TRANSLATIONS");

			AssertElementNotPresent(
				key_index = "6",
				locator1 = "TranslationManager#SELECT_TRANSLATION_LANGUAGE_MENU_ITEM_INDEX");
		}

		task ("Assert link to Manage Translations is available") {
			Click(locator1 = "TranslationManager#MANAGE_TRANSLATIONS");

			AssertElementPresent(locator1 = "Portlet#MODAL_TITLE");
		}
	}

	@description = "LPS-147946. Validates languages are searchable from the Manage Translations list."
	@priority = "3"
	test CanSearchLanguageFromManageTranslationsList {
		task ("Given manage translations modal") {
			TranslationManagerPortlet.goToManageTranslationsModal(
				key_currentLocale = "en-us",
				key_title = "Admin");
		}

		task ("When search for Catalan language") {
			ManageTranslations.searchLanguage(key_language = "Catalan");
		}

		task ("Then assert Catalan language is only present in Manage Translations table list") {
			AssertElementPresent(
				locator1 = "ManageTranslations#LIST_ITEM_LANGUAGE",
				value1 = "Catalan");

			AssertElementNotPresent(
				locator1 = "ManageTranslations#TABLE_BODY_ROW_INDEX",
				key_index = "2");
		}
	}

	@description = "LPS-147612. Verifies translation results displayed by different language."
	@priority = "3"
	test TranslationResultsDisplayedByDifferentLanguage {
		task ("Input different languages with the same word") {
			TranslationManagerPortlet.useDifferentLanguageInputText(
				key_currentLocale = "en-us",
				key_locale = "fr-FR",
				key_title = "Admin",
				languagefirst = "Welcome to Liferay",
				languagesecond = "Bienvenue sur Liferay");
		}

		task ("View translation results displayed by different language and the language selector is changed to the selected language") {
			AssertElementPresent(
				key_currentLocale = "fr-fr",
				locator1 = "TranslationManager#SELECT_TRANSLATION_LANGUAGE_AUI_TAG_ADMINMENU");

			AssertElementPresent(
				key_title = "Admin",
				locator1 = "TranslationManager#AUI_TAG_INPUT_TEXT_FIELD",
				value1 = "Bienvenue sur Liferay");

			AssertElementPresent(
				key_title = "Admin",
				locator1 = "TranslationManager#AUI_TAG_DEFAULT_LANGUAGE_TEXT",
				value1 = "Welcome to Liferay");
		}
	}

}