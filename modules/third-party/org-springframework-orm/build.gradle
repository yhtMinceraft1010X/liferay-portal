import com.liferay.gradle.plugins.patcher.PatchTask

apply plugin: "com.liferay.patcher"

task deployJar(type: Copy)
task jarPatched(type: Zip)
task patch(type: PatchTask)

dependencies {
	compile group: "org.hibernate", name: "hibernate-core", version: "5.6.7.Final"
	compile group: "org.springframework", name: "spring-orm", version: "5.2.20.RELEASE"
}

deploy {
	finalizedBy deployJar
}

deployJar {
	ext {
		autoClean = false
	}

	from jar
	into "../../../lib/portal"

	rename {
		"spring-orm.jar"
	}
}

jar {
	setActions([])

	dependsOn jarPatched
}

jarPatched {
	archiveName = jar.archiveName
	destinationDir = jar.destinationDir
	duplicatesStrategy = "exclude"

	from copyPatchOriginalLibClasses
}

javadoc {
	enabled = false
}

patch {
	ext {
		autoClean = false
	}

	fileNames "org/springframework/orm/hibernate5/SpringSessionSynchronization.java"

	originalLibModuleName = "spring-orm"
}