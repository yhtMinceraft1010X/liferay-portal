import com.liferay.gradle.plugins.defaults.tasks.ReplaceRegexTask
import com.liferay.gradle.util.copy.RenameDependencyClosure

apply plugin: "com.liferay.poshi.runner"

buildCSS {
	ext {
		autoClean = false
	}
}

dependencies {
	compile group: "com.deque", name: "axe-selenium", version: "1.1"
	compile group: "com.github.lewka", name: "ocular", version: "1.0.5"
	compile group: "com.github.scribejava", name: "scribejava-core", version: "3.2.0"
	compile group: "com.google.guava", name: "guava", version: "30.1-jre"
	compile group: "com.jayway.jsonpath", name: "json-path", version: "2.1.0"
	compile group: "com.liferay", name: "com.liferay.poshi.core", version: "1.0.63"
	compile group: "com.liferay", name: "jodd.util", version: "6.0.1.LIFERAY-PATCHED-1"
	compile group: "com.liferay", name: "net.jsourcerer.webdriver.JSErrorCollector", version: "0.6"
	compile group: "com.sun.mail", name: "jakarta.mail", version: "1.6.6"
	compile group: "commons-io", name: "commons-io", version: "2.8.0"
	compile group: "commons-lang", name: "commons-lang", version: "2.6"
	compile group: "junit", name: "junit", version: "4.13.1"
	compile group: "net.lightbody.bmp", name: "browsermob-core", version: "2.1.5"
	compile group: "org.bytedeco.javacpp-presets", name: "opencv", version: "2.4.10-0.10"
	compile group: "org.dom4j", name: "dom4j", transitive: false, version: "2.1.3"
	compile group: "org.json", name: "json", version: "20180813"
	compile group: "org.jsoup", name: "jsoup", version: "1.10.2"
	compile group: "org.seleniumhq.selenium", name: "selenium-java", version: "3.141.59"
	compile group: "org.sikuli", name: "sikuli-api", version: "1.2.0"
	compile group: "xml-apis", name: "xml-apis", version: "1.4.01"

	compileOnly project(":test:data-guard-connector")
}

task release(type: ReplaceRegexTask)

deploy {
	from configurations.runtime
	rename new RenameDependencyClosure(project, configurations.runtime.name)
}

liferay {
	deployDir = project.properties["sdk.dir"] + "/dependencies/com.liferay.poshi.runner/lib"
}

liferayOSGi {
	expandCompileInclude = true
}

release {
	String releaseVersion = project.getVersion()

	if (project.hasProperty("releaseVersion")) {
		releaseVersion = getProperty("releaseVersion")
	}

	setReplacement(releaseVersion)

	match(/private Object _version = "(\d.+)";/, "../../../sdk/gradle-plugins-poshi-runner/src/main/java/com/liferay/gradle/plugins/poshi/runner/PoshiRunnerExtension.java")
}

test {
	systemProperty "webdriver.chrome.driver", "${buildDir}/webdriver/chromedriver"

	dependsOn downloadChromeDriver
}